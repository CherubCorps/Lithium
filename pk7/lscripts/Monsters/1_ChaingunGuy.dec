actor Lith_ChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
   Health 60
   AttackSound ""
   DropItem "Lith_Dummy"
   Speed 9
   Tag "$LITH_INFO_SHORT_ChaingunGuy"
   
   states
   {
   Missile:
      CPOS E 2 A_FaceTarget
      CPOS E 0 A_PlaySound("enemies/chainguy/bolt", CHAN_WEAPON|CHAN_NOPAUSE)
      CPOS E 0 A_GiveToTarget("Lith_AdrenalineToken", 1)
      CPOS E 12 A_FaceTarget
   MissileLoop:
      CPOS F 0 bright A_JumpIfInTargetInventory("Lith_HasBarrier", 1, 2)
      CPOS F 0 A_Jump(256, "MissileFire")
      CPOS F 0 A_PlaySound("enemies/chainguy/fire")
      CPOS F 4 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      CPOS E 0 A_PlaySound("enemies/chainguy/fire")
      CPOS E 4 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      goto MissileDone
   MissileFire:
      CPOS F 0 A_PlaySound("enemies/chainguy/fire")
      CPOS F 4 bright A_CustomBulletAttack(22.5, 0, 1, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
      CPOS E 0 A_PlaySound("enemies/chainguy/fire")
      CPOS E 4 bright A_CustomBulletAttack(22.5, 0, 1, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
      goto MissileDone
   MissileDone:
      CPOS F 1 A_CPosRefire
      goto MissileLoop
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ChaingunGuy * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ChaingunGuy)
      goto Super::Death
   }
}

// EOF
