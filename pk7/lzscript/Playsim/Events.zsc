#include "lzscript/Playsim/EndSeq.zsc"

class Lith_Events : EventHandler;

bool m_bosslevel;

override void WorldLoaded(WorldEvent ev)
{
   static const string[] bosslevels = {
      "058fb092ea1b70da1e3cbf501c4a91a1", // E1M8
      "effe91df41ad41f6973c06f0ad67ddb9", // E2M8
      "ef128313112110ed6c1549af96af26c9", // E3M8
      "2dc939e508ab8eb68af79d5b60568711", // E4M8
      "5eecd88f4491f516d590ce4bbf45f532"  // MAP30
   };

   if(ev.isSaveGame)
      return;

   if(ev.isReopen)
      CallACS("Lith_WorldReopen");
   else
   {
      CallACS("__lithlib.bin_init");
      CallACS("__lithmain.bin_init");

      let sum = level.getChecksum();
      for(int i = 0; i < bosslevels.size(); i++) if(sum == bosslevels[i])
      {
         m_bosslevel = true;
         break;
      }
   }
}

override void WorldThingDestroyed(WorldEvent ev)
{
   if(ev.thing is "Lith_InventoryItem")
      Lith_InventoryItem(ev.thing).Lith_Destroy();
}

override void WorldUnloaded(WorldEvent ev)
{
   let it = ThinkerIterator.Create("Lith_InventoryItem");
   for(Lith_InventoryItem mo; mo = Lith_InventoryItem(it.next());)
      if(!mo.owner) mo.Lith_Destroy();
}

override void UiTick()
{
   // because we override finales in the MAPINFO, this should only trigger
   // at the very end of the game. hopefully.
   // EDIT: it didn't, so I had to add more hacks
   if(gamestate == GS_FINALE && (!level.nextmap || level.nextmap.left(6) == "enDSeQ"))
      CallACS("Lith_Finale");

   if(m_eseq == ESEQ_HALT && __lith_end != "")
   {
      LoadEnding(String.Format("lfiles/End_%s.txt", __lith_end));
      CVar.FindCVar('__lith_end').setString("");
   }
   else if(m_eseq >= ESEQ_RUN)
      RunEndSequence();
}

override void RenderOverlay(RenderEvent ev)
   {if(m_eseq >= ESEQ_RUN) DrawEndSequence(ev);}

override void PlayerEntered(PlayerEvent ev)
{
   PlayerInfo p = players[ev.playerNumber];

   // in case the player isn't derived from Lith_Player
   if(p && p.mo && !p.mo.findInventory("Lith_AAGH"))
   {
      p.mo.giveInventoryType("Lith_AAGH");
      p.mo.giveInventoryType("Lith_IDOL");
   }
}

override void WorldThingSpawned(WorldEvent ev)
{
   let th = ev.thing;

   if(th is "CommanderKeen")
   {
      th.bNOBLOOD = true;
      th.setTag("Commander Keen");
      return;
   }

   if(th.bISMONSTER)
   {
      th.ACS_NamedExecuteAlways("Lith_MonsterInfo");

      if(CallACS("LWData", wdata_fun) & lfun_ragnarok)
         Lith_HERMES.PrepareForRagnarok(th);

      if(m_bosslevel && th.bBOSS)
      {
         th.health *= frandom(2, 3);
         th.reactiontime = 0;
         th.damageMultiply *= frandom(1.1, 1.6);
         th.damageFactor   *= frandom(0.3, 0.5);
      }
   }
}

override void WorldTick()
{
   if(m_bosslevel) for(int i = 0, m = level.lines.size(); i < m; i++) for(int j = 0; j < 3; j++)
      level.lines[i].sidedef[0].setTextureYoffset(j, sin(level.totaltime)*4);
}

override void WorldThingDamaged(WorldEvent ev)
{
   let src = ev.damagesource;
   let dst = ev.thing;

   if(!dst) return;

   if(ev.damageType == 'Bullet' && ev.damage >= 8 && !dst.bNOBLOOD)
   {
      let mo = Actor.Spawn("Lith_Dummy", dst.pos + (0, 0, dst.height / 2));
      float vol = clamp(ev.damage / 14.0, 0, 1);
      mo.A_PlaySound("body/bullethit", CHAN_BODY, vol * frandom(0.5, 1.1));
   }

   if(!src) return;

   if(src != dst && src.countInv("Lith_MonsterID") && (dst.bCOUNTKILL || dst.player))
      src.ACS_ScriptCall("Lith_GiveMonsterEXP", ev.damage * 3.77);
}

override void CheckReplacement(ReplaceEvent ev)
{
   bool isfinal = ev.isFinal;
   ev.isFinal = false;
   switch(ev.replacee.getClassName()) {
   case 'MiniZorchRecharge':
   case 'BlasterAmmo':
   case 'GoldWandAmmo':
   case 'MaceAmmo':            ev.replacement = "Clip";                  break;
   case 'MiniZorchPack':
   case 'BlasterHefty':
   case 'GoldWandHefty':
   case 'MaceHefty':           ev.replacement = "ClipBox";               break;
   case 'LargeZorchRecharge':
   case 'CrossbowAmmo':        ev.replacement = "Shell";                 break;
   case 'LargeZorchPack':
   case 'CrossbowHefty':       ev.replacement = "ShellBox";              break;
   case 'PropulsorZorch':
   case 'PhoenixRodAmmo':      ev.replacement = "RocketAmmo";            break;
   case 'PropulsorZorchPack':
   case 'ArtiTimeBomb':
   case 'PhoenixRodHefty':     ev.replacement = "RocketBox";             break;
   case 'PhasingZorch':
   case 'SkullRodAmmo':        ev.replacement = "Cell";                  break;
   case 'PhasingZorchPack':
   case 'ArtiEgg':
   case 'SkullRodHefty':       ev.replacement = "CellPack";              break;
   case 'Bootspoon':
   case 'Staff':               ev.replacement = "Fist";                  break;
   case 'SuperBootspork':
   case 'Gauntlets':           ev.replacement = "Chainsaw";              break;
   case 'MiniZorcher':
   case 'GoldWand':            ev.replacement = "Pistol";                break;
   case 'LargeZorcher':
   case 'Crossbow':            ev.replacement = "Shotgun";               break;
   case 'SuperLargeZorcher':   ev.replacement = "SuperShotgun";          break;
   case 'RapidZorcher':
   case 'Blaster':             ev.replacement = "Chaingun";              break;
   case 'ZorchPropulsor':
   case 'PhoenixRod':          ev.replacement = "RocketLauncher";        break;
   case 'PhasingZorcher':
   case 'Mace':                ev.replacement = "PlasmaRifle";           break;
   case 'LAZDevice':
   case 'SkullRod':            ev.replacement = "BFG9000";               break;
   case 'BowlOfVegetables':
   case 'ArtiHealth':          ev.replacement = "Medikit";               break;
   case 'ArtiInvisibility':    ev.replacement = "BlurSphere";            break;
   case 'ArtiInvulnerability': ev.replacement = "InvulnerabilitySphere"; break;
   case 'SuperchargeBreakfast':
   case 'ArtiSuperHealth':     ev.replacement = "SoulSphere";            break;
   case 'ArtiTomeOfPower':     ev.replacement = "Berserk";               break;
   case 'UltraGoggles':
   case 'ArtiTorch':           ev.replacement = "Infrared";              break;
   case 'Zorchpack':
   case 'BagOfHolding':        ev.replacement = "Backpack";              break;
   case 'BowlOfFruit':
   case 'CrystalVial':         ev.replacement = "StimPack";              break;
   case 'SuperChexArmor':
   case 'EnchantedShield':     ev.replacement = "BlueArmor";             break;
   case 'ChexArmor':
   case 'SilverShield':        ev.replacement = "GreenArmor";            break;
   case 'ComputerAreaMap':
   case 'SuperMap':            ev.replacement = "AllMap";                break;
   case 'GlassOfWater':        ev.replacement = "HealthBonus";           break;
   case 'SlimeRepellent':      ev.replacement = "ArmorBonus";            break;
   case 'SlimeProofSuit':      ev.replacement = "RadSuit";               break;
   default: ev.isFinal = isfinal; break;
   }
}

override bool InputProcess(InputEvent ev)
{
   let cl = Lith_URANUS.Instance();

   if(!cl) return false;

   if((ev.type == InputEvent.Type_KeyDown ||
       ev.type == InputEvent.Type_KeyUp) &&
      (ev.keyScan == InputEvent.Key_LShift ||
       ev.keyScan == InputEvent.Key_RShift))
   {
      cl.m_pdata[consoleplayer].shiftDown =
         (ev.type == InputEvent.Type_KeyDown);

      return false;
   }
   else if(Lith_Player(players[consoleplayer].mo).m_grabInput &&
      ev.type == InputEvent.Type_KeyDown && ev.keyString)
   {
      int ch = ev.keyChar;

      if(cl.m_pdata[consoleplayer].shiftDown &&
         (ch >= Ch_A && ch <= Ch_Z || ch >= Ch_L_A && ch <= Ch_L_Z))
      {
         ch -= 32;
      }

      EventHandler.SendNetworkEvent("Lith_Key", ch);
      return true;
   }

   return false;
}

override void NetworkProcess(ConsoleEvent ev)
{
   if(ev.name == "Lith_Key")
      CallACS("Lith_KeyDown", ev.player, ev.args[0]);
}

// EOF
