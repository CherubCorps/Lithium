actor Lith_DiffPlasma : Lith_PlasmaBolt
{
   Damage (8*random(6,9))
   Speed 25
   DeathSound "weapons/plasdiff/explode"

   states
   {
   Spawn:
      TNT1 A 0
   Trail:
      TNT1 A 0 A_JumpIf(user_trailstep >= 5, "TrailDone")
      TNT1 A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaBoltTrail_Blue",
                                   ((user_trailstep * 10.0) * velx) / -35.0,
                                  -((user_trailstep * 10.0) * vely) / -35.0,
                             2.0 + ((user_trailstep * 10.0) * velz) / -35.0,
                             0, 0, 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      TNT1 A 0 A_SetUserVar(user_trailstep, 0)
      goto Step
   Step:
      TNT1 A 1
      TNT1 A 0 A_Jump(256, "Trail")
   Death:
      TNT1 AAAA 0 A_SpawnParticle("Blue", SPF_FULLBRIGHT, 15, 4, 0,
         frandom(-5, 5),frandom(-5, 5),frandom(-5, 5),
         frandom(-5, 5),frandom(-5, 5),frandom(-5, 5))
      PLSE ABCDE 4 bright
      stop
   }
}

actor Lith_PlasmaDiffuser : Lith_Weapon
{
   Tag "$LITH_TXT_INFO_SHORT_PlasmaDiffuser"
   Weapon.UpSound "weapons/plasdiff/draw"
   Weapon.SlotNumber 6
   Weapon.SlotPriority 0.9

   +WEAPON.CHEATNOTWEAPON
   
   states
   {
   Ready:
      PDFG A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      PDFG AAA 0 A_Lower
      PDFG A 1
      loop
   
   Select:
      PDFG AAA 0 A_Raise
      PDFG A 1
      loop
   
   Fire:
      PDFG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PDFG A 0 A_TakeInventory("Lith_PlasmaAmmo", 60, TIF_NOTAKEINFINITE)
      PDFG A 0 A_GunFlash("DummyFlash")
      PDFG A 0 A_PlaySound("weapons/plasdiff/fire", CHAN_WEAPON)
      PDFG A 0 A_FireCustomMissile("Lith_DiffPlasma", 0, 0, 0, -9)
      PDFG A 0 A_Jump(128, 3)
      PDFG B 1 offset(0, 40) bright
      PDFG A 0 A_Jump(256, 2)
      PDFG C 1 offset(0, 40) bright
      PDFG "#" 3 offset(0, 35) bright
      PDFG A 1 offset(0, 34) bright A_ReFire
      PDFG A 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireEmpty:
      PDFG A 0 A_StopSound(CHAN_7)
      PDFG A 0 A_StopSound(CHAN_6)
      PDFG A 1 A_PlaySound("weapons/plasdiff/empty", 7)
      PDFG A 1 offset(0, 37)
      PDFG A 1 offset(0, 36)
      PDFG A 1 offset(0, 35)
      PDFG A 1 offset(0, 34)
      PDFG A 1 offset(0, 32)
      PDFG A 1 A_ReFire
      goto Ready
   }
}
