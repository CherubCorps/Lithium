/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * These are originally from LegenDoom. Thanks, Yholl!
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_GunSmoke : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.5;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;

      Projectile;
      +NOINTERACTION
      +THRUACTORS
      +FORCEXYBILLBOARD
   }

   override void PostBeginPlay()
   {
      Super.PostBeginPlay();
      Alpha = FRandom(.1, .4);
      Vel *= .01;
      Scale *= FRandom(.07, .14);
      if(Random() < 128) Scale.X = -Scale.X;
   }

   states
   {
   Spawn:
      TNT1 A 0;
      SMOK ABCDEFGHIJKLMNOPQ 5 A_FadeTo(0, 0.01, FTF_REMOVE);
      stop;
   }
}

class Lith_GunSmokeEmitter : Actor
{
   default
   {
      Radius 4;
      Height 4;
      Speed 7;
      Damage 0;

      Projectile;
      +THRUACTORS
      +NOCLIP
   }

   const flags = SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY;

   states
   {
   Spawn:
      TNT1 A 1;
   Death:
      TNT1 A 0 {
         for(int i = 0, n = Random(5, 7); i < n; i++)
            A_SpawnItemEx("Lith_GunSmoke", FRandom(-1,1),FRandom(-1,1),FRandom(-1,1), zvel: Vel.Z, flags: flags);
      }
      stop;
   }
}

class Lith_GunSmokeSpawner : Actor
{
   default
   {
      Radius 4;
      Height 4;
      Speed 7;
      Damage 0;
      Lith_GunSmokeSpawner.Particles 10;

      Projectile;
      +THRUACTORS
      +NOCLIP
   }

   meta int m_Part;

   property Particles: m_Part;

   const flags = SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY;

   void Lith_A_SpawnSmoke(float vx, float vy)
   {
      A_SpawnItemEx("Lith_GunSmoke", FRandom(-4,4),FRandom(-1,1),FRandom(-2,2), Vel.X + vx,Vel.Y + vy,Vel.Z, 0, flags);
   }

   void Lith_A_SpawnParticles(color c)
   {
      A_SpawnParticle(c, SPF_FULLBRIGHT|SPF_RELATIVE, Random(3, 8), Random(1, 2), FRandom(-15, 15), FRandom(-1,1),FRandom(-1,1),FRandom(-1,1), FRandom(1, 16),FRandom(-0.1, 0.1),(Vel.Z*1.5)+FRandom(-1,1), 0.0, 0.0, -0.05, 0.98, -1);
   }

   states
   {
   Spawn:
      TNT1 A 1;
   Death:
      TNT1 A 0 {
         Lith_A_SpawnSmoke(0, FRandom(-0.2, 0.2));
         for(int i = 0, n = Random(m_Part/2, m_Part); i < n; i++)
            Lith_A_SpawnSmoke(FRandom(-10, 10), FRandom(-10, 10));
         for(int i = 0, j = Random(m_Part + 1, m_Part + 3); i < j; i++)
            Lith_A_SpawnParticles("Yellow");
         for(int i = 0, j = Random(m_Part + 1, m_Part + 2); i < j; i++)
            Lith_A_SpawnParticles("White");
      }
      stop;
   }
}

class Lith_GunSmokeSpawnerSmall : Lith_GunSmokeSpawner
{
   default
   {
      Lith_GunSmokeSpawner.Particles 5;
   }
}

/* EOF */
