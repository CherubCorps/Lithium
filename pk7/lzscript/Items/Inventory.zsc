/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_InventoryItem : Inventory abstract {
   enum Lith_InventoryItemType {
      _itemt_item,
      _itemt_bagitem,
   }

   default {
      Inventory.PickupMessage "";
      Inventory.PickupSound "";
      Lith_InventoryItem.InvEquip _cont_store;
      Lith_InventoryItem.ItemType _itemt_item;

      +Inventory.Untossable
      +Inventory.Undroppable
      +CastSpriteShadow
   }

   int    m_itemType;
   int    m_invEquip;
   string m_invName;
   int    m_invSell;
   int    m_invItem;
   int    m_invType;
   int    m_invW, m_invH;

   string m_infoPage;

   int m_w, m_h;

   property ItemType: m_itemType;
   property InvType:  m_invType;
   property InvSize:  m_invW, m_invH;
   property InvEquip: m_invEquip;
   property InvName:  m_invName;
   property InvSell:  m_invSell;
   property Size:     m_w, m_h;
   property InfoPage: m_infoPage;

   private void Lith_OnAttach(Actor mo) {
   }

   bool Lith_OnBody() {
      return CallACS("Lith_ItemOnBody", m_invItem);
   }

   void Lith_Detach() {
      if(!self) return; m_invItem = 0; Destroy();
   }

   bool Lith_UseItem(Actor mo) {
      return false;
   }

   void Lith_Destroy() {
      if(m_invItem) {
         uint ip = m_invItem;
         m_invItem = 0;
         CallACS("Lith_ItemDetach", ip);
      }
   }

   private void Lith_CreateItem() {
      switch(m_itemType) {
      case _itemt_item:    m_invItem = CallACS("Lith_ItemCreate");    break;
      case _itemt_bagitem: m_invItem = CallACS("Lith_BagItemCreate"); break;
      }
   }

   private string Lith_GetName() {
      return StringTable.Localize("LITH_ITEM_TAG_" .. m_invName, false);
   }

   override Inventory CreateCopy(Actor mo) {
      let copy = Lith_InventoryItem(Super.CreateCopy(mo));

      if(copy != self) {
         copy.setTag(Lith_GetName());
         copy.m_invName  = m_invName;
         copy.m_invSell  = m_invSell;
         copy.m_w        = m_w;
         copy.m_h        = m_h;
         copy.m_infoPage = m_infoPage;
         copy.Lith_CreateItem();
      }

      return copy;
   }

   override bool HandlePickup(Inventory item) {return false;}

   override void PostBeginPlay() {
      Super.PostBeginPlay();
      setTag(Lith_GetName());
      Lith_CreateItem();
   }

   override void AttachToOwner(Actor mo) {
      let id = Lith_IDOL(mo.FindInventory("Lith_IDOL"));
      if(CallACS("Lith_ItemAttach", m_invItem)) {
         if(m_infoPage) Lith_HERMES.UnlockBip(mo, m_infoPage);
         Lith_OnAttach(mo);
      }
      Super.AttachToOwner(id);
   }

   override bool CanPickup(Actor mo) {
      if(Super.CanPickup(mo)) {
         let id = Lith_IDOL(mo.FindInventory("Lith_IDOL"));
         return m_invItem && id && CallACS("Lith_ItemCanPlace", m_invItem);
      } else {
         return false;
      }
   }

   override bool TryPickup(in out Actor mo) {
      if(Super.TryPickup(mo)) {
         Lith_HERMES.Log(msg_item, 3, Lith_GetName());
         return true;
      } else {
         return false;
      }
   }
}

class Lith_BasicPickup : Inventory {
   default {
      Inventory.PickupMessage "";
      Inventory.PickupSound "";
      Lith_BasicPickup.LogType msg_item;
      +Lith_BasicPickup.DrawPopup
      +Inventory.Transfer
      +CastSpriteShadow
   }

   private meta string m_LogName;
   private meta int    m_LogType;
   private meta int    m_LogLevl;

   private int m_Flags;

   string m_InfoPage;

   property  LogName: m_LogName, m_LogLevl;
   property  LogType: m_LogType;
   property InfoPage: m_InfoPage;

   flagdef Vacuum:    m_Flags, 0;
   flagdef DrawPopup: m_Flags, 1;

   virtual void Lith_OnPickup(Actor mo) {
      if(m_LogName)
         Lith_HERMES.Log(m_LogType, m_LogLevl, "_" .. m_LogName);
      if(m_InfoPage)
         Lith_HERMES.UnlockBip(mo, m_InfoPage);
   }

   override bool TryPickup(in out Actor mo) {
      Lith_OnPickup(mo);
      if(bDrawPopup) {
         callAcs("Lith_ItemPopupTag");
      }
      GoAwayAndDie();
      return true;
   }
}

/* IDOL: Inventory Dummies are Overly Luxurious */
class Lith_IDOL : Inventory;

default {
   +Inventory.KeepDepleted
   +Inventory.Untossable
   +Inventory.Undroppable
}

Lith_InventoryItem Lith_Find(uint ip) {
   for(Inventory it = inv; it; it = it.inv) {
      if(it.Owner == self && it is "Lith_InventoryItem") {
         let item = Lith_InventoryItem(it);
         if(item.m_invItem == ip)
            return item;
      }
   }

   return null;
}

void Lith_Remove(uint ip) {
   let it = Lith_Find(ip);
   it.Lith_Detach();
}

bool Lith_Use(uint ip) {
   let it = Lith_Find(ip);
   return it.Lith_UseItem(owner);
}

/* EOF */
