class Lith_MissileLauncher : Lith_Weapon;

default
{
   Tag "$LITH_INFO_SHORT_MissileLauncher";
   Weapon.UpSound "weapons/missile/draw";
   Weapon.SlotNumber 5;
   Weapon.SlotPriority 0.9;
   Weapon.BobStyle "Smooth";
   Weapon.BobSpeed 1.3;
   Weapon.BobRangeY 1.1;
   Weapon.BobRangeX 0.9;
   Lith_Weapon.MagSize 10;

   +WEAPON.CHEATNOTWEAPON
}

int m_reset;

states(weapon)
{
Ready:
   MISG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   MISG AAA 0 A_Lower;
   MISG A 1;
   loop;
Select:
   MISG AAA 0 A_Raise;
   MISG A 1;
   loop;

Reload:
   MISG A 0 Lith_A_JumpIfMagFull;
   MISG A 1;
   MISG A 1 offset(-2, 34);
   MISG A 1 offset(-4, 37);
   MISG A 1 offset(-9, 43) A_PlaySound("weapons/missile/reloadout", CHAN_6);
   MISG A 1 offset(-12, 47);
   MISG A 1 offset(-14, 52);
   MISG A 2 offset(-12, 55);
   MISG A 3 offset(-11, 57);
   MISG A 8 offset(-10, 60);
   MISG A 8 offset(-11, 60);
   MISG A 0 Lith_A_ReloadFinish;
   MISG A 2 offset(-13, 59) A_PlaySound("weapons/missile/reloadin", CHAN_7);
   MISG A 2 offset(-11, 49);
   MISG A 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   MISG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB);
   MISG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB);
   MISG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB);
   MISG A 4 A_WeaponReady(WRF_NOBOB);
   goto Ready;

Fire:
   MISG A 0 {invoker.m_reset = 0;}
Fire1:
   MISG A 1;
   MISG A 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1);
   goto FireEmpty;
   MISG A 0 Lith_A_JumpIfMagEmpty;
   MISG A 0 Lith_A_MagIncr;
   
   MISG A 0 A_AlertMonsters;
   MISG A 0 A_PlaySound("weapons/missile/fire", CHAN_WEAPON);
   MISG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE);
   MISG AAA 0 A_FireProjectile("Lith_MiniMissile", frandom(-7, 7), false, frandom(-2, 2), frandom(-2, 2), FPF_NOAUTOAIM, frandom(-5, 5));
   MISG A 0 {invoker.m_reset = min(invoker.m_reset, 5);}
   MISG A 0 A_JumpIf(invoker.m_reset >= 5, "FireAnim6"); // fuck you eat crime
   MISG A 0 A_JumpIf(invoker.m_reset >= 4, "FireAnim5");
   MISG A 0 A_JumpIf(invoker.m_reset >= 3, "FireAnim4");
   MISG A 0 A_JumpIf(invoker.m_reset >= 2, "FireAnim3");
   MISG A 0 A_JumpIf(invoker.m_reset >= 1, "FireAnim2");
FireAnim: // choke on the crime
   MISG B 1 bright offset(0, 32);
   MISG C 1 bright offset(0, 33);
   MISG D 1 bright offset(0, 34);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting;
FireAnim2: // be consumed by the crime
   MISG B 1 bright offset(0, 33);
   MISG C 1 bright offset(0, 34);
   MISG D 1 bright offset(0, 35);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting2;
FireAnim3: // fall victim to the crime
   MISG B 1 bright offset(0, 34);
   MISG C 1 bright offset(0, 35);
   MISG D 1 bright offset(0, 36);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting3;
FireAnim4: // crime is love
   MISG B 1 bright offset(0, 35);
   MISG C 1 bright offset(0, 36);
   MISG D 1 bright offset(0, 37);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting4;
FireAnim5: // crime is life
   MISG B 1 bright offset(0, 36);
   MISG C 1 bright offset(0, 37);
   MISG D 1 bright offset(0, 38);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting5;
FireAnim6: // worship the crime, for it brings graf salt
   MISG B 1 bright offset(0, 37);
   MISG C 1 bright offset(0, 38);
   MISG D 1 bright offset(0, 39);
   MISG A 0 {invoker.m_reset++;}
   goto AnimResetting6;

AnimResetting6: // oh yessssss
   MISG A 1 offset(0, 39) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
AnimResetting5:
   MISG A 1 offset(0, 37) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
AnimResetting4:
   MISG A 1 offset(0, 36) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
AnimResetting3:
   MISG A 1 offset(0, 35) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
AnimResetting2:
   MISG A 1 offset(0, 34) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
AnimResetting:
   MISG A 1 offset(0, 33) A_ReFire("Fire1");
   MISG A 0 {invoker.m_reset--;}
   MISG AAAAA 1 offset(0, 32) A_ReFire("Fire1");
   goto Ready;

FireEmpty: // fuck u i ain't doing more
   MISG A 1;
   MISG A 2 offset(0, 41);
   MISG A 1 offset(0, 45);
   MISG A 1 offset(0, 43);
   MISG A 1 offset(0, 40) A_PlaySound("weapons/missile/empty", CHAN_5);
   MISG A 1 offset(0, 38);
   MISG A 1 offset(0, 36);
   MISG A 1 offset(0, 33);
   MISG A 1 offset(0, 32);
   MISG A 5;
   MISG AAAA 1 A_ReFire;
   goto Ready;
}

// EOF
