/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_AllMap : Lith_CustomInventory replaces Allmap
{
   default
   {
      Tag "Computer Area Map";
      Inventory.PickupSound "player/pickup/allmap";
      Lith_CustomInventory.InfoPage "AllMap";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      PMAP ABCDCB 6 bright;
      loop;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "AllMap");
      TNT1 A 0 A_GiveInventory("MapRevealer");
      stop;
   }
}

class Lith_Berserk : Lith_CustomInventory replaces Berserk
{
   default
   {
      Tag "Berserk";
      Inventory.PickupSound "player/pickup/berserk";
      Lith_CustomInventory.InfoPage "Berserk";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      PSTR A -1;
      stop;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "Berserk");
      TNT1 A 0 A_GiveInventory("PowerStrength");
      TNT1 A 0 HealThing(100, 0);
      stop;
   }
}

class Lith_BlurSphereReplacer : RandomSpawner replaces BlurSphere
{
   default
   {
      Tag "Barrier";
      DropItem "Lith_BlurSphere", 255, 9998;
      DropItem "Lith_SteggleSphere", 255, 1;
      DropItem "Lith_ShobonSphere", 255, 1;
   }
}

class Lith_Infrared : Lith_CustomInventory replaces Infrared
{
   default
   {
      Tag "CB-Goggles";
      Inventory.PickupSound "player/pickup/infrared";
      Lith_CustomInventory.InfoPage "Infrared";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      PVIS A 6 bright;
      PVIS B 6;
      loop;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "Infrared");
      TNT1 A 0 A_GiveInventory("Lith_PowerLightAmp");
      stop;
   }
}

class Lith_InvulnerabilitySphereSprite : Lith_MagicSphereItemSprite
{
   default {Lith_AnimItemSprite.Phases 24;}
   states {SetSpr: PINV AAAAAABBBBBBCCCCCCDDDDDD 0 Lith_A_JumpPhase; stop;}
}

class Lith_MegasphereSprite : Lith_MagicSphereItemSprite
{
   default {Lith_AnimItemSprite.Phases 30;}
   states {SetSpr: MEGA AAAAAABBBBBBCCCCCCDDDDDDBBBBBB 0 Lith_A_JumpPhase; stop;}
}

class Lith_BlurSphereSprite : Lith_MagicSphereItemSprite
{
   default {Lith_AnimItemSprite.Phases 36; +VISIBILITYPULSE}
   states {SetSpr: PINS AAAAAABBBBBBCCCCCCDDDDDDCCCCCCBBBBBB 0 Lith_A_JumpPhase; stop;}
}

class Lith_Megasphere : Lith_CustomInventory replaces Megasphere
{
   default
   {
      Tag "Tao'chyan";
      Inventory.PickupSound "player/pickup/mega";
      Lith_CustomInventory.InfoPage "Taochyan";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnItemEx("Lith_MegasphereSprite", 0,0,0, 0,0,0, 0, SXF_SETMASTER);
      TNT1 A 1;
      wait;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 4, "Megasphere");
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_GiveHealthBonus", 200);
      TNT1 A 0 A_GiveInventory("Lith_MegaProtection", 100);
      stop;
   }
}

class Lith_RadSuit : Lith_CustomInventory replaces RadSuit
{
   default
   {
      Tag "Radiation Shielding Suit";
      Inventory.PickupSound "player/pickup/radsuit";
      Lith_CustomInventory.InfoPage "RadSuit";

      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      SUIT A -1 bright;
      stop;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "Radsuit");
      TNT1 A 0 A_GiveInventory("Lith_PowerIronFeet");
      stop;
   }
}

class Lith_SoulSphereSprite : Lith_MagicSphereItemSprite
{
   default {Lith_AnimItemSprite.Phases 30;}
   states {SetSpr: SOUL AAAAAABBBBBBCCCCCCDDDDDDBBBBBB 0 Lith_A_JumpPhase; stop;}
}

class Lith_SoulSphere : Lith_CustomInventory replaces SoulSphere
{
   default
   {
      Tag "Heart";
      Inventory.PickupSound "player/pickup/super";
      Lith_CustomInventory.InfoPage "Heart";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnItemEx("Lith_SoulSphereSprite", 0,0,0, 0,0,0, 0, SXF_SETMASTER);
      TNT1 A 1;
      wait;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 4, "Soulsphere");
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_GiveHealthBonus", 100);
      stop;
   }
}

class Lith_ArtiFly : Lith_CustomInventory replaces ArtiFly
{
   default
   {
      Tag "Wings of Wrath";
      Inventory.PickupFlash "PickupFlash";
      Inventory.PickupSound "misc/p_pkup";
      Inventory.RespawnTics 4320;

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states
   {
   Spawn:
      SOAR ABCB 5;
      loop;
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "ArtiFly");
      TNT1 A 0 A_GiveInventory("PowerFlight");
      stop;
   }
}

class Lith_ArtiTeleport : Lith_CustomInventory replaces ArtiTeleport
{
   default
   {
      Inventory.PickupFlash "PickupFlash";
      Inventory.PickupSound "misc/p_pkup";
      Inventory.RespawnTics 4320;
      Tag "Chaos Device";

      +COUNTITEM
      +INVENTORY.FANCYPICKUPSOUND
   }

   states(Actor)
   {
   Spawn:
      ATLP ABCB 4;
      loop;
   }

   states(Item)
   {
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "ArtiTeleport");
      TNT1 A 0 {UseInventory(GiveInventoryType("ArtiTeleport"));}
      stop;
   }
}

class Lith_PowerLightAmp : PowerLightAmp
   {override void Tick() {if(!level.isFrozen()) Super.Tick();}}

class Lith_PowerInvulnerable : PowerInvulnerable
   {override void Tick() {if(!level.isFrozen()) Super.Tick();}}

class Lith_PowerIronFeet : PowerIronFeet
   {override void Tick() {if(!level.isFrozen()) Super.Tick();}}

/* this is a rather terrible hack, but there's actually no other way to give */
/* the player the special inverted screen color map in the engine... */
class Lith_PowerInvulnerableGiver : PowerupGiver
{
   default
   {
      Inventory.MaxAmount 0;
      Powerup.Type "Lith_PowerInvulnerable";
      Powerup.Color "InverseMap";
      +INVENTORY.AUTOACTIVATE
      +INVENTORY.ALWAYSPICKUP
      +INVENTORY.BIGPOWERUP
   }
}

class Lith_PowerInvulnerableGiver2 : Lith_PowerInvulnerableGiver
   {default {Powerup.Color "GoldMap";}}

class Lith_InvulnerabilitySphere : Lith_CustomInventory replaces InvulnerabilitySphere
{
   default
   {
      Tag "Invulnerability";
      Inventory.PickupSound "player/pickup/invuln";
      Lith_CustomInventory.InfoPage "Invulnerability";

      +COUNTITEM
      +INVENTORY.BIGPOWERUP
      +INVENTORY.FANCYPICKUPSOUND
   }

   states(Actor)
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnItemEx("Lith_InvulnerabilitySphereSprite", flags: SXF_SETMASTER);
      TNT1 A 1;
      wait;
   }

   states(Item)
   {
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 6, "Invuln");
      TNT1 A 0 {
         if(CVar.GetCVar('lith_player_altinvuln', Player).GetBool())
            A_GiveInventory("Lith_PowerInvulnerableGiver2");
         else
            A_GiveInventory("Lith_PowerInvulnerableGiver");
      }
      stop;
   }
}

class Lith_Backpack : Lith_ScoreItem replaces Backpack
{
   default
   {
      Tag "Backpack";
      Inventory.PickupSound "player/pickup/item";
      Lith_BasicPickup.LogName "Backpack", 5;
      Lith_BasicPickup.InfoPage "Backpack";
      Lith_ScoreItem.Score Score_Backpack;

      +COUNTITEM
   }

   override void PostBeginPlay()
   {
      Super.PostBeginPlay();
      A_GiveInventory("Lith_BulletAmmo", AmmoAmt_BulletBackpk);
      A_GiveInventory("Lith_ShellAmmo",  AmmoAmt_ShellBackpk);
      A_GiveInventory("Lith_RocketAmmo", AmmoAmt_RocketBackpk);
      A_GiveInventory("Lith_PlasmaAmmo", AmmoAmt_CellBackpk);
      A_GiveInventory("Lith_CannonAmmo", AmmoAmt_CannonBackpk);
   }

   override void Lith_OnPickup(Actor mo)
   {
      mo.ACS_ScriptCall("Lith_Discount");
      Super.Lith_OnPickup(mo);
   }

   states
   {
   Spawn:
      BPAK A -1;
      stop;
   }
}

class Lith_Shobon : Actor
{
   default
   {
      +NOINTERACTION
   }

   vector2 m_DoruPos;

   override void Tick()
   {
      Super.Tick();

      if(Master)
      {
         let v = Master.Vel;

         if(!(v.X ~== 0) || !(v.Y ~== 0))
         {
            float a  = Atan2(v.Y, v.X) - 180;
            float m  = v.XY.Length();
            float x_ = Lith_UTIL.Lerp(m_DoruPos.X, Cos(a) * 32, m / 32.);
            float y_ = Lith_UTIL.Lerp(m_DoruPos.Y, Sin(a) * 32, m / 32.);
            m_DoruPos = (x_, y_);
         }

         SetOrigin(Master.Pos + m_DoruPos, true);
      }
   }

   states
   {
   Spawn:
      SHOB A -1;
      stop;
   }
}

class Lith_ShobonSphere : Lith_CustomInventory
{
   default
   {
      Tag "Shobon Sphere";
      Inventory.PickupSound "misc/p_pkup";

      +COUNTITEM
      +INVENTORY.BIGPOWERUP
      +INVENTORY.FANCYPICKUPSOUND
   }

   states(Actor)
   {
   Spawn:
      TNT1 A 0;
      SHOB B 0 A_SpawnItemEx("Lith_ItemSprite", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_TRANSFERSPRITEFRAME);
      TNT1 A 1;
      wait;
   }

   states(Item)
   {
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "ShobonSphere");
      TNT1 A 0 A_SpawnItemEx("Lith_Shobon", flags: SXF_SETMASTER);
      stop;
   }
}

/* EOF */
