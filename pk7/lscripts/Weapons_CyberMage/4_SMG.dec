actor Lith_SMGTrail
{
   RenderStyle "Add"
   Scale 0.3
   Alpha 0.5
   
   +BRIGHT
   +NOINTERACTION
   
   states
   {
   Spawn:
      PUFF A 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_SMGTracer : FastProjectile
{
   MissileType "Lith_SMGTrail"
   MissileHeight 8
   RenderStyle "Translucent"
   Alpha 0.9
   Scale 0.5
   Radius 2
   Height 20
   Damage (0)
   Speed 128
   
   +NODAMAGE
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   }
}

actor Lith_SMGTrail2 : Lith_SMGTrail
{
   Translation "160:167=16:28", "64:79=22:28"
}

actor Lith_SMGTrail3
{
   Alpha 0.8
   Scale 0.3
   RenderStyle "Add"
   Translation "160:167=192:199", "64:79=197:201"
   
   +BRIGHT
   +NOINTERACTION
   
   states
   {
   Spawn:
      PUFF A 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_SMGTracer2 : Lith_SMGTracer
{
   MissileType "Lith_SMGTrail2"
   Damage (1)
}

actor Lith_SMGTracer3
{
   MissileType "Lith_SMGTrail2"
   Damage (5 * random(1, 3))
   Speed 50
   
   Projectile
   +SEEKERMISSILE
   +SCREENSEEKER
   
   states
   {
   Spawn:
      TNT1 A 0 A_SeekerMissile(0, 5, SMF_LOOK)
      TNT1 A 1 A_SpawnItemEx("Lith_SMGTrail3")
      loop
   }
}

actor Lith_SMGShotsFired : Inventory
{
   Inventory.MaxAmount 50
}

actor Lith_SMGShotsFired2 : Inventory
{
   Inventory.MaxAmount 150
}

actor Lith_SMGHeat : Inventory
{
   Inventory.MaxAmount 500
}

actor Lith_SMG : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_SMG"
   Weapon.UpSound "weapons/smg/draw"
   Weapon.SlotNumber 4
   Weapon.SlotPriority 1
   
   const int lvar_spreadx = 1;
   const int lvar_spready = 2;
   const int lvar_recoil = 3;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_ChaingunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CSMG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
   DeselectLoop:
      CSMG AAA 0 A_Lower
      CSMG A 1
      loop
   
   Select:
      CSMG AAA 0 A_Raise
      CSMG A 1
      loop
      
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 0, "ReloadHeat")
      TNT1 A 0 ACS_NamedExecuteWithResult("LPVarSet", lvar_recoil, 0.1*FIX)
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_A), 3)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 0, "Reload")
      TNT1 A 0 A_Jump(256, 2)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired2", 0, "Reload")
      
      TNT1 A 0 A_JumpIf(!CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_C), 2)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 450, "Ready")
      
      TNT1 A 0 A_PlaySound("weapons/smg/fire", CHAN_WEAPON, 1, true)
   Fire1:
      TNT1 A 0 A_JumpIf(!CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_C), 2)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 450, "FireAnim1")
      
      TNT1 A 0 A_GiveInventory("Lith_SMGHeat", random(14,27))
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 0, "ReloadHeatFromFiring")
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_A), 3)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 0, "ReloadFromFiring")
      TNT1 A 0 A_Jump(256, 2)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired2", 0, "ReloadFromFiring")
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_A), 3)
      TNT1 A 0 A_GiveInventory("Lith_SMGShotsFired", 1)
      TNT1 A 0 A_Jump(256, 2)
      TNT1 A 0 A_GiveInventory("Lith_SMGShotsFired2", 1)
      TNT1 A 0 
      
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 ACS_NamedExecuteWithResult("LPVarSet", lvar_spreadx, frandom(-5, 5) * (CallACS("LPVarGet", lvar_recoil) / FIX) * FIX)
      TNT1 A 0 ACS_NamedExecuteWithResult("LPVarSet", lvar_spready, frandom(-1, 1) * (CallACS("LPVarGet", lvar_recoil) / FIX) * FIX)
      TNT1 A 0 ACS_NamedExecuteWithResult("LPVarAdd", lvar_recoil, 0.05 * FIX)
      TNT1 A 0 ACS_NamedExecuteWithResult("LPVarMin", lvar_recoil, 1 * FIX)
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_B), "FireTrace")
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_A), 4)
      TNT1 AA 0 A_FireCustomMissile("Lith_SMGTracer",
         CallACS("LPVarGet", lvar_spreadx)/FIX, false, 0, 0, 0,
         CallACS("LPVarGet", lvar_spready)/FIX)
      TNT1 A 0 A_Jump(256, 3)
      TNT1 AA 0 A_FireCustomMissile("Lith_SMGTracer2",
         CallACS("LPVarGet", lvar_spreadx)/FIX, false, 0, 0, 0,
         CallACS("LPVarGet", lvar_spready)/FIX)
      
      TNT1 AA 0 A_FireBullets(
         CallACS("LPVarGet", lvar_spreadx)/FIX,
         CallACS("LPVarGet", lvar_spready)/FIX, -1, 8, "BulletPuff", FBF_NORANDOM|FBF_EXPLICITANGLE)
      goto FireAnim
      
   FireTrace:
      TNT1 AA 0 A_FireCustomMissile("Lith_SMGTracer3",
         CallACS("LPVarGet", lvar_spreadx)/FIX, false, 0, 0, 0,
         CallACS("LPVarGet", lvar_spready)/FIX)
      
   FireAnim:
      TNT1 A 0 offset(0, 32)
      CSMG BC 1 bright
      CSMG A 1
      CSMG A 1 A_ReFire("Fire1")
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
      goto Ready
      
   FireAnim1:
      CSMG A 1
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
      goto Ready
   
   ReloadHeatFromFiring:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
   
   User1:
   ReloadHeat:
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 200, 1)
      goto Ready
      CSMG A 1 offset(1, 32)
      CSMG A 1 offset(3, 32)
      CSMG A 1 offset(4, 32)
      CSMG A 1 offset(8, 38)
      CSMG A 2 offset(12, 45) A_PlaySound("weapons/smg/open", CHAN_6)
      CSMG A 2 offset(16, 53)
      CSMG A 2 offset(19, 60)
      CSMG A 2 offset(21, 64)
      CSMG A 3 offset(23, 67)
      CSMG A 3 offset(22, 69) A_PlaySound("weapons/smg/eject", CHAN_6)
      CSMG A 3 offset(19, 70)
      CSMG A 0 A_TakeInventory("Lith_SMGHeat", 999)
      CSMG A 10 offset(18, 72) A_PlaySound("weapons/smg/heat", CHAN_7)
      CSMG A 3 offset(17, 69)
      CSMG A 3 offset(19, 67)
      CSMG A 8 offset(20, 66) A_PlaySound("weapons/smg/insert", CHAN_6)
      CSMG A 10 offset(21, 62)
      CSMG A 5 offset(20, 60)
      CSMG A 4 offset(21, 63)
      CSMG A 3 offset(23, 66)
      CSMG A 2 offset(24, 68)
      CSMG A 2 offset(25, 63)
      CSMG A 2 offset(24, 59)
      CSMG A 2 offset(22, 57) A_PlaySound("weapons/smg/close", CHAN_6)
      CSMG A 1 offset(20, 54)
      CSMG A 1 offset(16, 49)
      CSMG A 1 offset(12, 44)
      CSMG A 1 offset(10, 42)
      CSMG A 1 offset(8, 40)
      CSMG A 1 offset(5, 38)
      CSMG A 2 offset(2, 35)
      CSMG A 4 offset(0, 32) 
      goto Ready
   
   ReloadFromFiring:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
   
   Reload:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SMG_A), 3)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 1, "Reload1")
      TNT1 A 0 A_Jump(256, 2)
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired2", 1, "ReloadLong")
      TNT1 A 0
      goto Ready
   
   Reload1:
      CSMG A 1
      CSMG A 1 offset(-2, 34)
      CSMG A 1 offset(-4, 37)
      CSMG A 1 offset(-9, 43) A_PlaySound("weapons/smg/magout", CHAN_6)
      CSMG A 1 offset(-12, 47)
      CSMG A 1 offset(-14, 52)
      CSMG A 2 offset(-12, 55)
      CSMG A 3 offset(-11, 57)
      CSMG A 8 offset(-10, 60)
      CSMG A 8 offset(-11, 60)
      CSMG A 0 A_TakeInventory("Lith_SMGShotsFired", 999)
      CSMG A 2 offset(-13, 59) A_PlaySound("weapons/smg/magin", CHAN_7)
      CSMG A 2 offset(-11, 49)
      CSMG A 2 offset(-11, 47)
      CSMG A 7 offset(-11, 45)
      CSMG A 1 offset(-12, 49)
      CSMG A 1 offset(-8, 53)
      CSMG A 1 offset(-1, 57)
      CSMG A 1 offset(3, 56)
      CSMG A 1 offset(8, 52)
      CSMG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB)
      CSMG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB)
      CSMG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB)
      CSMG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB)
      CSMG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB)
      CSMG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB)
      CSMG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 A_WeaponReady(WRF_NOBOB)
      goto Ready
   
   ReloadLong:
      CSMG A 1
      CSMG A 1 offset(-2, 34)
      CSMG A 1 offset(-4, 37)
      CSMG A 1 offset(-9, 43) A_PlaySound("weapons/smg/magout", CHAN_6)
      CSMG A 1 offset(-12, 47)
      CSMG A 2 offset(-14, 52)
      CSMG A 2 offset(-12, 55)
      CSMG A 3 offset(-11, 57)
      CSMG A 12 offset(-10, 60)
      CSMG A 12 offset(-11, 60)
      CSMG A 0 A_TakeInventory("Lith_SMGShotsFired2", 999)
      CSMG A 2 offset(-13, 59) A_PlaySound("weapons/smg/magin2", CHAN_7)
      CSMG A 2 offset(-11, 49)
      CSMG A 3 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 10 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 3 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CSMG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB)
      CSMG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 A_WeaponReady(WRF_NOBOB)
      goto Ready
   }
}

