/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_Explosive : Actor;

bool CheckMarathon(bool target = true)
   {return ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_7777777, target);}

void Lith_A_ExplodeSound(int chan = CHAN_6, bool target = true)
{
   if(CheckMarathon(target)) A_PlaySound("marathon/explode", chan);
   else                      A_PlaySound("explosion",        chan);
}

state Lith_A_JumpExplosion(bool target = true)
{
   if(CheckMarathon(target)) return ResolveState("ExplodeMarathon");
   else                      return ResolveState("Explode");
}

state Lith_A_DoExplosion()
{
   bNOGRAVITY = true;
   Lith_A_ExplodeSound();
   return Lith_A_JumpExplosion();
}

states
{
Explode:
   MEXP AB 2 bright light("Lith_Explode1");
   MEXP CD 3 bright light("Lith_Explode2");
   MEXP EF 4 bright light("Lith_Explode3");
   stop;
ExplodeMarathon:
   MA7G BC 4 bright light("Lith_Explode1");
   MA7G DE 4 bright light("Lith_Explode2");
   MA7G FG 4 bright light("Lith_Explode3");
   stop;
}

/* EOF */
