actor Lith_ItemPickup : Lith_Pickup
{
   Inventory.PickupSound "player/pickup/evil"

   states
   {
   SpawnLoop:
      TNT1 A 1 A_CheckSight("NoGlow")
      TNT1 A 0 Lith_A_ItemGlow("Lith_ItemGlow")
      loop
   NoGlow:
      TNT1 A 1
      TNT1 A 0 A_CheckSight("NoGlow")
      goto SpawnLoop
   }
}

actor Lith_DivisionSigilSprite : Lith_AnimItemSprite
{
   Lith_AnimItemSprite.Phases 23

   +BRIGHT

   states(actor)
   {
   SetSpr:
      DIVI ABCDEFGHIJKLMNOPQRSTUVW 0 Lith_A_JumpPhase
      stop
   }
}

actor Lith_DivisionSigil : Lith_ItemPickup
{
   //$Category "Items"
   //$Sprite "DIVIA0"
   
   Inventory.Icon "DIVSIGI1"
   Inventory.MaxAmount 1
   Tag "$LITH_INV_SIGIL"
   
   +INVENTORY.INVBAR
   -INVENTORY.PICKUPFLASH
   
   states
   {
   Spawn:
      DIVI A 0
      DIVI A 0 A_SpawnItemEx("Lith_DivisionSigilSprite", 0,0,0, 0,0,0, 0, Lith_ISF)
      goto SpawnLoop
   Pickup:
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_hassigil), "PickupNope")
      TNT1 A 0 A_PlaySound("player/getsigil", CHAN_AUTO, 1.0, false, ATTN_NONE)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_GetSigil")
      TNT1 A 0 A_RailWait
      stop
   Use:
   PickupNope:
      TNT1 A 0
      fail
   }
}

actor Lith_ClawOfImp : Lith_ItemPickup
{
   Tag "Claw of Imp"
   
   states
   {
   Spawn:
      DITM A 0
      DITM A 0 A_SpawnItemEx("Lith_ItemSprite", 0,0,0, 0,0,0, 0, Lith_ISF)
      goto SpawnLoop
   Pickup:
      TNT1 A 0 A_RailWait
      stop
   }
}
