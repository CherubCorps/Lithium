actor Lith_CircleSpearTrail
{
   RenderStyle "Add"

   +NOINTERACTION
   +NOTIMEFREEZE
   +BRIGHT

   states
   {
   Spawn:
      BLLT S 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_CircleSpear
{
   RenderStyle "Add"
   Damage (77)

   Projectile
   +NOTIMEFREEZE
   +RIPPER
   +BRIGHT

   var int user_trigger;
   var int user_vel;

   states
   {
   Spawn:
      BLLT S 1 A_JumpIf(user_trigger, "Go")
      wait
   Go:
      BLLT S random(0, 2)
      BLLT S 0 A_SetUserVar(user_vel, random(20, 30))
      BLLT S 0 A_ChangeVelocity(cos(angle)*user_vel, sin(angle)*user_vel, 0, CVF_REPLACE)
      BLLT S 3 A_SpawnItemEx("Lith_CircleSpearTrail")
      wait
   Death:
      BLLT S 1 A_FadeOut
      wait
   }
}

actor Lith_CircleSpearThrower
{
   RenderStyle "None"

   +NOGRAVITY
   +NOCLIP
   +NOTIMEFREEZE

   states
   {
   Spawn:
      TNT1 A 0
      TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_CircleSpear", frandom(-8,8),frandom(-8,8),frandom(-16,16), 0,0,0, frandom(-60,60), SXF_TRANSFERPOINTERS, 0, tid)
      stop
   }
}

actor Lith_CircleElectricity
{
   Translation "96:111=168:191", "192:207=168:191"
   RenderStyle "Add"
   Damage 7

   Projectile
   +RANDOMIZE
   +NOTIMEFREEZE
   +RIPPER
   +BRIGHT

   states
   {
   Spawn:
      ZAP1 FFEFEFDDEFDEEDEEDDDCCCBCBBBCBBBBAAABA 1 A_FadeOut(0.01)
      ZAP1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.02)
      stop
   }
}

actor Lith_CircleWallFx
{
   RenderStyle "Add"
   XScale 0.7
   YScale 5.1
   Alpha 0.7

   +NOTIMEFREEZE
   +NOCLIP
   +NOGRAVITY
   +BRIGHT

   states
   {
   Spawn:
      BLLT E 0
      BLLT EEE 1 A_FadeIn
      BLLT E 0 A_Explode(128, 128, 0)
   FadeLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP)
      BLLT E 0 A_JumpIf(alpha <= 0.05, "Fire")
      loop
   Fire:
      TNT1 A 2
      TNT1 AA 0 A_SpawnItemEx("Lith_CircleElectricity", frandom(-32,32),frandom(-32,32),0, frandom(-4,4),frandom(-4,4),frandom(-4,4), 0, SXF_TRANSFERPOINTERS)
      stop
   }
}

actor Lith_CircleWallTrail : Lith_CircleWallFx
{
   XScale 0.5
   YScale 5

   +NOINTERACTION

   states
   {
   Spawn:
      BLLT E 1 A_FadeOut
      wait
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_CircleWall : Lith_CircleWallFx
{
   XScale 0.5
   YScale 5
   Radius 8
   Height 32

   var int user_trigger;

   states
   {
   Spawn:
      BLLT E 1 A_ScaleVelocity(0.9)
      BLLT E 0 A_JumpIf(user_trigger, "Fire")
      loop
   Fire:
      BLLT E 0 A_SpawnItemEx("Lith_CircleWallFX", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      BLLT E 0 A_SetUserVar(user_trigger, false)
   FireDoneLoop:
      BLLT E 1 A_ScaleVelocity(0.9)
      BLLT E 0 A_JumpIf(user_trigger, "FireOut")
      loop
   FireOut:
      BLLT E 1 A_ChangeVelocity(cos(angle)*4, sin(angle)*4, 0, CVF_REPLACE)
   FireOutLoop:
      BLLT E 0 A_FadeOut(0.05)
      BLLT E 0 A_Explode(128, 128, 0)
      BLLT E 0 A_SpawnItemEx("Lith_CircleWallTrail", -8)
      BLLT E 1 A_ChangeVelocity(velx*1.34, vely*1.34, 0, CVF_REPLACE)
      loop
   }
}

actor Lith_CircleParticle
{
   RenderStyle "Add"
   Scale 0.7

   +NOINTERACTION
   +NOTIMEFREEZE
   +BRIGHT

   var int user_trigger;
   var int user_fxtid;

   states
   {
   Spawn:
      BLLT E 1
   SpawnLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP)
      BLLT E 0 A_JumpIf(alpha <= 0.05, "FadeOutDone")
      loop
   FadeOutDone:
      BLLT E 1 A_JumpIf(user_trigger, "DoFadeIn")
      loop
   DoFadeIn:
      BLLT E 1 A_FadeIn(0.1, FTF_CLAMP)
      BLLT E 0 A_JumpIf(alpha >= 1, "Done")
      loop
   Done:
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,7, 0, SXF_TRANSFERPOINTERS, 0, user_fxtid)
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,2, 0, SXF_TRANSFERPOINTERS, 0, user_fxtid)
      BLLT E 1 A_FadeOut(0.34)
      stop
   }
}

actor Lith_Cercle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Cercle"
   Weapon.SlotNumber 0
   Weapon.SlotPriority 1
   Weapon.UpSound "weapons/cercle/draw"

   +WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      TNT1 A 1 bright Lith_A_Ready
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 7)
   SelectLoop:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 0, 1)
      goto Ready
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 0x7FFFFFFF)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Cercle", false)
      TNT1 A 10
      goto Ready
   }
}

// EOF
