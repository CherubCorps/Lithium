actor Lith_Heretic_ArtiInvisibility : Lith_BlurSphereReplacer replaces ArtiInvisibility
{
}

actor Lith_Heretic_BagOfHolding : Lith_ScoreItem replaces BagOfHolding
{
   +COUNTITEM
   +FLOATBOB
   
   states
   {
   Spawn:
      BAGH A 0 nodelay A_GiveInventory("Lith_ScoreCount", Score_Backpack)
      BAGH A 0 A_GiveInventory("Lith_ShellAmmo",  AmmoAmt_ShellBackpk)
      BAGH A 0 A_GiveInventory("Lith_RocketAmmo", AmmoAmt_RocketBackpk)
      BAGH A 0 A_GiveInventory("Lith_PlasmaAmmo", AmmoAmt_CellBackpk)
      BAGH A 0 A_GiveInventory("Lith_CannonAmmo", AmmoAmt_CannonBackpk)
   SpawnLoop:
      BAGH ABCDECB 5 bright
      loop
   Pickup:
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_Discount", 0)
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, msg_backpack)
      goto Super::Pickup
   }
}

actor Lith_Heretic_CrystalVial : Lith_CustomInventory replaces CrystalVial
{
   +FLOATBOB
   
   states
   {
   Spawn:
      PTN1 ABC 3
      loop
   Pickup:
      TNT1 A 0 A_JumpIf(!CallACS("Lith_CheckHealth", 10), "Nope")
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, msg_stimpack)
      TNT1 A 0 A_GiveInventory("CrystalVial")
      stop
   }
}

actor Lith_Heretic_SuperMap : Lith_CustomInventory replaces SuperMap
{
   Inventory.PickupSound "misc/p_pkup"
   
   +COUNTITEM
   +INVENTORY.FANCYPICKUPSOUND
   +FLOATBOB
   
   states
   {
   Spawn:
      SPMP A 6 bright
      SPMP BCB 6
      loop
   Pickup:
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, msg_allmap)
      TNT1 A 0 A_GiveInventory("SuperMap")
      stop
   }
}

// EOF

