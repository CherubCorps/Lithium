// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RifleBulletPainless : Lith_RifleBullet {default {+PAINLESS}}

class Lith_RifleBullet : Lith_BulletPuff;

default
{
   Mass 9999;
   DamageType "Bullet";
   Species "Lith_Player";

   +DONTHARMSPECIES
   +MTHRUSPECIES
   +FORCERADIUSDMG
   +PUFFONACTORS
   +PUFFGETSOWNER
}

states
{
Crash:
   TNT1 A 0;
   TNT1 A 0 A_Explode(7, 32, 0);
   TNT1 A 0 A_JumpIf(Lith_UTIL.pData(pdata_upgrade, UPGR_Seven7s), "CrashMarathon");
   goto Super::Spawn;
Spawn:
   TNT1 A 0;
   TNT1 A 0 A_Explode(7, 32, 0);
   TNT1 A 0 A_JumpIf(Lith_UTIL.pData(pdata_upgrade, UPGR_Seven7s), "SpawnMarathon");
   goto Super::Spawn;
XDeath:
   TNT1 A 0;
   TNT1 A 0 A_JumpIf(!Lith_UTIL.pData(pdata_upgrade, UPGR_Seven7s), 2);
   TNT1 A 0 A_StartSound("marathon/hit", lch_body);
   TNT1 A 0 A_Explode(7, 32, 0);
   stop;
CrashMarathon: TNT1 A 0 A_StartSound("marathon/hitwall",  lch_body); goto MarathonPuff;
SpawnMarathon: TNT1 A 0 A_StartSound("marathon/hitsolid", lch_body); goto MarathonPuff;
MarathonPuff:
   TNT1 A 0 A_SetScale(0.5);
   TNT1 A 0 A_SetTranslucent(1, 1);
   MPUF ABCDEFG 2 bright;
   stop;
}

/* EOF */
