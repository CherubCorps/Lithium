actor Lith_CannonExplosionParticle
{
   Mass 5
   Scale 0.5
   Gravity 0.8
   
   -NOGRAVITY
   
   states
   {
   Spawn:
      MEXP AB 2 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      MEXP CD 3 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      MEXP EF 4 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_CannonSmokeParticle : Lith_CannonExplosionParticle
{
   Mass 3
   Gravity 0.4
   RenderStyle "Add"
   
   states
   {
   Spawn:
      SMOK ABCDEFGHIJKLM 1 A_FadeOut(0.06)
      stop
   }
}

actor Lith_CannonBall
{
   Radius 13
   Height 8
   Scale 3.5
   Damage (400)
   Speed 42
   Gravity 0.55
   BounceType "Hexen"
   Species "Lith_Player"
   DamageType "Lith_PlayerMissile"
   
   Projectile
   +RIPPER
   +BOUNCEONACTORS
   +BOUNCEAUTOOFFFLOORONLY
   +MTHRUSPECIES
   +USEBOUNCESTATE
   +FORCERADIUSDMG
   +CANBOUNCEWATER
   -NOGRAVITY
   
   var int user_angle;
   var int user_time;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SetUserVar(user_angle, angle * FIX)
      TNT1 A 0 A_SetUserVar(user_time, 35 * 10)
      TNT1 A 2
   SpawnLoop:
      TNT1 A 0 A_JumpIf(user_time <= 0, "Death")
      TNT1 A 0 A_SetUserVar(user_time, user_time - 1)
      TNT1 A 0 A_Explode(20, 60, 0)
      CBAL A 1 bright A_SetAngle(user_angle / FIX)
      loop
   Bounce.Floor:
   Bounce.Ceiling:
      TNT1 A 0 A_PlaySound("weapons/cannon/impactfloor", CHAN_5)
      TNT1 A 0 A_Quake(1, 10, 0, 512)
      TNT1 A 0 A_Explode
      goto SpawnLoop
   Bounce.Wall:
      TNT1 A 0 A_PlaySound("weapons/cannon/impactwall", CHAN_6)
      TNT1 A 0 A_Quake(1, 10, 0, 512)
      TNT1 A 0 A_Explode
      goto SpawnLoop
   Death:
      CBAL B 0 A_PlaySound("weapons/cannon/explode", CHAN_7)
      CBAL B 0 A_PlaySound("weapons/cannon/explode", CHAN_BODY)
      CBAL B 0 A_SetScale(2)
      CBAL B 0 A_NoGravity
      CBAL B 0 A_Jump(256, "Splodey")
   Splodey:
      TNT1 A 0 A_Quake(3, 20, 0, 640)
      TNT1 AAAAAA 0 A_Explode(128, 512, 0)
      TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_CannonExplosionParticle", 0,0,8, frandom(-8,8),frandom(-8,8),frandom(6,12))
      goto ExplodeAnim
   ExplodeAnim:
      TNT1 A 0 A_Quake(4, 20, 0, 640)
      TNT1 A 0 A_SetScale(radius / 30)
      TNT1 A 0 A_SetTranslucent(1, 1)
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_SmallCannonBall : Lith_CannonBall
{
   Radius 8
   Scale 2
   Damage (100)
   
   states
   {
   Splodey:
      TNT1 AAA 0 A_Explode(128, 512, 0)
      TNT1 AAAAAAAAA 0 A_SpawnItemEx("Lith_CannonExplosionParticle", 0,0,8, frandom(-8,8),frandom(-8,8),frandom(6,12), 0)
      goto ExplodeAnim
   }
}

// EOF

