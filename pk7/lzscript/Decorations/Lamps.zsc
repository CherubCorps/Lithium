// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_LampSpark : Actor {
   default {
      Radius 6;
      Height 6;
      RenderStyle "Add";
      +NoInteraction
   }
   override void markPrecacheSounds() {
      super.markPrecacheSounds();
      markSound("amb/spark");
   }

   states {
   Spawn:
      LSPR A 0;
      LSPR A 0 {scale.x *= randompick(-1, 1);}
      LSPR A 1 bright;
      LSPR B 1 bright A_StartSound("amb/spark", lch_body, attenuation: ATTN_NORM);
      LSPR CD 2 bright;
      LSPR EF 3;
      LSPR GH 4 A_FadeOut;
      stop;
   }
}

class Lith_TechLamp : Lith_FlareDecor replaces TechLamp
{
   default
   {
      Health 100;
      Radius 16;
      Height 80;
      DeathHeight 80;
      ProjectilePassHeight -80;
      Lith_FlareDecor.Type "Lith_TechLampFlare";
      Lith_SwitchableDecor.Noise "amb/lamp1";
   }
   override void markPrecacheSounds() {
      super.markPrecacheSounds();
      markSound("amb/shatter");
   }

   states
   {
   Spawn:    LI02 IJKL 4 bright; loop;
   Inactive: LI02 N -1; stop;
   Death:    LI02 M 0;
   Death1:
      #### # 1 A_StartSound("amb/shatter", lch_body3, attenuation: ATTN_NORM);
      #### # 35 A_SpawnItemEx("Lith_LampSpark", frandom(-0.5, 0.5), frandom(-0.5, 0.5), height-8, 0, 0, frandom(-1,1), frandom(-1,1), random(2,4), 168);
      wait;
   }
}

class Lith_TechLamp2 : Lith_TechLamp replaces TechLamp2
{
   default
   {
      Health 80;
      Height 60;
      DeathHeight 60;
      ProjectilePassHeight -60;
      Lith_FlareDecor.Type "Lith_TechLamp2Flare";
      Lith_SwitchableDecor.Noise "amb/lamp2";
   }

   states {
   Spawn:    LI02 OPQR 4 bright; loop;
   Inactive: LI02 T -1; stop;
   Death:    LI02 S 0; goto Death1;
   }
}

class Lith_Column : Lith_TechLamp2 replaces Column
{
   default
   {
      Health 50;
      Height 48;
      DeathHeight 48;
      ProjectilePassHeight -48;
      Lith_FlareDecor.Type "Lith_ColumnFlare";
   }

   states {
   Spawn:    LI02 BCDEFEDC 4 bright; loop;
   Inactive: LI02 H -1; stop;
   Death:    LI02 G 0; goto Death1;
   }
}

class Lith_TechLampFlare  : Lith_DecorFlareB   {default {Height 65;}}
class Lith_TechLamp2Flare : Lith_TechLampFlare {default {Height 50; Scale 0.7;}}
class Lith_ColumnFlare    : Lith_DecorFlareY   {default {Height 40; Scale 0.7;}}

/* EOF */
