class Lith_Boss_Michael : Lith_BasicPhantom
{
   default
   {
      Tag "Michael, Brother of Isaac";
      Health 4000;
      Speed 7;
      
      +THRUSPECIES
      +NOINFIGHTING
   }

   override void Lith_OnDeath()
      {if(master && master.health > 0) A_KillMaster();}
   
   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("Melee", null);
      loop;
   Melee:
      PLAY F 0 A_FaceTarget;
      PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "enemies/michael/hit", "enemies/michael/swing", "Melee");
      PLAY F 5;
      goto See;
   }
}

class Lith_Boss_Johnson : Lith_BasicPhantom
{
   default
   {
      Tag "Isaac, Superbiorem Est";
      Health 40;
      Speed 3;
      
      +THRUSPECIES
      +NOINFIGHTING
   }

   override void Tick()
   {
      if(health < 20) setTag("Johnson, Ally of Isaac");
      Super.Tick();
   }

   override void Lith_OnDeath()
   {
      let m = Lith_Boss_Isaac(master);
      if(m) m.m_dupes--;
   }

   states
   {
   See:
      PLAY AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
      loop;
   Missile:
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON);
      PLAY EE 0 A_SpawnProjectile("Lith_IsaacThompsonBullet2", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH|CMF_BADPITCH, frandom(-5, 5));
      PLAY E 1 A_FaceTarget;
      PLAY F 1 A_FaceTarget;
      PLAY F 2 A_MonsterRefire(0, "See");
      loop;
   Death:
      PLAY H 10;
      PLAY I 1 A_FadeOut(0.01);
      wait;
   }
}

class Lith_Boss_Isaac : Lith_Phantom
{
   default
   {
      Tag "Isaac, Superbiorem Est";
      Health 4000;
   }

   override void Lith_PostInit()
   {
      if(m_phase == 3)
         A_SpawnItemEx("Lith_Boss_Michael", flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);

      Super.Lith_PostInit();
   }

   override void Lith_OnDeath()
   {
      Super.Lith_OnDeath();
      A_KillChildren();
   }

   void Lith_A_SpawnDupe()
   {
      if(random(0, 32) == 0 && m_phase == 3 && m_dupes < 3)
      {
         m_dupes++;
         A_SpawnItemEx("Lith_Boss_Johnson", flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);
      }
   }

   int m_dupes;

   states
   {
   See:
      TNT1 A 0 Lith_A_SpawnDupe;
      PLAY AAAABBBBCCCCDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
      loop;
   Missile:
      TNT1 A 0 Lith_A_SpawnDupe;
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON);
      PLAY EE 0 A_SpawnProjectile("Lith_IsaacThompsonBullet", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH|CMF_BADPITCH, frandom(-5, 5));
      PLAY E 1 A_FaceTarget;
      PLAY F 1 A_FaceTarget;
      PLAY F 0 A_Chase(null, null, CHF_FASTCHASE);
      PLAY F 2 A_MonsterRefire(0, "See");
      loop;
   }
}

// EOF

