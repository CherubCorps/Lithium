#include "lscripts/Weapons/ShotgunTrail.dec"

actor Lith_ShotgunScopedToken : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_ShotgunJustFrame : Inventory
{
   Inventory.MaxAmount 3
}

actor Lith_Shotgun : Lith_Weapon replaces Shotgun
{
   Tag "Shotgun"
   Weapon.UpSound "weapons/shotgun/draw"
   Weapon.SlotNumber 3
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   AltReady:
      SCOP A 1 A_WeaponReady(WRF_NOBOB)
      loop
   Ready:
      PKSG A 1 A_WeaponReady
      PKSG A 0 A_TakeInventory("Lith_ShotgunJustFrame")
      loop
   Deselect:
      PKSG A 0
      {
         A_TakeInventory("Lith_ShotgunScopedToken", 1);
         A_ZoomFactor(1.0);
      }
   DeselectLoop:
      PKSG AAA 0 A_Lower
      PKSG A 1
      loop
   Select:
      // fuck youuuuuuuuuuuuuuuuuuuuuuuuuuuuu
      PKSG A 0 A_ZoomFactor(1.0)
      PKSG A 0 A_GiveInventory("Lith_ShotgunJustFrame", 3)
   SelectLoop:
      PKSG AAA 0 A_Raise
      PKSG A 1
      loop
   Fire:
      TNT1 A 0
      {
         if(CountInv("Lith_ShotgunJustFrame"))
         {
            A_TakeInventory("Lith_ShotgunJustFrame", 0x7FFFFFFF);
            A_FireBullets(5.3, 2.3, 8, 4, "BulletPuff");
            A_PlaySound("weapons/shotgun/just", 7);
            A_RailAttack(70, 0, false, None, LightBlue, RGF_SILENT | RGF_FULLBRIGHT, 128, "BulletPuff", 0, 0, 0, 48, 3, 3);
         }
         
         A_FireBullets(5.3, 2.3, 8, 6, "BulletPuff");
         A_RailAttack(105, 0, true, None, Green, RGF_SILENT | RGF_FULLBRIGHT, 2, "BulletPuff", 1, 1, 0, 0, 0.9, 1, "Lith_ShotgunTrail", 0);
         A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6);
         A_PlaySound("weapons/shotgun/fire", CHAN_WEAPON);
         A_Recoil(0.7);
         
         if(CountInv("Lith_ShotgunScopedToken"))
            { return state("FireFinish_Scoped"); }
         else
            { return state("FireFinish_NoScopeThough_DidntHaveThe700ForTheScope"); }
      }
   FireFinish_Scoped:
      SCOP A 4 bright
      SCOP A 25
      SCOP A 5 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP A 7
      {
         A_ReFire;
         A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB);
      }
      goto FireFinish
   FireFinish_NoScopeThough_DidntHaveThe700ForTheScope:
      PKSG I 1 A_GunFlash
      PKSG IJJ 1
      PKSG KJI 1
      PKSG B 2
      PKSG C 1
      PKSG D 2
      PKSG E 2
      PKSG F 2
      PKSG G 1
      PKSG H 3
      PKSG GFEDC 2
      PKSG B 1
      PKSG A 3
      PKSG A 7 A_ReFire
      goto FireFinish
   FireFinish:
      TNT1 A 0
      {
         if(!CountInv("Lith_ShotgunScopedToken"))
            { return state("Ready"); }
         else
            { return state("AltReady"); }
      }
   AltFire:
      "####" A 1
      "####" A 0
      {
         if(CountInv("Lith_ShotgunScopedToken"))
         {
            A_TakeInventory("Lith_ShotgunScopedToken", 1);
            A_ZoomFactor(1.0, ZOOM_INSTANT);
            A_PlaySound("weapons/shotgun/zoomout", 6);
            return state("Ready");
         }
         else
         {
            A_GiveInventory("Lith_ShotgunScopedToken", 1);
            A_ZoomFactor(CallACS("Lith_GetCVar", lith_weapons_zoomfactor) / FIX);
            A_PlaySound("weapons/shotgun/zoomin", 6);
            return state("AltReady");
         }
      }
   Flash:
      SHTF A 2 Bright A_Light1
      SHTF B 2 Bright A_Light2
      goto LightDone
   Spawn:
      SHOT A 0
      SHOT A 0 A_SetUserVar("user_pickupparm", weapon_shotgun)
      goto SpawnLoop
   }
}

