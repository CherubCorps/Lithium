// AAGH: Armors Are Goddamn Heavy
class Lith_AAGH : Inventory
{
   default
   {
      +INVENTORY.KEEPDEPLETED
      +INVENTORY.UNTOSSABLE
      +INVENTORY.UNDROPPABLE
   }

   Lith_SlottedItem m_slotted[aslot_max];

   static Lith_DamageType Lith_NameToDT(name type)
   {
      switch(type)
      {
      default:
      case 'Bullet':
      case 'Lith_Bullets':   return ldt_bullets;
      case 'Plasma':
      case 'Railgun':
      case 'Electric':
      case 'Lith_Energy':    return ldt_energy;
      case 'Fire':
      case 'Lith_Fire':      return ldt_fire;
      case 'FireMagic':
      case 'Lith_FireMagic': return ldt_firemagic;
      case 'Magic':
      case 'Lith_Magic':     return ldt_magic;
      case 'Shrapnel':
      case 'Explosion':
      case 'Lith_Shrapnel':  return ldt_shrapnel;
      case 'Ice':
      case 'Lith_Ice':       return ldt_ice;
      }
   }

   bool Lith_AddSlot(Lith_SlottedItem o)
   {
      let slot = o.m_slot;

      if(!m_slotted[slot])
      {
         o.Lith_Unlink();
         m_slotted[slot] = o;

//       Console.printf("%p: slotted: %i %s", self, slot, o.getClassName());

         return true;
      }
      else
         return false;
   }

   void Lith_OmitSlot(int slot)
   {
      m_slotted[slot] = null;
   }

   bool Lith_RemoveSlot(int slot)
   {
      if(m_slotted[slot])
      {
         if(m_slotted[slot].Lith_Attach())
         {
            m_slotted[slot] = null;

//          Console.printf("%p: removed: %i", self, slot);
         }
         else
            return false;
      }

      return true;
   }

   int Lith_SaveAmt(name type = 'None')
   {
      int save = 0;
      for(int i = 0; i < aslot_max; i++)
         if(m_slotted[i])
            save += m_slotted[i].Lith_SaveAmt(type);
      return save;
   }

   override void AbsorbDamage(int damage, name type, out int newdamage)
   {
      if(DamageTypeDefinition.IgnoreArmor(type)) return;

      int save = Lith_SaveAmt(type);

      if(damage - save < 1) newdamage = 1;
      else                  newdamage = damage - save;

//    Console.printf("DT save: (%s) %i (%i -> %i)", type, save, damage, newdamage);
   }
}

class Lith_Armor : Lith_SlottedItem abstract
{
   default
   {
      Lith_InventoryItem.Size 4, 2;
      Lith_InventoryItem.InvType "Armor";

      +INVENTORY.ISARMOR
   }

   private int m_save0;
   private int m_save1;

   protected int m_cursave;

   property SlotSave: m_slot, m_save0, m_save1;

   override int Lith_SaveAmt(name type) {return m_cursave;}

   override Inventory CreateCopy(Actor mo)
   {
      let copy = Lith_Armor(Super.CreateCopy(mo));

      copy.m_save0   = m_save0;
      copy.m_save1   = m_save1;
      copy.m_cursave = m_cursave;

      return copy;
   }

   override void BeginPlay()
   {
      Super.BeginPlay();
      m_cursave = m_save0;
   }
}

class Lith_Armor_Resistant : Lith_Armor
{
   protected int m_resist;

   property Resist: m_resist;

   override Inventory CreateCopy(Actor mo)
   {
      let copy = Lith_Armor_Resistant(Super.CreateCopy(mo));

      copy.m_resist = m_resist;

      return copy;
   }

   override int Lith_SaveAmt(name type)
   {
      let dt = Lith_AAGH.Lith_NameToDT(type);
      return m_resist == dt ? m_cursave * 2 : m_cursave;
   }
}

class Lith_GreenArmor : Lith_RandomSpawn replaces GreenArmor
{
   default
   {
      Lith_RandomSpawn.SpawnNum lrsn_garmor;
   }
}

class Lith_BlueArmor : Lith_RandomSpawn replaces BlueArmor
{
   default
   {
      Lith_RandomSpawn.SpawnNum lrsn_barmor;
   }
}

// EOF
