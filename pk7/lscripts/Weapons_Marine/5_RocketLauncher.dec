actor Lith_RocketLoaded : Inventory
{
   Inventory.MaxAmount 8
}

actor Lith_RocketIterated : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_RocketAltMode : Inventory
{
   Inventory.MaxAmount 2
}

actor Lith_GrenadeLauncher : Lith_MWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_GrenadeLauncher"
   Weapon.UpSound "weapons/rocket/draw"
   Weapon.SlotNumber 5
   Weapon.SlotPriority 1
   Weapon.BobStyle "Smooth"
   Weapon.BobSpeed 1.3
   Weapon.BobRangeY 1.1
   Weapon.BobRangeX 0.9
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_RocketLauncherPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      GRLG A 1 A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      GRLG AAA 0 A_Lower
      GRLG A 1
      loop
   Select:
      GRLG AAA 0 A_Raise
      GRLG A 1
      loop
   
   Fire:
      GRLG A 1
      GRLG A 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1)
      goto FireEmpty
      GRLG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_ChargeRPG), "FireUnreal")
      GRLG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_HomingRPG), "FireHoming")
   FireReal:
      GRLG A 0 A_AlertMonsters
      GRLG A 0 A_PlaySound("weapons/rocket/fire", CHAN_WEAPON)
      GRLG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      GRLG A 0 A_FireCustomMissile("Lith_Rocket")
   FireAnim:
      GRLG A 0 A_TakeInventory("Lith_RocketAltMode", 2)
      GRLG B 1 bright A_GunFlash
      GRLG B 1 bright
      GRLG C 1 bright offset(0, 33)
      GRLG CD 1 bright offset(0, 34)
      GRLG D 1 offset(0, 56)
      GRLG E 1 offset(0, 59) A_PlaySound("weapons/rocket/load", CHAN_7)
      GRLG A 1 offset(0, 56)
      GRLG A 1 offset(0, 50)
      GRLG A 1 offset(0, 45)
      GRLG A 1 offset(0, 38)
      GRLG A 1 offset(0, 35)
      GRLG A 1 offset(0, 34)
      GRLG A 1 offset(0, 33)
      GRLG AAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   AltFire:
      GRLG A 1
      GRLG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_HomingRPG), "Ready")
      GRLG A 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1)
      goto FireEmpty
      GRLG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_ChargeRPG), "FireReal")
      GRLG A 0 A_AlertMonsters
      GRLG A 0 A_PlaySound("weapons/rocket/grenade", CHAN_WEAPON)
      GRLG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      GRLG A 0 A_FireCustomMissile("Lith_RifleGrenade")
      goto FireAnim
   
   Flash:
      TNT1 A 2 A_Light2
      TNT1 A 1 A_Light1
      goto LightDone
   
   FireEmpty:
      GRLG A 1
      GRLG A 2 offset(0, 41)
      GRLG A 1 offset(0, 45)
      GRLG A 1 offset(0, 43)
      GRLG A 1 offset(0, 40) A_PlaySound("weapons/rocket/empty", CHAN_5)
      GRLG A 1 offset(0, 38)
      GRLG A 1 offset(0, 36)
      GRLG A 1 offset(0, 33)
      GRLG A 1 offset(0, 32)
      GRLG A 5
      GRLG AAAA 1 A_ReFire
      goto Ready
   
   //
   // Homing Launcher
   //
   FireHoming:
      GRLG A 1
      GRLG A 0 A_AlertMonsters
      GRLG A 0 A_PlaySound("weapons/rocket/fire", CHAN_WEAPON)
      GRLG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      GRLG A 0 A_FireCustomMissile("Lith_HomingRocket")
      goto FireAnim
   
   //
   // Charge Launcher
   //
   FireUnreal:
      GRLG A 0 A_GiveInventory("Lith_RocketLoaded", 1)
      GRLG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
   FireUnrealRefire:
      GRLG A 2
      GRLG A 0 A_JumpIfInventory("Lith_RocketLoaded", 0, "FireUnrealReal")
      GRLG A 0 A_JumpIfInventory("Lith_RocketIterated", 0, "FireUnrealLoad")
      GRLG A 0 A_GiveInventory("Lith_RocketIterated", 1)
      goto FireUnrealCheckRefire
   FireUnrealLoad:
      GRLG A 0 A_PlaySound("weapons/rocket/lock", CHAN_5)
      GRLG A 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1)
      goto FireUnrealReal
      GRLG A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      GRLG A 0 A_GiveInventory("Lith_RocketLoaded", 1)
      GRLR A 1
      GRLR BC 2
      GRLR D 4 A_GunFlash("AltModeChanger", GFF_NOEXTCHANGE)
      GRLR EF 2
      GRLR GH 1
   
   FireUnrealCheckRefire:
      GRLG A 2
      GRLG A 0 A_ReFire("FireUnrealRefire")
   FireUnrealReal:
      GRLG A 0 A_TakeInventory("Lith_RocketIterated", 1)
      GRLG A 0 A_AlertMonsters
      GRLG A 0 A_PlaySound("weapons/rocket/launch", CHAN_6)
      GRLG A 0 A_PlaySound("weapons/rocket/fire", CHAN_WEAPON)
      GRLG A 0 A_JumpIfInventory("Lith_RocketLoaded", 1, "FireThatShit")
      GRLG A 0 A_FireCustomMissile("Lith_Rocket")
      goto FireAnim
   FireThatShit:
      GRLG A 0 A_JumpIfInventory("Lith_RocketAltMode", 0, "FireThatShitNades")
      GRLG A 0 A_JumpIfInventory("Lith_RocketAltMode", 1, "FireThatShitSpiral")
      GRLG A 0 A_FireCustomMissile("Lith_Rocket", 0, 1, random(-24,24))
      GRLG A 0 A_TakeInventory("Lith_RocketLoaded", 1)
      GRLG A 0 A_JumpIfInventory("Lith_RocketLoaded", 1, "FireThatShit")
      goto FireAnim
   FireThatShitSpiral:
      GRLG A 0 A_FireCustomMissile("Lith_RocketHax", 0, 1, random(-4,4))
      GRLG A 0 A_TakeInventory("Lith_RocketLoaded", 1)
      GRLG A 0 A_JumpIfInventory("Lith_RocketLoaded", 1, "FireThatShit")
      goto FireAnim
   FireThatShitNades:
      GRLG A 0 A_FireCustomMissile("Lith_RifleGrenade", random(-4,4), 1, random(-7,7), random(-4,4))
      GRLG A 0 A_TakeInventory("Lith_RocketLoaded", 1)
      GRLG A 0 A_JumpIfInventory("Lith_RocketLoaded", 1, "FireThatShit")
      goto FireAnim
   
   AltModeChanger:
      TNT1 A 0 A_JumpIf((CallACS("Lith_GetPlayerData", pdata_buttons) & X_BT_ALTATTACK) != 0, "ChangeAltMode")
      stop
   ChangeAltMode:
      TNT1 A 0 A_JumpIfInventory("Lith_RocketAltMode", 0, "ChangeAltMode2")
      TNT1 A 0 A_JumpIfInventory("Lith_RocketAltMode", 1, "ChangeAltMode1")
      TNT1 A 0 A_PlaySound("weapons/rocket/setalt")
      TNT1 A 0 A_GiveInventory("Lith_RocketAltMode", 1)
      stop
   ChangeAltMode1:
      TNT1 A 0 A_PlaySound("weapons/rocket/setalt1")
      TNT1 A 0 A_GiveInventory("Lith_RocketAltMode", 1)
      stop
   ChangeAltMode2:
      TNT1 A 0
      stop
   }
}

// EOF

