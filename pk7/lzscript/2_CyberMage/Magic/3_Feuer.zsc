// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_FeuerTest : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.3;

      +NOINTERACTION
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT E -1;
      stop;
   }
}

class Lith_FeuerFX : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 1.5;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT R 0;
      BLLT R 0 A_Jump(256, 1, 2, 3);
      BLLT R 0 A_SetScale(-scale.x,  scale.y);
      goto Done;
      BLLT R 0 A_SetScale( scale.x, -scale.y);
      goto Done;
      BLLT R 0 A_SetScale(-scale.x, -scale.y);
   Done:
      BLLT R 0 A_SetScale(scale.x * frandom(0.5, 1));
      BLLT R 10;
      BLLT R 1 A_FadeOut;
      stop;
   }
}

class Lith_FeuerFinal : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.3;
      DamageType "Magic";
      Damage 10;

      Projectile;
      +NOGRAVITY
   }

   states
   {
   Spawn:
      BLLT L 0;
   Death:
      BLLT LLLLLLLLLLLLLLL 0 A_SpawnItemEx("Lith_FeuerFX", 0,0,0, frandom(-4,4),frandom(-4,4),frandom(-2,2));
      BLLT L 0 A_PlaySound("weapons/feuer/final", CHAN_WEAPON);
      BLLT L 0 A_Explode(96, 128, 0);
      BLLT L 5;
      BLLT L 1 A_FadeOut;
      wait;
   }
}

class Lith_FeuerExplosion : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.15;
      DamageType "Magic";
      DamageFunction 10;

      Projectile;
      +NOGRAVITY
   }

   states
   {
   Spawn:
      BLLT L 1;
   Death:
      BLLT LLLLL 0 A_SpawnItemEx("Lith_FeuerFX", 0,0,0, frandom(-1,1),frandom(-1,1),frandom(-1,1));
      BLLT L 0 A_PlaySound("weapons/feuer/explode", CHAN_BODY);
      BLLT L 0 A_Explode(5, 20, 0);
      BLLT L 5;
      BLLT L 1 A_FadeOut;
      wait;
   }
}

class Lith_Feuer : Lith_CWeapon;

default
{
   Tag "Feuer";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/feuer/draw";

   -WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 3);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   /*
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Feuer", true,  false);
   TNT1 A 1 ACS_NamedExecuteWithResult("Lith_Feuer", false, false);
   TNT1 A 1 A_ReFire;
   */
   TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 30, 1);
   goto Ready;
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 15);
   TNT1 A 0 A_PlaySound("weapons/feuer/fire", CHAN_WEAPON);
   TNT1 A 15 ACS_NamedExecuteWithResult("Lith_Feuer", true,  true);
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 15);
   TNT1 A 0 A_PlaySound("weapons/feuer/fire", CHAN_WEAPON);
   TNT1 A 20 ACS_NamedExecuteWithResult("Lith_Feuer", false, true);
   goto Ready;
}

// EOF
