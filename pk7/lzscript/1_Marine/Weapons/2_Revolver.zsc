class Lith_Revolver : Lith_MWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Revolver";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 0.9;
   Weapon.BobSpeed 2.5;
   Weapon.BobRangeX 0.2;
   Weapon.BobRangeY 0.3;
   Weapon.UpSound "weapons/revolver/draw";
   Lith_Weapon.MagSize 6;
   
   +WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   REVO A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   REVO AAA 0 A_Lower;
   REVO A 1;
   loop;
Select:
   REVO AAA 0 A_Raise;
   REVO A 1;
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 Lith_A_MagIncr;
   TNT1 A 0 A_GunFlash("DummyFlash");
   TNT1 A 0 A_FireBullets(1, 1, -1, 45);
   TNT1 A 0 Lith_A_GunSmokeSmall;
   TNT1 A 0 Lith_A_RecoilUp(0.75);
   TNT1 A 0 A_PlaySound("weapons/revolver/fire", CHAN_WEAPON);
   REVO B 1 bright A_WeaponReady(LITH_BOBONLY);
   TNT1 A 0 Lith_A_RecoilDn(0.25);
   REVO C 1 bright A_WeaponReady(LITH_BOBONLY);
   TNT1 A 0 Lith_A_RecoilDn(0.25);
   REVO D 1 bright A_WeaponReady(LITH_BOBONLY);
   TNT1 A 0 Lith_A_RecoilDn(0.25);
   REVO EFD 2 A_WeaponReady(LITH_BOBONLY);
   REVO AAA 1 A_WeaponReady(WRF_ALLOWRELOAD);
   goto Ready;

Reload:
   TNT1 A 0 Lith_A_JumpIfMagFull;
   REVO A 0 A_PlaySound("weapons/revolver/reload", CHAN_7);
   REVO A 1 offset(0, 33);
   REVO A 1 offset(0, 34);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO A 1 offset(0, 35);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO A 1 offset(-1, 37);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO A 1 offset(-4, 40);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO G 1 offset(-7, 42);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO G 1 offset(-9, 48);
   REVO G 0 Lith_A_ReloadBobDn(0.1);
   REVO G 0 {
      for(int i = 0; i < invoker.m_fired; i++)
         Lith_A_Casing("Lith_CasSmall", 10, frandom(-1,1), 44, frandom(1,2), frandom(1,3), 0);
   }
   REVO H 1 offset(-11, 50);
   REVO H 0 Lith_A_ReloadBobDn(0.1);
   REVO H 1 offset(-12, 55);
   REVO I 1 offset(-13, 64);
   REVO I 1 offset(-13, 69);
   TNT1 A 16;
   TNT1 A 0 Lith_A_ReloadBobUp(0.5);
   TNT1 A 2;
   TNT1 A 0 Lith_A_ReloadBobUp(0.5);
   TNT1 A 2;
   TNT1 A 0 Lith_A_ReloadFinish;
   REVO J 0 Lith_A_ReloadBobUp(0.3);
   REVO J 1 offset(2, 64);
   REVO J 0 Lith_A_ReloadBobUp(0.2);
   REVO J 1 offset(2, 55);
   REVO K 1 offset(2, 50);
   REVO K 1 offset(2, 45);
   REVO K 1 offset(1, 40);
   REVO A 1 offset(1, 35);
   REVO A 1 offset(1, 33);
   REVO A 1 offset(1, 32);
   goto Ready;
}

// EOF
