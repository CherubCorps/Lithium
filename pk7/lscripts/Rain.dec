// Effects taken from Realm667.
// I think Tormenter made these, sorry if that's incorrect.

actor Lith_RainDrop
{
   Height 2
   Radius 2
   
   +MISSILE
   +NOBLOCKMAP
   -NOGRAVITY
   
   states
   {
   Spawn:
      RNDR A 1 A_JumpIf(waterlevel > 0, "Death")
      loop
   Death:
      RNDR BCDEFGH 3 A_FadeOut(0.15)
      stop
   }
}

actor Lith_BloodRainDrop : Lith_RainDrop
{
   Translation "80:111=175:191"
}

actor Lith_RainDropSpawner
{
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_JumpIf(CallACS("Lith_CheckCeilingSky"), "Nope")
      TNT1 A 0 A_Jump(256, "DoThing")
   DoThing:
      TNT1 A 0 A_SpawnItemEx("Lith_RainDrop")
      stop
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_BloodRainDropSpawner : Lith_RainDropSpawner
{
   states
   {
   DoThing:
      TNT1 A 0 A_SpawnItemEx("Lith_BloodRainDrop")
      stop
   }
}

actor Lith_SpawnRain : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_RainDropSpawner",
         frandom(16, 1024), 0, frandom(256,512), 0, 0, 0, frandom(0, 360), 128)
      TNT1 AAAAAAA 0 A_SpawnItemEx("Lith_RainDropSpawner",
         frandom(512, 1536), 0, frandom(256,512), 0, 0, 0, frandom(-90, 90), 128)
      stop
   }
}

actor Lith_SpawnBloodRain : Lith_SpawnRain
{
   states
   {
   Pickup:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_BloodRainDropSpawner",
         frandom(16, 1024), 0, frandom(256,512), 0, 0, 0, frandom(0, 360), 128)
      TNT1 AAAAAAA 0 A_SpawnItemEx("Lith_BloodRainDropSpawner",
         frandom(512, 1536), 0, frandom(256,512), 0, 0, 0, frandom(-90, 90), 128)
      stop
   }
}

// EOF

