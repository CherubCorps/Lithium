/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_JDJSmoke : Actor
{
   default
   {
      Alpha 0.7;
      RenderStyle "Add";
      Scale 0.3;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      FHFX VVVVVUUUUUVVVUUUVVWWW random(2, 3);
      FHFX W 1 A_FadeOut;
      wait;
   }
}

class Lith_JDJTrail : Actor
{
   default
   {
      Alpha 0.7;
      RenderStyle "Add";
      Scale 0.07;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT K 0;
      BLLT K 10 {
         for(int i = 0; i < 5; i++)
            A_SpawnItemEx("Lith_JDJSmoke", frandom(-1,1),frandom(-1,1),frandom(-1,1), frandom(-0.8,0.8),frandom(-0.8,0.8),frandom(-0.4,0.4));
      }
      BLLT K 1 A_FadeOut(0.02);
      wait;
   }
}

class Lith_FatMac : Lith_DWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_PlasmaRiflePickup";
   Tag "$LITH_INFO_SHORT_FatMac";
   Weapon.SlotNumber 6;
   Weapon.SlotPriority 1;

   +WEAPON.NOAUTOFIRE;
}

action(Weapon) void Lith_A_FireFatMac()
{
   A_TakeInventory("Lith_PlasmaAmmo", 1000);
   A_GunFlash("DummyFlash");
   A_SetBlend("White", 0.6, 10);
   Lith_FireBullets(0, 0, 8, 200, FBF_NORANDOM, "Lith_700Bullet");
   A_RailAttack(700, 0, false, "", "White", RGF_SILENT, 4, sparsity: 10, spawnclass: "Lith_JDJTrail");
   A_StartSound("weapons/fatmac/fire", lch_weapon);

   let vl = self.Vel.XY.Length();
   if(vl > 8)
   {
      self.A_DamageSelf(int(vl * 2), 'FatMacRecoil');

      if(Random(0, 10000) == 0)
         Lith_HERMES.Log(self, msg_huds, 1, "This is my arm fucker");
   }

   A_Recoil(14 + vl * 2);
   Lith_A_RecoilUp(3 + vl * 1.1);
}

states(Weapon)
{
Ready:
   PISG A 1 Lith_A_Ready;
   loop;

Deselect:
   PISG A 1 A_Lower(18);
   loop;

Select:
   PISG A 1 A_Raise(18);
   loop;

FireEmpty:
   PISG A 1;
   PISG A 2 offset(0, 38);
   PISG A 1 offset(0, 40);
   PISG A 1 offset(0, 37) A_StartSound("weapons/fatmac/empty", lch_dryweapon);
   PISG A 1 offset(0, 34);
   PISG A 1 offset(0, 33);
   PISG A 5 offset(0, 32);
   goto ReadySelector;
Fire:
   TNT1 A 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") < 700, "FireEmpty");
   PISG A 1 bright offset(0, 50) Lith_A_FireFatMac;
   PISG A 1 bright offset(0, 46);
   PISG A 1 offset(0, 43);
   PISG A 1 offset(0, 40);
   PISG A 1 offset(0, 39);
   PISG A 1 offset(0, 38);
   PISG A 1 offset(0, 36);
   PISG A 2 offset(0, 34);
   PISG A 2 offset(0, 33);
   PISG A 1 offset(0, 32);
   PISG A 1 offset(1, 32) Lith_A_RecoilDn(0.375);
   PISG A 1 offset(3, 32) Lith_A_RecoilDn(0.375);
   PISG A 1 offset(4, 32) Lith_A_RecoilDn(0.375);
   PISG A 2 offset(8, 38) Lith_A_RecoilDn(0.375);
   PISG A 3 offset(12, 45);
   PISG A 4 offset(16, 53);
   PISG A 10 offset(19, 60);
   PISG A 4 offset(21, 64);
   PISG A 2 offset(23, 67);
   PISG A 2 offset(22, 74) A_StartSound("weapons/fatmac/boltback", lch_reload);
   PISG A 4 offset(19, 78);
   PISG A 20 offset(18, 80);
   PISG A 3 offset(17, 74) A_StartSound("weapons/fatmac/boltforward", lch_reload2);
   PISG A 3 offset(19, 67);
   PISG A 2 offset(20, 66);
   PISG A 2 offset(21, 62);
   PISG A 2 offset(20, 60);
   PISG A 2 offset(21, 63);
   PISG A 2 offset(23, 66);
   PISG A 1 offset(24, 68);
   PISG A 1 offset(25, 63);
   PISG A 1 offset(24, 59);
   PISG A 1 offset(22, 57);
   PISG A 1 offset(20, 54);
   PISG A 2 offset(16, 49);
   PISG A 2 offset(12, 44);
   PISG A 2 offset(10, 42);
   PISG A 2 offset(8, 40);
   PISG A 3 offset(5, 38);
   PISG A 3 offset(2, 35);
   PISG A 5 offset(0, 32);
   goto ReadySelector;
}

/* EOF */
