// ALL THINGS HERE TAKEN FROM LEGENDOOM
class Lith_Knif : Inventory {default {Inventory.MaxAmount 1;}}

class Lith_KnifPuf : Actor
{
   default
   {
      RenderStyle "Translucent";
      Alpha 0.5;
      VSpeed 0.5;
      Mass 5;

      +NOINTERACTION
      -ALLOWPARTICLES
      +PUFFONACTORS
      +THRUGHOST
      +MTHRUSPECIES
      +NOEXTREMEDEATH
      +FORCEPAIN
      +HITTRACER
   }

   states
   {
   Spawn:
   XDeath:
      TNT1 A 0;
      TNT1 A 0 A_JumpIf(tracer ? tracer.bNOBLOOD : true, "Crash");
      TNT1 A 0 A_PlaySound("weapons/knife/hitflesh", CHAN_WEAPON, 1, 0, 1.8);
      stop;
   Melee:
   Crash:
      TNT1 A 0;
      TNT1 A 0 A_PlaySound("weapons/knife/hitwall", CHAN_WEAPON, 1, 0, 1.8);
      PUFF A 2 bright;
      PUFF BCD 2;
      stop;
   }
}

class Lith_Fist : Lith_MWeapon replaces Fist; // why would you pick up a fist

default
{
   Tag "$LITH_INFO_SHORT_Fist";
   Weapon.Kickback 100;
   Weapon.SlotNumber 1;
   Weapon.SlotPriority 0.9;
   AttackSound "*fist"; // there is nothing wrong here MOVE ALONG
   DamageType "Melee";

   +WEAPON.WIMPY_WEAPON
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
}

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_FistPickup");
   stop;
}

states(Weapon)
{
Ready:
   TNT1 A 1 Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "Fire1");
   KNIF A 1 A_PlaySound("weapons/knife/swing1", CHAN_WEAPON);
   KNIF BCD 1;
   goto FireNormal;
Fire1:
   TNT1 A 1 A_PlaySound("weapons/knife/swing2", CHAN_WEAPON);
   KNIF FGH 1;
FireNormal:
   TNT1 A 0 A_CustomPunch((CountInv("PowerStrength") ? 20 : 5) * random(1, 10), true, CPF_NOTURN, "Lith_KnifPuf", 96);
   TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "FireEnd2");
   KNIF E 1;
   TNT1 A 1;
   goto FireEnd;
FireEnd2:
   KNIF IJ 1;
FireEnd:
   TNT1 A 10;
   TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "FireTakeKnif");
   TNT1 A 0 A_GiveInventory("Lith_Knif", 1);
   TNT1 AAAAA 1 A_ReFire;
   TNT1 A 0 A_TakeInventory("Lith_Knif", 1);
   goto Ready;
FireTakeKnif:
   TNT1 A 0 A_TakeInventory("Lith_Knif", 1);
   TNT1 AAAAA 1 A_ReFire;
   goto Ready;
}

// EOF
