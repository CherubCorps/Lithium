actor Lith_IonRifleFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "112:127=193:197", "160:167=193:199", "224:231=192:198"
   Height 1
   Radius 1
   
   +NOBLOCKMAP
   +NOGRAVITY
}

actor Lith_IonRifleParticle1 : Lith_IonRifleFXBase
{
   Alpha 0.5
   XScale 0.5
   YScale 0.4
   
   states
   {
   Spawn:
      BFE1 ABC 3 bright
   SpawnLoop:
      BFE1 C 0 bright A_SetScale(scalex, scaley * 0.875)
      BFE1 C 1 bright A_FadeOut(0.025)
      loop
   }
}

actor Lith_IonRifleParticle2 : Lith_IonRifleFXBase
{
   Alpha 0.5
   XScale 0.5
   YScale 0.4
   
   states
   {
   Spawn:
      BFE2 ABC 3 bright
   SpawnLoop:
      BFE2 C 0 bright A_SetScale(scalex, scaley * 0.875)
      BFE2 C 1 bright A_FadeOut(0.025)
      loop
   }
}

actor Lith_IonRifleExplosion
{
   Radius 0
   Height 0
   RenderStyle "Add"
   
   +NOBLOCKMAP
   +NOGRAVITY
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_Explode(128, 128*1.5, 0)
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_IonRifleParticle1", frandom(-32, 32),frandom(-32, 32),frandom(-32, 32), frandom(-1, 1),frandom(-1, 1),frandom(-1, 1))
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_IonRifleParticle2", frandom(-24, 24),frandom(-24, 24),frandom(-24, 24), frandom(-0.5, 0.5),frandom(-0.5, 0.5),frandom(-0.5, 0.5))
      stop
   }
}

actor Lith_IonRifleHax : FastProjectile
{
   Speed 32000
   Damage 100
   Height 1
   Radius 1
   
   +NOGRAVITY
   +NOBLOCKMAP
   +THRUACTORS
   +EXTREMEDEATH
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 1
      loop
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_IonRifleExplosion", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_IonRifleParticle
{
   RenderStyle "Add"
   Scale 0.05
   
   +NOINTERACTION
   +BRIGHT
   
   states
   {
   Spawn:
      BLLT K 5
   SpawnLoop:
      BLLT K 0 A_SetScale(scalex * 1.05)
      BLLT K 1 A_FadeOut(0.15)
      loop
   }
}

actor Lith_IonRifleShotsFired : Inventory
{
   Inventory.MaxAmount 6
}

actor Lith_IonRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_IonRifle"
   Weapon.UpSound "weapons/ionrifle/draw"
   Weapon.SlotNumber 5
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   AltReady:
      SCOP C 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
      loop
   Ready:
      SNPG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      loop
   
   ReadySelector:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "AltReady")
      goto Ready
   
   Deselect:
      SNPG A 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      SNPG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      SNPG AAA 0 A_Lower
      SNPG A 1
      loop
   
   Select:
      SNPG A 0 A_ZoomFactor(1.0)
   SelectLoop:
      SNPG AAA 0 A_Raise
      SNPG A 1
      loop
   
   Fire:
      "####" "#" 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1)
      goto FireEmpty
      TNT1 A 0 A_JumpIfInventory("Lith_IonRifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_IonRifleShotsFired", 1)
      TNT1 A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_FireCustomMissile("Lith_IonRifleHax")
      TNT1 A 0 A_RailAttack(200, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 0, 16, 0, "Lith_IonRifleParticle")
      TNT1 A 0 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 0, 0.5, 0)
      TNT1 A 0 A_Recoil(2)
      TNT1 A 0 A_PlaySound("weapons/ionrifle/fire", CHAN_WEAPON)
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FireAnim_Scoped")
      
   FireAnim_Unscoped:
      SNPG B 1 bright
      SNPG C 1 bright
      SNPG DEFG 1
      SNPG A 1
      goto BoltAnim
   
   FireAnim_Scoped:
      SCOP C 1 bright A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.80)
      SCOP C 1 bright A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.85)
      SCOP C 1 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.90)
      SCOP C 1 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.95)
      SCOP C 3 A_ZoomFactor(1 +  CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      
   BoltAnim:
      "####" "#" 1 offset(1, 32)
      "####" "#" 1 offset(3, 32)
      "####" "#" 1 offset(4, 32)
      "####" "#" 1 offset(8, 38)
      "####" "#" 1 offset(12, 45)
      "####" "#" 1 offset(16, 53)
      "####" "#" 1 offset(19, 60) A_PlaySound("weapons/ionrifle/eject", CHAN_5)
      "####" "#" 1 offset(21, 64)
      "####" "#" 1 offset(23, 67)
      "####" "#" 1 offset(22, 74)
      "####" "#" 1 offset(19, 78)
      "####" "#" 5 offset(18, 80)
      "####" "#" 5 offset(17, 80)
      "####" "#" 5 offset(16, 80) A_PlaySound("weapons/ionrifle/load", CHAN_6)
      "####" "#" 1 offset(17, 74)
      "####" "#" 1 offset(19, 67)
      "####" "#" 1 offset(20, 66)
      "####" "#" 1 offset(21, 62)
      "####" "#" 1 offset(20, 60)
      "####" "#" 1 offset(21, 63)
      "####" "#" 1 offset(23, 66)
      "####" "#" 1 offset(24, 68)
      "####" "#" 1 offset(25, 63)
      "####" "#" 1 offset(24, 59)
      "####" "#" 1 offset(22, 57)
      "####" "#" 1 offset(20, 54)
      "####" "#" 1 offset(16, 49)
      "####" "#" 1 offset(12, 44)
      "####" "#" 1 offset(10, 42)
      "####" "#" 1 offset(8, 40)
      "####" "#" 1 offset(5, 38)
      "####" "#" 1 offset(2, 35)
      "####" "#" 5 offset(1, 32)
      goto ReadySelector
   
   AltFire:
      "####" A 1
      "####" A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" A 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" A 0 A_PlaySound("weapons/ionrifle/zoomout", 7)
      goto Ready
   ScopeIn:
      "####" A 0 A_GiveInventory("Lith_WeaponScopedToken", 1)
      "####" A 0 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX))
      "####" A 0 A_PlaySound("weapons/ionrifle/zoomin", 6)
      goto AltReady
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, 1)
      goto Reload1
      TNT1 A 0 A_PlaySound("weapons/ionrifle/zoomout", 7)
      TNT1 A 0 A_TakeInventory("Lith_WeaponScopedToken", 999)
      TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
   Reload1:
      TNT1 A 0 A_JumpIfInventory("Lith_IonRifleShotsFired", 1, 1)
      goto ReadySelector
      SNPG A 1
      SNPG A 1 offset(-2, 34)
      SNPG A 1 offset(-4, 37)
      SNPG A 1 offset(-9, 43) A_PlaySound("weapons/ionrifle/magout", CHAN_6)
      SNPG A 1 offset(-12, 47)
      SNPG A 1 offset(-14, 52)
      SNPG A 2 offset(-12, 55)
      SNPG A 3 offset(-11, 57)
      SNPG A 8 offset(-10, 60)
      SNPG A 8 offset(-11, 60)
      SNPG A 8 offset(-12, 60)
      SNPG A 0 A_TakeInventory("Lith_IonRifleShotsFired", 999)
      SNPG A 2 offset(-13, 59) A_PlaySound("weapons/ionrifle/magin", CHAN_7)
      SNPG A 2 offset(-11, 49)
      SNPG A 2 offset(-11, 47)
      SNPG A 7 offset(-11, 45)
      SNPG A 7 offset(-12, 45)
      SNPG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SNPG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      SNPG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      SNPG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      SNPG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      SNPG A 4 A_WeaponReady(WRF_NOBOB)
      goto ReadySelector
   
   FireEmpty:
      "####" "#" 1
      "####" "#" 2 offset(0, 41)
      "####" "#" 1 offset(0, 45)
      "####" "#" 1 offset(0, 43)
      "####" "#" 1 offset(0, 40) A_PlaySound("weapons/ionrifle/empty", CHAN_5)
      "####" "#" 1 offset(0, 38)
      "####" "#" 1 offset(0, 36)
      "####" "#" 1 offset(0, 33)
      "####" "#" 1 offset(0, 32)
      "####" "#" 5
      "####" "####" 1 A_ReFire
      goto ReadySelector
   }
}

