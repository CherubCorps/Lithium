class Lith_Mateba : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Mateba";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 1;
   Weapon.BobSpeed 2.5;
   Weapon.BobRangeX 0.2;
   Weapon.BobRangeY 0.3;
   Weapon.UpSound "weapons/mateba/draw";
   Lith_Weapon.MagSize 12;
   
   +WEAPON.NOAUTOFIRE
}

states(actor)
{
SpawnPickup:
   TNT1 A 0 A_SpawnItemEx("Lith_PistolPickup", 0,0,0, 0,0,0, 0, Lith_PF);
   stop;
}

states(weapon)
{
Ready:
   MTBG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   MTBG AAA 0 A_Lower;
   MTBG A 1;
   loop;
Select:
   MTBG AAA 0 A_Raise;
   MTBG A 1;
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 Lith_A_MagIncr;
   TNT1 A 0 A_GunFlash("DummyFlash");
   TNT1 A 0 {
      if(invoker.m_fired & 1)
         Lith_A_Casing("Lith_CasSmallHot", 14, 2, 32, frandom(1,2), frandom(3,5), frandom(2,3));
   }

   TNT1 A 0 A_JumpIf(!CallACS("LPData", pdata_upgrade, UPGR_Mateba_A), 2);
   TNT1 A 0 Lith_A_JumpIfMagEmpty("Finalize");

   TNT1 A 0 Lith_A_RecoilDn(0.2);
   TNT1 A 0 A_FireBullets(2, 2, -1, 30, "BulletPuff", FBF_NORANDOM);
   TNT1 A 0 Lith_A_GunSmokeSmall;
   TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON);
   MTBG B 1 bright offset(0, 50);
   TNT1 A 0 Lith_A_RecoilUp(0.1);
   MTBG C 1 bright offset(0, 46);
   TNT1 A 0 Lith_A_RecoilUp(0.1);
   MTBG A 1 offset(0, 40);
   MTBG A 1 offset(0, 37);
   MTBG A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 A_WeaponReady(WRF_NOBOB);
   goto Ready;

Finalize:
   TNT1 A 0 Lith_A_GunSmokeSmall;
   TNT1 A 0 A_SpawnItemEx("Lith_FinalizerEffect", cos(pitch)*24, 0, ((height-19)+2) - sin(pitch)*24);
   TNT1 A 0 A_FireProjectile("Lith_FinalizerShot", 0, false, 0, 10);
   TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON);
   TNT1 A 0 A_PlaySound("weapons/mateba/finalize", CHAN_6);
   TNT1 A 0 A_PlaySound("weapons/shotgun/just", CHAN_5);
   MTBG B 1 bright offset(0, 55);
   MTBG C 1 bright offset(0, 51);
   MTBG A 1 offset(0, 45);
   MTBG A 1 offset(0, 43);
   MTBG A 1 offset(0, 40);
   MTBG A 1 offset(0, 37);
   MTBG A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 A_WeaponReady(WRF_NOBOB);
   goto Ready;

Reload:
   TNT1 A 0 Lith_A_JumpIfMagFull;
   MTBG E 2;
   MTBG F 2;
   MTBG G 2;
   MTBG H 2;
   MTBG I 4 A_PlaySound("weapons/mateba/open", 7);
   MTBG J 3;
   MTBG K 2;
   MTBG L 3 A_PlaySound("weapons/mateba/eject", 6);
   MTBG L 0 {
      for(int i = 0; i < invoker.m_fired / 2; i++)
         Lith_A_Casing("Lith_CasSmall", 10, frandom(1,2), 32, frandom(1,2), frandom(1,3), 0);
   }
   MTBG M 3 Lith_A_ReloadFinish;
   MTBG N 3;
   MTBG O 2 A_PlaySound("weapons/mateba/load", 5);
   MTBG P 3;
   MTBG Q 3 A_PlaySound("weapons/mateba/close", 7);
   MTBG R 3;
   MTBG S 4;
   goto Ready;
}

// EOF
