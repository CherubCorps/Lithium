/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_InformantPlayer : Lith_Player
{
   default
   {
      Player.DisplayName "Informant";
      Player.SoundClass "informant";
      Player.Portrait "lgfx/Portraits/Informant.png";
      Player.ViewHeight 44;
      Player.AttackZOffset 11;
      Player.JumpZ 14;
      Tag "Informant";

      +NOBLOOD;
   }

   private int m_DodgeDbc;
   private int m_IFrames;

   override void Tick()
   {
      if(m_IFrames) m_IFrames--;

      if(health > 0)
      {
         if(m_DodgeDbc)
            m_DodgeDbc--;
         else if(player && player.buttons & BT_SPEED)
         {
            let a = angle - atan2(player.cmd.sidemove, player.cmd.forwardmove);
            let c = cos(a) * 32;
            let s = sin(a) * 32;
            CallACS("Lith_DodgeView");
            A_ChangeVelocity(c, s);
            m_IFrames = 20;
            m_DodgeDbc = TICRATE;
         }
      }

      Super.Tick();
   }

   override int DamageMobj(Actor inflictor, Actor source, int damage, name mod, int flags, double angle)
   {
      if(m_IFrames)
         return 0;
      else
         return Super.DamageMobj(inflictor, source, damage, mod, flags, angle);
   }
}

class Lith_IWeapon : Lith_Weapon
{
   default
   {
      Inventory.RestrictedTo "Lith_InformantPlayer";
   }
}

/* EOF */
