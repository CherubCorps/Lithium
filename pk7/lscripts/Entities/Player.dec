actor Lith_Player : DoomPlayer
{
   Player.StartItem "Lith_Pistol"
   Player.StartItem "Lith_Fist"
   Player.SideMove 2, 1
   Player.ForwardMove 2, 1
   Player.ViewHeight 48
   Player.AttackZOffset 15
   DamageFactor "Lith_PlayerMissile", 0
   Species "Lith_Player"
   
   states
   {
   Pain.Lith_Bullets:   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 1) goto Super::Pain
   Pain.Railgun:
   Pain.Electric:
   Pain.Lith_Energy:    TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 2) goto Super::Pain
   Pain.Fire:
   Pain.Lith_Fire:      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 3) goto Super::Pain
   Pain.Lith_FireMagic: TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 4) goto Super::Pain
   Pain.Lith_Magic:     TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 5) goto Super::Pain
   Pain.Melee:
   Pain.Lith_Melee:     TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 6) goto Super::Pain
   Pain.Lith_Shrapnel:  TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 7) goto Super::Pain
   }
}

actor Lith_RA_Hax       : PowerProtection {Powerup.Duration 0x7FFFFFFF}
actor Lith_RA_Bullets   : Lith_RA_Hax {DamageFactor "Lith_Bullets",   0.05}
actor Lith_RA_Energy    : Lith_RA_Hax {DamageFactor "Lith_Energy",    0.05 DamageFactor "Railgun", 0.05 DamageFactor "Electric", 0.05}
actor Lith_RA_Fire      : Lith_RA_Hax {DamageFactor "Lith_Fire",      0.05 DamageFactor "Fire", 0.05}
actor Lith_RA_FireMagic : Lith_RA_Hax {DamageFactor "Lith_FireMagic", 0.05}
actor Lith_RA_Magic     : Lith_RA_Hax {DamageFactor "Lith_Magic",     0.05}
actor Lith_RA_Melee     : Lith_RA_Hax {DamageFactor "Lith_Melee",     0.01 DamageFactor "Melee", 0.05}
actor Lith_RA_Shrapnel  : Lith_RA_Hax {DamageFactor "Lith_Shrapnel",  0.05}

actor Lith_RocketBooster : PowerFlight
{
   Powerup.Duration 60
}

actor Lith_CameraHax // fuck
{
   Radius 2
   Height 2
   
   +NOINTERACTION
}

actor Lith_TimeHax : PowerTimeFreezer
{
   Powerup.Duration 1
}

actor Lith_TimeHax2 : PowerTimeFreezer
{
   Powerup.Duration -80
}

actor Lith_ExplodoBoots
{
   DamageType "Lith_PlayerMissile"
   
   +PAINLESS
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_Explode(20, 128, 0)
      stop
   }
}

actor Lith_EXPLOOOSION
{
   DamageType "Lith_PlayerMissile"
   
   states
   {
   Spawn:
      MEXP A  0 nodelay A_Explode
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_Die : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfHealthLower(1000, 2)
      TNT1 A 0 A_RailWait
      stop
      TNT1 A 0 A_Die
      stop
   }
}

actor Lith_Nuke : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0 A_PlaySound("player/nuke", CHAN_7, 1.0, false, ATTN_NONE)
      TNT1 A 0 A_RadiusGive("Lith_Die", 6144, RGF_MONSTERS)
      TNT1 A 0 A_AlertMonsters
      stop
   }
}

actor Lith_PlayerDeathParticle
{
   RenderStyle "Subtract"
   Alpha 0.9
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      TNT1 A random(3, 30)
      LDTH AAAAAAAAAAAAAAAAAAAAA 1 A_SetScale(frandom(0.1, 0.2))
      LDTH AAAA 3 A_FadeOut(0.1)
      LDTH AAAA 4 A_FadeOut(0.1)
      stop
   }
}

actor Lith_PlayerDeathParticle2
{
   RenderStyle "Subtract"
   Alpha 0.9
   Scale 0.2
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      TNT1 A random(3, 10)
      LDTH AAAA 12 A_FadeOut(0.1)
      LDTH AAAA 4 A_FadeOut(0.1)
      stop
   }
}

actor Lith_PlayerDeath : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("player/death2", CHAN_6, 1.0, false, ATTN_NONE)
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_PlayerDeathParticle", frandom(-32,32), frandom(-32,32), frandom(0, 64), frandom(-2, 2), frandom(-2, 2), frandom(1, 2), 0, SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_PlayerDeathNuke : Lith_Nuke
{
   states
   {
   Pickup:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_PlayerDeathParticle2", frandom(-32,32), frandom(-32,32), frandom(0, 64), frandom(-16, 16), frandom(-16, 16), frandom(1.5, 2.5), 0, SXF_NOCHECKPOSITION)
      goto Super::Pickup
   }
}

// EOF

