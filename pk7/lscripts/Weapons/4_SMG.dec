actor Lith_SMGTrail
{
   RenderStyle "Add"
   Scale 0.3
   Alpha 0.5
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PUFF A 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_SMGTracer : FastProjectile
{
   MissileType "Lith_SMGTrail"
   MissileHeight 8
   RenderStyle "Translucent"
   Alpha 0.9
   Scale 0.5
   Radius 2
   Height 20
   Damage (0)
   Speed 128
   
   +NODAMAGE
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   }
}

actor Lith_SMGShotsFired : Inventory
{
   Inventory.MaxAmount 50
}

actor Lith_SMGHeat : Inventory
{
   Inventory.MaxAmount 500
}

actor Lith_SMG : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_SMG"
   Weapon.UpSound "weapons/smg/draw"
   Weapon.SlotNumber 4
   Weapon.SlotPriority 1
   
   const int lvar_spreadx = 1;
   const int lvar_spready = 2;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_ChaingunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CSMG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1)
      loop
   
   Deselect:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
   DeselectLoop:
      CSMG AAA 0 A_Lower
      CSMG A 1
      loop
   
   Select:
      CSMG AAA 0 A_Raise
      CSMG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 0, "ReloadHeat")
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 0, "Reload")
      TNT1 A 0 A_PlaySound("weapons/smg/fire", CHAN_WEAPON, 1, true)
   Fire1:
      TNT1 A 0 A_GiveInventory("Lith_SMGHeat", random(15,30))
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 0, "ReloadHeatFromFiring")
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 0, "ReloadFromFiring")
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_GiveInventory("Lith_SMGShotsFired", 1)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_SetVar", lvar_spreadx, frandom(-2.885, 2.885)*FIX)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_SetVar", lvar_spready, frandom(-1, 1)*FIX)
      TNT1 AA 0 A_FireCustomMissile("Lith_SMGTracer",
         CallACS("Lith_GetVar", lvar_spreadx)/FIX, false, 0, 0, 0,
         CallACS("Lith_GetVar", lvar_spready)/FIX)
      TNT1 AA 0 A_FireBullets(
         CallACS("Lith_GetVar", lvar_spreadx)/FIX,
         CallACS("Lith_GetVar", lvar_spready)/FIX, -1, 7, "Lith_RifleBulletPainless", FBF_NORANDOM|FBF_EXPLICITANGLE)
      CSMG BC 1 bright
      CSMG A 1
      CSMG A 1 A_ReFire("Fire1")
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
      goto Ready
   
   ReloadHeatFromFiring:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
   
   User1:
   ReloadHeat:
      TNT1 A 0 A_JumpIfInventory("Lith_SMGHeat", 200, 1)
      goto Ready
      CSMG A 5
      CSMG A 7 A_PlaySound("weapons/smg/open", CHAN_6)
      CSMG A 2 A_PlaySound("weapons/smg/eject", CHAN_6)
      CSMG A 0 A_TakeInventory("Lith_SMGHeat", 999)
      CSMG A 35 A_PlaySound("weapons/smg/heat", CHAN_7)
      CSMG A 40 A_PlaySound("weapons/smg/insert", CHAN_6)
      CSMG A 10 A_PlaySound("weapons/smg/close", CHAN_6)
      goto Ready
   
   ReloadFromFiring:
      CSMG A 0 A_StopSound(CHAN_WEAPON)
      CSMG A 0 A_PlaySound("weapons/smg/firedone", CHAN_5)
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_SMGShotsFired", 1, 1)
      goto Ready
      CSMG A 1
      CSMG A 1 offset(-2, 34)
      CSMG A 1 offset(-4, 37)
      CSMG A 1 offset(-9, 43) A_PlaySound("weapons/smg/magout", CHAN_6)
      CSMG A 1 offset(-12, 47)
      CSMG A 1 offset(-14, 52)
      CSMG A 2 offset(-12, 55)
      CSMG A 3 offset(-11, 57)
      CSMG A 8 offset(-10, 60)
      CSMG A 8 offset(-11, 60)
      CSMG A 0 A_TakeInventory("Lith_SMGShotsFired", 999)
      CSMG A 2 offset(-13, 59) A_PlaySound("weapons/smg/magin", CHAN_7)
      CSMG A 2 offset(-11, 49)
      CSMG A 2 offset(-11, 47)
      CSMG A 7 offset(-11, 45)
      CSMG A 1 offset(-12, 49)
      CSMG A 1 offset(-8, 53)
      CSMG A 1 offset(-1, 57)
      CSMG A 1 offset(3, 56)
      CSMG A 1 offset(8, 52)
      CSMG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB)
      CSMG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB)
      CSMG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB)
      CSMG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB)
      CSMG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB)
      CSMG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB)
      CSMG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB)
      CSMG A 4 A_WeaponReady(WRF_NOBOB)
      goto Ready
   }
}

