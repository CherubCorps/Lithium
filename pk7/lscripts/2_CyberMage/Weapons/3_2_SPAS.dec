actor Lith_SPASShotsFired : Inventory
{
   Inventory.MaxAmount 8
}

actor Lith_VoidShotTrail
{
   RenderStyle "Add"
   Alpha 0.9
   Scale 0.05
   Translation "0:255=%[0,0,0]:[0.8,0.1,0.75]"

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      BLLT N 0
   SpawnLoop:
      "####" "#" 0 A_JumpIf(scalex < 0.005, "Death")
      "####" "#" 1 
      "####" "#" 0 A_SetScale(scalex * 0.9)
      loop
   Death:
      TNT1 A 0
      stop
   }
}

actor Lith_VoidShotTrail2 : Lith_VoidShotTrail
{
   Scale 0.025
   Translation "None"

   states
   {
   Spawn:
      BLLT L 0
      goto SpawnLoop
   }
}

actor Lith_VoidShotTrail3 : Lith_VoidShotTrail
{
   Scale 0.15
}

actor Lith_VoidShotEffect : Lith_MonsterSoulEffect
{
   Translation "0:255=%[0,0,0]:[0.8,0.1,0.75]"
}

actor Lith_VoidShot
{
   RenderStyle "Add"
   Alpha 0.9
   Scale 0.05
   Radius 4
   Height 4
   Damage 5
   Speed 16
   Translation "0:255=%[0,0,0]:[0.8,0.1,0.75]"

   Projectile
   +BRIGHT
   +SEEKERMISSILE

   states
   {
   Spawn:
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail")
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail2")
      BLLT N 0 A_Jump(256, 1, 2)
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail3")
      BLLT N 0 A_Jump(256, 1, 2, 3, 3)
      BLLT N 0 A_ChangeVelocity(0,frandom(-1,1),frandom(-1,1), CVF_RELATIVE)
      BLLT N 0 A_SeekerMissile(5, 0.7, SMF_LOOK|SMF_PRECISE|SMF_CURSPEED)
      BLLT N 1
      loop
   Death:
      BLLT N 0 A_PlaySound("ghost/hit", CHAN_5)
      TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("Lith_VoidShotEffect", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2))
      stop
   }
}

actor Lith_SPAS : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_SPAS"
   Weapon.UpSound "weapons/cshotgun/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 0.95
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_SuperShotgunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CSHG A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      CSHG AAA 0 A_Lower
      CSHG A 1
      loop
   
   Select:
      CSHG AAA 0 A_Raise
      CSHG A 1
      loop
   
   ReloadCheck:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SPAS_B), "Fire1")
      goto Reload

   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShellAmmo", 1, 1)
      goto FireEmpty
      TNT1 A 0 A_JumpIfInventory("Lith_SPASShotsFired", 0, "ReloadCheck")
      TNT1 A 0 A_TakeInventory("Lith_ShellAmmo", 1, TIF_NOTAKEINFINITE)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SPAS_B), "Fire1")
      TNT1 A 0 A_GiveInventory("Lith_SPASShotsFired", 1)
      
   Fire1:
      // don't ask
      TNT1 A 0 A_JumpIf(z - floorz < 24, 3)
      TNT1 A 0 A_Recoil(15)
      TNT1 A 0 A_FireBullets(10.5, 3.5, 3, 3)
      
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_Recoil(0.7)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SPAS_A), "FireVoid")
      TNT1 A 0 A_FireBullets(10.5, 3.5, 12, 6)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SPAS_B), "FireAnim3")

   FireAnim:
      TNT1 A 0 A_PlaySound("weapons/cshotgun/fire", CHAN_WEAPON)
      CSHG B 1 bright offset(0, 35) A_Light(1)
      CSHG B 1 bright offset(0, 35)
      CSHG C 1 bright offset(0, 34) A_Light(2)
      CSHG C 1 bright offset(0, 33)
      CSHG D 1 offset(0, 33) A_Light(0)
      CSHG D 1 offset(0, 32)
      goto Ready

   FireVoid:
      TNT1 AAAAAA 0 A_FireCustomMissile("Lith_VoidShot", frandom(-5,5), false, 0, 0, FPF_NOAUTOAIM, frandom(-2,2))

   FireAnim2:
      //TNT1 A 0 A_PlaySound("weapons/cshotgun/firemag", CHAN_WEAPON)
      TNT1 A 0 A_PlaySound("weapons/cshotgun/fire", CHAN_WEAPON)
      CSHG B 1 bright offset(0, 38) A_Light(1)
      CSHG B 1 bright offset(0, 38)
      CSHG C 1 bright offset(0, 37) A_Light(2)
      CSHG D 1 bright offset(0, 36)
      CSHG D 1 offset(0, 35) A_Light(0)
      CSHG A 1 offset(0, 35)
      CSHG A 1 offset(0, 33)
      CSHG A 1 offset(0, 32)
      goto Ready

   FireAnim3:
      TNT1 A 0 A_FireBullets(11, 5, 2, 2)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/cshotgun/fire2", CHAN_WEAPON)
      CSHG B 1 bright offset(0, 38) A_Light(1)
      CSHG B 1 bright offset(0, 38)
      CSHG C 1 bright offset(0, 37)
      CSHG C 1 bright offset(0, 37) A_Light(2)
      CSHG D 1 bright offset(0, 36)
      CSHG D 1 bright offset(0, 36)
      CSHG D 1 offset(0, 35) A_Light(0)
      CSHG A 1 offset(0, 35)
      CSHG A 1 offset(0, 34)
      CSHG A 1 offset(0, 33)
      CSHG A 1 offset(0, 32)
      goto Ready
   
   Reload:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_SPAS_B), "Ready")
      TNT1 A 0 A_JumpIfInventory("Lith_SPASShotsFired", 1, 1)
      goto Ready
      CSHR ABCDE 1
      CSHR FG 2
      CSHR H 2 A_PlaySound("weapons/cshotgun/load", CHAN_6)
      CSHR IJKLMNOPQR 2
      CSHR A 0 A_TakeInventory("Lith_SPASShotsFired", 999)
      CSHR S 2 A_PlaySound("weapons/cshotgun/pump", CHAN_7)
      CSHR TUVWXYZB 2
      goto Ready
   
   FireEmpty:
      CSHG A 1
      CSHG A 2 offset(0, 38)
      CSHG A 1 offset(0, 40)
      CSHG A 1 offset(0, 37) A_PlaySound("weapons/cshotgun/empty", CHAN_5)
      CSHG A 1 offset(0, 34)
      CSHG A 1 offset(0, 33)
      CSHG A 5 offset(0, 32)
      goto Ready
   }
}

