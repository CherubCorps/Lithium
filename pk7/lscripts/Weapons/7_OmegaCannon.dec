actor Lith_Weapon_OmegaCannon : Lith_Weapon_OmegaRailgun replaces BFG9000
{
   -WEAPON.CHEATNOTWEAPON
   
   states
   {
   FireCannonBig:
      CNNG A 10 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      CNNG A 1 bright
      CNNG A 1 bright offset(-1, 33)
      CNNG A 1 bright offset(-1, 34)
      CNNG A 1 bright offset(-1, 36)
      CNNG A 2 bright offset(1, 37)
      CNNG A 3 bright offset(1, 38)
      CNNG C 2 bright offset(0, 62) A_FireCustomMissile("Lith_CannonBall", 0, false, 0, -24, FPF_NOAUTOAIM)
      CNNG C 1 bright offset(0, 57)
      CNNG C 1 bright offset(0, 47)
      CNNG C 1 bright offset(0, 44)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG B 1 bright offset(0, 35)
      CNNG B 1 bright offset(0, 32)
      CNNG AAAAAAAAAAAAAAAAAAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireCannonSmall:
      CNNG C 1 bright offset(0, 48)
      CNNG C 1 bright offset(0, 57) A_FireCustomMissile("Lith_SmallCannonBall", 0, false, 0, -16, FPF_NOAUTOAIM)
      CNNG C 1 bright offset(0, 55)
      CNNG C 1 bright offset(0, 53)
      CNNG C 1 bright offset(0, 49)
      CNNG B 1 bright offset(0, 45)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG A 1 bright offset(0, 34)
      CNNG A 1 offset(0, 32)
      CNNG A 8
      CNNG AAAAAAAAAA 1 A_ReFire
      goto Ready
   }
}

actor Lith_CannonExplosionParticle
{
   Mass 5
   Scale 0.5
   Gravity 0.8
   
   -NOGRAVITY
   
   states
   {
   Spawn:
      MEXP AB 2 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      MEXP CD 3 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      MEXP EF 4 bright A_SpawnItemEx("Lith_CannonSmokeParticle", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_CannonSmokeParticle : Lith_CannonExplosionParticle
{
   Mass 3
   Gravity 0.4
   RenderStyle "Add"
   
   states
   {
   Spawn:
      SMOK ABCDEFGHIJKLM 1 A_FadeOut(0.06)
      stop
   }
}

actor Lith_CannonBall
{
   Radius 50
   Height 16
   Scale 3.5
   Damage (120)
   Speed 42
   Gravity 0.55
   BounceType Doom
   Species "Lith_Player"
   DamageType "Lith_PlayerMissile"
   SeeSound "weapons/cannon/fire"
   DeathSound "weapons/cannon/explode"
   
   Projectile
   +RIPPER
   +BOUNCEONACTORS
   +BOUNCEAUTOOFFFLOORONLY
   +MTHRUSPECIES
   -NOGRAVITY
   
   var int user_angle;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SetUserVar(user_angle, angle * FIX)
      TNT1 A 2
   SpawnLoop:
      CBAL A 1 bright A_SetAngle(user_angle / FIX)
      loop
   Death:
      CBAL B 0 A_SetScale(2)
      CBAL B 0 A_NoGravity
      CBAL B 0 A_Jump(256, "Splodey")
   Splodey:
      TNT1 A 0 A_Quake(3, 20, 0, 640)
      TNT1 AAAAAA 0 A_Explode
      TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_CannonExplosionParticle", 0,0,8, frandom(-8,8),frandom(-8,8),frandom(6,12), 0, SXF_NOCHECKPOSITION)
      goto ExplodeAnim
   ExplodeAnim:
      TNT1 A 0 A_Quake(4, 20, 0, 640)
      TNT1 A 0 A_SetScale(radius / 30)
      TNT1 A 0 A_SetTranslucent(1, 1)
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_SmallCannonBall : Lith_CannonBall
{
   Radius 30
   Scale 2
   Damage (60)
   
   states
   {
   Splodey:
      TNT1 AAA 0 A_Explode
      TNT1 AAAAAAAAA 0 A_SpawnItemEx("Lith_CannonExplosionParticle", 0,0,8, frandom(-8,8),frandom(-8,8),frandom(6,12), 0, SXF_NOCHECKPOSITION)
      goto ExplodeAnim
   }
}

// EOF

