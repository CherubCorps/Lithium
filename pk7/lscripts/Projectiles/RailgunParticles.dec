actor Lith_OmegaRailFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "Lith_OmegaRail"
   Height 1
   Radius 1
   
   +NOBLOCKMAP
   +NOGRAVITY
}

actor Lith_OmegaRailParticle1
{
   RenderStyle "Add"
   Alpha 0.8
   Translation "Lith_OmegaRailParticle"
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PLSS A 10
      PLSS A 1 A_FadeOut(0.01)
      wait
   }
}

actor Lith_OmegaRailParticle2 : Lith_OmegaRailFXBase
{
   Alpha 0.5
   XScale 0.3
   YScale 0.5
   
   states
   {
   Spawn:
      BFE1 C 10 bright A_FadeOut(0.1)
      loop
   }
}

actor Lith_OmegaRailBeam : Lith_OmegaRailFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "Lith_OmegaRail"
   XScale 0.8
   YScale 0.7
   Species "Lith_Player"
   DamageType "Lith_Energy"
   
   +DONTHARMSPECIES
   +EXTREMEDEATH
   +FORCERADIUSDMG
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_OmegaRailParticle1", frandom(-57, 57),frandom(-57, 57),frandom(-57, 57))
      BFE1 C 25 bright A_Explode(24, 256, 0, false, 256)
      BFE1 DEF 4 bright
      stop
   }
}

actor Lith_OmegaRailExplosion : Lith_OmegaRailFXBase
{
   Scale 2
   
   +EXTREMEDEATH
   +FORCERADIUSDMG
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/rail/impact")
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_OmegaRailParticle2", frandom(-32, 32),frandom(-32, 32),frandom(-32, 32), frandom(-2, 2),frandom(-2, 2),frandom(-2, 2))
      BFE1 C 15 bright A_Explode(128, 512, 0, false, 512)
      BFE1 DEF 3 bright
      stop
   }
}

actor Lith_OmegaRailHax : FastProjectile
{
   Speed 32000
   Damage 300
   Height 1
   Radius 1
   
   +NOGRAVITY
   +NOBLOCKMAP
   +THRUACTORS
   +EXTREMEDEATH
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 1
      loop
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_OmegaRailExplosion", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
      stop
   }
}

// EOF
