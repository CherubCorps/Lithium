#include "lscripts/Weapons/RifleBullet.dec"
#include "lscripts/Weapons/RifleGrenade.dec"

actor Lith_RifleShotsFired : Inventory
{
   Inventory.MaxAmount 40
}

actor Lith_RifleBurstIter : Inventory
{
   Inventory.MaxAmount 5
}

actor Lith_CombatRifle : Lith_Weapon replaces Chaingun
{
   Tag "$LITH_TXT_INFO_SHORT_Rifle"
   Weapon.UpSound "weapons/rifle/draw"
   Weapon.SlotNumber 4
   
   states
   {
   Ready:
      CRIF A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      wait
   Deselect:
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
   DeselectLoop:
      CRIF AAA 0 A_Lower
      CRIF A 1
      loop
   Select:
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
   SelectLoop:
      CRIF AAA 0 A_Raise
      CRIF A 1
      loop
   Fire:
      CRIF A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_rifle_firemode) == rifle_firemode_burst,   "FireBurst")
      CRIF A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_rifle_firemode) == rifle_firemode_grenade, "FireGrenade")
      
      TNT1 A 0 A_JumpIfInventory("Lith_RifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_RifleShotsFired", 1)
      
      CRIF A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777), 3)
      CRIF A 0 A_PlaySound("weapons/rifle/fire", CHAN_WEAPON)
      CRIF A 0 A_Jump(256, 2)
      CRIF A 0 A_PlaySound("marathon/rifle/fire", CHAN_WEAPON)
      
      CRIF A 0 A_Jump(128, 4)
      CRIF A 0 A_AlertMonsters
      CRIF A 0 A_FireCustomMissile("Lith_RifleBullet",         frandom(-5.77, 5.77), true, 0, 0, 0, frandom(-2, 2))
      CRIF A 0 A_Jump(256, 2)
      CRIF A 0 A_FireCustomMissile("Lith_RifleBulletPainless", frandom(-5.44, 5.44), true, 0, 0, 0, frandom(-2, 2))
      CRIF A 0 A_FireCustomMissile("Lith_RifleBulletPainless", frandom(-5.44, 5.44), true, 0, 0, 0, frandom(-2, 2))
      CRIF B 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF C 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF A 2 A_WeaponReady(LITH_BOBONLY)
      CRIF AAAAAA 1 A_ReFire
      goto Ready
   FireBurst:
      TNT1 A 0 A_JumpIfInventory("Lith_RifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_RifleShotsFired", 1)
      
      CRIF A 0 A_PlaySound("weapons/rifle/firesnipe", CHAN_WEAPON)
      CRIF A 0 A_AlertMonsters
      CRIF A 0 A_FireCustomMissile("Lith_RifleBullet", frandom(-0.5, 0.5), true, 0, 0, 0, frandom(-0.5, 0.5))
      CRIF A 0 A_FireCustomMissile("Lith_RifleBullet", frandom(-0.5, 0.5), true, 0, 0, 0, frandom(-0.5, 0.5))
      CRIF B 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF C 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF A 3 A_WeaponReady(LITH_BOBONLY)
      CRIF A 0 A_GiveInventory("Lith_RifleBurstIter", 1)
      CRIF A 0 A_JumpIfInventory("Lith_RifleBurstIter", 5, 1)
      loop
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
      CRIF A 6 A_WeaponReady(LITH_BOBONLY)
      CRIF AAAAAA 1 A_Refire
      goto Ready
   FireGrenade:
      CRIF A 3 A_WeaponReady(LITH_BOBONLY)
      
      CRIF A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777), 3)
      CRIF A 0 A_PlaySound("weapons/rifle/grenade", CHAN_WEAPON)
      CRIF A 0 A_Jump(256, 2)
      CRIF A 0 A_PlaySound("marathon/rifle/grenade", CHAN_WEAPON)
      
      CRIF A 0 A_AlertMonsters
      CRIF G 2 bright A_FireCustomMissile("Lith_RifleGrenade")
      CRIF H 2 bright
      CRIF I 2 bright
      CRIF A 5
      // oh jesus
      
      CRIF A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777), 3)
      CRIF A 0 A_PlaySound("weapons/rifle/rldmount", CHAN_6)
      CRIF A 0 A_Jump(256, 2)
      CRIF A 0 A_PlaySound("marathon/rifle/reload", CHAN_6)
      
      CRIF A 1 offset(1, 32)
      CRIF A 1 offset(3, 32)
      CRIF A 1 offset(4, 32)
      CRIF A 1 offset(8, 38)
      CRIF A 1 offset(12, 45)
      CRIF A 1 offset(16, 53)
      CRIF A 1 offset(19, 60)
      CRIF A 1 offset(21, 64)
      CRIF A 1 offset(23, 67)
      CRIF A 1 offset(22, 69)
      CRIF A 1 offset(19, 70)
      CRIF A 7 offset(18, 72)
      CRIF A 1 offset(17, 69)
      CRIF A 1 offset(19, 67)
      CRIF A 1 offset(20, 66)
      CRIF A 1 offset(21, 62)
      CRIF A 1 offset(20, 60)
      CRIF A 1 offset(21, 63)
      CRIF A 1 offset(23, 66)
      CRIF A 1 offset(24, 68)
      CRIF A 1 offset(25, 63)
      CRIF A 1 offset(24, 59)
      CRIF A 1 offset(22, 57)
      CRIF A 1 offset(20, 54)
      CRIF A 1 offset(16, 49)
      CRIF A 1 offset(12, 44)
      CRIF A 1 offset(10, 42)
      CRIF A 1 offset(8, 40)
      CRIF A 1 offset(5, 38)
      CRIF A 1 offset(2, 35)
      CRIF A 5 offset(0, 32) A_WeaponReady(LITH_BOBONLY)
      goto Ready
   AltFire:
      CRIF A 2 A_WeaponReady(LITH_BOBONLY)
      CRIF A 0 ACS_NamedExecuteAlways("Lith_SwitchRifleFiremode", 0)
      CRIF DEF 1 A_WeaponReady(LITH_BOBONLY)
      CRIF A 7 A_WeaponReady(WRF_NOSECONDARY)
      goto Ready
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_RifleShotsFired", 1, 1)
      goto Ready
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
      CRIF A 1
      CRIF A 1 offset(-2, 34)
      CRIF A 1 offset(-4, 37)
      CRIF A 1 offset(-9, 43) A_PlaySound("weapons/rifle/reloadout", CHAN_6)
      CRIF A 1 offset(-12, 47)
      CRIF A 1 offset(-14, 52)
      CRIF A 2 offset(-12, 55)
      CRIF A 3 offset(-11, 57)
      CRIF A 8 offset(-10, 60)
      CRIF A 8 offset(-11, 60)
      CRIF A 0 A_TakeInventory("Lith_RifleShotsFired", 999)
      CRIF A 2 offset(-13, 59) A_PlaySound("weapons/rifle/reloadin", CHAN_7)
      CRIF A 2 offset(-11, 49)
      CRIF A 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      CRIF A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB)
      CRIF A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB)
      CRIF A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB)
      CRIF A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB)
      CRIF A 4 A_WeaponReady(WRF_NOBOB)
      goto Ready
   Spawn:
      RIFL A 0
      RIFL A 0 A_SetUserVar("user_pickupparm", weapon_rifle)
      goto SpawnLoop
   }
}

