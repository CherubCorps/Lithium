class Lith_RendHitSound : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_PlaySound("weapons/rend/hit", CHAN_5);
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Rend", true);
      stop;
   }
}

class Lith_RendHit : FastProjectile
{
   default
   {
      DamageFunction 50 * random(1, 2);
      Radius 8;
      Height 8;
      Speed  196;

      +FORCEPAIN
   }

   states
   {
   Spawn:
      TNT1 A 1;
      stop;
   XDeath:
      TNT1 A 0 A_GiveInventory("Lith_RendHitSound", 1, AAPTR_TARGET);
      stop;
   }
}

class Lith_RendHit2 : Lith_RendHit {default {DamageFunction 50 * random(2, 3);}}

class Lith_Rend : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Rend";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/rend/draw";

   +WEAPON.NOAUTOFIRE
}

states
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 4);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 10, 1);
   goto Ready;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 10);
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Rend", false, random(1, 4));
   TNT1 A 0 A_PlaySound("weapons/rend/slash", CHAN_7);
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_JumpIfInventory("PowerStrength", 0, 2);
   TNT1 A 0 A_FireProjectile("Lith_RendHit");
   goto FireDone;
   TNT1 A 0 A_FireProjectile("Lith_RendHit2");
FireDone:
   TNT1 A 5;
   TNT1 A 0 A_ReFire;
   TNT1 A 5 A_PlaySound("weapons/rend/end", CHAN_6);
   goto Ready;
}

// EOF
