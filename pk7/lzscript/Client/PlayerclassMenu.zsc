// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class ListMenuItemLith_PlayerDisplay : ListMenuItemPlayerDisplay {
   void init(ListMenuDescriptor menu, int x, int y, color c1, color c2) {
      super.init(menu, x, y, c1, c2);
   }

   override void draw(bool selected, ListMenuDescriptor desc) {
      if(mMode == 0 && updatePlayerClass()) {
         let pcl = getDefaultByType((class<PlayerPawn>)(mPlayerClass.type));
         let por = Lith_RenderProvider.getTextureForName(":Portraits:" .. pcl.soundclass);
         int porx, pory; [porx, pory] = TexMan.getSize(por);
         int scrw  = Screen.getWidth();
         int scrh  = Screen.getHeight();
         let scale = double(scrh) / pory;
         let sc240 = 240.0 / pory;
         let xy    = Screen.virtualToRealCoords((320-(porx*sc240),0), (scrw, scrh), (320, 240));
         int x     = int(xy.x);
         int dw    = int(porx*scale);
         int dh    = int(pory*scale);
         Screen.drawTexture(por, true, x, 0,
                            DTA_KEEPRATIO,true,
                            DTA_DESTWIDTH,dw,
                            DTA_DESTHEIGHT,dh);
         super.draw(selected, desc);
      }
      screen.drawTexture(Lith_RenderProvider.getTextureForName(":Menu:ClassBack"), true, 4, 48, DTA_Alpha, 0.9, DTA_Clean, true);
      screen.drawTexture(Lith_RenderProvider.getTextureForName(":Menu:ClassDescBack"), true, 100, 126, DTA_Alpha, 0.9, DTA_Clean, true);
   }
}

class Lith_PlayerclassMenu : ListMenu;

override void Init(Menu parent, ListMenuDescriptor desc) {
   Super.Init(parent, desc);
   for(int i = 0; i < mDesc.mItems.size(); ++i) {
      let sel = ListMenuItemTextItem(mDesc.mItems[i]);
      if(sel) {
         sel.mColor = Font.findFontColor(Lith_HERMES.localize(sel.mText .. "_Col"));
      }
   }
}

override void Drawer() {
   Super.Drawer();
   if(mDesc.mSelectedItem < 0) return;
   let sel = ListMenuItemTextItem(mDesc.mItems[mDesc.mSelectedItem]);
   if(sel) {
      let nsf = "_Nam";
      let psf = "_Pro";
      if(sel.mText == "$LITH_CLASS_MARINE" && !(__lith_fun & lfun_lane)) {
         nsf = "_DeN";
         psf = "_DPr";
      }
      let col = Lith_HERMES.localize(sel.mText .. "_Col");
      let nam = Lith_HERMES.localize(sel.mText .. nsf);
      let pro = Lith_HERMES.localize(sel.mText .. psf);
      let dsc = Lith_HERMES.localize(sel.mText .. "_Dsc");
      let txt = Lith_HERMES.localize(sel.mText);
      let cr  = Font.FindFontColor(col);
      Font bigf = menuDelegate.pickFont("bigupper");
      Font midf = menuDelegate.pickFont("lmidfont");
      Font smlf = menuDelegate.pickFont("smallfont");
      Font trmf = menuDelegate.pickFont("ltrmfont");
      Screen.DrawText(bigf, cr, 168, 75+59, txt, DTA_Clean, true);
      Screen.DrawText(midf, cr, 168, 75+73, nam, DTA_Clean, true);
      Screen.DrawText(smlf, cr, 317-smlf.stringWidth(pro), 75+83, pro, DTA_Clean, true);
      if(cr == Font.CR_BLACK) {
         Screen.DrawText(smlf, cr, 176, 75+81, "NOT IMPLEMENTED", DTA_Clean, true);
      }
      Screen.DrawText(trmf, cr, 104, 75+108, dsc, DTA_Clean, true);
   }
}

/* EOF */
