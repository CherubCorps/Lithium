actor Lith_StarShrapnel
{
   Damage (15)
   Speed 32
   Radius 2
   Height 2
   Scale 0.7
   Mass 0
   RenderStyle "Add"
   
   Projectile
   +NODAMAGETHRUST
   +SEEKERMISSILE
   +SCREENSEEKER
   
   states
   {
   Spawn:
      BLLT M 4 bright A_SeekerMissile(0, 2, SMF_LOOK)
      loop
   Death:
      MICX A 0 bright A_PlaySound("weapons/dark/impact", CHAN_BODY, 0.3)
      MICX A 0 bright A_SetScale(0.5)
      MICX ABCDEFG 1 bright
      stop
   }
}

actor Lith_StarWoosh1
{
   Damage (2)
   Radius 8
   Height 8
   Scale 0.7
   Speed 3
   RenderStyle "Add"
   
   Projectile
   +NODAMAGETHRUST
   
   states
   {
   Death:
   Spawn:
      TNT1 A 0
      BLLT L 0 bright A_JumpIf(scalex < 0.01, "ded")
      BLLT L 2 bright A_SetScale(scalex*frandom(0.9,1.0))
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarWoosh2 : Lith_StarWoosh1
{
   Speed 4
   
   states
   {
   Spawn:
      BLLT L 0 bright A_SetScale(scalex*frandom(0.95,1.0))
      BLLT L 1 bright A_FadeOut(0.05)
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarProjectile
{
   Damage (20)
   Speed 2
   Scale 0.7
   Radius 32
   Height 8
   Mass 500
   RenderStyle "Add"
   
   Projectile
   +RIPPER
   +FORCERADIUSDMG
   
   var int user_soundtimer;
   var int user_rushtimer;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/dark/wind", CHAN_7, 1.0, true)
   Spawn1:
      BLLT L 0 bright A_AlertMonsters
      BLLT L 0 bright A_SetUserVar(user_rushtimer, user_rushtimer + 1)
      BLLT L 0 bright A_JumpIf(user_rushtimer > 35*7, "Spawn3")
      BLLT L 0 bright A_SetUserVar(user_soundtimer, user_soundtimer - 1)
      BLLT L 0 bright A_JumpIf(user_soundtimer > 0, "Spawn2")
      BLLT L 0 bright A_SetUserVar(user_soundtimer, 3)
      BLLT L 0 bright A_PlaySound("weapons/dark/fire", CHAN_5)
   Spawn2:
      BLLT L 0 bright A_CustomMissile("Lith_StarWoosh1", 0, frandom(-2,2), angle-180, CMF_TRACKOWNER|CMF_ABSOLUTEANGLE)
      BLLT LLLLL 0 bright A_CustomMissile("Lith_StarShrapnel",
         CallACS("Lith_CircleSpread", 32*FIX, 32*FIX, false)/FIX,
         CallACS("Lith_CircleSpread", 32*FIX, 32*FIX, true)/FIX,
         0, CMF_TRACKOWNER|CMF_AIMDIRECTION)
      goto Spawn4
   Spawn3:
      BLLT L 0 A_ChangeVelocity(velx*1.1, vely*1.1, velz*1.1, CVF_REPLACE)
   Spawn4:
      BLLT L 0 bright A_CustomMissile("Lith_StarWoosh2", 0, 0, 0, CMF_TRACKOWNER|CMF_AIMDIRECTION)
      BLLT L 0 bright A_Quake(1, 2, 0, 512)
      BLLT L 0 bright A_Quake(3, 2, 0, 256)
      BLLT L 0 bright A_Quake(5, 2, 0, 64)
      BLLT L 1 bright
      goto Spawn1
   Death:
      SDXP A 0 A_Explode(128, 512, 0)
      SDXP A 0 A_StopSound(CHAN_7)
      SDXP A 0 A_PlaySound("weapons/shipgun/explode", CHAN_6)
      SDXP A 0 A_SetScale(1)
      SDXP ABCDEFGHIJKLMNOPQRS 1 bright
      SDXP T 1 bright A_FadeOut
      wait
   }
}

actor Lith_StarDestroyer : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_StarDestroyer"
   Weapon.UpSound "weapons/shipgun/draw"
   Weapon.SlotNumber 7
   Weapon.SlotPriority 1
   Weapon.BobStyle "InverseSmooth"
   Weapon.BobRangeX 2.0
   Weapon.BobSpeed 0.85
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_BFG9000Pickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      PISG A 1 A_WeaponReady
      loop
   
   Deselect:
      PISG AAA 0 A_Lower
      PISG A 1
      loop
   
   Select:
      PISG AA 0 A_Raise
      PISG A 1
      loop
   
   Fire:
      PISG A 0 A_JumpIfInventory("Lith_CannonAmmo", 5, 1)
      goto FireEmpty
      PISG A 0 A_TakeInventory("Lith_CannonAmmo", 5, TIF_NOTAKEINFINITE)
      PISG A 0 A_AlertMonsters
      PISG A 10 A_PlaySound("weapons/shipgun/prepare", CHAN_WEAPON)
      PISG A 1 bright
      PISG A 1 bright offset(-1, 33)
      PISG A 1 bright offset(-1, 34)
      PISG A 1 bright offset(-1, 36)
      PISG A 2 bright offset(1, 37)
      PISG A 3 bright offset(1, 38)
      PISG A 0 bright A_GunFlash("DummyFlash")
      PISG A 0 bright A_PlaySound("weapons/shipgun/fire", CHAN_5)
      PISG C 2 bright offset(0, 62) A_FireCustomMissile("Lith_StarProjectile")
      PISG C 1 bright offset(0, 57)
      PISG C 1 bright offset(0, 47)
      PISG C 1 bright offset(0, 44)
      PISG B 1 bright offset(0, 40)
      PISG B 1 bright offset(0, 37)
      PISG B 1 bright offset(0, 35)
      PISG B 1 bright offset(0, 32)
      PISG AAAAAAAAAAAAAAAAAAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireEmpty:
      PISG A 2 A_PlaySound("weapons/shipgun/empty", 7)
      PISG A 1 offset(0, 28)
      PISG A 1 offset(0, 29)
      PISG A 1 offset(0, 32)
      PISG A 1 offset(0, 35)
      PISG A 1 offset(0, 40)
      PISG A 1 offset(0, 42)
      PISG A 1 offset(0, 43)
      PISG A 1 offset(0, 44)
      PISG A 1 offset(0, 40)
      PISG A 1 offset(0, 37)
      PISG A 1 offset(0, 34)
      PISG A 1 offset(0, 32)
      PISG A 1 A_ReFire
      goto Ready
   }
}

// EOF

