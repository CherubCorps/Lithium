actor Lith_CPlasmaRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_CPlasmaRifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   Weapon.SlotPriority 1
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaRiflePickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      PLSG A 1 A_WeaponReady
      loop
   
   Deselect:
      PLSG AAA 0 A_Lower
      PLSG A 1
      loop
   
   Select:
      PLSG AAA 0 A_Raise
      PLSG A 1
      loop
   
   Fire:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSG A 0 A_GunFlash("DummyFlash")
      PLSG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      PLSG AAAA 0 A_FireCustomMissile("Lith_PlasmaBolt",
                                      CallACS("Lith_CircleSpread", 5 * IFIX, 3 * IFIX, false) / FIX,
                                      0, 0, -9, 0,
                                      CallACS("Lith_CircleSpread", 0, 0, true) / FIX)
      PLSJ A 1 offset(0, 37) bright
      PLSJ C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      goto Ready
   
   FireEmpty:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 A_PlaySound("weapons/plasma/empty", 7)
      PLSG A 1 offset(0, 37)
      PLSG A 1 offset(0, 36)
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG A 1 A_ReFire
      goto Ready
   
   AltFire:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 90, TIF_NOTAKEINFINITE)
      PLSK A 0 A_GunFlash("DummyFlash")
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", 0, 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(-2.7, -1.9), 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(2.7, 1.9), 0, 0, -9)
      PLSK A 1 offset(0, 37) bright A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON)
      PLSK C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      Goto Ready
   }
}
