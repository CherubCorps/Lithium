// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_ExplosionAfterSmoke : Lith_BulletAfterSmoke {
   default {
      Alpha 0.1;
   }
}

class Lith_Explosive : Actor;

override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("explosion");
   markSound("marathon/explode");
}

bool checkMarathon() {
   return Lith_UTIL.pData(_pdt_upgrade, UPGR_Seven7s);
}

void lith_a_explodeSound(int chan = lch_body2) {
   double p = fRandom(0.9, 1.1);
   if(checkMarathon()) a_startSound("marathon/explode", chan, pitch: p);
   else                a_startSound("explosion",        chan, pitch: p);
}

state lith_a_jumpExplosion() {
   if(checkMarathon()) return resolveState("ExplodeMarathon");
   else                return resolveState("Explode");
}

state lith_a_doExplosion(float fxAmount = 1.0) {
   int numEmbers    = int(fRandom(10.0, 20.0) * fxAmount);
   int numParticles = int(50.0 * fxAmount);

   if(fxAmount < 1.0) {
      fxAmount *= 2.0;
   }

   for(int i = 0; i < numEmbers; i++) {
      a_spawnItemEx("Lith_HotEmber",
                    fRandom(-4, 4) * fxAmount,
                    fRandom(-4, 4) * fxAmount,
                    fRandom(-4, 4) * fxAmount,

                    fRandom(-8, 8)  * fxAmount,
                    fRandom(-8, 8)  * fxAmount,
                    fRandom(-4, 14) * fxAmount,

                    0, SXF_NOCHECKPOSITION);
   }

   for(int i = 0; i < numParticles; i++) {
      a_spawnParticle("FFFF00", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
                      fRandom(-8,8) * fxAmount,
                      fRandom(-8,8) * fxAmount,
                      fRandom(-2,2) * fxAmount,

                      fRandom( 0,1)  * fxAmount,
                      fRandom(-5,5)  * fxAmount,
                      fRandom( 5,10) * fxAmount,

                      0, 0, fRandom(-0.5, -3),
                      1.0, -1);

      a_spawnParticle("444444", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
                      fRandom(-8,8) * fxAmount,
                      fRandom(-8,8) * fxAmount,
                      fRandom(-2,2) * fxAmount,

                      fRandom( 0,1)  * fxAmount,
                      fRandom(-5,5)  * fxAmount,
                      fRandom( 5,10) * fxAmount,

                      0, 0, fRandom(-0.5, -3),
                      1.0, -1);
   }

   a_spawnItemEx("Lith_ExplosionAfterSmoke",
                 fRandom(-1,1) * fxAmount,
                 fRandom(-1,1) * fxAmount,
                 fRandom(-2,2) * fxAmount,

                 fRandom(-0.5,0.5) * fxAmount,
                 fRandom(-0.5,0.5) * fxAmount,
                 fRandom(-0.2,0.2) * fxAmount);

   bNoGravity = true;
   return lith_a_jumpExplosion();
}

void lith_a_scaleExplosion() {
   scale *= 1.03;
}

states {
Explode:
   MEXP AB 1 bright light("Lith_Explode1") lith_a_scaleExplosion;
   MEXP CCDD 1 bright light("Lith_Explode2") lith_a_scaleExplosion;
   MEXP EEEFFF 1 bright light("Lith_Explode3") lith_a_scaleExplosion;
   stop;
ExplodeMarathon:
   MA7G BC 4 bright light("Lith_Explode1");
   MA7G DE 4 bright light("Lith_Explode2");
   MA7G FG 4 bright light("Lith_Explode3");
   stop;
}

/* EOF */
