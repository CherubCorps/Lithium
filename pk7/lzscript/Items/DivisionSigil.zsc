/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ItemPickup : Lith_Pickup {
   default {
      Inventory.PickupSound "player/pickup/evil";
      Lith_Pickup.Glow "Lith_ItemGlow";
   }
}

class Lith_DivisionSigilSprite : Lith_AnimItemSprite {
   default {
      Lith_AnimItemSprite.Phases 23;
      +BRIGHT
   }

   states(Actor) {
   SetSpr:
      DIVI ABCDEFGHIJKLMNOPQRSTUVW 0 Lith_A_JumpPhase;
      stop;
   }
}

class Lith_DivisionSigil : Lith_ItemPickup {
   default {
      Inventory.Icon "DIVSIGI1";
      Inventory.MaxAmount 1;
      Tag "Division Sigil";

      +INVENTORY.INVBAR
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      A_SpawnItemEx("Lith_DivisionSigilSprite", 0,0,0, 0,0,0, 0, Lith_ISF);
   }

   override bool canPickup(Actor mo) {
      if(super.canPickup(mo)) {
         return !ACS_ExecuteWithResult(lsc_pdata, pdata_hassigil);
      } else {
         return false;
      }
   }

   override bool tryPickup(in out Actor mo) {
      if(super.tryPickup(mo)) {
         mo.A_StartSound("player/getsigil", lch_body3, attenuation: ATTN_NONE);
         mo.acs_scriptCall("Lith_GetSigil");
         return true;
      } else {
         return false;
      }
   }
}

class Lith_ClawOfImp : Lith_ItemPickup {
   default {
      Tag "Claw of Imp";
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      A_SpawnItemEx("Lith_ItemSprite", 0,0,0, 0,0,0, 0, Lith_ISF);
   }
}

/* EOF */
