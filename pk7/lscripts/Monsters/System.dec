actor Lith_MonsterID : Inventory
{
   Inventory.MaxAmount 0x7FFFFFFF
   +INVENTORY.UNDROPPABLE
}

actor Lith_MonsterInvalid : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_MonsterBarrier
{
   RenderStyle "Add"
   XScale 0.3
   YScale 0.6
   VSpeed 2
   
   +NOTIMEFREEZE
   +NOINTERACTION
   
   states
   {
   Spawn:
      LBAR A 1
   SpawnLoop:
      LBAR A 1 A_FadeOut
      loop
   }
}

actor Lith_MonsterBarrierLook : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_BeginAngles", x, y)
      TNT1 A 0 A_RadiusGive("Lith_GetAngles", 256, RGF_PLAYERS)
      stop
   }
}

actor Lith_MonsterSoulEffect
{
   RenderStyle "Add"
   Scale 0.25
   
   +NOTIMEFREEZE
   +NOINTERACTION
   +BRIGHT
   
   states
   {
   Spawn:
      BLLT N 0 A_SetScale(scalex * 0.9)
      BLLT N 1 A_FadeOut
      loop
   }
}

actor Lith_MonsterSoul
{
   RenderStyle "Add"
   Scale 0.25
   Damage 7
   Radius 4
   Height 4
   Speed 7
   
   Projectile
   +FLOATBOB
   +SEEKERMISSILE
   +MTHRUSPECIES
   +THRUSPECIES
   +BRIGHT
   
   states
   {
   Spawn:
      BLLT N 0
      BLLT N 0 A_PlaySound("ghost/spawn", CHAN_5, 1, false, 0.5)
      BLLT N 0 ThrustThingZ(0, 4, 0, 0)
   SpawnLoop:
      BLLT N 0 A_SpawnItemEx("Lith_MonsterSoulEffect")
      BLLT N 0 A_SeekerMissile(0, 360, SMF_LOOK|SMF_PRECISE, 256, 1)
      BLLT N 1 A_FadeOut
      loop
   Death:
      BLLT N 0 A_PlaySound("ghost/hit", CHAN_5, 1, false, 0.5)
      TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("Lith_MonsterSoulEffect", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2))
      stop
   }
}

actor Lith_RunMonsterInfo : CustomInventory
{
   +NOTIMEFREEZE
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfInventory("Lith_MonsterID", 1, "Nope")
      TNT1 A 0 A_JumpIfInventory("Lith_MonsterInvalid", 1, "Nope")
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MonsterInfo")
      stop
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_MonsterInfoEmitter
{
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_RadiusGive("Lith_RunMonsterInfo", 16384, RGF_MONSTERS|RGF_NOSIGHT|RGF_CUBE)
      stop
   }
}

// EOF

