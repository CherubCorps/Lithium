actor Lith_ShockRifleShotsFired : Inventory
{
   Inventory.MaxAmount 7
}

actor Lith_ShockFX
{
   RenderStyle "AddStencil"
   StencilColor "LightBlue"
   Alpha 0.5

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      "####" "#" 1
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_ShockEnemyGiver : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   +FORCEPAIN

   states
   {
   Pickup:
      TNT1 A 0 A_DamageSelf(5, "Lith_Energy", "None", "None", AAPTR_TARGET)
      TNT1 A 0 A_SpawnItemEx("Lith_ShockFX", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME)
      stop
   }
}

actor Lith_ShockEffect
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.6

   +NOINTERACTION
   +RANDOMIZE
   +BRIGHT

   states
   {
   Spawn:
      ZAP1 FEDCBA 1
      stop
   }
}

actor Lith_SuperShockEffect
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.6
   Translation "160:167=192:199"

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      SDXP IJKLMNOPQRS 1
      SDXP S 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_ShockHitCount : Inventory
{
   Inventory.MaxAmount 6
}

actor Lith_ShockPuff : Lith_BulletPuff
{
   +HITTRACER
   +FORCEPAIN
   +PUFFONACTORS
   +PUFFGETSOWNER

   states
   {
   Crash:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/erifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      goto Super::Spawn
   Melee:
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/erifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_ShockRif_B, true), "Binding")
      TNT1 A 0 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      stop
   Binding:
      TNT1 A 0 A_GiveInventory("Lith_ShockHitCount", 1, AAPTR_TRACER)
      TNT1 A 0 A_JumpIfInventory("Lith_ShockHitCount", 0, "Explode", AAPTR_TRACER)
      TNT1 AAA 3 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      stop
   Explode:
      TNT1 A 0 A_TakeInventory("Lith_ShockHitCount", 999, 0, AAPTR_TRACER)
      TNT1 A 0 A_SpawnItemEx("Lith_SuperShockEffect")
      TNT1 A 0 A_PlaySound("weapons/erifle/shock")
      TNT1 A 0 A_Explode(64, 512, 0)
      TNT1 AAAAA 3 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      stop
   }
}

actor Lith_ShockRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_ShockRifle"
   Weapon.UpSound "weapons/erifle/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_ShotgunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CSRG A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      CSRG AAA 0 A_Lower
      CSRG A 1
      loop
   
   Select:
      CSRG AAA 0 A_Raise
      CSRG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_ShockRifleShotsFired", 1)
      CSRG A 1
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_PlaySound("weapons/erifle/fire", CHAN_WEAPON)
      TNT1 A 0 A_FireBullets(1, 0, -1, 37*frandom(1,1.5), "Lith_ShockPuff", FBF_NORANDOM)
      CSRG B 1 bright offset(0, 40) A_Light(1)
      CSRG C 1 bright offset(0, 35) A_Light(0)
      CSRG A 1 offset(0, 34)
      CSRG A 1 offset(0, 33)
      goto Ready

   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 1, 1)
      goto Ready
      CSRG D 1 offset(1, 32)
      CSRG E 1 offset(3, 32)
      CSRG F 1 offset(4, 32)
      CSRG F 1 offset(8, 38)
      CSRG F 1 offset(12, 45)
      CSRG F 1 offset(16, 53)
      CSRG F 1 offset(19, 60)
      CSRG F 0 A_PlaySound("weapons/erifle/reloadout", CHAN_6)
      CSRG F 1 offset(21, 64)
      CSRG F 1 offset(23, 67)
      CSRG F 1 offset(22, 69)
      CSRG F 1 offset(19, 70)
      CSRG F 7 offset(18, 72)
      CSRG F 1 offset(17, 69)
      CSRG F 1 offset(19, 67)
      CSRG F 1 offset(20, 66)
      CSRG F 1 offset(21, 62)
      CSRG E 1 offset(20, 60)
      CSRG E 0 A_TakeInventory("Lith_ShockRifleShotsFired", 999)
      CSRG D 1 offset(21, 63) A_PlaySound("weapons/erifle/reloadin", CHAN_7)
      CSRG A 1 offset(23, 66)
      CSRG A 1 offset(24, 68)
      CSRG A 1 offset(25, 63)
      CSRG A 1 offset(24, 59)
      CSRG A 1 offset(22, 57)
      CSRG A 1 offset(20, 54)
      CSRG A 1 offset(16, 49)
      CSRG A 1 offset(12, 44)
      CSRG A 1 offset(10, 42)
      CSRG A 1 offset(8, 40)
      CSRG A 1 offset(5, 38)
      CSRG A 1 offset(2, 35)
      CSRG AAAAAAA 1 offset(1, 32) A_ReFire
      goto Ready
   }
}

// EOF
