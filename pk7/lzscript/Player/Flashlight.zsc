/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson & InsanityBringer. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_SpotLight : DynamicLight {
   default {
      DynamicLight.Type "Point";
      +DYNAMICLIGHT.ATTENUATE
      +DYNAMICLIGHT.SPOT
   }
}

class Lith_Flashlight : Thinker;

Lith_Player m_p;

Lith_SpotLight[2] m_light;

static Lith_Flashlight create(Lith_Player pl) {
   let light = new("Lith_Flashlight");

   light.m_p = pl;
   light.m_light[0] = Lith_SpotLight(Actor.Spawn("Lith_SpotLight", pl.pos));
   light.m_light[1] = Lith_SpotLight(Actor.Spawn("Lith_SpotLight", pl.pos));

   return light;
}

override void tick() {
   super.tick();

   double intn = m_p.acs_scriptCall("Lith_GetFlashlightIntensity") / FIX;

   int r = clamp(CVar.getCVar('lith_light_r', m_p.player).getInt(), 0, 255);
   int g = clamp(CVar.getCVar('lith_light_g', m_p.player).getInt(), 0, 255);
   int b = clamp(CVar.getCVar('lith_light_b', m_p.player).getInt(), 0, 255);

   int ri = int(CVar.getCVar('lith_light_radius', m_p.player).getInt() * intn);
   int ro = ri / 2;

   m_light[0].args[DynamicLight.LIGHT_RED]       = r;
   m_light[0].args[DynamicLight.LIGHT_GREEN]     = g;
   m_light[0].args[DynamicLight.LIGHT_BLUE]      = b;
   m_light[0].args[DynamicLight.LIGHT_INTENSITY] = ri;
   m_light[0].spotInnerAngle = 0;
   m_light[0].spotOuterAngle = 20;

   m_light[1].args[DynamicLight.LIGHT_RED]       = int(r * 0.77);
   m_light[1].args[DynamicLight.LIGHT_GREEN]     = int(g * 0.77);
   m_light[1].args[DynamicLight.LIGHT_BLUE]      = int(b * 0.77);
   m_light[1].args[DynamicLight.LIGHT_INTENSITY] = ro;
   m_light[1].spotInnerAngle = 15;
   m_light[1].spotOuterAngle = 45;

   for(int i = 0; i < 2; i++) {
      m_light[i].A_SetAngle(m_p.angle, true);
      m_light[i].A_SetPitch(m_p.pitch, true);
      m_light[i].setOrigin((m_p.pos.xy, m_p.player.viewz), true);
   }
}

/* EOF */
