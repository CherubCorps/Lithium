class Lith_SpiderMastermindBulletTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.6;
      Alpha 0.8;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      PUFF A 1 A_FadeOut(0.05);
      wait;
   }
}

class Lith_SpiderMastermindBullet : FastProjectile
{
   default
   {
      MissileType "Lith_SpiderMastermindBulletTrail";
      RenderStyle "Translucent";
      Alpha 0.9;
      Radius 1;
      Height 2;
      Damage 0;
      Speed 256;

      +BLOODSPLATTER
      +MTHRUSPECIES
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   }
}

class Lith_XH_SpiderMastermind : Lith_SpiderMastermind
{
   default
   {
      +MISSILEEVENMORE
   }
}

class Lith_SpiderMastermind : Lith_Monster replaces SpiderMastermind;

default
{
   Lith_Monster.Score Score_SpiderDemon;
   Health 4000;
   Speed 16;
   Mass 9000;
   Height 100;
   Radius 128;
   MinMissileChance 120;
   SeeSound "spider/sight";
   PainSound "spider/pain";
   DeathSound "spider/death";
   ActiveSound "spider/active";
   Obituary "$OB_SPIDER";
   Tag "$LITH_INFO_SHORT_SpiderMastermind";

   +BOSS
   +MISSILEMORE
   +FLOORCLIP
   +NORADIUSDMG
   +DONTMORPH
   +BOSSDEATH
}

void Lith_A_Metal()
{
   A_PlaySound("spider/walk", CHAN_BODY, 1, false, ATTN_IDLE);
   A_StopSound(CHAN_WEAPON);
   A_Chase(flags: CHF_DONTMOVE);
   A_Stop();
}

state Lith_A_SpiderMastermindShoot(int spreadx, int spready)
{
   float x = frandom(-5 * spreadx, 5 * spready);
   float y = frandom(-3 * spreadx, 3 * spready);

   // replace with A_SpawnProjectile when it's fixed
   // edit: it wasn't fixed lol
   A_SpawnProjectile("Lith_SpiderMastermindBullet", 32, 0, x, CMF_OFFSETPITCH|CMF_BADPITCH, y);
   A_CustomBulletAttack(x, y, 1, 3 * random(1, 5), flags: CBAF_NORANDOM|CBAF_EXPLICITANGLE);

   if(random(0, 500) == 0)
   {
      vel.z += 10;
      Thrust(16, angle + randompick(90, -90));
      return ResolveState("Jump");
   }
   else
      return null;
}

void Lith_A_SpiderMastermindExplode()
{
   A_PlaySound("explosion", CHAN_BODY, 1, false, 0.1);

   for(int i = 0; i < 10; i++)
      A_SpawnItemEx("Lith_CannonExplosionParticle", 0,0,8, frandom(-8,8),frandom(-8,8),frandom(8,14));
}

states
{
Spawn:
   SPID AB 10 A_Look;
   loop;

See:
   SPID A 7 Lith_A_Metal;
   SPID BBCC 3 A_Chase;
   SPID D 7 Lith_A_Metal;
   SPID EEFF 3 A_Chase;
   loop;

Missile:
   SPID A 2 bright;
   SPID A 0 A_PlaySound("enemies/spiderdemon/ready", CHAN_5|CHAN_NOPAUSE, 1, false, 0.3);
   SPID A 0 A_GiveToTarget("Lith_AdrenalineToken", 1);
   SPID A 20 bright A_FaceTarget;
   SPID A 0 A_PlaySound("enemies/spiderdemon/fire", CHAN_WEAPON, 1, true, 0.4);
MissileLoop:
   SPID G 0 bright Lith_A_SpiderMastermindShoot(3, 3);
   SPID G 1 bright;
   SPID H 0 bright Lith_A_SpiderMastermindShoot(1, 1);
   SPID H 1 bright A_MonsterRefire(40, "See");
   loop;
Jump:
   SPID I 10 A_StopSound(CHAN_WEAPON);
   goto Missile;

Death:
   SPID J -1;
Crash:
   SPID J 0 A_StopSound(CHAN_WEAPON);
   SPID J 20 Lith_A_Die;
   SPID KLMNOPQR 10 Lith_A_SpiderMastermindExplode;
   SPID S 30;
   SPID S -1 A_BossDeath;
   stop;
}

// EOF
