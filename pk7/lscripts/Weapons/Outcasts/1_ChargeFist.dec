actor Lith_FistCharge : Inventory
{
   Inventory.MaxAmount 0x7FFFFFFF // Yes.
}

actor Lith_FistHit : Powerup // :^)
{
   Powerup.Duration 5
}

actor Lith_ChargeFistQuakerOats
{
   +NOINTERACTION
   +NOGRAVITY
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 2 A_Quake(5, 2, 0, 128, "")
      TNT1 A 2 A_Quake(2, 2, 0, 128, "")
      TNT1 A 2 A_Quake(1, 2, 0, 128, "")
      stop
   }
}

actor Lith_ChargeFistPuff
{
   RenderStyle "Translucent"
   Alpha 0.6
   
   +NOINTERACTION
   +HITTRACER
   +PUFFONACTORS
   +PUFFGETSOWNER
   
   states
   {
   Crash:
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_GiveInventory("Lith_FistHit", 1, AAPTR_TARGET)
      goto Animation
   XDeath:
      TNT1 A 0
      TNT1 A 0 A_GiveInventory("Lith_FistHit", 1, AAPTR_TARGET)
      TNT1 A 0 A_PlaySound("weapons/cfist/hit", CHAN_WEAPON, 1, 0, 1.3) // much squish loud yes
      TNT1 A 0 A_PlaySound("weapons/cfist/hit", CHAN_ITEM, 1, 0, 1.3)
      goto Animation
   Animation:
      TNT1 A 0 A_SpawnItemEx("Lith_ChargeFistQuakerOats", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
      TNT1 A 0 A_PlaySound("weapons/cfist/hitwall", CHAN_BODY, 1, 0, 0.9)
      FHFX STUVW 3
      stop
   }
}

actor Lith_ChargeFist : Lith_Weapon
{
   Tag "$LITH_TXT_INFO_SHORT_ChargeFist"
   Weapon.Kickback 100
   Weapon.SlotNumber 1
   Weapon.SlotPriority 1
   DamageType "Lith_Melee"
   
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
   +WEAPON.CHEATNOTWEAPON
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_ChainsawPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      TNT1 A 0 A_TakeInventory("Lith_FistCharge", 0x7FFFFFFF)
      TNT1 A 1 Lith_A_Ready
      loop
   
   Deselect:
      TNT1 A 0 A_StopSound(CHAN_5)
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 9 A_GiveInventory("Lith_FistCharge", 35)
      TNT1 A 0 A_ReFire("Fire1")
      goto Release1
   Fire1:
      TNT1 A 0 A_GiveInventory("Lith_FistCharge", 35)
      CFIS A 0 A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_marine,    "Fire2")
      CCFS A 0 //A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_cybermage, "Fire2")
   Fire2:
      "####" A 1 A_PlaySound("weapons/cfist/ready", CHAN_6)
      "####" B 1 A_PlaySound("weapons/cfist/charge", CHAN_5, 1, true)
      "####" C 2
      "####" D 2
      "####" E 3
      "####" F 4
      "####" G 7
      goto WaitForRelease
   WaitForRelease:
      "####" G 1 A_GiveInventory("Lith_FistCharge", 2)
      "####" G 1 A_ReFire("WaitForRelease")
   Release:
      TNT1 A 0 A_PlaySound("weapons/cfist/chargeend", CHAN_5)
      TNT1 A 0 A_GunFlash("SekritHax", GFF_NOEXTCHANGE) // OH NO NOT yholehax
   Release1:
      CFI2 A 0 A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_marine,    "Release2")
      CCF2 A 0 //A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_cybermage, "Release2")
      // GOTTA RELEASE FAST
   Release2:
      "####" A 0 A_ChangeVelocity(cos(pitch) * 3, 0, sin(pitch) * -3, 1)
      "####" A 0 A_AlertMonsters(128)
      "####" A 1 A_PlaySound("weapons/cfist/fire", CHAN_6)
      "####" B 1
      "####" A 0 A_CustomPunch(CallACS("Lith_ChargeFistDamage"), true, 0, "Lith_ChargeFistPuff", 128)
      "####" A 0 A_JumpIfInventory("Lith_FistHit", 1, "HitRecoil")
      "####" C 2 // Missed, so you quickly bring your fist back
      "####" D 5
      "####" H 2
      "####" I 1
      "####" J 1
      "####" K 1
      TNT1 A 1
      TNT1 AAAAAAAAAA 1 A_ReFire
      goto Ready
   HitRecoil:
      "####" A 0 A_AlertMonsters
      "####" A 0 A_ChangeVelocity(cos(pitch) * -2, 0, sin(pitch) * 2, 1)
      "####" C 1
      "####" DEFG 1 // Shaking from the force of the impact
      "####" DEFG 2
      "####" H 2
      "####" I 2
      "####" J 2
      "####" K 2
      TNT1 A 1
      TNT1 AAAAAAAAAA 1 A_ReFire
      goto Ready
	  
   SekritHax: // the crimera has struck again
      CFIS A 0 A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_marine,    "SekritHax2")
      CCFS A 0 //A_JumpIf(CallACS("LPData", pdata_pclass) == pcl_cybermage, "SekritHax2")
   SekritHax2:
      "####" HI 1
      stop
   }
}
   
// EOF
