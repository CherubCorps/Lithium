// Effects taken from Realm667.
// I think Tormenter made these originally, sorry if that's incorrect.

actor Lith_RainDrop
{
   Height 2
   Radius 2
   XScale 0.5
   YScale 2.0
   RenderStyle "Add"
   Alpha 0.25
   
   +NOTRIGGER
   +NOBLOCKMAP
   +DROPOFF
   +NOTELEPORT
   +NOTIMEFREEZE
   +NOGRAVITY
   +DONTSPLASH
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_JumpIf(!CallACS("Lith_RainDropSpawn"), "Nope")
      TNT1 A 0 A_ChangeVelocity(0, 0, frandom(-12, -16), CVF_REPLACE)
      TNT1 A 0 A_SetScale(0.5, frandom(1.5, 3.5))
   Looping:
      RNDR A 5 A_JumpIf(waterlevel > 0 || velz == 0, "Death")
      loop
   Death:
      RNDR A 0 A_SetScale(1, 1)
      RNDR A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
      RNDR BCDEFGH 3 A_FadeOut(0.0375)
      stop
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_BloodRainDrop : Lith_RainDrop
{
   RenderStyle "Subtract"
   Alpha 0.5
   Translation "80:111=[0,64,64]:[0,128,128]"
}

actor Lith_SpawnRain : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_RainDrop",
         frandom(16, 1024), 0, frandom(256,512), 0, 0, 0, frandom(0, 360), 128)
      TNT1 AAAAAAA 0 A_SpawnItemEx("Lith_RainDrop",
         frandom(512, 1536), 0, frandom(256,512), 0, 0, 0, frandom(-90, 90), 128)
      stop
   }
}

actor Lith_SpawnBloodRain : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_BloodRainDrop",
         frandom(16, 1024), 0, frandom(256,512), 0, 0, 0, frandom(0, 360), 128)
      TNT1 AAAAAAA 0 A_SpawnItemEx("Lith_BloodRainDrop",
         frandom(512, 1536), 0, frandom(256,512), 0, 0, 0, frandom(-90, 90), 128)
      stop
   }
}

// EOF
