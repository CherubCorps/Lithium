actor Lith_PulseChargeTrail
{
   RenderStyle "Add"
   Scale 0.05

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      PBAL A 0
      PBAL A 0 A_JumpIf(scalex > 0.15, "FadeOut")
      PBAL A 1 A_SetScale(scalex * frandom(1.05,1.2))
      loop

   FadeOut:
      PBAL A 0 A_JumpIf(scalex < 0.05, "Death")
      PBAL A 1 A_SetScale(scalex * frandom(0.7,0.9))
      loop

   Death:
      PBAL A 0
      stop
   }
}

actor Lith_PulseCharge
{
   Speed 45
   DamageType "Electric"
   Damage (20 * random(2, 4))
   Radius 8
   Height 8
   DeathSound "weapons/plasma/explode"

   Projectile

   states
   {
   Spawn:
      TNT1 AAA 0 A_SpawnParticle("Green", SPF_FULLBRIGHT, random(20, 40), frandom(1, 4), 0, frandom(-6,6),frandom(-6,6),frandom(-6,6), 0,0,0, 0,0,0)
      TNT1 A 1 A_SpawnItemEx("Lith_PulseChargeTrail")
      loop
   Death:
      TNT1 A 0 A_Explode(96, 32, 0)
      TNT1 AAAAA 0 A_SpawnItemEx("Lith_PulseChargeTrail", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2), 0, SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_CPlasmaRifle : Lith_CWeapon
{
   Tag "$LITH_INFO_SHORT_CPlasmaRifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   Weapon.SlotPriority 1
   
   states
   {
   SpawnPickup:
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaRiflePickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CPLG A 1 Lith_A_Ready
      loop
   
   Deselect:
      CPLG AAA 0 A_Lower
      CPLG A 1
      loop
   
   Select:
      CPLG AAA 0 A_Raise
      CPLG A 1
      loop
   
   Fire:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      CPLG A 0 A_GunFlash("DummyFlash")
      CPLG A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_CPlasma_A), "FirePulse")

   FireNormal:
      CPLG A 0 A_TakeInventory("Lith_PlasmaAmmo", 30, TIF_NOTAKEINFINITE)
      CPLG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      CPLG AAAAA 0 A_FireProjectile("Lith_PlasmaBolt", CallACS("Lith_CircleSpread", 5*IFIX, 3*IFIX, false)/FIX, false, 0, -9, 0, -(CallACS("Lith_CircleSpread", 0, 0, true)/FIX))
      goto FireAnim1

   FirePulse:
      CPLG A 0 A_TakeInventory("Lith_PlasmaAmmo", 80, TIF_NOTAKEINFINITE)
      CPLG A 0 A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON)
      CPLG A 0 A_FireProjectile("Lith_PulseCharge")
      goto FireAnim2

   FireAnim1:
      CPLG B 2 offset(0, 36) bright
      CPLG C 1 offset(0, 35) bright
      CPLG D 1 offset(0, 32)
      CPLG AAAA 1 A_ReFire
      goto Ready

   FireAnim2:
      CPLG B 2 offset(0, 40) bright
      CPLG C 1 offset(0, 37) bright
      CPLG C 1 offset(0, 34) bright
      CPLG D 1 offset(0, 33)
      CPLG D 1 offset(0, 32)
      CPLG AAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireEmpty:
      CPLG A 0 A_StopSound(CHAN_7)
      CPLG A 0 A_StopSound(CHAN_6)
      CPLG A 1 A_PlaySound("weapons/plasma/empty", CHAN_7)
      CPLG A 1 offset(0, 37)
      CPLG A 1 offset(0, 36)
      CPLG A 1 offset(0, 35)
      CPLG A 1 offset(0, 34)
      CPLG A 1 offset(0, 32)
      CPLG A 1 A_ReFire
      goto Ready
   }
}
