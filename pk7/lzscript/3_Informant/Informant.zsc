class Lith_InformantPlayer : Lith_Player
{
   const Lith_DodgeTime = TICRATE;

   private int m_dodgeDbc;

   default
   {
      Player.StartItem "Lith_Claymore";
      Player.StartItem "Lith_Gladius";
      Player.DisplayName "Informant";
      Player.SoundClass "informant";
      Player.Portrait "lgfx/Portraits/Informant.png";
      Player.ViewHeight 44;
      Player.AttackZOffset 11;
      Player.JumpZ 14;
      Tag "Informant";

      +NOBLOOD;
   }

   private void Lith_InformantThink()
   {
      if(m_dodgeDbc)
         m_dodgeDbc--;
      else if(player && player.buttons & BT_SPEED)
      {
         let a = angle - atan2(player.cmd.sidemove, player.cmd.forwardmove);
         let c = cos(a) * 32;
         let s = sin(a) * 32;
         CallACS("Lith_DodgeView");
         A_GiveInventory("Lith_InvulnStep", 1);
         A_ChangeVelocity(c, s);
         m_dodgeDbc = Lith_DodgeTime;
      }
   }

   override void Tick()
   {
      if(health > 0)
         Lith_InformantThink();

      Super.Tick();
   }
}

// TODO: make this not break invuln spheres
class Lith_InvulnStep : PowerInvulnerable
{
   default
   {
      Powerup.Duration 10;
   }
}

class Lith_IWeapon : Lith_Weapon
{
   default
   {
      Inventory.RestrictedTo "Lith_InformantPlayer";
   }
}

#include "lzscript/3_Informant/Weapons/2_Flintlock.zsc"

// EOF
