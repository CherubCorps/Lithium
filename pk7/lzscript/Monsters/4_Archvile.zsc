class Lith_ArchvileFire : ArchvileFire replaces ArchvileFire
{
   default
   {
      Translation "160:161=160:161", "162:163=164:165", "164:165=35:36", "166:167=42:43";
      +FORCEYBILLBOARD
   }
}

class Lith_ArchvileImmolator : Actor
{
   void Lith_A_SpawnParticles(color c)
   {
      A_SpawnParticle(c, SPF_FULLBRIGHT|SPF_RELATIVE, 35, random(2,6), 0,
         frandom(-16,16),frandom(-16,16),frandom(0,1),
         0,0,frandom(0,10),
         frandom(-0.1, 0.1),frandom(-0.1, 0.1));
      A_SpawnParticle(c, SPF_FULLBRIGHT|SPF_RELATIVE, 35, random(2,6), 0,
         frandom(-16,16),frandom(-16,16),frandom(0,1),
         frandom(-10,10),frandom(-10,10),0,
         frandom(-0.1, 0.1),frandom(-0.1, 0.1));
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 1 A_SpawnItemEx("Lith_ArchvileImmolatorBlast",0,0,floorz);
      TNT1 AAAAAAAAAA 0 Lith_A_SpawnParticles("FFFF73");
      TNT1 AAAAAAAAAA 0 Lith_A_SpawnParticles("732B00");
      TNT1 AAAAAAAAAA 0 Lith_A_SpawnParticles("7F1B1B");
      stop;
   }
}

class Lith_ArchvileImmolatorBlast : Actor
{
   default
   {
      Translation "192:199=160:167", "200:207=32:39", "240:247=40:47";
      Mass int.max;

      +FLOORHUGGER
      +FORCEYBILLBOARD
      -NOGRAVITY
   }

   states
   {
   Spawn:
      DVBL AB 1 bright;
      DVBL C 2 bright;
      DVBL DE 3 bright;
      DVBL FGH 4 bright;
      DVBL IJ 5 A_FadeOut;
      stop;
   }
}

class Lith_Archvile : Archvile replaces Archvile
{
   default
   {
      Health 800;
      GibHealth 400;
      PainChance 2;
      Tag "$LITH_INFO_SHORT_Archvile";
   }

   states
   {
   Missile:
      VILE G 0 bright A_VileStart;
      VILE G 10 bright A_FaceTarget;
      VILE H 8 bright A_VileTarget;
      VILE IJKLM 8 bright A_FaceTarget;
      VILE N 0 bright A_JumpIfTargetInLOS(1);
      goto Missile1;
      VILE N 0 bright A_GiveToTarget("Lith_AdrenalineToken", 1);
   Missile1:
      VILE N 8 bright A_FaceTarget;
      VILE N 0 bright {
         if(target && checkSight(target))
            A_VileTarget("Lith_ArchvileImmolator");
      }
      VILE O 8 bright A_VileAttack("vile/stop", 20, 70, 70, 1.0, "FireMagic");
      VILE P 20 bright;
      goto Super::See;
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Archvile);
      goto Super::Death;
   XDeath:
      VILX A 5;
      VILX B 5 A_XScream;
      VILX C 5 A_NoBlocking;
      VILX DEF 5;
      VILX G -1;
      stop;
   }
}

// EOF
