// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_PKTrail : Actor
{
   default
   {
      Speed 14;
      Radius 2;
      Height 5;
      Damage 2;
      RenderStyle "Add";
      Alpha 1;
      DamageType "Magic";

      Projectile;
   }

   states
   {
   Spawn:
      BAL7 AAAABBBB 1 bright;
      loop;
   Death:
      BAL7 CDE 6 bright;
      stop;
   }
}

class Lith_PolarKnight : Actor
{
   default
   {
      Scale 0.45;
      Radius 2;
      Height 5;
      Speed 14;
      Damage 7;
      RenderStyle "Add";
      Alpha 0.8;
      SeeSound "enemies/hellknight/throw";
      DamageType "Magic";

      Projectile;
   }

   states
   {
   Spawn:
      BLLT F 1 bright;
      loop;
   Death:
      BLLT F 1 bright A_FadeOut(0.08);
      loop;
   }
}

class Lith_XH_HellKnight : Lith_HellKnight
{
}

class Lith_HellKnight : Lith_BaronOfHell replaces HellKnight;

default
{
   Lith_Monster.Score Score_HellKnight;
   Health 500;
   Speed 4;
   Species "Lith_HellKnight";
   BloodColor "DarkGreen";
   SeeSound "knight/sight";
   ActiveSound "knight/active";
   PainSound "knight/pain";
   DeathSound "knight/death";
   Obituary "$OB_KNIGHT";
   HitObituary "$OB_KNIGHTHIT";
   Tag "Hell Knight";

   -BOSSDEATH;
}

states
{
Spawn:
   BOS2 AB 10 A_Look;
   loop;

See:
   BOS2 AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase;
   loop;

Melee:
Missile:
   BOS2 EF 6 A_FaceTarget;
   BOS2 G 1;
   BOS2 G 0 A_CustomComboAttack("Lith_PolarKnight", 32, 10 * random(1, 8), "baron/melee");
   BOS2 GGG 1 A_SpawnProjectile("Lith_PKTrail", 32, 0, 0, CMF_BADPITCH);
   BOS2 G 0 ResolveState("See");
   stop;

Pain:
   BOS2 H 2;
   BOS2 H 2 A_Pain;
   BOS2 H 0 ResolveState("See");
   stop;

Death:
   BOS2 I -1;
Crash:
   BOS2 I 8 Lith_A_Die;
   BOS2 JKLMN 8;
   BOS2 O -1;
   stop;

Raise:
   BOS2 ONMLKJI 8;
   BOS2 A 0 ResolveState("See");
   stop;
}

// EOF
