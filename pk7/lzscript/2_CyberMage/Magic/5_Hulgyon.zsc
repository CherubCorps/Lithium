/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_HulgyonEffect : Actor
{
   default
   {
      RenderStyle "Add";

      +BRIGHT
      +NODAMAGETHRUST
      +NOGRAVITY
      +THRUACTORS
   }

   states
   {
   Spawn:
      BLLT P 0;
      BLLT P 0 A_Explode(48, 90, 0);
      BLLT P 5;
      BLLT P 1 A_FadeOut(0.2);
      wait;
   }
}

class Lith_HulgyonSprite : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.3;

      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT T 0;
      BLLT T 0 A_Warp(AAPTR_MASTER, Pos.X,Pos.Y,Pos.Z, 0, WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET|WARPF_COPYINTERPOLATION);
   Spawn1:
      BLLT T 1 bright;
      stop;
   }
}

class Lith_HulgyonSprite2 : Lith_HulgyonSprite
{
   default
   {
      Alpha 0.3;
   }

   states
   {
   Spawn:
      BLLT T 7 bright;
      stop;
   }
}

class Lith_Hulgyon : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Hulgyon";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/hulgyon/draw";

   +WEAPON.DONTBOB
   -WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   TNT1 A 0 A_SpawnItemEx("Lith_HulgyonSprite", Cos(Pitch)*32, 0, (Height-19)-(Sin(Pitch)*32),Vel.X,Vel.Y,Vel.Z,0,SXF_ISMASTER|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_ScriptCall("Lith_MagicSelect", 5);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_JumpIf(CountInv("Lith_Mana") < 50, "ReadySelector");
   TNT1 A 10 {
      A_FireProjectile("Lith_HulgyonProjectile");
      A_TakeInventory("Lith_Mana", 50);
   }
   TNT1 AAAAAAAAAA 1 A_SpawnItemEx("Lith_HulgyonSprite2", Cos(Pitch)*32, 0, (Height-19)-(Sin(Pitch)*32), flags: SXF_NOCHECKPOSITION);
   goto ReadySelector;
}

/* EOF */
