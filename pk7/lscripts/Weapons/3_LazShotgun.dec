actor Lith_LazShotgunTrail
{
   RenderStyle "Add"
   Scale 0.34
   
   +NOINTERACTION
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      BLLT E 4 bright
      BLLT E 2 bright A_FadeOut(0.5)
      BLLT E 1 bright A_FadeOut(0.2)
      stop
   }
}

actor Lith_LazShotgun : Lith_Weapon
{
   Tag "$LITH_TXT_INFO_SHORT_LazShotgun"
   Weapon.UpSound "weapons/lshotgun/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 0.9
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      // bullshit hack
      LSHT A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_pclass) == pcl_marine, "ReadyLoop")
      CLST A 0 //A_JumpIf(CallACS("Lith_GetPlayerData", pdata_pclass) == pcl_cybermage, "ReadyLoop")
   ReadyLoop:
      "####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      "####" AAA 0 A_Lower
      "####" A 1
      loop
   
   Select:
      LSHT A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_pclass) == pcl_marine, "SelectLoop")
      CLST A 0 //A_JumpIf(CallACS("Lith_GetPlayerData", pdata_pclass) == pcl_cybermage, "SelectLoop")
   SelectLoop:
      "####" AAA 0 A_Raise
      "####" A 1
      loop
   
   Fire:
      "####" A 0 A_AlertMonsters
      "####" AAAAAAA 0 A_RailAttack(7 * random(1, 4), 0, true, "None", "Red", RGF_SILENT | RGF_FULLBRIGHT, 2, "BulletPuff", 4, 4, 0, 10, 5, 0, "Lith_LazShotgunTrail", -8)
      "####" A 0 A_PlaySound("weapons/lshotgun/fire", CHAN_WEAPON)
      "####" A 0 A_Recoil(0.5)
      "####" I 2 bright A_SetPitch(pitch - 0.75)
      "####" H 1 bright
      "####" A 4
      "####" BB 1 A_SetPitch(pitch + 0.375)
      "####" C 2 A_PlaySound("weapons/lshotgun/open", CHAN_6)
      "####" D 2
      "####" E 3
      "####" F 5
      "####" G 2 A_PlaySound("weapons/lshotgun/close", CHAN_7)
      "####" ED 2
      "####" CA 3
      // [screams externally]
      "####" A 2 A_WeaponReady(LITH_BOBONLY)
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      "####" A 0 A_WeaponReady(LITH_BOBONLY)
      "####" A 1 A_ReFire
      goto Ready
   }
}

