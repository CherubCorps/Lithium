/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_SniperRifleMagazine : Lith_DroppedMagazine
{
   default
   {
      Scale 0.7;
      Mass 150;
   }

   states
   {
   Spawn1:
      MGZS ABCD 3 {user_side = 0;}
      MGZS EFGH 3 {user_side = 1;}
      loop;
   Death:
      MGZS C 0 A_JumpIf(user_side == 0, "Done");
      MGZS G 0 A_JumpIf(user_side == 1, "Done");
      stop;
   }
}

class Lith_SniperRifleMagazine2 : Lith_SniperRifleMagazine
{
   default
   {
      BounceSound "weapons/magbouncef";
      Mass 155;
   }

   states
   {
   Spawn1:
      MGZS IJKL 3 {user_side = 0;}
      MGZS MNOP 3 {user_side = 1;}
      loop;
   Death:
      MGZS K 0 A_JumpIf(user_side == 0, "Done");
      MGZS O 0 A_JumpIf(user_side == 1, "Done");
      stop;
   }
}

class Lith_SniperRifle : Lith_MWeapon;

default
{
   Lith_Weapon.MagSize 8;
   Lith_Weapon.ScopeOutSound "weapons/sniper/zoomin";
   Lith_Weapon.ScopeOutSound "weapons/sniper/zoomout";
   Lith_Weapon.ScopeZoom 1.2;
   Tag "$LITH_INFO_SHORT_SniperRifle";
   Weapon.SlotNumber 4;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/sniper/draw";

   +WEAPON.NOAUTOFIRE
}

action(Weapon) void Lith_A_FireSniper()
{
   Lith_A_MagIncr();
   A_GunFlash("DummyFlash");
   Lith_FireBullets(0, 0, 1, 161, FBF_NORANDOM, "Lith_SniperBullet");
   A_Recoil(1);
   Lith_A_RecoilUp(2);
   A_StartSound("weapons/sniper/fire", lch_weapon);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

states(Weapon)
{
AltReady:
   SCOP A 1 Lith_A_Ready(WRF_NOBOB|WRF_ALLOWRELOAD);
   loop;
Ready:
   SNPG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   SNPG A 0 Lith_A_ScopeOut;
DeselectLoop:
   SNPG AA 0 A_Lower;
   SNPG A 1 A_Lower;
   loop;

Select:
   SNPG A 0 Lith_A_ScopeOut;
SelectLoop:
   SNPG AA 0 A_Raise;
   SNPG A 1 A_Raise;
   loop;

Fire:
   #### # 0 Lith_A_JumpIfMagEmpty;
   #### # 0 Lith_A_FireSniper;
   #### # 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FireAnim_Scoped");
FireAnim_Unscoped:
   SNPG B 1 bright;
   SNPG C 1 bright Lith_A_RecoilDn(0.25);
   SNPG D 1 Lith_A_RecoilDn(0.25);
   SNPG E 1 Lith_A_RecoilDn(0.25);
   SNPG F 1 Lith_A_RecoilDn(0.25);
   SNPG G 1;
   SNPG A 1;
   goto BoltAnim;
FireAnim_Scoped:
   SCOP A 1 bright Lith_A_ZoomFactorScoped(0.80);
   SCOP A 1 bright {Lith_A_RecoilDn(0.25); Lith_A_ZoomFactorScoped(0.85);}
   SCOP A 1 {Lith_A_RecoilDn(0.25); Lith_A_ZoomFactorScoped(0.90);}
   SCOP A 1 {Lith_A_RecoilDn(0.25); Lith_A_ZoomFactorScoped(0.95);}
   SCOP A 3 {Lith_A_RecoilDn(0.25); Lith_A_ZoomFactorScoped(1.00);}
BoltAnim:
   #### # 1 offset(1, 32) Lith_A_RecoilDn(0.25);
   #### # 1 offset(3, 32) Lith_A_RecoilDn(0.25);
   #### # 1 offset(4, 32) Lith_A_RecoilDn(0.25);
   #### # 1 offset(8, 38) Lith_A_RecoilDn(0.25);
   #### # 1 offset(12, 45);
   #### # 1 offset(16, 53);
   #### # 1 offset(19, 60);
   #### # 1 offset(21, 64);
   #### # 1 offset(23, 67);
   #### # 0 Lith_A_ReloadBobDn(0.5);
   #### # 1 offset(22, 74) A_StartSound("weapons/sniper/boltback", lch_weapon2);
   #### # 1 offset(19, 78) Lith_A_ReloadBobDn(0.5);
   #### # 5 offset(18, 80) Lith_A_Casing("Lith_CasBigHot", 15, 4, 30, frandom(3,4), frandom(7,8), frandom(2,4));
   #### # 1 offset(17, 74) A_StartSound("weapons/sniper/boltforward", lch_weapon3);
   #### # 1 offset(19, 67);
   #### # 1 offset(20, 66);
   #### # 1 offset(21, 62);
   #### # 1 offset(20, 60);
   #### # 1 offset(21, 63);
   #### # 1 offset(23, 66);
   #### # 1 offset(24, 68);
   #### # 1 offset(25, 63);
   #### # 1 offset(24, 59);
   #### # 1 offset(22, 57);
   #### # 1 offset(20, 54);
   #### # 1 offset(16, 49);
   #### # 1 offset(12, 44);
   #### # 1 offset(10, 42);
   #### # 1 offset(8, 40);
   #### # 1 offset(5, 38);
   #### # 1 offset(2, 35);
   #### # 5 offset(0, 32);
   goto ReadySelector;
FireEmpty:
   #### # 1;
   #### # 1 offset(0, 45);
   #### # 1 offset(0, 43);
   #### # 1 offset(0, 40) A_StartSound("weapons/ionrifle/empty", lch_dryweapon);
   #### # 1 offset(0, 38);
   #### # 1 offset(0, 36);
   #### # 1 offset(0, 33);
   #### # 1 offset(0, 32);
   #### # 2;
   goto ReadySelector;

AltFire:
   #### A 1;
   #### A 0 Lith_A_ScopeToggle;
   goto ReadySelector;

Reload:
   #### # 0 Lith_A_JumpIfMagFull;
   SNPG A 0 Lith_A_ScopeOut;
   SNPG A 1;
   SNPG A 1 offset(-2, 34);
   SNPG A 1 offset(-4, 37);
   SNPG A 0 Lith_A_ReloadBobUp(0.5);
   SNPG A 1 offset(-9, 43) A_StartSound("weapons/sniper/magout", lch_reload);
   SNPG A 1 offset(-12, 47);
   SNPG A 1 offset(-14, 52);
   SNPG A 2 offset(-12, 55) Lith_A_DropMag("Lith_SniperRifleMagazine", "Lith_SniperRifleMagazine2", 8,-1,20, frandom(0,1),frandom(-1,-2),0);
   SNPG A 3 offset(-11, 57);
   SNPG A 8 offset(-10, 60);
   SNPG A 8 offset(-11, 60);
   SNPG A 0 Lith_A_ReloadFinish;
   SNPG A 0 Lith_A_ReloadBobDn(0.5);
   SNPG A 2 offset(-13, 59) A_StartSound("weapons/sniper/magin", lch_reload2);
   SNPG A 2 offset(-11, 49);
   SNPG A 2 offset(-11, 47);
   SNPG A 7 offset(-11, 45);
   SNPG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   SNPG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   SNPG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   SNPG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   SNPG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   SNPG A 4 A_WeaponReady(WRF_NOBOB);
   goto ReadySelector;
}

/* EOF */
