class Lith_VoidShotTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.9;
      Scale 0.05;
      Translation "Lith_VoidShot";

      +NOINTERACTION
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT N 0;
   SpawnLoop:
      #### # 0 A_JumpIf(scale.x < 0.005, "Death");
      #### # 1 ;
      #### # 0 A_SetScale(scale.x * 0.9);
      loop;
   Death:
      TNT1 A 0;
      stop;
   }
}

class Lith_VoidShotTrail2 : Lith_VoidShotTrail
{
   default
   {
      Scale 0.025;
      Translation "None";
   }

   states {Spawn: BLLT L 0; goto SpawnLoop;}
}

class Lith_VoidShotTrail3 : Lith_VoidShotTrail {default {Scale 0.15;}}
class Lith_VoidShotEffect : Lith_MonsterSoulEffect {default {Translation "Lith_VoidShot";}}

class Lith_VoidShot : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.9;
      Scale 0.05;
      Radius 4;
      Height 4;
      Damage 5;
      Speed 16;
      Translation "Lith_VoidShot";

      Projectile;
      +BRIGHT
      +SEEKERMISSILE
   }

   states
   {
   Spawn:
      BLLT N 0;
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail");
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail2");
      BLLT N 0 A_Jump(256, 1, 2);
      BLLT N 0 A_SpawnItemEx("Lith_VoidShotTrail3");
      BLLT N 0 A_Jump(256, 1, 2, 3, 3);
      BLLT N 0 A_ChangeVelocity(0,frandom(-1,1),frandom(-1,1), CVF_RELATIVE);
      BLLT N 0 A_SeekerMissile(5, 1, SMF_LOOK|SMF_PRECISE|SMF_CURSPEED);
      BLLT N 1;
      loop;
   Death:
      BLLT N 0 A_PlaySound("ghost/hit", CHAN_5);
      TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("Lith_VoidShotEffect", 0,0,0, frandom(-2,2),frandom(-2,2),frandom(-2,2));
      stop;
   }
}

class Lith_SPAS : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_SPAS";
   Weapon.UpSound "weapons/cshotgun/draw";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.95;
   Lith_Weapon.MagSize 8;

   +WEAPON.NOAUTOFIRE
}

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_SuperShotgunPickup");
   stop;
}

states(Weapon)
{
Ready:
   CSHG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   CSHG AAA 0 A_Lower;
   CSHG A 1;
   loop;

Select:
   CSHG AAA 0 A_Raise;
   CSHG A 1;
   loop;

Fire:
   TNT1 A 0 A_JumpIfInventory("Lith_ShellAmmo", 1, 1);
   goto FireEmpty;
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 A_TakeInventory("Lith_ShellAmmo", 1, TIF_NOTAKEINFINITE);
   TNT1 A 0 Lith_A_Casing("Lith_CasShellHot", 30, 6, 20, frandom(3,4), frandom(7,8), frandom(7,9));
   TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_SPAS_B), "Fire1");
   TNT1 A 0 Lith_A_MagIncr;
Fire1:
   // don't ask
   TNT1 A 0 A_JumpIf(pos.z - floorz < 24, "Fire2");
   TNT1 A 0 A_Recoil(15);
   TNT1 A 0 A_FireBullets(10.5, 3.5, 3, 3);
Fire2:
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_Recoil(0.7);
   TNT1 A 0 Lith_A_RecoilUp(0.4);
   TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_SPAS_A), "FireVoid");
   TNT1 A 0 A_FireBullets(10.5, 3.5, 12, 6);
   TNT1 A 0 Lith_A_GunSmoke;
   TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_SPAS_B), "FireAnim3");
FireAnim:
   TNT1 A 0 A_PlaySound("weapons/cshotgun/fire", CHAN_WEAPON);
   CSHG B 1 bright offset(0, 35) A_Light(1);
   CSHG B 1 bright offset(0, 35);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG C 1 bright offset(0, 34) A_Light(2);
   CSHG C 1 bright offset(0, 33);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG D 1 offset(0, 33) A_Light(0);
   CSHG D 1 offset(0, 32);
   goto Ready;
FireVoid:
   TNT1 AAAAAA 0 A_FireProjectile("Lith_VoidShot", frandom(-5,5), false, 0, 0, FPF_NOAUTOAIM, -frandom(-2,2));
FireAnim2:
   TNT1 A 0 A_PlaySound("weapons/cshotgun/fire", CHAN_WEAPON);
   CSHG B 1 bright offset(0, 38) A_Light(1);
   CSHG B 1 bright offset(0, 38);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG C 1 bright offset(0, 37) A_Light(2);
   CSHG D 1 bright offset(0, 36);
   CSHG D 1 offset(0, 35) A_Light(0);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG A 1 offset(0, 35);
   CSHG A 1 offset(0, 33);
   CSHG A 1 offset(0, 32);
   goto Ready;
FireAnim3:
   TNT1 A 0 A_FireBullets(11, 5, 2, 2);
   TNT1 A 0 Lith_A_GunSmoke;
   TNT1 A 0 A_PlaySound("weapons/cshotgun/fire2", CHAN_WEAPON);
   CSHG B 1 bright offset(0, 38) A_Light(1);
   CSHG B 1 bright offset(0, 38);
   CSHG C 1 bright offset(0, 37);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG C 1 bright offset(0, 37) A_Light(2);
   CSHG D 1 bright offset(0, 36);
   CSHG D 1 bright offset(0, 36);
   CSHG D 1 offset(0, 35) A_Light(0);
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   CSHG A 1 offset(0, 35);
   CSHG A 1 offset(0, 34);
   CSHG A 1 offset(0, 33);
   CSHG A 1 offset(0, 32);
   goto Ready;
FireEmpty:
   CSHG A 1;
   CSHG A 2 offset(0, 38);
   CSHG A 1 offset(0, 40);
   CSHG A 1 offset(0, 37) A_PlaySound("weapons/cshotgun/empty", CHAN_5);
   CSHG A 1 offset(0, 34);
   CSHG A 1 offset(0, 33);
   CSHG A 5 offset(0, 32);
   goto Ready;

Reload:
   TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_SPAS_B), "Ready");
   TNT1 A 0 Lith_A_JumpIfMagFull;
   CSHR ABCDE 1;
   CSHR FG 2;
   CSHR H 2 A_PlaySound("weapons/cshotgun/load", CHAN_6);
   CSHR IJKLMNOPQR 2;
   CSHR A 0 Lith_A_ReloadFinish;
   CSHR S 0 Lith_A_ReloadBobDn(1.5);
   CSHR S 2 A_PlaySound("weapons/cshotgun/pump", CHAN_7);
   CSHR TUVWXYZB 2;
   goto Ready;
}

// EOF
