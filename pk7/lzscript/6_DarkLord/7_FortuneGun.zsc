/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_FortuneProjectilePuff : Lith_Dummy {
   default {
      DamageType "Electric";
      Species "Lith_Player";

      +DONTHARMSPECIES
   }
}

class Lith_FortuneShotWind : Actor {
   default {
      RenderStyle "Add";
      Radius 1;
      Height 1;

      +NOINTERACTION
      +BRIGHT
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      roll = frandom(0, 360);
      alpha = frandom(0.5, 0.7);
      scale.x = frandom(0.2, 1.0);
   }

   states {
   Spawn:
      ____ A 20;
      ____ A 1 A_FadeOut;
      wait;
   }
}

class Lith_FortuneShotWindSpawner : Actor {
   default {
      Radius 1;
      Height 1;
      +NOINTERACTION
   }

   states {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 {
         if(random(0,1)) A_SpawnItemEx("Lith_FortuneShotWind", frandom(-8,8),frandom(-8,8),frandom(-8,8), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
      }
      stop;
   }
}

class Lith_FortuneExplosion : Lith_Explosive {
   default {
      DamageType "Shrapnel";
      +NOGRAVITY
   }

   states {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_Explode(200, 200, XF_NOSPLASH);
      TNT1 A 0 Lith_A_ExplodeSound;
      TNT1 A 0 Lith_A_DoExplosion;
      stop;
   }
}

class Lith_FortuneShot : FastProjectile {
   default {
      Radius 5;
      Height 5;
      DamageFunction 500;
      Speed 170;
      MissileHeight 8;
      MissileType "Lith_FortuneShotWindSpawner";
   }

   states {
   Spawn:
      TNT1 A -1;
      stop;
   Death:
      TNT1 A 1 A_StartSound("weapons/fortune/hit", lch_body, attenuation: 0.2, pitch: frandom(0.5, 1.0));
      TNT1 A 0 A_SpawnItemEx("Lith_FortuneExplosion", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
      stop;
   }
}

class Lith_FortuneProjectile : Actor {
   default {
      Height 1;
      Radius 1;
      Projectile;
   }

   states {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnProjectile("Lith_FortuneShot", flags: CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_ABSOLUTEPITCH, pitch: pitch);
      TNT1 AAAAAAAA 2 {
         A_StartSound("weapons/fortune/spark", lch_body, 0, frandom(0.1, 0.3), 2.0, frandom(0.5, 1.5));
         A_CustomRailgun(100, 0, "", "#91c3ff", RGF_SILENT|RGF_NOPIERCING|RGF_EXPLICITANGLE|RGF_FULLBRIGHT|RGF_CENTERZ, 0, 50, "Lith_FortuneProjectilePuff", 0, 0, 16000, 10, 0.2, 0, spawnofs_z: 32);
      }
      stop;
   }
}

class Lith_FortuneGun : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_BFG9000Pickup";
   Lith_Weapon.MagSize 4;
   Tag "$LITH_INFO_SHORT_FortuneGun";
   Weapon.SlotNumber 7;
   Weapon.SlotPriority 1;
   Weapon.AmmoType "Lith_CannonAmmo";

   +WEAPON.NOAUTOFIRE;
}

action(Weapon) void Lith_A_FireFortune() {
   lith_takeAmmo();
   A_StartSound("weapons/fortune/fire", lch_weapon, attenuation: 0.5, pitch: frandom(0.9, 1.05));
   A_GunFlash("DummyFlash");
   A_Recoil(4);
   A_Quake(2, 15, 0, 128);
   let pr = A_FireProjectile("Lith_FortuneProjectile", spawnheight: -32);
   pr.pitch = self.pitch;
   Lith_A_RecoilUp(10);
}

states(Weapon) {
Ready:
   PISG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   PISG A 1 Lith_A_Lower(_raise_slow);
   loop;

Select:
   PISG A 1 Lith_A_Raise(_raise_slow);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo;
   #### # 0 Lith_A_JumpIfMagEmpty;
   #### # 0 Lith_A_MagIncr;
   PISG A 25 Lith_A_FireFortune;
   goto ReadySelector;
FireEmpty:
   PISG A 2 offset(0, 42);
   PISG A 2 offset(0, 38);
   PISG A 2 offset(0, 35) A_StartSound("weapons/cshotgun/empty", lch_dryweapon);
   PISG A 1 offset(0, 34);
   PISG A 1 offset(0, 33);
   PISG A 1 offset(0, 32);
   goto ReadySelector;

Reload:
   PISG A 1 Lith_A_JumpIfMagFull;
   PISG A 30 A_StartSound("weapons/fortune/charge", lch_weapon2);
   PISG A 2 Lith_A_ReloadFinish;
   goto ReadySelector;
}

/* EOF */
