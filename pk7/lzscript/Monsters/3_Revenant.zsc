class Lith_Revenant : Revenant replaces Revenant
{
   default
   {
      Health 350;
      Tag "$LITH_INFO_SHORT_Revenant";
   }

   states
   {
   Death:
      #### # 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Revenant);
      goto Super::Death;
   XDeath:
      REVX A 3;
      REVX B 4 A_XScream;
      REVX C 5 A_NoBlocking;
      REVX DE 5;
      REVX F -1;
      stop;
   }
}

class Lith_RevenantTracer : RevenantTracer replaces RevenantTracer
{
   default
   {
      DamageType "Shrapnel";
   }

   int m_timer;

   states
   {
   Spawn:
      FATB A 0;
      FATB A 0 A_JumpIf(m_timer++ > 35 * 10, "Death");
      FATB AB 2 bright {
         if(level.time & 3 == 0) {
            if(level.time % 20 == 0) A_PlaySound("enemies/revenant/home");
            A_SpawnItemEx("Lith_RifleGrenadeSmoke", -8);
            A_SeekerMissile(1, 45, 0);
         }
      }
      loop;
   }
}

class Lith_XH_RevenantTracer : Lith_RevenantTracer
{
   default
   {
      Scale 0.7;
      Damage 1;
      Radius 2;
      Height 2;
   }

   states
   {
   Spawn:
      FATB A 0;
      FATB A 0 A_JumpIf(m_timer++ > 35 * 5, "Death");
      FATB AB 2 bright A_SeekerMissile(1, 45, 0);
      loop;
   }
}

// EOF
