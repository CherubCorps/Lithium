class Lith_CircleSpearTrail : Actor
{
   default
   {
      RenderStyle "Add";

      +NOINTERACTION
      +NOTIMEFREEZE
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT S 1 A_FadeOut(0.2);
      wait;
   }
}

class Lith_CircleSpear : Actor
{
   default
   {
      RenderStyle "Add";
      DamageFunction 77;
      DamageType "Magic";
      Species "Lith_Player";

      Projectile;
      +DONTHARMSPECIES
      +MTHRUSPECIES
      +NOTIMEFREEZE
      +RIPPER
      +BRIGHT
   }

   int m_trigger;
   int m_vel;

   states
   {
   Spawn:
      BLLT S 1 A_JumpIf(m_trigger, "Go");
      wait;
   Go:
      BLLT S random(0, 2);
      BLLT S 0 {m_vel = random(20, 30);}
      BLLT S 0 A_ChangeVelocity(cos(angle)*m_vel, sin(angle)*m_vel, 0, CVF_REPLACE);
      BLLT S 3 A_SpawnItemEx("Lith_CircleSpearTrail");
      wait;
   Death:
      BLLT S 1 A_FadeOut;
      wait;
   }
}

class Lith_CircleSpearThrower : Actor
{
   default
   {
      RenderStyle "None";

      +NOGRAVITY
      +NOCLIP
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_CircleSpear", frandom(-8,8),frandom(-8,8),frandom(-16,16), 0,0,0, frandom(-60,60), SXF_TRANSFERPOINTERS, 0, tid);
      stop;
   }
}

class Lith_CircleElectricity : Actor
{
   default
   {
      Translation "Lith_CircleElectricity";
      RenderStyle "Add";
      Damage 7;
      DamageType "Magic";
      Species "Lith_Player";

      Projectile;
      +DONTHARMSPECIES
      +MTHRUSPECIES
      +RANDOMIZE
      +NOTIMEFREEZE
      +RIPPER
      +BRIGHT
   }

   states
   {
   Spawn:
      ZAP1 FFEFEFDDEFDEEDEEDDDCCCBCBBBCBBBBAAABA 1 A_FadeOut(0.01);
      ZAP1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.02);
      stop;
   }
}

class Lith_CircleWallFx : Actor
{
   default
   {
      RenderStyle "Add";
      XScale 0.7;
      YScale 5.1;
      Alpha 0.7;
      DamageType "Magic";
      Species "Lith_Player";

      +DONTHARMSPECIES
      +NOTIMEFREEZE
      +NOCLIP
      +NOGRAVITY
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT E 0;
      BLLT EEE 1 A_FadeIn;
      BLLT E 0 A_Explode(128, 128, 0);
   FadeLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha <= 0.05, "Fire");
      loop;
   Fire:
      TNT1 A 2;
      TNT1 AA 0 A_SpawnItemEx("Lith_CircleElectricity", frandom(-32,32),frandom(-32,32),0, frandom(-4,4),frandom(-4,4),frandom(-4,4), 0, SXF_TRANSFERPOINTERS);
      stop;
   }
}

class Lith_CircleWallTrail : Lith_CircleWallFx
{
   default
   {
      XScale 0.5;
      YScale 5;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT E 1 A_FadeOut;
      wait;
   Nope:
      TNT1 A 0;
      stop;
   }
}

class Lith_CircleWall : Lith_CircleWallFx
{
   default
   {
      XScale 0.5;
      YScale 5;
      Radius 8;
      Height 32;
      DamageType "Magic";
      Species "Lith_Player";

      +DONTHARMSPECIES
   }

   int m_trigger;

   states
   {
   Spawn:
      BLLT E 1 A_ScaleVelocity(0.9);
      BLLT E 0 A_JumpIf(m_trigger, "Fire");
      loop;
   Fire:
      BLLT E 0 A_SpawnItemEx("Lith_CircleWallFX", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS);
      BLLT E 0 {m_trigger = false;}
   FireDoneLoop:
      BLLT E 1 A_ScaleVelocity(0.9);
      BLLT E 0 A_JumpIf(m_trigger, "FireOut");
      loop;
   FireOut:
      BLLT E 1 A_ChangeVelocity(cos(angle)*4, sin(angle)*4, 0, CVF_REPLACE);
   FireOutLoop:
      BLLT E 0 A_FadeOut(0.05);
      BLLT E 0 A_Explode(128, 128, 0);
      BLLT E 0 A_SpawnItemEx("Lith_CircleWallTrail", -8);
      BLLT E 1 A_ChangeVelocity(vel.x*1.34, vel.y*1.34, 0, CVF_REPLACE);
      loop;
   }
}

class Lith_CircleParticle : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.7;

      +NOINTERACTION
      +NOTIMEFREEZE
      +BRIGHT
   }

   int m_trigger;
   int m_fxtid;

   states
   {
   Spawn:
      BLLT E 1;
   SpawnLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha <= 0.05, "FadeOutDone");
      loop;
   FadeOutDone:
      BLLT E 1 A_JumpIf(m_trigger, "DoFadeIn");
      loop;
   DoFadeIn:
      BLLT E 1 A_FadeIn(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha >= 1, "Done");
      loop;
   Done:
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,7, 0, SXF_TRANSFERPOINTERS, 0, m_fxtid);
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,2, 0, SXF_TRANSFERPOINTERS, 0, m_fxtid);
      BLLT E 1 A_FadeOut(0.34);
      stop;
   }
}

class Lith_Cercle : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Cercle";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/cercle/draw";

   +WEAPON.NOAUTOFIRE
}

states
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 7);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 0, 1);
   goto Ready;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 0x7FFFFFFF);
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Cercle", false);
   TNT1 A 10;
   goto Ready;
}

// EOF
