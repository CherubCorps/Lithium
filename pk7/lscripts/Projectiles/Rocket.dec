actor Lith_RocketTrail
{
   RenderStyle Add
   Scale 0.076
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK A 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_Rocket
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 20
   Damage 20
   DeathSound "explosion"
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   Gravity 0.2
   
   Projectile
   +ROCKETTRAIL
   +MTHRUSPECIES
   +EXPLODEONWATER
   -NOGRAVITY
   
   var int user_xv;
   var int user_yv;
   var int user_zv;
   
   states
   {
   Spawn:
      MISL A 0
      MISL A 0 A_SetUserVar(user_xv, velx * FIX)
      MISL A 0 A_SetUserVar(user_yv, vely * FIX)
      MISL A 0 A_SetUserVar(user_zv, velz * FIX)
      MISL A 0 A_ChangeVelocity(velx*0.05, vely*0.05, 0, CVF_REPLACE)
      MISL A 7 bright
      MISL A 0 A_ChangeFlag("NOGRAVITY", true)
      MISL A 0 A_ChangeVelocity(user_xv/FIX, user_yv/FIX, user_zv/FIX + 0.5, CVF_REPLACE)
      MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true)
      MISL A 0 A_Jump(256, "SpawnLoop")
   SpawnLoop:
      MISL A 0 A_SpawnItemEx("Lith_RocketTrail", 0, 0, 4, frandom(-0.5,0.5), frandom(-0.5,0.5), 0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      goto Done
   Done:
      MISL A 1 bright
      MISL A 0 A_Jump(256, "SpawnLoop")
   Death:
      TNT1 A 0 A_Explode
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_RocketHax : Lith_MonsterHax
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 10
   Damage 0
   
   Projectile
   +SKYEXPLODE
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1
      TNT1 A 0 A_SpawnItemEx("Lith_SpiralRocket", 0,0,0, 0,0,0, 0, SXF_ORIGINATOR|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
   SpawnLoop:
      TNT1 A 1
      loop
   Death:
      TNT1 A 1
      TNT1 A 0 A_KillChildren
      TNT1 A 1
      stop
   }
}

actor Lith_RocketDamageHax
{
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   Speed 16
   Damage 20
   Radius 2
   Height 2
   
   Projectile
   +MTHRUSPECIES
   
   states
   {
   Spawn:
      TNT1 A 4
      stop
   }
}

actor Lith_SpiralRocket : Lith_MonsterHax
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 0
   DeathSound "explosion"
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   
   Projectile
   +ROCKETTRAIL
   +MTHRUSPECIES
   +EXPLODEONWATER
   +SKYEXPLODE
   
   var int user_trailstep;
   var int user_speedstep;
   var int user_spiraldist;
   var int user_spiralanglez;
   
   states
   {
   Spawn:
      MISL A 0
      MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true)
      MISL A 0 A_SetUserVar(user_spiraldist,   random(0, 359))
      MISL A 0 A_SetUserVar(user_spiralanglez, user_spiraldist)
   SpawnLoop:
      MISL A 0 A_SetUserVar(user_spiraldist,   (user_spiraldist + 7)   % 360)
      MISL A 0 A_SetUserVar(user_spiralanglez, (user_spiralanglez + 7) % 360)
      MISL A 0 A_Warp(AAPTR_MASTER, cos(90.0) * (sin(user_spiraldist) * 12.0),
                                    sin(90.0) * (sin(user_spiraldist) * 12.0),
                                    cos(user_spiralanglez) * 12.0,
                      0.0, WARPF_NOCHECKPOSITION)
   TheRealSpawnShady:
      MISL A 0 A_JumpIf(user_trailstep >= 4, "TrailDone")
      MISL A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      MISL A 0 A_SpawnItemEx("Lith_RocketTrail",
                                   ((user_trailstep * 8.0) * CallACS("Lith_VelHax", 1)) / -35.0,
                                  -((user_trailstep * 8.0) * CallACS("Lith_VelHax", 2)) / -35.0,
                             4.0 + ((user_trailstep * 8.0) * CallACS("Lith_VelHax", 3)) / -35.0,
                             frandom(-0.5,0.5), frandom(-0.5,0.5), 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      MISL A 0 A_SetUserVar(user_trailstep, 0)
      MISL A 0 A_JumpIf(CallACS("Lith_Velocity", CallACS("Lith_VelHax",1)*FIX, CallACS("Lith_VelHax",2)*FIX)/FIX >= 30, "Done")
      MISL A 0 A_SetUserVar(user_speedstep, user_speedstep + 1)
      MISL A 0 A_ScaleVelocity(1.0 + (user_speedstep / 20.0), AAPTR_MASTER)
   Done:
      MISL A 1 bright A_Stop
      MISL A 0 A_Warp(AAPTR_MASTER)
      goto SpawnLoop
   Death:
      TNT1 A 0 A_Explode
      // Because these projectiles won't ever actually hit anything, we need to do this crap to damage cyberdemons.
      TNT1 A 0 A_CustomMissile("Lith_RocketDamageHax", 4, 0, angle, CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE)
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

// EOF

