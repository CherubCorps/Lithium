class Lith_BoughtItem : CustomInventory
{
   int lith_upgradeid;
   int lith_upgradeweap;
   int lith_upgradebody;
   int lith_upgradeextr;
   
   default
   {
      Inventory.PickupMessage "";
      
      +INVENTORY.ALWAYSPICKUP
      +INVENTORY.TRANSFER
      +FORCEXYBILLBOARD
   }
   
   override void PlayPickupSound(Actor toucher)
   {
      int chan = CHAN_PICKUP;
      sound snd;
      
      if(toucher && toucher.checkLocalView(consoleplayer))
         chan |= CHAN_NOPAUSE;
      
           if(lith_upgradebody) snd = "player/pickup/upgrbody";
      else if(lith_upgradeweap) snd = "player/pickup/upgrweap";
      else if(lith_upgradeextr) snd = "player/pickup/upgrextr";
      else                      snd = "player/pickup/item";
      
      toucher.A_PlaySound(snd, chan, 1, false, ATTN_NONE);
   }
   
   states(actor)
   {
   Spawn:
      TNT1 A random(1, 35*2);
      TNT1 A 0 A_SpawnItemEx("Lith_P67TeleFog");
      UCAN A 0 A_JumpIf(lith_upgradebody, "Upgrade");
      WUPG A 0 A_JumpIf(lith_upgradeweap, "Upgrade");
      TNT1 A 0 A_JumpIf(lith_upgradeextr, "UpgradeExtr");
   Item:
      BPAK B 5 bright;
      BPAK B 5;
      loop;
   Upgrade:
      #### # 5 bright;
      #### # 5;
      loop;
   UpgradeExtr:
      TNT1 A 0 A_ChangeFlag("FLOATBOB", true);
   UpgradeExtrLoop:
      IOBJ A 3 bright;
      IOBJ B 2;
      IOBJ B 3 bright;
      IOBJ A 2;
      loop;
   }
   
   states(item)
   {
   Pickup:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_BoughtItemPickup", invoker.lith_upgradeid);
      stop;
   }
}

// EOF

