actor Lith_ZombieMan : ZombieMan replaces ZombieMan
{
   DropItem "Lith_Dummy"
   MinMissileChance 120
   Speed 9
   
   var int user_shots;
   
   states
   {
   Missile:
      POSS E 0 A_SetUserVar(user_shots, random(1, 3))
      POSS E 5 A_FaceTarget
   MissileLoop:
      POSS E 5 A_FaceTarget
      POSS F 4 bright A_WolfAttack(WAF_NORANDOM | WAF_USEPUFF, "enemies/zombie/fire", 3.0 / 4.0, frandom(1, 4) * 3.0)
      POSS E 0 A_SetUserVar(user_shots, user_shots - 1)
      POSS E 0 A_JumpIf(user_shots == 0, "MissileEnd")
      loop
   MissileEnd:
      POSS E 8
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteAlways("Lith_GiveScoreToTarget", 0, Score_ZombieMan)
      goto Super::Death
   }
}

actor Lith_ShotgunGuy : ShotgunGuy replaces ShotgunGuy
{
   DropItem "Lith_Dummy"
   MinMissileChance 120
   Speed 9
   
   states
   {
   Missile:
      SPOS E 10 A_FaceTarget
      SPOS F 0 bright A_PlaySound("enemies/shotguy/fire", CHAN_WEAPON)
      SPOS FFF 0 bright A_WolfAttack(WAF_NORANDOM | WAF_USEPUFF,
         "",
         4.0 / 5.0,
         frandom(1, 5) * 2.4)
      SPOS F 10 bright
      SPOS E 10
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteAlways("Lith_GiveScoreToTarget", 0, Score_ShotgunGuy)
      goto Super::Death
   }
}

actor Lith_ChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
   DropItem "Lith_Dummy"
   Speed 9
   
   states
   {
   Missile:
      CPOS E 2 A_FaceTarget
      CPOS E 0 A_PlaySound("enemies/chainguy/bolt", CHAN_WEAPON)
      CPOS E 12 A_FaceTarget
   MissileLoop:
      CPOS FE 4 bright A_WolfAttack(WAF_NORANDOM | WAF_USEPUFF,
         "enemies/chainguy/fire",
         3.0 / 4.0,
         frandom(1, 4) * 3.0)
      CPOS F 1 A_CPosRefire
      loop
   Death:
      "####" "#" 0 ACS_NamedExecuteAlways("Lith_GiveScoreToTarget", 0, Score_ChaingunGuy)
      goto Super::Death
   }
}

