actor Lith_ItemPickup : CustomInventory
{
   Inventory.PickupMessage ""
   Inventory.PickupSound "player/pickup/evil"
   
   const int Lith_ISF = SXF_TRANSFERSPRITEFRAME|SXF_SETMASTER;
   
   var int user_glowangle;
   var int user_glowzangle;
   
   states
   {
   SpawnLoop:
      TNT1 A 1 A_CheckSight("LoopButDontShowThatShitOffDawg")
      TNT1 A 0 A_SetUserVar(user_glowangle,  (user_glowangle  + 5) % 360)
      TNT1 A 0 A_SetUserVar(user_glowzangle, (user_glowzangle + 2) % 360)
      
      // xyz
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", sin(user_glowangle) * 32.0,
                                              cos(user_glowangle) * 32.0,
                                              24.0 + (sin(user_glowzangle) * 9.0),
                                              0, 0, 0, 0)
      
      // yz
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", 0,
                                              sin(user_glowangle) * 32.0,
                                              24.0 + (cos(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", cos(user_glowangle) * 32.0,
                                              sin(user_glowangle) * 32.0,
                                              24.0 + (sin(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", cos(user_glowangle) * 32.0,
                                              ((1.0 - sin(user_glowangle)) * 32.0) - 32.0,
                                              24.0 + (sin(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      
      // xz
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", sin(user_glowangle) * 32.0,
                                              0,
                                              24.0 + (cos(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", sin(user_glowangle) * 32.0,
                                              cos(user_glowangle) * 32.0,
                                              24.0 + (sin(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_ItemGlow", ((1.0 - sin(user_glowangle)) * 32.0) - 32.0,
                                              cos(user_glowangle) * 32.0,
                                              24.0 + (sin(user_glowangle) * 32.0),
                                              0, 0, 0, 0)
      loop
   LoopButDontShowThatShitOffDawg:
      TNT1 A 1
      TNT1 A 0 A_CheckSight("LoopButDontShowThatShitOffDawg")
      goto SpawnLoop
   }
}

actor Lith_DivisionSigilSprite : Lith_ItemSprite
{
   var int user_phase;

   const int lvar_phaseiter = 1;
   
   states
   {
   SpawnLoop:
      DIVI A 0 A_SetUserVar(user_phase, (user_phase + 1) % 23)
      DIVI A 0 ACS_NamedExecuteWithResult("LWVarSet", lvar_phaseiter, 0)
      DIVI ABCDEFGHIJKLMNOPQRSTUVW 0 A_JumpIf(user_phase == CallACS("LWVarInc", lvar_phaseiter), "Super::SpawnLoop")
      goto Super::SpawnLoop
   }
}

actor Lith_DivisionSigil : Lith_ItemPickup
{
   //$Category "Items"
   //$Sprite "DIVIA0"
   
   Inventory.Icon "DIVSIGI1"
   Inventory.MaxAmount 1
   Tag "$LITH_INV_SIGIL"
   
   +INVENTORY.INVBAR
   -INVENTORY.PICKUPFLASH
   
   states
   {
   Spawn:
      DIVI A 0
      DIVI A 0 A_SpawnItemEx("Lith_DivisionSigilSprite", 0,0,0, 0,0,0, 0, Lith_ISF)
      goto SpawnLoop
   Pickup:
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_has_sigil), "PickupNope")
      TNT1 A 0 A_PlaySound("player/getsigil", CHAN_AUTO, 1.0, false, ATTN_NONE)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_GetSigil")
      TNT1 A 0 A_RailWait
      stop
   Use:
   PickupNope:
      TNT1 A 0
      fail
   }
}

actor Lith_ClawOfImp : Lith_ItemPickup
{
   Tag "Claw of Imp"
   
   states
   {
   Spawn:
      DITM A 0
      DITM A 0 A_SpawnItemEx("Lith_ItemSprite", 0,0,0, 0,0,0, 0, Lith_ISF)
      goto SpawnLoop
   Pickup:
      TNT1 A 0 A_RailWait
      stop
   }
}
