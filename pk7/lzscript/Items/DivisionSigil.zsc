/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_DivisionSigilSprite : Lith_AnimItemSprite {
   default {
      Lith_AnimItemSprite.Phases 23;
      +BRIGHT
   }

   states(Actor) {
   SetSpr:
      DIVI ABCDEFGHIJKLMNOPQRSTUVW 0 Lith_A_JumpPhase;
      stop;
   }
}

class Lith_DivisionSigil : Lith_BagItemPickup {
   default {
      Tag "Division Sigil";
      Lith_InventoryItem.InvName "DivisionSigil";
      Lith_InventoryItem.Size 3, 3;
      Lith_BagItem.InvType _cont_store;
      Lith_BagItem.InvSize 7, 7;
      +LITH_BAGITEMPICKUP.PKUP_NOSPRITE
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      spawn("Lith_DivisionSigilSprite", pos).master = self;
   }

   override bool canPickup(Actor mo) {
      if(super.canPickup(mo)) {
         return !Lith_UTIL.pData(mo, pdata_hassigil);
      } else {
         return false;
      }
   }

   override bool tryPickup(in out Actor mo) {
      mo.A_StartSound("player/getsigil", lch_body3, attenuation: ATTN_NONE);
      mo.acs_scriptCall("Lith_GetSigil");
      return super.tryPickup(mo);
   }
}

class Lith_ClawOfImp : Lith_InventoryItemPickup {
   default {
      Tag "Claw of Imp";
      Lith_InventoryItem.InvName "ClawOfImp";
      Lith_InventoryItem.Size 4, 2;
      Lith_InventoryItem.InvSell 2000;
      Lith_InventoryItemPickup.Sprite "DITMA";
      +LITH_INVENTORYITEMPICKUP.PKUP_GENERIC
   }
}

class Lith_InventoryItemPickup : Lith_InventoryItem {
   mixin Lith_Pickup;
   mixin Lith_ItemPickup;
   default {
      Lith_InventoryItemPickup.Glow "Lith_ItemGlow";
   }
}

class Lith_BagItemPickup : Lith_BagItem {
   mixin Lith_Pickup;
   mixin Lith_ItemPickup;
   default {
      Lith_BagItemPickup.Glow "Lith_ItemGlow";
   }
}

mixin class Lith_ItemPickup {
   default {
      Inventory.PickupSound "player/pickup/evil";
      Lith_InventoryItem.InvEquip _cont_store;
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      Lith_PickupPostBeginPlay();
   }

   override void tick() {
      super.tick();
      Lith_PickupTick();
   }
}

/* EOF */
