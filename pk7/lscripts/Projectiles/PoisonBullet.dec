actor Lith_PoisonFX
{
   RenderStyle "AddStencil"
   StencilColor "Green"
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      "####" "#" 2
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_PoisonFXTimer : Inventory
{
   Inventory.MaxAmount 20
}

actor Lith_PoisonFXReset : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_PoisonFXGiver : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0 A_DamageSelf(10, "Poison")
      TNT1 A 0 A_PlaySound("weapons/shotgun/poison/acid", CHAN_5, 1, false, 0.5)
      TNT1 A 0 A_SpawnItemEx("Lith_PoisonFX", 0,0,8, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_PoisonFXTicker")
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_PoisonFXGiverGiver : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0
      TNT1 A 0 A_JumpIfInventory("Lith_PoisonFXTimer", 1, "Reset")
   Pickup1:
      TNT1 A 0 A_GiveInventory("Lith_PoisonFXTimer", 20)
      TNT1 A 0 A_GiveInventory("Lith_PoisonFXGiver", 1)
      goto Done
   Reset:
      TNT1 A 0 A_GiveInventory("Lith_PoisonFXReset", 1)
   Done:
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_PoisonBlob
{
   Alpha 1
   Scale 0.5
   Mass 20
   
   +NOBLOCKMAP
   +NOCLIP
   -NOGRAVITY
   
   states
   {
   Spawn:
      BLLT G 5
      BLLT G 0 A_PlaySound("weapons/shotgun/poison/acid", CHAN_5)
   SpawnLoop:
      BLLT G 0 A_JumpIf(z == floorz, "Crash") // ffs
      BLLT G 1 A_FadeOut(0.01)
      loop
   Crash:
      RADB ABCDEFGHIJKLM 1 A_FadeOut(0.01)
      wait
   }
}

actor Lith_PoisonBullet : Lith_BulletPuff
{
   RenderStyle "Add"
   DamageType "Lith_Bullets"
   Species "Lith_Player"
   Alpha 0.9
   
   +DONTHARMSPECIES
   +NOGRAVITY
   +NOBLOCKMAP
   +FLOORCLIP
   +NOEXTREMEDEATH
   +FORCEXYBILLBOARD
   +PUFFGETSOWNER
   +MTHRUSPECIES
   +HITTRACER
   +PUFFONACTORS
   -ALLOWPARTICLES
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_GiveInventory("Lith_PoisonFXGiverGiver", 1, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("NOBLOOD", "Crash", AAPTR_TRACER)
      TNT1 A 0 A_SpawnItemEx("Blood")
      stop
   Crash:
      XPUF Q 0
      XPUF QQQQQ 0 A_SpawnItemEx("Lith_PoisonBlob", 0,0,0, frandom(-1,1),frandom(-1,1),0, 0, 0, 64)
      XPUF Q 0 A_SpawnItemEx("Lith_PuffSmoke", 0, 0, 4.0 + 0.1 * random(-10, 10))
      XPUF Q 0 A_Jump(256, "PuffNormal", "PuffMirrored")
   PuffNormal:
      XPUF Q 0 A_Jump(32, "PuffNormalAlt")
      XPUF QRSTU 1 bright
   PuffNormalEnd:
      XPUF FGH 1 bright
      stop
   PuffNormalAlt:
      XPUF A 0 A_PlaySound("effects/puff/ricochet")
      XPUF ABCDE 1 bright
      goto PuffNormalEnd
   PuffMirrored:
      XPUF V 0 A_Jump(32, "PuffMirroredAlt")
      XPUF VWXYZ 1 bright
   PuffMirroredEnd:
      XPUF NOP 1 bright
      stop
   PuffMirroredAlt:
      XPUF I 0 A_PlaySound("effects/puff/ricochet")
      XPUF IJKLM 1 bright
      goto PuffMirroredEnd
   }
}

// EOF

