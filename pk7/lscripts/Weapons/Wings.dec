actor Lith_HeavenlyImage
{
   RenderStyle "AddStencil"
   StencilColor "White"
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      "####" "#" 3
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_IReallyHateTheseNoisyWings : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 Thing_Hate(0, CallACS("Lith_GetWorldData", wdata_ptid), 0)
      TNT1 A 0 A_Die
      stop
   }
}

actor Lith_WingsOfDeath : Lith_Weapon
{
   Tag "Wings of Death"
   Weapon.SlotNumber 0

   +WEAPON.CHEATNOTWEAPON

   states
   {
   Ready:
      TNT1 A 1 A_WeaponReady(WRF_NOFIRE)
      TNT1 A 0 A_SetBlend("FF FF FF", 0.1, 5)
      TNT1 A 0 A_SpawnItemEx("Lith_HeavenlyImage", 0,-4,0, -5,-7,1, 0, SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION)
      TNT1 A 0 A_SpawnItemEx("Lith_HeavenlyImage", 0, 4,0, -5, 7,1, 0, SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION)
      TNT1 A 0 A_GiveInventory("PowerFlight")
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RadiusGive("Lith_IReallyHateTheseNoisyWings", 1024, RGF_MONSTERS)
      TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 40 == 0, "Flap")
      TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 2  == 0, "Ready")
      TNT1 A 0 A_ChangeVelocity(0, 0, -0.2)
      loop
   Flap:
      TNT1 A 0 A_ChangeVelocity(0, 0, 4)
      TNT1 A 0 A_PlaySound("player/wings", CHAN_WEAPON)
      goto Ready
   Deselect:
      TNT1 A 0 A_TakeInventory("PowerFlight")
      TNT1 A 0 A_Lower
      wait
   Select:
      TNT1 A 0 A_Raise
      wait
   Fire:
      goto Ready
   }
}

// EOF
