/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_AssassinPlayer : Lith_Player {
   default {
      Player.DisplayName "$LITH_CLASS_ASSASSIN";
      Player.SoundClass "assassin";
      Player.Portrait "lgfx/Portraits/Assassin.png";
      Tag "$LITH_CLASS_ASSASSIN";
   }

   int  m_dodgeDbc;
   bool m_isSprinting;
   bool m_wasHorizontalDodge;

   override void giveDefaultInventory() {
      super.giveDefaultInventory();
      lith_giveDefWeapon("Lith_DualPistols");
   }

   override void tick() {
      if(health > 0) {
         if(m_dodgeDbc) {
            if(m_dodgeDbc < 20) {
               vel.x *= 0.9;
               vel.y *= 0.9;
            }
            m_dodgeDbc--;
            if(player && m_dodgeDbc == 15 && player.buttons & BT_SPEED &&
               m_wasHorizontalDodge) {
               m_isSprinting = true;
            }
         } else if(player) {
            if(m_isSprinting) {
               if(!(player.buttons & BT_SPEED)) {
                  m_isSprinting = false;
               }
            } else if(player.buttons & BT_SPEED) {
               let a = angle -
                  atan2(player.cmd.sideMove, player.cmd.forwardMove);
               let c = cos(a) * 20;
               let s = sin(a) * 20;
               a_changeVelocity(c, s);
               a_startSound("player/assassin/roll", lch_body);
               m_dodgeDbc = 40;
               m_wasHorizontalDodge = true;
            } else if(!(player.oldButtons & BT_JUMP) &&
                      player.buttons & BT_JUMP && !player.onGround) {
               vel.z = 16.0;
               a_startSound("player/assassin/djump", lch_body);
               m_dodgeDbc = 40;
               m_wasHorizontalDodge = false;
            }
         }
      }

      super.tick();
   }
}

class Lith_AssassinSprint : PowerSpeed {
   default {
      Powerup.Duration 2;
      Speed 1.5;

      +POWERSPEED.NOTRAIL
   }
}

#include "lzscript/5_Assassin/0_Weapon.zsc"
#include "lzscript/5_Assassin/2_DualPistols.zsc"
#include "lzscript/5_Assassin/4_BurstRifle.zsc"

/* EOF */
