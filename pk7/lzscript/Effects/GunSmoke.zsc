// These are originally from LegenDoom. Thanks, Yholl!
class Lith_GunSmoke : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.25;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;
      
      Projectile;
      +THRUACTORS
      +FORCEXYBILLBOARD
   }
   
   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 {
         A_ScaleVelocity(0.01);
         A_SetScale(scale.x * frandom(0.04, 0.18));
         if(random() < 128)
            A_SetScale(scale.x - (scale.x * 2), scale.y);
      }
      SMOK ABCDEFGHIJKLMNOPQ 1 A_FadeTo(0, 0.01, 1);
      stop;
   }
}

class Lith_GunSmokeEmitter : Actor
{
   default
   {
      Radius 4;
      Height 4;
      Speed 7;
      Damage 0;
      
      Projectile;
      +THRUACTORS
      +NOCLIP
   }

   const m_flags = SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY;
   
   states
   {
   Spawn:
      TNT1 A 1;
   Death:
      TNT1 A 0 {
         for(int i = 0; i < 3; i++)
            A_SpawnItemEx("Lith_GunSmoke", zvel: vel.z, flags: m_flags);
      }
      stop;
   }
}

class Lith_GunSmokeSpawner : Actor
{
   default
   {
      Radius 4;
      Height 4;
      Speed 7;
      Damage 0;
      Lith_GunSmokeSpawner.Particles 4;
      
      Projectile;
      +THRUACTORS
      +NOCLIP
   }

   meta int m_part;

   property Particles: m_part;

   const m_flags = SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY;

   void Lith_A_SpawnSmoke(float vx, float vy)
   {
      A_SpawnItemEx("Lith_GunSmoke", 0, 0, 0, vel.x + vx, vel.y + vy, vel.z, 0, m_flags);
   }

   void Lith_A_SpawnParticles(color c)
   {
      A_SpawnParticle(c, SPF_FULLBRIGHT|SPF_RELATIVE, random(3, 8), random(1, 2), frandom(-15, 15), 0, 0, 0, frandom(1.0, 16.0), frandom(-0.1, 0.1), (vel.z * 1.5) + frandom(-0.5, 0.5), 0.0, 0.0, -0.05, 0.98, -1);
   }
   
   states
   {
   Spawn:
      TNT1 A 1;
   Death:
      TNT1 A 0 {
         Lith_A_SpawnSmoke(0, frandom(-0.2, 0.2));
         for(int i = 0; i < m_part / 2; i++)
            Lith_A_SpawnSmoke(frandom(-0.2, 0.2), frandom(-0.2, 0.2));
         for(int i = 0, j = random(m_part + 1, m_part + 3); i < j; i++)
            Lith_A_SpawnParticles("Orange");
         for(int i = 0, j = random(m_part + 1, m_part + 2); i < j; i++)
            Lith_A_SpawnParticles("Yellow");
         for(int i = 0, j = random(m_part + 0, m_part + 1); i < j; i++)
            Lith_A_SpawnParticles("White");
      }
      stop;
   }
}

class Lith_GunSmokeSpawnerSmall : Lith_GunSmokeSpawner
{
   default
   {
      Lith_GunSmokeSpawner.Particles 1;
   }
}

// EOF
