#include "lscripts/Weapons/RifleBullet.dec"
#include "lscripts/Weapons/RifleGrenade.dec"

actor Lith_RifleBurstIter : Inventory
{
   Inventory.MaxAmount 5
}

actor Lith_CombatRifle : Lith_Weapon replaces Chaingun
{
   Tag "Combat Rifle"
   Weapon.SlotNumber 4
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   OhFuck:
      TNT1 A 1 A_Log("oh god what the fuck")
      stop
   Ready:
      CRIF A 1 A_WeaponReady
      wait
   Deselect:
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
   DeselectLoop:
      CRIF A 1 { A_Lower; A_Lower; }
      loop
   Select:
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
   SelectLoop:
      CRIF A 1 { A_Raise; A_Raise; }
      loop
   Fire:
      CRIF A 0 A_JumpIf(CallACS("Lith_GetRifleFiremode") == rifle_firemode_burst,   "FireBurst")
      CRIF A 0 A_JumpIf(CallACS("Lith_GetRifleFiremode") == rifle_firemode_grenade, "FireGrenade")
      CRIF A 0 A_PlaySound("weapons/rifle/fire", CHAN_WEAPON)
      CRIF A 0 A_FireCustomMissile(
         "Lith_RifleBullet",         frandom(-4, 4), true, 0, 0, 0, frandom(-1.6, 1.6))
      CRIF A 0 A_FireCustomMissile(
         "Lith_RifleBulletPainless", frandom(-4, 4), true, 0, 0, 0, frandom(-1.6, 1.6))
      CRIF B 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF C 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF A 2 A_WeaponReady(LITH_BOBONLY)
      CRIF A 4 A_ReFire
      goto Ready
   FireBurst:
      CRIF A 0 A_PlaySound("weapons/rifle/fire", CHAN_WEAPON)
      CRIF A 0 A_FireCustomMissile(
         "Lith_RifleBullet", frandom(-2, 2), true, 0, 0, 0, frandom(-1.6, 1.6))
      CRIF A 0 A_FireCustomMissile(
         "Lith_RifleBullet", frandom(-2, 2), true, 0, 0, 0, frandom(-1.6, 1.6))
      CRIF B 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF C 1 bright A_WeaponReady(LITH_BOBONLY)
      CRIF A 0 A_GiveInventory("Lith_RifleBurstIter", 1)
      CRIF A 0 A_JumpIf(CountInv("Lith_RifleBurstIter") < 5, "FireBurst")
      CRIF A 0 A_TakeInventory("Lith_RifleBurstIter", 999)
      CRIF A 4 A_WeaponReady(LITH_BOBONLY)
      goto Ready
   FireGrenade:
      CRIF A 3 A_WeaponReady(LITH_BOBONLY)
      CRIF G 2 bright A_FireCustomMissile("Lith_RifleGrenade")
      CRIF H 2 bright
      CRIF I 2 bright
      CRIF A 5
      CRIF A 1 Offset(1, 32) A_PlaySound("weapons/rifle/rldmount", CHAN_WEAPON)
      CRIF A 1 offset(3, 32)
      CRIF A 1 offset(4, 32)
      CRIF A 1 offset(8, 38)
      CRIF A 1 offset(12, 45)
      CRIF A 1 offset(16, 53)
      CRIF A 1 offset(19, 60)
      CRIF A 1 offset(21, 64)
      CRIF A 1 offset(23, 67)
      CRIF A 1 offset(22, 69)
      CRIF A 1 offset(19, 70)
      CRIF A 7 offset(18, 72)
      CRIF A 1 offset(17, 69)
      CRIF A 1 offset(19, 67)
      CRIF A 1 offset(20, 66)
      CRIF A 1 offset(21, 62)
      CRIF A 1 offset(20, 60)
      CRIF A 1 offset(21, 63)
      CRIF A 1 offset(23, 66)
      CRIF A 1 offset(24, 68)
      CRIF A 1 offset(25, 63)
      CRIF A 1 offset(24, 59)
      CRIF A 1 offset(22, 57)
      CRIF A 1 offset(20, 54)
      CRIF A 1 offset(16, 49)
      CRIF A 1 offset(12, 44)
      CRIF A 1 offset(10, 42)
      CRIF A 1 offset(8, 40)
      CRIF A 1 offset(5, 38)
      CRIF A 1 offset(2, 35)
      CRIF A 15 offset(0, 32)
      goto Ready
   AltFire:
      CRIF A 2 A_WeaponReady(LITH_BOBONLY)
      CRIF A 0 ACS_NamedExecuteAlways("Lith_SwitchRifleFiremode", 0)
      CRIF DEF 1 A_WeaponReady(LITH_BOBONLY)
      goto Ready
   Spawn:
      RIFL A 0
      RIFL A 0 A_SetUserVar("user_pickupparm", weapon_combatrifle)
      goto SpawnLoop
   }
}

