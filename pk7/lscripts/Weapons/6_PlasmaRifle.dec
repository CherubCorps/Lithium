#include "lscripts/Weapons/PlasmaBolt.dec"
#include "lscripts/Weapons/PlasmaLaser.dec"

actor Lith_ParticleBeam : Lith_PlasmaLaze
{
   RenderStyle "Add"
   Scale 0.4
   Mass 0
   Translation "112:127=192:207", "224:231=192:194", "160:167=192:207", "249:249=196:196"
   DamageType "Lith_PlayerMissile"
   
   +FORCEXYBILLBOARD
   +NOTIMEFREEZE
   +FORCERADIUSDMG
   
   states
   {
   Spawn:
      BLLT A 0
      BLLT A 2 A_Explode(4, 256, 0)
      BFS1 A 1 A_FadeOut(0.1)
      wait
   }
}

actor Lith_PartBeamShotsFired : Inventory
{
   Inventory.MaxAmount 5
}

actor Lith_PlasmaRifle : Lith_MWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_PlasmaRifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   Weapon.SlotPriority 1
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaRiflePickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      PLSG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER4)
      loop
   
   Deselect:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
   DeselectLoop:
      PLSG AAA 0 A_Lower
      PLSG A 1
      loop
   
   Select:
      PLSG AAA 0 A_Raise
      PLSG A 1
      loop
   
   Fire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PlasLaser), "FireLaser")
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PartBeam),  "FireParticleBeam")
   FireReal:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSG A 0 A_GunFlash("DummyFlash")
      PLSG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      PLSG AAAA 0 A_FireCustomMissile("Lith_PlasmaBolt",
                                      CallACS("Lith_CircleSpread", 5 * IFIX, 3 * IFIX, false) / FIX,
                                      0, 0, -9, 0,
                                      CallACS("Lith_CircleSpread", 0, 0, true) / FIX)
      PLSJ A 1 offset(0, 37) bright
      PLSJ C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      goto Ready
   
   FireLaserReal:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(0, 0, 1, 0, "Lith_PlasmaHit", FBF_NORANDOMPUFFZ)
      TNT1 A 0 A_RailAttack(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_TorgueMode) ? 1 : 15, 0, false, "None", "Red", RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 0, "Lith_Dummy", 0, 0, 0, 1, 3, 0, "Lith_PlasmaLaze")
      stop
   
   FireLaser:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_start")
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_loop", CHAN_7, 0.4, true)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_hum",  CHAN_6, 0.4, true)
      PLSG AA 0 A_GunFlash("FireLaserReal")
      PLSK A 2 offset(0, 35) bright A_GunFlash("FireLaserReal")
   FireLaserLoop:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A  0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSK B  1 offset(0, 39)  bright A_GunFlash("FireLaserReal")
      PLSK BB 1 offset(-2, 38) bright A_GunFlash("FireLaserReal")
      PLSK B  1 offset(1, 37)  bright A_GunFlash("FireLaserReal")
      PLSK B  1 offset(-1, 40) bright A_GunFlash("FireLaserReal")
      PLSK CC 1 offset(1, 37)  bright A_ReFire("FireLaserLoop")
   FireLaserDone:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 offset(1, 36) A_PlaySound("weapons/plasma/laser/fire_end")
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 33)
      PLSG A 1 offset(0, 32)
      PLSG A 1
      goto Ready
   
   FireParticleBeam:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSG A 0 A_JumpIfInventory("Lith_PartBeamShotsFired", 0, "Reload")
      PLSG A 3 A_PlaySound("weapons/plasma/part/charge", CHAN_6)
      PLSG A 3 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 5, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "None", -2)
      PLSG A 3
      PLSG A 3 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 4, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "None", -2)
      PLSG A 2
      PLSG A 3 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 3, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "None", -2)
      PLSG A 1
      PLSG A 3 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 2, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "None", -2)
      PLSG A 1
      PLSG A 3 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 2, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "None", -2)
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 200, TIF_NOTAKEINFINITE)
      PLSG A 0 A_GiveInventory("Lith_PartBeamShotsFired", 1)
      PLSG A 0 A_AlertMonsters
      PLSG A 0 A_PlaySound("weapons/plasma/part/fire", CHAN_WEAPON)
      PLSG A 0 A_RailAttack(300, 0, false, "None", "Blue", RGF_SILENT|RGF_FULLBRIGHT, 0, "Lith_Dummy", 0, 0, 0, 0, 16, 0, "Lith_ParticleBeam", -8)
      PLSF A 1 bright offset(0, 55)
      PLSF A 1 bright offset(0, 45)
      PLSF B 1 bright offset(0, 37)
      PLSF B 1 bright offset(0, 36)
      PLSF C 1 bright offset(0, 35)
      PLSG A 1 offset(0, 35)
      PLSG A 3 offset(0, 34)
      PLSG A 4 offset(0, 33)
      PLSG A 5 offset(0, 32)
      PLSG AAAAA 1 A_ReFire
      goto Ready
   
   FireEmpty:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 A_PlaySound("weapons/plasma/empty", 7)
      PLSG A 1 offset(0, 37)
      PLSG A 1 offset(0, 36)
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG A 1 A_ReFire
      goto Ready
   
   AltFire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PlasLaser), "FireReal")
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PartBeam), "FireReal")
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 90, TIF_NOTAKEINFINITE)
      PLSK A 0 A_GunFlash("DummyFlash")
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", 0, 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(-2.7, -1.9), 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(2.7, 1.9), 0, 0, -9)
      PLSK A 1 offset(0, 37) bright A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON)
      PLSK C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      Goto Ready
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_PartBeamShotsFired", 1, 1)
      goto Ready
      PLSG A 1
      PLSG A 1 offset(-2, 34)
      PLSG A 1 offset(-4, 37)
      PLSG A 1 offset(-9, 43) A_PlaySound("weapons/plasma/part/reloadout", CHAN_6)
      PLSG A 1 offset(-12, 47)
      PLSG A 1 offset(-14, 52)
      PLSG A 2 offset(-12, 55)
      PLSG A 3 offset(-11, 57)
      PLSG A 8 offset(-10, 60)
      PLSG A 8 offset(-11, 60)
      PLSG A 0 A_TakeInventory("Lith_PartBeamShotsFired", 999)
      PLSG A 2 offset(-13, 59) A_PlaySound("weapons/plasma/part/reloadin", CHAN_7)
      PLSG A 2 offset(-11, 49)
      PLSG A 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PLSG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB)
      PLSG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB)
      PLSG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB)
      PLSG A 4 A_WeaponReady(WRF_NOBOB)
      goto Ready
   }
}
