#include "lscripts/Weapons/ShotgunTrail.dec"

actor Lith_GaussShotsFired : Inventory
{
   Inventory.MaxAmount 10
}

actor Lith_ShotgunScopedToken : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_ShotgunJustFrame : Inventory
{
   Inventory.MaxAmount 3
}

actor Lith_GaussExplosionParticle
{
   Scale 0.4
   Mass 3000
   BounceType Doom
   
   +BRIGHT
   +NOBLOCKMAP
   +FORCEXYBILLBOARD
   
   states
   {
   Spawn:
      BLLT BBBCCCDDD 1 A_FadeOut(0.06)
      wait
   }
}

actor Lith_GaussPuff
{
   RenderStyle "Translucent"
   Species "Lith_Player"
   DamageType "Lith_Energy"
   
   +BRIGHT
   +PUFFONACTORS
   +PUFFGETSOWNER
   +NOGRAVITY
   +NOBLOCKMAP
   +FLOORCLIP
   +EXTREMEDEATH
   +FORCEXYBILLBOARD
   +MTHRUSPECIES
   -ALLOWPARTICLES
   
   States
   {
   Crash:
      BLLT A 0
      BLLT A 0 A_PlaySound("weapons/shotgun/gauss/puff")
      BLLT A 0 A_Explode(64, 24, 0)
      BLLT A 1 A_FadeOut(0.05)
      wait
   Spawn:
   Melee:
      BLLT A 0
      BLLT A 0 A_PlaySound("weapons/shotgun/gauss/puff")
      BLLT A 0 A_PlaySound("weapons/shotgun/gauss/spark", CHAN_5)
      BLLT AAAAAAAAA 0 A_SpawnItemEx("Lith_GaussExplosionParticle",
                                     frandom(-4, 4), frandom(-4, 4), frandom(-8, 8),
                                     frandom(-4, 4), frandom(-4, 4), frandom(4, 7),
                                     0, SXF_NOCHECKPOSITION)
      BLLT A 1 A_FadeOut(0.1)
      wait
   }
}

actor Lith_Shotgun : Lith_Weapon replaces Shotgun
{
   Tag "$LITH_TXT_INFO_SHORT_Shotgun"
   Weapon.UpSound "weapons/shotgun/draw"
   Weapon.SlotNumber 3
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   AltReady:
      SCOP A 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
      loop
   Ready:
      PKSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      PKSG A 0 A_TakeInventory("Lith_ShotgunJustFrame", 1)
      loop
   Deselect:
      PKSG A 0 A_TakeInventory("Lith_ShotgunScopedToken", 1)
      PKSG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      PKSG AAA 0 A_Lower
      PKSG A 1
      loop
   Select:
      // fuck youuuuuuuuuuuuuuuuuuuuuuuuuuuuu
      PKSG A 0 A_ZoomFactor(1.0)
      PKSG A 0 A_GiveInventory("Lith_ShotgunJustFrame", 3)
   SelectLoop:
      PKSG AAA 0 A_Raise
      PKSG A 1
      loop
   //
   //
   //
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunJustFrame", 1, "FireJust")
   FireCheck:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_shotgun_gauss), "FireGauss")
      goto FireRail
   FireJust:
      TNT1 A 0 A_TakeInventory("Lith_ShotgunJustFrame", 0x7FFFFFFF)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(5.3, 2.3, 8, 4, "BulletPuff")
      TNT1 A 0 A_PlaySound("weapons/shotgun/just", 7)
      TNT1 A 0 A_RailAttack(70, 0, false, None, LightBlue, RGF_SILENT | RGF_FULLBRIGHT, 128,
                            "BulletPuff", 0, 0, 0, 48, 3, 3)
      goto FireCheck
   FireFinish:
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "AltReady")
      goto Ready
   //
   // Gauss fire
   //
   FireGauss:
      TNT1 A 0 A_JumpIfInventory("Lith_GaussShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_GaussShotsFired", 1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(0, 0, 1, 15, "Lith_GaussPuff")
      TNT1 A 0 A_Quake(1, 5, 0, 64)
      TNT1 A 0 A_RailAttack(10, 0, false, LightBlue, Blue, RGF_NOPIERCING|RGF_FULLBRIGHT, 0,
                            "Lith_Dummy", 0.05, 0.05, 0, 5, frandom(0.35, 1), 0.5)
      TNT1 A 0 A_PlaySound("weapons/shotgun/gauss/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.777)
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "FireFinish_Gauss_Scoped")
      // ---
      PKSG I 1 A_GunFlash("FlashGauss")
      PKSG IJJKJI 1
      PKSG AAAAAAA 1 A_ReFire
      goto FireFinish
   FireFinish_Gauss_Scoped:
      SCOP A 1 bright A_Light(3)
      SCOP A 1 bright A_Light(2)
      SCOP A 0 A_Light(1)
      SCOP A 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP A 0 A_Light0
      SCOP A 4 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP AAAAAAA 1 A_ReFire
      goto FireFinish
   FlashGauss:
      SHTF C 1 Bright A_Light(3)
      SHTF D 1 Bright A_Light(2)
      TNT1 A 1 A_Light(1)
      goto LightDone
   //
   // Rail fire
   //
   FireRail:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(5.3, 2.3, 8, 6, "BulletPuff")
      TNT1 A 0 A_RailAttack(105, 0, true, None, Green, RGF_SILENT | RGF_FULLBRIGHT, 2,
                            "BulletPuff", 1, 1, 0, 0, 0.9, 1, "Lith_ShotgunTrail", 0)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/shotgun/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.7)
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "FireFinish_Rail_Scoped")
      // ---
      PKSG I 1 A_GunFlash("FlashRail")
      PKSG IJJKJI 1
      PKSG B 2
      PKSG C 1
      PKSG DEF 2
      PKSG G 1
      PKSG H 3
      PKSG GFEDC 2
      PKSG B 1
      PKSG A 3
      PKSG AAAAAAA 1 A_ReFire
      goto FireFinish
   FireFinish_Rail_Scoped:
      SCOP A 1 bright A_Light1
      SCOP A 1 bright A_Light2
      SCOP A 2 A_Light0
      SCOP A 24
      SCOP A 5 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP A 0 A_ReFire
      SCOP A 7 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      goto FireFinish
   FlashRail:
      SHTF A 2 Bright A_Light1
      SHTF B 2 Bright A_Light2
      goto LightDone
   //
   // Reload
   //
   Reload:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_shotgun_gauss), 1)
      goto FireFinish
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, 1)
      goto Reload1
      TNT1 A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      TNT1 A 0 A_TakeInventory("Lith_ShotgunScopedToken", 999)
      TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
   Reload1:
      TNT1 A 0 A_JumpIfInventory("Lith_GaussShotsFired", 1, 1)
      goto FireFinish
      PKSG A 1
      PKSG A 1 offset(-2, 34)
      PKSG A 1 offset(-4, 37)
      PKSG A 1 offset(-9, 43) A_PlaySound("weapons/shotgun/gauss/reloadout", CHAN_6)
      PKSG A 1 offset(-12, 47)
      PKSG A 1 offset(-14, 52)
      PKSG A 2 offset(-12, 55)
      PKSG A 3 offset(-11, 57)
      PKSG A 8 offset(-10, 60)
      PKSG A 8 offset(-11, 60)
      PKSG A 0 A_TakeInventory("Lith_GaussShotsFired", 999)
      PKSG A 2 offset(-13, 59) A_PlaySound("weapons/shotgun/gauss/reloadin", CHAN_7)
      PKSG A 2 offset(-11, 49)
      PKSG A 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      PKSG B 2 offset(2, 37)
      PKSG C 1 offset(0, 32) A_PlaySound("weapons/shotgun/open", CHAN_6)
      PKSG DEF 2
      PKSG G 1 A_PlaySound("weapons/shotgun/close", CHAN_7)
      PKSG H 3
      PKSG GFEDC 2
      PKSG B 1
      PKSG A 3
      PKSG A 7 A_ReFire
      goto FireFinish
   //
   //
   //
   AltFire:
      "####" A 1
      "####" A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" A 0 A_TakeInventory("Lith_ShotgunScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      goto Ready
   ScopeIn:
      "####" A 0 A_GiveInventory("Lith_ShotgunScopedToken", 1)
      "####" A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomin", 6)
      goto AltReady
   Spawn:
      SHOT A 0
      SHOT A 0 A_SetUserVar("user_pickupparm", weapon_shotgun)
      goto SpawnLoop
   }
}

