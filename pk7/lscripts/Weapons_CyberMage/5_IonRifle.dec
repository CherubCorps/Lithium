actor Lith_IonRifleFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "112:127=193:197", "160:167=193:199", "224:231=192:198"
   Height 1
   Radius 1
   
   +NOBLOCKMAP
   +NOGRAVITY
}

actor Lith_IonRifleParticle1 : Lith_IonRifleFXBase
{
   Alpha 0.5
   XScale 0.5
   YScale 0.4
   
   states
   {
   Spawn:
      BFE1 ABC 3 bright
   SpawnLoop:
      BFE1 C 0 bright A_SetScale(scalex, scaley * 0.875)
      BFE1 C 1 bright A_FadeOut(0.025)
      loop
   }
}

actor Lith_IonRifleParticle2 : Lith_IonRifleFXBase
{
   Alpha 0.5
   XScale 0.5
   YScale 0.4
   
   states
   {
   Spawn:
      BFE2 ABC 3 bright
   SpawnLoop:
      BFE2 C 0 bright A_SetScale(scalex, scaley * 0.875)
      BFE2 C 1 bright A_FadeOut(0.025)
      loop
   }
}

actor Lith_IonizedFX
{
   RenderStyle "AddStencil"
   StencilColor "Red"
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      "####" "#" 2
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_IonizedFXSpawner : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_SpawnItemEx("Lith_IonizedFX", 0,0,0, 0,0,-1, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME)
      stop
   }
}

actor Lith_Ionized : PowerProtection // HAH NOT SO PROTECTED NOW ARE YOU
{
   DamageFactor "Normal", 1.5
   Powerup.Duration -5
}

actor Lith_IonRifleExplosion
{
   Radius 0
   Height 0
   RenderStyle "Add"
   DamageType "Lith_Plasma"
   
   +NOBLOCKMAP
   +NOGRAVITY
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1 A_Explode(96, 256, 0)
      TNT1 A 0 A_JumpIf(!CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_IonRifle_A, true), 2)
      TNT1 A 0 A_RadiusGive("Lith_Ionized", 192, RGF_MONSTERS, 1)
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_IonRifleParticle1", frandom(-32, 32),frandom(-32, 32),frandom(-32, 32), frandom(-1, 1),frandom(-1, 1),frandom(-1, 1))
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_IonRifleParticle2", frandom(-24, 24),frandom(-24, 24),frandom(-24, 24), frandom(-0.5, 0.5),frandom(-0.5, 0.5),frandom(-0.5, 0.5))
      stop
   }
}

actor Lith_IonRifleHax : FastProjectile
{
   Speed 32000
   Damage 100
   Height 1
   Radius 1
   DamageType "Lith_Plasma"
   
   +NOGRAVITY
   +NOBLOCKMAP
   +THRUACTORS
   +EXTREMEDEATH
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 1
      loop
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_IonRifleExplosion", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
      stop
   }
}

actor Lith_IonRifleParticle
{
   RenderStyle "Add"
   Scale 0.05
   
   +NOINTERACTION
   +BRIGHT
   
   states
   {
   Spawn:
      BLLT K 10
   SpawnLoop:
      BLLT K 0 A_SetScale(scalex * 1.025)
      BLLT K 1 A_FadeOut(0.075)
      loop
   }
}

actor Lith_IonRifleShotsFired : Inventory
{
   Inventory.MaxAmount 6
}

actor Lith_IonRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_IonRifle"
   Weapon.UpSound "weapons/ionrifle/draw"
   Weapon.SlotNumber 5
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE

   const int lvar_charge = 1;
   
   states
   {
   AltReady:
      SCOP C 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   Ready:
      IONG A 1 A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   ReadySelector:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "AltReady")
      goto Ready
   
   Deselect:
      IONG A 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      IONG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      IONG AAA 0 A_Lower
      IONG A 1
      loop
   
   Select:
      IONG A 0 A_ZoomFactor(1.0)
   SelectLoop:
      IONG AAA 0 A_Raise
      IONG A 1
      loop
   
   Fire:
      "####" "#" 0 A_JumpIfInventory("Lith_RocketAmmo", 1, 1)
      goto FireEmpty
      "####" "#" 0 A_JumpIfInventory("Lith_IonRifleShotsFired", 0, "Reload")
      "####" "#" 0 ACS_NamedExecuteWithResult("LPVarSet", lvar_charge, 1*IFIX)
      "####" "#" 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_IonRifle_B), "Charge1")
      goto DoFire
      // HELP
   Charge1:
      "####" "#" 2
      "####" "#" 1 A_ReFire("Charge2")
      goto DoFire
   Charge2:
      "####" "#" 0 A_PlaySound("weapons/ionrifle/charge", CHAN_WEAPON)
      "####" "#" 2 offset(0, 33) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.05*FIX)
      "####" "#" 1 A_ReFire("Charge3")
      goto DoFire
   Charge3:
      "####" "#" 2 offset(-1, 34) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.05*FIX)
      "####" "#" 1 A_ReFire("Charge4")
      goto DoFire
   Charge4:
      "####" "#" 2 offset(0, 35) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.07*FIX)
      "####" "#" 1 A_ReFire("Charge5")
      goto DoFire
   Charge5:
      "####" "#" 2 offset(1, 36) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.07*FIX)
      "####" "#" 1 A_ReFire("Charge6")
      goto DoFire
   Charge6:
      "####" "#" 2 offset(0, 37) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.09*FIX)
      "####" "#" 1 A_ReFire("Charge7")
      goto DoFire
   Charge7:
      "####" "#" 2 offset(0, 38) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.11*FIX)
      "####" "#" 1 A_ReFire("Charge8")
      goto DoFire
   Charge8:
      "####" "#" 2 offset(2, 38) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.13*FIX)
      "####" "#" 1 A_ReFire("Charge9")
      goto DoFire
   Charge9:
      "####" "#" 2 offset(0, 39) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.15*FIX)
      "####" "#" 1 A_ReFire("Charge10")
      goto DoFire
   Charge10:
      "####" "#" 2 offset(-2, 40) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.16*FIX)
      "####" "#" 1 A_ReFire("Charge11")
      goto DoFire
   Charge11:
      "####" "#" 2 offset(2, 40) ACS_NamedExecuteWithResult("LPVarAdd", lvar_charge, 0.17*FIX)
   DoFire:
      TNT1 A 0 A_GiveInventory("Lith_IonRifleShotsFired", 1)
      TNT1 A 0 A_TakeInventory("Lith_RocketAmmo", 1, TIF_NOTAKEINFINITE)
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_FireCustomMissile("Lith_IonRifleHax")
      TNT1 A 0 A_JumpIf(CallACS("LPVarGet", lvar_charge)/FIX < 1.5, 2)
      TNT1 A 0 A_FireCustomMissile("Lith_IonRifleHax")
      TNT1 A 0 A_JumpIf(CallACS("LPVarGet", lvar_charge)/FIX < 2, 2)
      TNT1 A 0 A_FireCustomMissile("Lith_IonRifleHax")
      TNT1 A 0 A_RailAttack(CallACS("LPVarGet", lvar_charge)/FIX * 300, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 0, 16, 0, "Lith_IonRifleParticle")
      TNT1 A 0 A_RailAttack(0, 0, false, "None", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "BulletPuff", 0, 0, 0, 0, 0.5, 0)
      TNT1 A 0 A_Recoil(CallACS("LPVarGet", lvar_charge)/FIX * 2)
      TNT1 A 0 A_JumpIf(CallACS("LPVarGet", lvar_charge)/FIX < 1.5, "Sound1")
      TNT1 A 0 A_PlaySound("weapons/ionrifle/cfire", CHAN_7)
      TNT1 A 0 A_JumpIf(CallACS("LPVarGet", lvar_charge)/FIX < 2, "Sound1")
      TNT1 A 0 A_PlaySound("weapons/ionrifle/fire2", CHAN_WEAPON)
      goto SoundDone
   Sound1:
      TNT1 A 0 A_PlaySound("weapons/ionrifle/fire", CHAN_WEAPON)
   SoundDone:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FireAnim_Scoped")
   FireAnim_Unscoped:
      IONG C 2 bright
      IONG B 2 bright
      IONG D 1 bright
      IONG E 1 bright
      IONG FGA 1
      goto ChillCheck
   FireAnim_Scoped:
      SCOP C 1 bright A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.80)
      SCOP C 1 bright A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.85)
      SCOP C 1 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.90)
      SCOP C 1 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.95)
      SCOP C 3 A_ZoomFactor(1 +  CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
   ChillCheck:
      "####" "#" 0 A_JumpIf(CallACS("LPVarGet", lvar_charge)/FIX < 1.3, "BoltAnim")
      "####" "#" 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "ChillOut1")
      "####" "#" 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      "####" "#" 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" "#" 0 A_PlaySound("weapons/ionrifle/zoomout", 7)
   ChillOut1:
      "####" "#" 1 offset(-1, 33) A_PlaySound("weapons/ionrifle/heat", CHAN_7)
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 2 offset(-4, 34)
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 2 offset(-9, 37)
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 3 offset(-11, 39)
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 4 offset(-12, 40)
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 5 offset(-13, 41)
   ChillOut2:
      "####" "#" 0 A_FireCustomMissile("Lith_GunSmokeEmitter", 0, 0, 2, 5)
      "####" "#" 2 A_JumpIf(CallACS("LPVarSub", lvar_charge, 0.06*FIX)/FIX < 1, "ChilledOut")
      loop
   ChilledOut:
      "####" "#" 4 offset(-12, 40)
      "####" "#" 3 offset(-11, 39)
      "####" "#" 2 offset(-9, 37)
      "####" "#" 2 offset(-4, 34)
      "####" "#" 1 offset(-1, 33)
      goto BoltAnim
   BoltAnim:
      "####" "#" 1 offset(1, 32)
      "####" "#" 1 offset(3, 32)
      "####" "#" 1 offset(4, 32)
      "####" "#" 1 offset(8, 38)
      "####" "#" 1 offset(12, 45)
      "####" "#" 1 offset(16, 53)
      "####" "#" 1 offset(19, 60) A_PlaySound("weapons/ionrifle/eject", CHAN_5)
      "####" "#" 1 offset(21, 64)
      "####" "#" 1 offset(23, 67)
      "####" "#" 1 offset(22, 74)
      "####" "#" 1 offset(19, 78)
      "####" "#" 5 offset(18, 80)
      "####" "#" 5 offset(17, 80)
      "####" "#" 5 offset(16, 80) A_PlaySound("weapons/ionrifle/load", CHAN_6)
      "####" "#" 1 offset(17, 74)
      "####" "#" 1 offset(19, 67)
      "####" "#" 1 offset(20, 66)
      "####" "#" 1 offset(21, 62)
      "####" "#" 1 offset(20, 60)
      "####" "#" 1 offset(21, 63)
      "####" "#" 1 offset(23, 66)
      "####" "#" 1 offset(24, 68)
      "####" "#" 1 offset(25, 63)
      "####" "#" 1 offset(24, 59)
      "####" "#" 1 offset(22, 57)
      "####" "#" 1 offset(20, 54)
      "####" "#" 1 offset(16, 49)
      "####" "#" 1 offset(12, 44)
      "####" "#" 1 offset(10, 42)
      "####" "#" 1 offset(8, 40)
      "####" "#" 1 offset(5, 38)
      "####" "#" 1 offset(2, 35)
      "####" "#" 5 offset(1, 32)
      "####" "#" 0 A_JumpIfInventory("Lith_IonRifleShotsFired", 0, "Reload")
      goto ReadySelector
   AltFire:
      "####" "#" 1
      "####" "#" 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" "#" 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      "####" "#" 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" "#" 0 A_PlaySound("weapons/ionrifle/zoomout", 7)
      goto Ready
   ScopeIn:
      "####" "#" 0 A_GiveInventory("Lith_WeaponScopedToken", 1)
      "####" "#" 0 A_ZoomFactor(1 + (CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX))
      "####" "#" 0 A_PlaySound("weapons/ionrifle/zoomin", 6)
      goto AltReady
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, 1)
      goto Reload1
      TNT1 A 0 A_PlaySound("weapons/ionrifle/zoomout", 7)
      TNT1 A 0 A_TakeInventory("Lith_WeaponScopedToken", 999)
      TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
   Reload1:
      TNT1 A 0 A_JumpIfInventory("Lith_IonRifleShotsFired", 1, 1)
      goto ReadySelector
      IONG A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-2, 34) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-4, 37) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-9, 43) A_PlaySound("weapons/ionrifle/magout", CHAN_6)
      IONG A 1 offset(-12, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-14, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 2 offset(-12, 55) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 3 offset(-11, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 8 offset(-10, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 8 offset(-11, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 8 offset(-12, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 0 A_TakeInventory("Lith_IonRifleShotsFired", 999)
      IONG A 2 offset(-13, 59) A_PlaySound("weapons/ionrifle/magin", CHAN_7)
      IONG A 2 offset(-11, 49)
      IONG A 2 offset(-11, 47)
      IONG A 7 offset(-11, 45)
      IONG A 7 offset(-12, 45)
      IONG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      IONG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      IONG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      IONG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      IONG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
      IONG A 4 A_WeaponReady(WRF_NOBOB)
      goto ReadySelector
   
   FireEmpty:
      "####" "#" 1
      "####" "#" 2 offset(0, 41)
      "####" "#" 1 offset(0, 45)
      "####" "#" 1 offset(0, 43)
      "####" "#" 1 offset(0, 40) A_PlaySound("weapons/ionrifle/empty", CHAN_5)
      "####" "#" 1 offset(0, 38)
      "####" "#" 1 offset(0, 36)
      "####" "#" 1 offset(0, 33)
      "####" "#" 1 offset(0, 32)
      "####" "#" 5
      "####" "####" 1 A_ReFire
      goto ReadySelector
   }
}

