actor Lith_Player : DoomPlayer
{
   Player.SideMove 2, 1
   Player.ForwardMove 2, 1
   Player.AirCapacity 0
   DamageFactor "Lith_PlayerMissile", 0
   Species "Lith_Player"
   
   states
   {
   Pain:
   Pain.Bullet:
   Pain.Lith_Bullets:   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 0) goto Super::Pain
   Pain.Plasma:
   Pain.Railgun:
   Pain.Electric:
   Pain.Lith_Energy:    TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 1) goto Super::Pain
   Pain.Fire:
   Pain.Lith_Fire:      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 2) goto Super::Pain
   Pain.Lith_FireMagic: TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 3) goto Super::Pain
   Pain.Lith_Magic:     TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 4) goto Super::Pain
   Pain.Melee:
   Pain.Lith_Melee:     TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 5) goto Super::Pain
   Pain.Lith_Shrapnel:  TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 6) goto Super::Pain
   Pain.Ice:            TNT1 A 0 ACS_NamedExecuteWithResult("Lith_RA_Give", 7) goto Super::Pain
   }
}

actor Lith_MarinePlayer : Lith_Player
{
   Player.StartItem "Lith_Pistol"
   Player.StartItem "Lith_Fist"
   Player.DisplayName "Marine"
   Player.SoundClass "marine"
   Player.Portrait "LITHMPT1"
   Player.ViewHeight 48
   Player.AttackZOffset 15
}

actor Lith_CyberMagePlayer : Lith_Player
{
   Player.StartItem "Lith_Mateba"
   Player.StartItem "Lith_CFist"
   Player.DisplayName "Cyber-Mage"
   Player.SoundClass "cybermage"
   Player.Portrait "LITHCPT1"
   Player.ViewHeight 45
   Player.AttackZOffset 12
   Player.JumpZ 9
}

actor Lith_RA_Hax : PowerProtection
{
   Powerup.Duration 0x7FFFFFFF
   ActiveSound "player/rarmor/damage"
}

actor Lith_RA_Bullets1   : Lith_RA_Hax {DamageFactor "Lith_Bullets",   0.3 DamageFactor "Bullet", 0.3}
actor Lith_RA_Energy1    : Lith_RA_Hax {DamageFactor "Lith_Energy",    0.3 DamageFactor "Railgun", 0.3 DamageFactor "Electric", 0.3 DamageFactor "Plasma", 0.3}
actor Lith_RA_Fire1      : Lith_RA_Hax {DamageFactor "Lith_Fire",      0.3 DamageFactor "Fire", 0.3}
actor Lith_RA_FireMagic1 : Lith_RA_Hax {DamageFactor "Lith_FireMagic", 0.3}
actor Lith_RA_Magic1     : Lith_RA_Hax {DamageFactor "Lith_Magic",     0.3}
actor Lith_RA_Melee1     : Lith_RA_Hax {DamageFactor "Lith_Melee",     0.3 DamageFactor "Melee", 0.3}
actor Lith_RA_Shrapnel1  : Lith_RA_Hax {DamageFactor "Lith_Shrapnel",  0.3}
actor Lith_RA_Ice1       : Lith_RA_Hax {DamageFactor "Ice",            0.3}

actor Lith_RA_Bullets2   : Lith_RA_Hax {DamageFactor "Lith_Bullets",   0.15 DamageFactor "Bullet", 0.15}
actor Lith_RA_Energy2    : Lith_RA_Hax {DamageFactor "Lith_Energy",    0.15 DamageFactor "Railgun", 0.15 DamageFactor "Electric", 0.15 DamageFactor "Plasma", 0.15}
actor Lith_RA_Fire2      : Lith_RA_Hax {DamageFactor "Lith_Fire",      0.15 DamageFactor "Fire", 0.15}
actor Lith_RA_FireMagic2 : Lith_RA_Hax {DamageFactor "Lith_FireMagic", 0.15}
actor Lith_RA_Magic2     : Lith_RA_Hax {DamageFactor "Lith_Magic",     0.15}
actor Lith_RA_Melee2     : Lith_RA_Hax {DamageFactor "Lith_Melee",     0.15 DamageFactor "Melee", 0.15}
actor Lith_RA_Shrapnel2  : Lith_RA_Hax {DamageFactor "Lith_Shrapnel",  0.15}
actor Lith_RA_Ice2       : Lith_RA_Hax {DamageFactor "Ice",            0.15}

actor Lith_RocketBooster : PowerFlight
{
   Powerup.Duration 60
   -INVENTORY.HUBPOWER
}

actor Lith_PuffItDownBro // Taken from DemonSteele
{
   RenderStyle Add
   Alpha 0.5
   Height 0
   Radius 0
   Scale 0.5
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK ABDEGHJKMNPQ 2 bright
      stop
   }
}

actor Lith_ExplodoBoots
{
   DamageType "Lith_PlayerMissile"
   Height 0
   Radius 0
   
   +NOTIMEFREEZE
   +NOGRAVITY
   +NOBLOCKMAP
   +PAINLESS
   +NODAMAGETHRUST
   +EXTREMEDEATH
   +FORCERADIUSDMG
   
   states
   {
   Spawn:
      TNT1 A 0
      // Taken from DemonSteele
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.0)*64.0, cos(0.0)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.0)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.1)*64.0, cos(0.1)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.1)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.2)*64.0, cos(0.2)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.2)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.3)*64.0, cos(0.3)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.3)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.4)*64.0, cos(0.4)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.4)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.5)*64.0, cos(0.5)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.5)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.6)*64.0, cos(0.6)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.6)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.7)*64.0, cos(0.7)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.7)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.8)*64.0, cos(0.8)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.8)
      TNT1 A 0 A_SpawnItemEx("Lith_PuffItDownBro", sin(0.9)*64.0, cos(0.9)*64.0, -1, 6.0, 0.0, 1.0, 360.0*0.9)
      TNT1 A 0 A_PlaySound("player/hitground", 0, 0.5, false, 0.5)
      TNT1 A 0 Radius_Quake(2, 8, 0, 16, 0)
      TNT1 A 0 A_Explode(100, 64, 0, 0, 64)
      stop
   }
}

actor Lith_EXPLOOOSION
{
   DamageType "Lith_PlayerMissile"
   
   +NOTIMEFREEZE
   +NOGRAVITY
   
   states
   {
   Spawn:
      MEXP A  0 nodelay A_Explode
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_GEExplosion
{
   DamageType "Lith_PlayerMissile"
   
   +NOTIMEFREEZE
   +NOGRAVITY
   
   states
   {
   Spawn:
      MEXP A 0 bright
      MEXP A 0 bright A_PlaySound("effects/geexplosion")
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_Death : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfHealthLower(1000, 2)
      TNT1 A 0 A_RailWait
      stop
      TNT1 A 0 A_Die
      stop
   }
}

actor Lith_Die : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_Die
      stop
   }
}

actor Lith_Nuke : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_PlaySound("player/nuke", CHAN_7, 1.0, false, ATTN_NONE)
      TNT1 A 0 A_RadiusGive("Lith_Death", 6144, RGF_MONSTERS)
      TNT1 A 0 A_AlertMonsters
      stop
   }
}

actor Lith_Blood : Blood replaces Blood
{
   states
   {
   Spawn:
      BLUD CBA 8
      BLUD A 0 A_RadiusGive("Lith_SpectralSteggles", 64, RGF_PLAYERS)
      stop
   }
}

actor Lith_ManaLeak
{
   VSpeed 2
   Scale 0.1
   RenderStyle "Add"
   
   +NOTIMEFREEZE
   +NOINTERACTION
   
   var int user_x;
   var int user_y;
   var int user_z;
   
   states
   {
   Spawn:
      BLLT J 0
      BLLT J 0 A_Warp(AAPTR_MASTER, user_x, user_y, z-user_z, 0, WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
      BLLT J 1 A_FadeOut
      loop
   }
}

actor Lith_StealthSystem : PowerInvisibility
{
   Powerup.Duration 0x7FFFFFFF
   Powerup.Mode "Opaque"
}

// EOF

