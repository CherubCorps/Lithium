actor Lith_CannonBall
{
   // Some actor properties taken from Error: Doom
   Radius 51
   Height 32
   Scale 3.5
   Damage (80)
   Speed 42
   Gravity 0.55
   BounceType Doom
   DamageType "PlayerMissile"
   SeeSound "weapons/cannon/fire"
   DeathSound "weapons/cannon/explode"
   
   Projectile
   +RIPPER
   +BOUNCEONACTORS
   +BOUNCEAUTOOFFFLOORONLY
   -NOGRAVITY
   
   var int user_angle;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SetUserVar(user_angle, angle * 65536.0)
      TNT1 A 2
   SpawnLoop:
      CBAL A 1 bright A_SetAngle(user_angle / 65536.0)
      loop
   Death:
      CBAL B 0
      {
         A_SetScale(2);
         A_NoGravity;
         return state("Splodey");
      }
   Splodey:
      CBAL BBBB 0 A_Explode
      goto Death1
   Death1:
      CBAL B 8 bright
      CBAL C 6 bright
      CBAL D 4 bright
      stop
   }
}

actor Lith_SmallCannonBall : Lith_CannonBall
{
   Radius 29
   Height 16
   Scale 2
   Damage (40)
   
   states
   {
   Splodey:
      CBAL BB 0 A_Explode
      goto Death1
   }
}

