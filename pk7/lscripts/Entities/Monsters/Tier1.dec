actor Lith_ZombieMan : ZombieMan replaces ZombieMan
{
   AttackSound ""
   DropItem "Lith_Dummy"
   MinMissileChance 150
   Speed 9
   Tag "$LITH_TXT_INFO_SHORT_ZombieMan"
   
   var int user_shots;
   
   states
   {
   Missile:
      POSS E 0 A_SetUserVar(user_shots, random(1, 3))
      POSS E 2 A_FaceTarget
      POSS E 0 A_PlaySound("enemies/zombie/ready", CHAN_BODY|CHAN_NOPAUSE)
      POSS E 0 A_GiveToTarget("Lith_AdrenalineToken", 1)
      POSS E 5 A_FaceTarget
   MissileLoop:
      POSS E 5 A_FaceTarget
      POSS F 0 bright A_PlaySound("enemies/zombie/fire", CHAN_WEAPON)
      POSS F 0 bright A_JumpIfInTargetInventory("Lith_HasBarrier", 1, 2)
      POSS F 0 A_Jump(256, "MissileFire")
      POSS F 4 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      goto MissileDone
   MissileFire:
      POSS F 4 bright A_CustomBulletAttack(22.5, 5, 1, random(1, 4) * 3, "BulletPuff", 0, CBAF_NORANDOM)
      goto MissileDone
   MissileDone:
      POSS E 0 A_SetUserVar(user_shots, user_shots - 1)
      POSS E 0 A_JumpIf(user_shots == 0, "MissileEnd")
      goto MissileLoop
   MissileEnd:
      POSS E 8
      goto See
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ZombieMan * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ZombieMan)
      goto Super::Death
   }
}

actor Lith_ShotgunGuy : ShotgunGuy replaces ShotgunGuy
{
   AttackSound ""
   DropItem "Lith_Dummy"
   MinMissileChance 150
   Speed 9
   Tag "$LITH_TXT_INFO_SHORT_ShotgunGuy"
   
   states
   {
   Missile:
      SPOS E 2 A_FaceTarget
      SPOS E 0 A_PlaySound("enemies/shotguy/ready", CHAN_BODY|CHAN_NOPAUSE)
      SPOS E 0 A_GiveToTarget("Lith_AdrenalineToken", 1)
      SPOS E 10 A_FaceTarget
      SPOS F 0 bright A_PlaySound("enemies/shotguy/fire", CHAN_WEAPON)
      SPOS F 0 bright A_JumpIfInTargetInventory("Lith_HasBarrier", 1, 2)
      SPOS F 0 A_Jump(256, "MissileFire")
      SPOS F 0 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      goto MissileDone
   MissileFire:
      SPOS F 0 A_CustomBulletAttack(22.5, 4, 3, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
      goto MissileDone
   MissileDone:
      SPOS F 10 bright
      SPOS E 10
      goto See
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ShotgunGuy * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ShotgunGuy)
      goto Super::Death
   }
}

actor Lith_ImpBall : DoomImpBall
{
   Speed 16
   FastSpeed 32
   Scale 1.35
   DamageType "Lith_Fire"
   
   states
   {
   Spawn:
      BAL1 AAAABBBB 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   Death:
      BAL1 A 0 A_SetScale(1.0)
      BAL1 CDE 6 bright
      stop
   }
}

actor Lith_Imp : DoomImp replaces DoomImp
{
   Speed 9
   Health 79
   MinMissileChance 160
   Scale 0.9
   Radius 18
   Height 50
   Tag "$LITH_TXT_INFO_SHORT_Imp"
   
   states
   {
   Spawn:
      TROO AB 10 A_Look
      loop
   See:
      TROO AABBCCDD 2 A_Chase
      loop
   Melee:
   Missile:
      TROO EF 6 A_FaceTarget
      TROO G 6 A_CustomComboAttack("Lith_ImpBall", 32, 3 * random(1, 8), "imp/melee")
      TROO G 2
      goto See
   Pain:
      TROO H 2
      TROO H 2 A_Pain
      TROO H 0 A_Jump(256, "See")
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Imp * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Imp)
      goto Super::Death
   Raise:
      TROO ML 8
      TROO KJI 6
      TROO A 0 A_Jump(256, "See")
   }
}

actor Lith_ChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
   DropItem "Lith_Dummy"
   Speed 9
   Tag "$LITH_TXT_INFO_SHORT_ChaingunGuy"
   
   states
   {
   Missile:
      CPOS E 2 A_FaceTarget
      CPOS E 0 A_PlaySound("enemies/chainguy/bolt", CHAN_WEAPON|CHAN_NOPAUSE)
      CPOS E 0 A_GiveToTarget("Lith_AdrenalineToken", 1)
      CPOS E 12 A_FaceTarget
   MissileLoop:
      CPOS F 0 bright A_JumpIfInTargetInventory("Lith_HasBarrier", 1, 2)
      CPOS F 0 A_Jump(256, "MissileFire")
      CPOS F 0 A_PlaySound("enemies/chainguy/fire")
      CPOS F 4 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      CPOS E 0 A_PlaySound("enemies/chainguy/fire")
      CPOS E 4 bright ACS_NamedExecuteAlways("Lith_BarrierBullets", 0)
      goto MissileDone
   MissileFire:
      CPOS FE 4 bright A_CustomBulletAttack(22.5, 0, 1, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
      goto MissileDone
   MissileDone:
      CPOS F 1 A_CPosRefire
      goto MissileLoop
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ChaingunGuy * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_ChaingunGuy)
      goto Super::Death
   }
}

// EOF

