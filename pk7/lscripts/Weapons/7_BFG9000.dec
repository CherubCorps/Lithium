actor Lith_CannonBall
{
   // Some actor properties taken from Error: Doom
	Radius 64
	Height 64
	Scale 4
	Damage (50)
	Speed 40
	Gravity 0.66
	BounceType Doom
   DamageType "PlayerMissile"
   SeeSound "weapons/cannon/fire"
   DeathSound "weapons/cannon/explode"
   
	Projectile
	+RIPPER
	-NOGRAVITY
   
   var int user_angle;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SetUserVar(user_angle, angle * 65536.0)
      TNT1 A 2
   SpawnLoop:
      CBAL A 1 bright A_SetAngle(user_angle / 65536.0)
      loop
   Death:
      CBAL B 0 {
         A_SetScale(2);
         A_NoGravity;
      }
      CBAL BBBB 0 A_Explode
      CBAL B 8 bright
      CBAL C 6 bright
      CBAL D 4 bright
      stop
   }
}

actor Lith_SmallCannonBall : Lith_CannonBall
{
	Radius 32
	Height 32
   Scale 2
	Damage (25)
   
   states
   {
   Death:
      CBAL B 0 {
         A_SetScale(2);
         A_NoGravity;
      }
      CBAL BB 0 A_Explode
      CBAL B 8 bright
      CBAL C 6 bright
      CBAL D 4 bright
      stop
   }
}

actor Lith_BFG9000 : Lith_Weapon replaces BFG9000
{
   Tag "BFG9000"
   Inventory.PickupMessage "You got the Biffgee Nintendo!"
   Weapon.SlotNumber 7
   Weapon.BobStyle InverseSmooth
   Weapon.BobRangeX 2.0
   Weapon.BobSpeed 0.85
   
   states
   {
   Ready:
      CNNG A 1 A_WeaponReady
      wait
   Deselect:
      CNNG A 1 A_Lower
      loop
   Select:
      CNNG A 1 A_Raise
      loop
   Fire:
      CNNG A 0 A_JumpIf(CallACS("Lith_FireScore", 30000), "FireEmpty")
      CNNG A 15 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      CNNG B 2 bright
      CNNG B 2 bright offset(0, 35)
      CNNG B 2 bright offset(0, 48)
      CNNG C 2 bright offset(0, 49) A_FireCustomMissile("Lith_SmallCannonBall", 0, 1)
      CNNG C 2 bright offset(0, 47)
      CNNG C 2 bright offset(0, 37)
      CNNG A 10 offset(0, 32) A_ReFire
      goto Ready
   AltFire:
      CNNG A 0 A_JumpIf(CallACS("Lith_FireScore", 60000), "FireEmpty")
      CNNG A 20 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      CNNG B 2 bright
      CNNG B 2 bright offset(0, 29)
      CNNG B 2 bright offset(0, 35)
      CNNG B 2 bright offset(0, 45)
      CNNG B 2 bright offset(0, 48)
      CNNG C 2 bright offset(0, 49) A_FireCustomMissile("Lith_CannonBall", 0, 1)
      CNNG C 2 bright offset(0, 47)
      CNNG C 2 bright offset(0, 44)
      CNNG C 2 bright offset(0, 37)
      CNNG C 2 bright offset(0, 32)
      CNNG A 20 offset(0, 32) A_ReFire
      goto Ready
   FireEmpty:
      CNNG A 2 A_PlaySound("weapons/cannon/empty", 7)
      CNNG A 2 offset(0, 29)
      CNNG A 2 offset(0, 35)
      CNNG A 2 offset(0, 45)
      CNNG A 2 offset(0, 44)
      CNNG A 2 offset(0, 37)
      CNNG A 2 offset(0, 32)
      CNNG A 1 A_ReFire
      goto Ready
   Spawn:
      BFUG A 0
      BFUG A 0 A_SetUserVar("user_pickupparm", weapon_bfg9000)
      goto SpawnLoop
   }
}
