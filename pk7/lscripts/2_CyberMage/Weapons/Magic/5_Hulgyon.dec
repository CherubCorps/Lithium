actor Lith_HulgyonEffect
{
   RenderStyle "Add"
   
   +NOGRAVITY
   +THRUACTORS
   +NODAMAGETHRUST
   +BRIGHT
   
   states
   {
   Spawn:
      BLLT P 0
      BLLT P 0 A_Explode(48, 90, 0)
      BLLT P 5
      BLLT P 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_HulgyonHit
{
   DamageType "Lith_Magic"
   
   +NOGRAVITY
   +THRUACTORS
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_PlaySound("weapons/hulgyon/proc")
      TNT1 A 1 A_SpawnItemEx("Lith_HulgyonEffect", 0,0, 0, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonEffect", 0,0, 4, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 1 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,10, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,14, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 1 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,20, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,24, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 1 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,30, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,34, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 1 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,40, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonEffect", 0,0,44, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      stop
   }
}

actor Lith_HulgyonProjectile
{
   Damage (0)
   Speed 30
   RenderStyle "Add"
   XScale 4
   Radius 4
   Height 32
   
   Projectile
   +FLOORHUGGER
   +RIPPER
   +BRIGHT
   
   var int user_lastz;
   var int user_curz;
   
   const int maxstep = 32;
   
   states
   {
   Spawn:
      BLLT J 0
      BLLT J 0 A_SetUserVar(user_lastz, z)
      
   SpawnLoop:
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      
      BLLT J 0 A_SpawnItemEx("Lith_HulgyonHit", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS)
      
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      
      BLLT J 0 A_SetUserVar(user_curz, z)
      BLLT J 1 A_JumpIf(z - user_lastz > maxstep || z - user_lastz < -maxstep, "Death")
      BLLT J 0 A_SetUserVar(user_lastz, user_curz)
      loop
   Death:
      TNT1 A 0
      stop
   }
}

actor Lith_HulgyonSprite
{
   RenderStyle "Add"
   Scale 0.3

   +NOTIMEFREEZE
   +NOINTERACTION

   states
   {
   Spawn:
      BLLT T 0
      BLLT T 0 A_Warp(AAPTR_MASTER, x,y,z, 0, WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET|WARPF_COPYINTERPOLATION)
   Spawn1:
      BLLT T 1 bright
      stop
   }
}

actor Lith_HulgyonSprite2 : Lith_HulgyonSprite
{
   Alpha 0.3

   states
   {
   Spawn:
      BLLT T 7 bright
      stop
   }
}

actor Lith_Hulgyon : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Hulgyon"
   Weapon.SlotNumber 0
   Weapon.SlotPriority 1
   Weapon.UpSound "weapons/hulgyon/draw"
   
   +WEAPON.DONTBOB
   -WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      TNT1 A 1 bright A_WeaponReady(CallACS("Lith_GetWRF"))
      TNT1 A 0 A_SpawnItemEx("Lith_HulgyonSprite", cos(pitch)*32, 0, (height-19)-(sin(pitch)*32),velx,vely,velz,0,SXF_ISMASTER|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 5)
   SelectLoop:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 50, 1)
      goto Ready
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 50)
      TNT1 A 10 A_FireCustomMissile("Lith_HulgyonProjectile")
      TNT1 AAAAAAAAAA 1 A_SpawnItemEx("Lith_HulgyonSprite2", cos(pitch)*32, 0, (height-19)-(sin(pitch)*32),0,0,0,0,SXF_NOCHECKPOSITION)
      goto Ready
   }
}

// EOF

