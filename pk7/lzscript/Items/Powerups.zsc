class Lith_Backpack : Lith_ScoreItem replaces Backpack
{
   default
   {
      Tag "Backpack";
      Inventory.PickupSound "player/pickup/item";
      Lith_BasicPickup.LogName "Backpack", 5;
      Lith_BasicPickup.InfoPage "Backpack";
      Lith_ScoreItem.Score Score_Backpack;

      +COUNTITEM
   }

   override void PostBeginPlay()
   {
      A_GiveInventory("Lith_BulletAmmo", AmmoAmt_BulletBackpk);
      A_GiveInventory("Lith_ShellAmmo",  AmmoAmt_ShellBackpk);
      A_GiveInventory("Lith_RocketAmmo", AmmoAmt_RocketBackpk);
      A_GiveInventory("Lith_PlasmaAmmo", AmmoAmt_CellBackpk);
      A_GiveInventory("Lith_CannonAmmo", AmmoAmt_CannonBackpk);
      Super.PostBeginPlay();
   }

   override bool TryPickup(in out Actor mo)
   {
      mo.ACS_ScriptCall("Lith_Discount");
      return Super.TryPickup(mo);
   }
   
   states
   {
   Spawn:
      BPAK A -1;
      stop;
   }
}

class Lith_Shobon : Actor
{
   default
   {
      +NOINTERACTION
   }

   vector2 m_dorupos;

   override void Tick()
   {
      Super.Tick();

      if(master)
      {
         let v = master.vel;

         if(!(v.x ~== 0) || !(v.y ~== 0))
         {
            float a  = atan2(v.y, v.x) - 180;
            float m  = v.xy.length();
            float x_ = Lith_UTIL.Lerp(m_dorupos.x, cos(a) * 32, m / 32.);
            float y_ = Lith_UTIL.Lerp(m_dorupos.y, sin(a) * 32, m / 32.);
            m_dorupos = (x_, y_);
         }

         SetOrigin(master.pos + m_dorupos, true);
      }
   }

   states
   {
   Spawn:
      SHOB A -1;
      stop;
   }
}

class Lith_ShobonSphere : Lith_CustomInventory
{
   default
   {
      //$Category "Powerups"

      Tag "Shobon Sphere";
      Inventory.PickupSound "misc/p_pkup";

      +COUNTITEM
      +INVENTORY.BIGPOWERUP
      +INVENTORY.FANCYPICKUPSOUND
   }

   states(Actor)
   {
   Spawn:
      TNT1 A 0;
      SHOB B 0 A_SpawnItemEx("Lith_ItemSprite", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_TRANSFERSPRITEFRAME);
      TNT1 A 1;
      wait;
   }

   states(Item)
   {
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "ShobonSphere");
      TNT1 A 0 A_SpawnItemEx("Lith_Shobon", 0,0,0, 0,0,0, 0, SXF_SETMASTER);
      stop;
   }
}

// EOF
