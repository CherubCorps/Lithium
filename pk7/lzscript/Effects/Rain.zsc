// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │                       Effects taken from Realm667.                       │
// │   I think Tormenter made these originally, sorry if that's incorrect.    │
// │               Thanks to Kate for making these look better.               │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RainDrop : Actor {
   default {
      Alpha 0.8;
      RenderStyle "Add";
      Height 2;
      Radius 2;
      XScale 0.5;
      YScale 1.0;
      +DontSplash
      +DropOff
      +ForceYBillboard
      +NoBlockmap
      +NoInteraction
      +NoTeleport
      +NoTrigger
   }
   bool m_done;
   override void beginPlay() {
      super.beginPlay();
      vel.z    = fRandom[lith_rain](-19, -26);
      scale.y *= vel.z;
      m_done   = false;
   }
   override void tick() {
      if(isFrozen()) {
         return;
      }
      if(m_done) {
         Lith_UTIL.stateTick(self);
         a_fadeOut(0.25 / 21.0);
      } else if(waterLevel > 0 || pos.z <= floorZ) {
         setOrigin((pos.xy, floorZ), false);
         vel = (0, 0, 0);
         scale = (1, 1);
         m_done = true;
         setState(resolveState("Death"));
      } else {
         setOrigin(pos + vel, true);
      }
   }
   states {
   Spawn:
      RNDR A -1;
      stop;
   Death:
      RNDR BCDEFGH 3;
      stop;
   }
}

class Lith_BloodRainDrop : Lith_RainDrop {
   default {
      Alpha 0.5;
      RenderStyle "Subtract";
      Translation "Lith_BloodRainDrop";
      +Bright
   }
}

class Lith_AbyssRainDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
   }
   states {
   Spawn:
      RNDZ A -1;
      stop;
   Death:
      RNDZ BCDEFGH 3;
      stop;
   }
}

class Lith_SnowDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
      YScale 0.1;
   }
   states {
   Spawn:
      RNDS A -1;
      stop;
   Death:
      RNDS BCDE 12;
      stop;
   }
}

class Lith_Ash : Lith_SnowDrop {
   default {
      Alpha 0.9;
      Scale 0.75;
      +RollSprite
   }
   float m_rollSpeed;
   override void postBeginPlay() {
      super.postBeginPlay();
      m_rollSpeed =  fRandom[lith_rain](0.4, 0.9) * fRandomPick[lith_rain](-1, 1);
      scale      *=  fRandom[lith_rain](0.25, 1.0);
      vel.z       = -fRandom[lith_rain](0.84, 1.2);
   }
   override void tick() {
      if(isFrozen()) {
         return;
      }
      super.tick();
      if(!m_done) {
         a_setRoll(roll + m_rollSpeed, SPF_INTERPOLATE);
      } else {
         scale = (0.8, 0.5);
         roll  = 0;
      }
   }
   states {
   Spawn:
      RNDA A -1;
      wait;
   }
}

/* EOF */
