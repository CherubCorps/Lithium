actor Lith_BaronOfHell : BaronOfHell replaces BaronOfHell
{
   PainChance 55
   Tag "$LITH_INFO_SHORT_BaronOfHell"
   Species "Lith_HellKnight"
   BloodColor "DarkGreen"
   
   +MISSILEMORE
   
   var int user_angle;
   
   states
   {
   Spawn:
      BOSS AB 10 A_Look
      loop
   See:
      BOSS AABBCCDD 3 A_Chase
      loop
   Melee:
      BOSS EF 6 A_FaceTarget
      BOSS G 4 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee")
      goto See
   Missile:
      BOSS EF 4 A_FaceTarget
      BOSS G 0 A_FaceTarget
      BOSS G 1 A_SpawnProjectile("Lith_BaronBall", 32, 0, 0, CMF_BADPITCH)
      BOSS G 0 A_Jump(32, "MissileDone")
      BOSS G 0 A_FaceTarget
      BOSS G 0 A_SetUserVar("user_angle", frandompick(-45, 45, 0))
      BOSS G 1 A_SpawnProjectile("Lith_BaronBallSilent", 32, 0, user_angle, CMF_AIMDIRECTION|CMF_BADPITCH)
      goto See
   MissileDone:
      BOSS G 3
      goto See
   Pain:
      BOSS H 1
      BOSS H 1 A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_BaronOfHell)
      goto Super::Death
   Raise:
      BOSS O 8
      BOSS NMLKJI 8
      goto See
   }
}

actor Lith_BaronHellfireTrailSpawner
{
   Radius 2
   Height 2

   +NOINTERACTION

   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_BaronHellfireTrail", -16, 0, 0, frandom(0.0, -1.0))
      TNT1 A 0 A_SpawnParticle("37732B", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 5, 0,
                          frandom(-8,8), frandom(-8,8), frandom(-2,2),
                          frandom(0, 10), frandom(-0.1, 0.1), frandom(-1, 1),
                          0, 0, 0.02,
                          1.0, -1)
      stop
   }
}

actor Lith_BaronHellfireTrail
{
   Radius 2
   Height 2
   Scale 0.666
   Renderstyle "Translucent"
   Alpha 0.7

   +DONTSPLASH
   +NOINTERACTION

   states
   {
   Spawn:
      BAL7 C 2
      BAL7 DE 3 A_FadeOut(0.333)
      stop
   }
}

actor Lith_BaronBall : BaronBall replaces BaronBall
{
   DamageType "Lith_Magic"
   Damage 4
   RenderStyle "Translucent"
   Alpha 1

   +RANDOMIZE

   states
   {
   Spawn:
      HLF1 A 0
      HLF1 AAAABBBB 1 bright A_SpawnItemEx("Lith_BaronHellfireTrailSpawner", 0, frandom(-0.5, 0.5), 0, frandom(0,-1))
      loop
   Death:
      BAL7 CDE 6 bright
      stop
   }
}

actor Lith_BaronBallSilent : Lith_BaronBall
{
   SeeSound ""
   DeathSound ""
   Decal ""
}

// EOF
