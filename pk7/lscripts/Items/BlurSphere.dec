actor Lith_BarrierFX
{
   RenderStyle "Add"
   Scale 0.3

   +NOTIMEFREEZE
   +NOINTERACTION

   states
   {
   Spawn:
      TNT1 A 0
      LBAR A 10 A_PlaySound("player/barrier", CHAN_NOPAUSE|CHAN_LISTENERZ)
   SpawnLoop:
      LBAR A 0 A_SetScale(scalex * 0.86)
      LBAR A 1 A_FadeOut(0.25)
      loop
   }
}

actor Lith_BarrierProtection : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIf(damage >= 20 || damage == 0, "Nope")
      TNT1 A 0 A_JumpIf(!CallACS("Lith_BarrierCheck"), "Nope")
      TNT1 A 0 A_Jump(64, "Nope")
      TNT1 A 0 Thing_Remove(0)
      TNT1 A 0 A_SpawnItemEx("Lith_BarrierFX")
      stop
   Nope:
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_BarrierSpell : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_RadiusGive("Lith_BarrierProtection", 64, RGF_MISSILES)
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_BlurSphereSprite : Lith_MagicSphereItemSprite
{
   Lith_AnimItemSprite.Phases 36
   +VISIBILITYPULSE
   states {SetSpr: PINS AAAAAABBBBBBCCCCCCDDDDDDCCCCCCBBBBBB 0 Lith_A_JumpPhase stop}
}

actor Lith_BlurSphere : Lith_CustomInventory
{
   //$Category "Powerups"

   Tag "Fragma"
   Inventory.PickupSound "player/pickup/barrier"
   Lith_CustomInventory.InfoPage "Fragma"

   +COUNTITEM
   +INVENTORY.BIGPOWERUP
   +INVENTORY.FANCYPICKUPSOUND
   +INVENTORY.ALWAYSPICKUP

   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_BlurSphereSprite", 0,0,0, 0,0,0, 0, SXF_SETMASTER)
      TNT1 A 1
      wait
   Pickup:
      TNT1 A 0 Lith_A_Log(msg_both, 5, "BlurSphere")
      TNT1 A 0 A_TakeInventory("Lith_HasBarrier")
      TNT1 A 0 A_GiveInventory("Lith_HasBarrier")
      stop
   }
}

// EOF

