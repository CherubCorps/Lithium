actor Lith_Weapon_OmegaCannon_Base : Lith_Weapon
{
   Tag "$LITH_TXT_INFO_SHORT_Cannon"
   Weapon.UpSound "weapons/cannon/draw"
   Weapon.SlotNumber 7
   Weapon.BobStyle InverseSmooth
   Weapon.BobRangeX 2.0
   Weapon.BobSpeed 0.85
   Decal "Scorch"
   
   +WEAPON.NOAUTOFIRE
   +WEAPON.CHEATNOTWEAPON
   
   states
   {
   Spawn:
      BFUG A 0
      BFUG A 0 A_SetUserVar("user_pickupparm", weapon_bfg)
      goto SpawnLoop
   
   Ready:
      CNNG A 0 A_TakeInventory("Lith_GTFO", 999)
      CNNG A 1 A_WeaponReady
      wait
   
   Deselect:
      CNNG A 0 A_TakeInventory("Lith_CannonScopedToken", 1)
      CNNG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      CNNG AAA 0 A_Lower
      CNNG A 1
      loop
   
   Select:
      CNNG A 0 A_ZoomFactor(1.0)
   SelectLoop:
      CNNG AA 0 A_Raise
      CNNG A 1
      loop
   
   Fire:
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_OmegaRail), "FireRail")
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 1, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 1, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      //
      CNNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PunctCannon), "FirePunct")
      CNNG A 0 A_Jump(256, "FireCannonSmall")
   
   AltFire:
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_OmegaRail), "Ready")
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PunctCannon), "ScopeToggle")
      //
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      CNNG A 0 A_Jump(256, "FireCannonBig")
   
   FireEmpty:
      "####" A 2 A_PlaySound("weapons/cannon/empty", 7)
      "####" A 1 offset(0, 28)
      "####" A 1 offset(0, 29)
      "####" A 1 offset(0, 32)
      "####" A 1 offset(0, 35)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 42)
      "####" A 1 offset(0, 43)
      "####" A 1 offset(0, 44)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 37)
      "####" A 1 offset(0, 34)
      "####" A 1 offset(0, 32)
      "####" A 1 A_ReFire
      "####" A 0 A_JumpIfInventory("Lith_CannonScopedToken", 1, "AltReady")
      goto Ready
   }
}

// EOF

