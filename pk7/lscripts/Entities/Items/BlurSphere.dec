actor Lith_HasBarrier : Inventory
{
   Inventory.MaxAmount 0x7FFFFFFF
}

actor Lith_BarrierFX
{
   RenderStyle "Add"
   Scale 0.3
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      LBAR A 10 nodelay A_PlaySound("player/barrier", CHAN_NOPAUSE|CHAN_LISTENERZ)
   SpawnLoop:
      LBAR A 0 A_SetScale(scalex * 0.86)
      LBAR A 1 A_FadeOut(0.25)
      loop
   }
}

actor Lith_BarrierProtection : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIf(damage >= 20, "Nope")
      TNT1 A 0 A_JumpIf(!CallACS("Lith_BarrierCheck"), "Nope")
      TNT1 A 0 A_Jump(64, "Nope")
      TNT1 A 0 Thing_Remove(0)
      TNT1 A 0 A_SpawnItemEx("Lith_BarrierFX")
      stop
   Nope:
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_BarrierSpell : CustomInventory
{
   states
   {
   Pickup:
      TNT1 A 0 A_RadiusGive("Lith_BarrierProtection", 64, RGF_MISSILES)
      TNT1 A 0 A_RailWait
      stop
   }
}

actor Lith_BlurSphere : Lith_CustomInventory
{
   Inventory.PickupSound "misc/p_pkup"
   
   +FLOATBOB
   +COUNTITEM
   +VISIBILITYPULSE
   +INVENTORY.BIGPOWERUP
   +INVENTORY.FANCYPICKUPSOUND
   
   states
   {
   Spawn:
      PINS ABCD 6 bright
      loop
   Pickup:
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, log_blursphere)
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_Barrier")
      TNT1 A 0 A_RailWait
      stop
   }
}

// EOF

