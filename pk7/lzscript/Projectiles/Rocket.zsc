class Lith_RocketTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.076;
      
      +NOINTERACTION
   }
   
   states
   {
   Spawn:
      SMOK A 1 A_FadeOut(0.05);
      wait;
   }
}

class Lith_Rocket : Actor
{
   default
   {
      Scale 0.7;
      Radius 2;
      Height 2;
      Speed 20;
      Damage 20;
      DeathSound "explosion";
      Species "Lith_Player";
      DamageType "Lith_Shrapnel";
      Gravity 0.2;
      
      Projectile;
      +ROCKETTRAIL
      +MTHRUSPECIES
      +EXPLODEONWATER
      -NOGRAVITY
   }
   
   vector3 m_pvel;
   
   states
   {
   Spawn:
      MISL A 0;
      MISL A 0 {m_pvel = vel;}
      MISL A 0 A_ChangeVelocity(vel.x*0.05, vel.y*0.05, 0, CVF_REPLACE);
      MISL A 7 bright;
      MISL A 0 {bNOGRAVITY = true;}
      MISL A 0 A_ChangeVelocity(m_pvel.x, m_pvel.y, m_pvel.z + 0.5, CVF_REPLACE);
      MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true);
      MISL A 0 A_Jump(256, "SpawnLoop");
   SpawnLoop:
      MISL A 0 A_SpawnItemEx("Lith_RocketTrail", 0, 0, 4, frandom(-0.5,0.5), frandom(-0.5,0.5), 0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION);
      goto Done;
   Done:
      MISL A 1 bright;
      MISL A 0 A_Jump(256, "SpawnLoop");
   Death:
      TNT1 A 0 A_Explode;
      MEXP AB 2 bright;
      MEXP CD 3 bright;
      MEXP EF 4 bright;
      stop;
   }
}

// EOF

