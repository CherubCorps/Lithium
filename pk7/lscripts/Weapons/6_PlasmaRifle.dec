#include "lscripts/Weapons/PlasmaBolt.dec"

actor Lith_PlasmaHit
{
   RenderStyle Add
   Alpha 0.25
   Scale 0.65
   
   +BRIGHT
   +NOINTERACTION
   +PUFFONACTORS
   +PUFFGETSOWNER
   +NOGRAVITY
   +NOBLOCKMAP
   +FLOORCLIP
   +EXTREMEDEATH
   +FORCEXYBILLBOARD
   -ALLOWPARTICLES
   
   States
   {
   ded:
      TNT1 A 0
      stop
   Crash:
      BLL2 A 0
      BLL2 A 0 A_JumpIf(CallACS("Lith_Oscillate"), "ded")
      BLL2 A 0 A_PlaySound("weapons/plasma/laser/spark", CHAN_7, 1.0, true)
      BLL2 A 0 A_Explode(64, 24, 0)
   PrettiesLoop:
      BLL2 A 0 A_JumpIf(alpha >= 1, "PrettiesDone")
      BLL2 A 1 A_FadeIn(0.07)
      loop
   PrettiesDone:
      BLL2 A 1 A_FadeOut(0.1)
      wait
   Spawn:
   Melee:
      BLL2 A 0
      BLL2 A 0 A_JumpIf(CallACS("Lith_Oscillate"), "ded")
      BLL2 A 0 A_PlaySound("weapons/plasma/laser/spark", CHAN_7, 1.0, true)
      BLL2 A 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_PlasmaLaze
{
   +NOINTERACTION
   +FORCEXYBILLBOARD
   +NOINTERACTION
   
   RenderStyle Add
   Scale 0.3
   
   states
   {
   ded:
      TNT1 A 0
      stop
   Spawn:
      BLLT E 0
      BLLT E 0 A_JumpIf(CallACS("Lith_OscillateN", 6) != 0, "ded")
      BLLT E 1
      stop
   }
}

actor Lith_PlasmaRifle : Lith_Weapon replaces PlasmaRifle
{
   Tag "Plasma Rifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   
   states
   {
   Ready:
      PLSG A 1 A_WeaponReady
      wait
   Deselect:
      PLSG AAA 0 A_Lower
      PLSG A 1
      loop
   Select:
      PLSG AAA 0 A_Raise
      PLSG A 1
      loop
   //
   // Plasma fire
   //
   Fire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_plasma_laser), "FireLaser")
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 17, TIF_NOTAKEINFINITE)
      PLSG AAAA 0 A_FireCustomMissile("Lith_PlasmaBolt",
                                      CallACS("Lith_CircleSpread", 5 * IFIX, 3 * IFIX, false) / FIX,
                                      0, 0, -9, 0,
                                      CallACS("Lith_CircleSpread", 0, 0, true) / FIX)
      PLSG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      PLSJ A 1 offset(0, 37) bright
      PLSJ C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 32)
      PLSG A 4 A_ReFire
      goto Ready
   //
   // Piercing plasma fire
   //
   AltFire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_plasma_laser), "Ready")
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 70, TIF_NOTAKEINFINITE)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", 0, 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(-2.7, -1.9), 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(2.7, 1.9), 0, 0, -9)
      PLSK A 1 offset(0, 37) bright A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON)
      PLSK C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG A 4 A_ReFire
      Goto Ready
   //
   // Laser fire
   //
   FireLaserReal:
   // Fuck copy-pasting this.
      TNT1 A 0 A_FireBullets(0, 0, 1, 0, "Lith_PlasmaHit", FBF_NORANDOMPUFFZ)
      TNT1 A 0 A_RailAttack(CallACS("Lith_GetPlayerData", pdata_EXPLOOOOOSIONS) ? 1 : 14, 0, false, None, Red, RGF_SILENT|RGF_FULLBRIGHT, 0, "Lith_Dummy", 0, 0, 0, 1, 0, 0, "Lith_PlasmaLaze")
      stop
   FireLaser:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 17, TIF_NOTAKEINFINITE)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_start")
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_loop", CHAN_7, 0.7, true)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_hum",  CHAN_6, 0.5, true)
      PLSG AA 0 A_GunFlash("FireLaserReal")
      PLSK A 2 offset(0, 35) bright A_GunFlash("FireLaserReal")
   FireLaserLoop:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 17, TIF_NOTAKEINFINITE)
      PLSK B 1  offset(0, 39) bright A_GunFlash("FireLaserReal")
      PLSK BB 1 offset(-2, 38) bright A_GunFlash("FireLaserReal")
      PLSK B 1  offset(1, 37) bright A_GunFlash("FireLaserReal")
      PLSK B 1  offset(-1, 40) bright A_GunFlash("FireLaserReal")
      PLSK C 2  offset(1, 37) bright A_ReFire("FireLaserLoop")
   FireLaserDone:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 offset(1, 36) A_PlaySound("weapons/plasma/laser/fire_end")
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 33)
      PLSG A 1 offset(0, 32)
      PLSG A 1
      goto Ready
   //
   //
   //
   FireEmpty:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 A_PlaySound("weapons/plasma/empty", 7)
      PLSG A 1 offset(0, 37)
      PLSG A 1 offset(0, 36)
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG A 1 A_ReFire
      goto Ready
   Spawn:
      PLAS A 0
      PLAS A 0 A_SetUserVar("user_pickupparm", weapon_plasma)
      goto SpawnLoop
   }
}
