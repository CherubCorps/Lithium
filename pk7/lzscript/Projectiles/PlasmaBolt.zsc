class Lith_PlasmaBoltTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.2;
      Scale 0.125;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 {return ResolveState("Particles");}
   SpawnPick:
      TNT1 A 0 A_Jump(256, "SpawnA", "SpawnB");
   SpawnA:
      PBAL A 1 bright;
      stop;
   SpawnB:
      PBAL B 1 bright;
      stop;
   }
}

class Lith_PlasmaBoltTrail_Green : Lith_PlasmaBoltTrail
{
   states
   {
   Particles:
      TNT1 A 0 A_SpawnParticle("Green", SPF_FULLBRIGHT, 6, 2, 0, 0,0,0, frandom(-2, 2),frandom(-2, 2),frandom(-2, 2));
      goto SpawnPick;
   }
}

class Lith_PlasmaBoltTrail_Red : Lith_PlasmaBoltTrail
{
   default
   {
      Translation "Lith_PlasmaBoltTrail_Red";
   }

   states
   {
   Particles:
      TNT1 A 0 A_SpawnParticle("Red", SPF_FULLBRIGHT, 6, 2, 0, 0,0,0, frandom(-2, 2),frandom(-2, 2),frandom(-2, 2));
      goto SpawnPick;
   }
}

class Lith_PlasmaBoltTrail_Blue : Lith_PlasmaBoltTrail
{
   default
   {
      Translation "Lith_PlasmaBoltTrail_Blue";
      Alpha 0.5;
   }

   states
   {
   Particles:
      TNT1 A 0 A_SpawnParticle("Blue", SPF_FULLBRIGHT, 6, 2, 0, 0,0,0, frandom(-2, 2),frandom(-2, 2),frandom(-2, 2));
      goto SpawnPick;
   }
}

class Lith_PlasmaBolt : Actor
{
   default
   {
      Radius 12;
      Height 12;
      Speed 50;
      DamageFunction 10;
      DamageType "Electric";
      Species "Lith_Player";

      Projectile;
      +DONTHARMSPECIES
      +MTHRUSPECIES
   }

   int m_trailstep;

   states
   {
   Spawn:
      TNT1 A 3;
      TNT1 A 0 {return ResolveState("Trail");}
   Trail:
      TNT1 A 0 A_JumpIf(m_trailstep >= 5, "TrailDone");
      TNT1 A 0 {m_trailstep++;}
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaBoltTrail_Green", ((m_trailstep * 10.0) * vel.x) / -35.0, -((m_trailstep * 10.0) * vel.y) / -35.0, 2.0 + ((m_trailstep * 10.0) * vel.z) / -35.0, flags: SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION);
      loop;
   TrailDone:
      TNT1 A 0 {m_trailstep = 0;}
      goto Step;
   Step:
      TNT1 A 1;
      TNT1 A 0 {return ResolveState("Trail");}
   Death:
      TNT1 AAAA 0 A_SpawnParticle("Green", SPF_FULLBRIGHT, 15, 4, 0, frandom(-5, 5),frandom(-5, 5),frandom(-5, 5), frandom(-5, 5),frandom(-5, 5),frandom(-5, 5));
      TNT1 A 2 A_PlaySound("weapons/plasma/explode", CHAN_AUTO);
      stop;
   }
}

class Lith_PenetratingPlasmaBolt : Lith_PlasmaBolt
{
   default
   {
      DamageFunction 5;
      Speed 47;

      +RIPPER
   }

   states
   {
   Trail:
      TNT1 A 0 A_JumpIf(m_trailstep >= 10, "TrailDone");
      TNT1 A 0 {m_trailstep++;}
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaBoltTrail_Red", ((m_trailstep * 10.0) * vel.x) / -35.0, -((m_trailstep * 10.0) * vel.y) / -35.0, 2.0 + ((m_trailstep * 10.0) * vel.z) / -35.0, flags: SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION);
      loop;
   TrailDone:
      TNT1 A 0 {m_trailstep = 0;}
      goto Step;
   Death:
      TNT1 AAAA 0 A_SpawnParticle("Red", SPF_FULLBRIGHT, 15, 4, 0, frandom(-5, 5),frandom(-5, 5),frandom(-5, 5), frandom(-5, 5),frandom(-5, 5),frandom(-5, 5));
      TNT1 A 2 A_PlaySound("weapons/plasma/explode", CHAN_AUTO);
      stop;
   }
}

