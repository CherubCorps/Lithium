class Lith_XH_ZombieMan : Lith_ZombieMan
{
   default
   {
      MinMissileChance 120;
   }

   override void Lith_A_ZombieManShot()
   {
      if(CheckIfCloser(target, 1024, false))
         A_WolfAttack(WAF_NORANDOM|WAF_USEPUFF, "enemies/zombie/fire", 3.0 / 4.0, frandom(1, 4) * 3.0);
      else
         A_WolfAttack(WAF_NORANDOM, "enemies/chainguy/fire", 1.0 / 5.0, frandom(1, 8) * 3.0);
   }
}

class Lith_ZombieMan : Lith_Monster replaces ZombieMan;

default
{
   Lith_Monster.Score Score_ZombieMan;
   Health 30;
   Radius 20;
   Height 56;
   Speed 9;
   PainChance 200;
   MinMissileChance 150;
   SeeSound "grunt/sight";
   PainSound "grunt/pain";
   DeathSound "grunt/death";
   ActiveSound "grunt/active";
   Obituary "$OB_ZOMBIE";
   Tag "$LITH_TXT_INFO_SHORT_ZombieMan";

   +FLOORCLIP
}

virtual void Lith_A_ZombieManShot()
{
   A_PlaySound("enemies/zombie/fire", CHAN_WEAPON);
   if(target && target.countInv("Lith_HasBarrier"))
      ACS_NamedExecuteAlways("Lith_BarrierBullets", 0);
   else
      A_CustomBulletAttack(22.5, 5, 1, random(1, 4) * 3, "BulletPuff", 0, CBAF_NORANDOM);
}

int m_shots;

states
{
Spawn:
   POSS AB 10 A_Look;
   loop;
See:
   POSS AABBCCDD 4 A_Chase;
   loop;
Missile:
   POSS E 0 {m_shots = random(1, 3);}
   POSS E 2 A_FaceTarget;
   POSS E 0 {
      A_PlaySound("enemies/zombie/ready", CHAN_BODY|CHAN_NOPAUSE);
      A_GiveToTarget("Lith_AdrenalineToken", 1);
   }
   POSS E 5 A_FaceTarget;
MissileLoop:
   POSS E 5 A_FaceTarget;
   POSS F 4 bright Lith_A_ZombieManShot;
   POSS F 0 A_JumpIf(--m_shots == 0, "MissileEnd");
   loop;
MissileEnd:
   POSS E 8;
   goto See;
Pain:
   POSS G 3;
   POSS G 3 A_Pain;
   goto See;
Death:
   POSS H -1;
   wait;
Crash:
   POSS H 5 Lith_A_Die;
   POSS IJK 5;
   POSS L -1;
   stop;
Death.Extreme:
   POSS M -1;
   wait;
Crash.Extreme:
   POSS M 5 Lith_A_XDie;
   POSS NOPQRST 5;
   POSS U -1;
   stop;
Raise:
   POSS K 5;
   POSS JTH 5;
   goto See;
}

// EOF
