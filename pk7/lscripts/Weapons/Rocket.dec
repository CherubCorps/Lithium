actor Lith_RocketTrail
{
   RenderStyle Add
   Scale 0.082
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK A 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_Rocket
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 5
   Damage (70)
   DeathSound "weapons/rocklx"
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   
   Projectile
   +ROCKETTRAIL
   +MTHRUSPECIES
   +EXPLODEONWATER
   
   var int user_trailstep;
   var int user_speedstep;
   
   states
   {
   Spawn:
      MISL A 0
      MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true)
   SpawnLoop:
      MISL A 0 A_JumpIf(user_trailstep >= 4, "TrailDone")
      MISL A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      MISL A 0 A_SpawnItemEx("Lith_RocketTrail",
                                   ((user_trailstep * 8.0) * velx) / -35.0,
                                  -((user_trailstep * 8.0) * vely) / -35.0,
                             4.0 + ((user_trailstep * 8.0) * velz) / -35.0,
                             frandom(-0.5,0.5), frandom(-0.5,0.5), 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      MISL A 0 A_SetUserVar(user_trailstep, 0)
      MISL A 0 A_JumpIf(CallACS("Lith_Velocity", velx*FIX, vely*FIX)/FIX >= 25, "Done")
      MISL A 0 A_SetUserVar(user_speedstep, user_speedstep + 1)
      MISL A 0 A_ScaleVelocity(1.0 + (user_speedstep / 20.0))
   Done:
      MISL A 1 bright
      goto SpawnLoop
   Death:
      TNT1 A 0 A_Explode
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_RocketHax : Lith_MonsterHax
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 10
   Damage 0
   
   Projectile
   +SKYEXPLODE
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1
      TNT1 A 0 A_SpawnItemEx("Lith_SpiralRocket", 0,0,0, 0,0,0, 0, SXF_ORIGINATOR|SXF_SETMASTER|SXF_TRANSFERPOINTERS)
   SpawnLoop:
      TNT1 A 1
      loop
   Death:
      TNT1 A 1
      TNT1 A 0 A_KillChildren
      TNT1 A 1
      stop
   }
}

actor Lith_SpiralRocket : Lith_MonsterHax
{
   Scale 0.7
   Radius 2
   Height 2
   Speed 0
   Damage (74)
   DeathSound "weapons/rocklx"
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   
   Projectile
   +ROCKETTRAIL
   +MTHRUSPECIES
   +EXPLODEONWATER
   +SKYEXPLODE
   
   var int user_trailstep;
   var int user_speedstep;
   var int user_spiraldist;
   var int user_spiralanglez;
   
   states
   {
   Spawn:
      MISL A 0
      MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true)
      MISL A 0 A_SetUserVar(user_spiraldist,   random(0, 359))
      MISL A 0 A_SetUserVar(user_spiralanglez, user_spiraldist)
   SpawnLoop:
      MISL A 0 A_SetUserVar(user_spiraldist,   (user_spiraldist + 7)   % 360)
      MISL A 0 A_SetUserVar(user_spiralanglez, (user_spiralanglez + 7) % 360)
      MISL A 0 A_Warp(AAPTR_MASTER, cos(90.0) * (sin(user_spiraldist) * 12.0),
                                    sin(90.0) * (sin(user_spiraldist) * 12.0),
                                    cos(user_spiralanglez) * 12.0,
                      0.0, WARPF_NOCHECKPOSITION)
   TheRealSpawnShady:
      MISL A 0 A_JumpIf(user_trailstep >= 4, "TrailDone")
      MISL A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      MISL A 0 A_SpawnItemEx("Lith_RocketTrail",
                                   ((user_trailstep * 8.0) * CallACS("Lith_VelHax", 1)) / -35.0,
                                  -((user_trailstep * 8.0) * CallACS("Lith_VelHax", 2)) / -35.0,
                             4.0 + ((user_trailstep * 8.0) * CallACS("Lith_VelHax", 3)) / -35.0,
                             frandom(-0.5,0.5), frandom(-0.5,0.5), 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      MISL A 0 A_SetUserVar(user_trailstep, 0)
      MISL A 0 A_JumpIf(CallACS("Lith_Velocity", CallACS("Lith_VelHax",1)*FIX, CallACS("Lith_VelHax",2)*FIX)/FIX >= 30, "Done")
      MISL A 0 A_SetUserVar(user_speedstep, user_speedstep + 1)
      MISL A 0 A_ScaleVelocity(1.0 + (user_speedstep / 20.0), AAPTR_MASTER)
   Done:
      MISL A 1 bright A_Stop
      MISL A 0 A_Warp(AAPTR_MASTER)
      goto SpawnLoop
   Death:
      TNT1 A 0 A_Explode
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

// EOF

