/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ShrapnelProjectile : Actor {
   default {
      BounceType "Doom";
      Damage 8;
      DamageType "Shrapnel";
      Gravity 0.5;
      Height 8;
      Radius 8;
      Species "Lith_Player";
      Speed 25;

      Projectile;
      +EXPLODEONWATER
      +MTHRUSPECIES
      -NOGRAVITY
   }

   states {
   Spawn:
      SGRN A 1;
      wait;
   }
}

class Lith_ShrapnelGun : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_ShrapnelGun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Lith_Weapon.AmmoType "Lith_ShellAmmo";
}

action(Weapon) void Lith_A_FireShrapnel() {
   A_FireProjectile("Lith_ShrapnelProjectile", frandom(-5, 5), false, frandom(-5, 5), frandom(-7, 7), FPF_NOAUTOAIM);
}

states(Weapon) {
Ready:
   PISG A 1 Lith_A_Ready;
   loop;

Deselect:
   PISG A 1 Lith_A_Lower(_raise_fast);
   loop;

Select:
   PISG A 1 Lith_A_Raise(_raise_fast);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo(4);
   PISG A 0 {
      lith_takeAmmo(4);
      A_StartSound("weapons/shrapnel/fire", lch_weapon);
      A_AlertMonsters();
      A_GunFlash("DummyFlash");
      Lith_A_RecoilUp(8);
      lith_a_setInterpolation(true);
   }
   PISG A 1 offset(0, 70) {for(int i = 0; i < 6; i++) Lith_A_FireShrapnel();}
   PISG A 1 offset(0, 75);
   PISG A 1 offset(0, 74);
   PISG A 1 offset(0, 72) {for(int i = 0; i < 4; i++) Lith_A_FireShrapnel();}
   PISG A 1 offset(0, 60);
   PISG A 1 offset(0, 55);
   PISG A 1 offset(0, 50) {for(int i = 0; i < 2; i++) Lith_A_FireShrapnel();}
   PISG A 1 offset(0, 45);
   PISG A 1 offset(0, 40);
   PISG A 1 offset(0, 36);
   PISG A 1 offset(0, 34);
   PISG A 1 offset(0, 33);
   PISG A 1 offset(0, 32);
   PISG A 10 lith_a_setInterpolation(false);
   goto ReadySelector;
FireEmpty:
   PISG A 2 offset(0, 42);
   PISG A 2 offset(0, 38);
   PISG A 2 offset(0, 35) A_StartSound("weapons/spas/empty", lch_dryweapon);
   PISG A 1 offset(0, 34);
   PISG A 1 offset(0, 33);
   PISG A 1 offset(0, 32);
   goto ReadySelector;
}

/* EOF */
