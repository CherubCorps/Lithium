#include "lscripts/Weapons/Cannonball.dec"
#include "lscripts/Weapons/Punctuator.dec"
#include "lscripts/Weapons/RailgunParticles.dec"

actor Lith_GTFO : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_OmegaRailDummy : Lith_Dummy
{
   Decal "Scorch"
}

actor Lith_BFG9000 : Lith_MWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_BFG9000"
   Weapon.UpSound "weapons/cannon/draw"
   Weapon.SlotNumber 7
   Weapon.SlotPriority 1
   Weapon.BobStyle "InverseSmooth"
   Weapon.BobRangeX 1.7
   Weapon.BobSpeed 0.7
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_BFG9000Pickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CNNG A 0 A_TakeInventory("Lith_GTFO", 999)
      DCNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_DarkCannon), 2)
      CNNG A 0
      "####" A 1 A_WeaponReady(WRF_ALLOWUSER4|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      DCNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_DarkCannon), 2)
      CNNG A 0
      "####" A 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      "####" AAA 0 A_Lower
      "####" A 1
      loop
   
   Select:
      DCNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_DarkCannon), 2)
      CNNG A 0
      "####" A 0 A_ZoomFactor(1.0)
   SelectLoop:
      "####" AA 0 A_Raise
      "####" A 1
      loop
   
   Fire:
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_OmegaRail), "FireRail")
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PunctCannon), "FirePunct")
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_DarkCannon), "FireDark")
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 1, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 1, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      goto FireCannonSmall
   
   AltFire:
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_OmegaRail), "Ready")
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_DarkCannon), "Ready")
      "####" A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PunctCannon), "ScopeToggle")
      //
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      goto FireCannonBig
   
   FireEmpty:
      "####" A 2 A_PlaySound("weapons/cannon/empty", 7)
      "####" A 1 offset(0, 28)
      "####" A 1 offset(0, 29)
      "####" A 1 offset(0, 32)
      "####" A 1 offset(0, 35)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 42)
      "####" A 1 offset(0, 43)
      "####" A 1 offset(0, 44)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 37)
      "####" A 1 offset(0, 34)
      "####" A 1 offset(0, 32)
      "####" A 1 A_ReFire
      "####" A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "AltReady")
      goto Ready
   
   FireDark:
      DCNG A 10 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      DCNG A 1 bright
      DCNG A 1 bright offset(-1, 33)
      DCNG A 1 bright offset(-1, 34)
      DCNG A 1 bright offset(-1, 36)
      DCNG A 2 bright offset(1, 37)
      DCNG A 3 bright offset(1, 38)
      DCNG A 0 bright A_GunFlash("DummyFlash")
      DCNG A 0 bright A_PlaySound("weapons/cannon/fire", CHAN_5)
      DCNG B 1 bright offset(0, 62) A_FireCustomMissile("Lith_DarkMatter", 0, false, 0, 0, FPF_NOAUTOAIM)
      DCNG C 1 bright
      DCNG D 1 bright offset(0, 57)
      DCNG E 1 bright offset(0, 47)
      DCNG F 1 bright offset(0, 44)
      DCNG G 1 bright offset(0, 40)
      DCNG H 1 bright offset(0, 37)
      DCNG I 1 bright offset(0, 35)
      DCNG J 1 bright offset(0, 32)
      DCNG KKKKLLLLLLLAAAAAAAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireCannonBig:
      CNNG A 10 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      CNNG A 1 bright
      CNNG A 1 bright offset(-1, 33)
      CNNG A 1 bright offset(-1, 34)
      CNNG A 1 bright offset(-1, 36)
      CNNG A 2 bright offset(1, 37)
      CNNG A 3 bright offset(1, 38)
      CNNG A 0 bright A_GunFlash("DummyFlash")
      CNNG A 0 bright A_PlaySound("weapons/cannon/fire", CHAN_5)
      CNNG C 2 bright offset(0, 62) A_FireCustomMissile("Lith_CannonBall", 0, false, 0, -24, FPF_NOAUTOAIM)
      CNNG C 1 bright offset(0, 57)
      CNNG C 1 bright offset(0, 47)
      CNNG C 1 bright offset(0, 44)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG B 1 bright offset(0, 35)
      CNNG B 1 bright offset(0, 32)
      CNNG AAAAAAAAAAAAAAAAAAAA 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireCannonSmall:
      CNNG A 0 bright A_PlaySound("weapons/cannon/fire", CHAN_5)
      CNNG C 1 bright offset(0, 48) A_GunFlash("DummyFlash")
      CNNG C 1 bright offset(0, 57) A_FireCustomMissile("Lith_SmallCannonBall", 0, false, 0, -16, FPF_NOAUTOAIM)
      CNNG C 1 bright offset(0, 55)
      CNNG C 1 bright offset(0, 53)
      CNNG C 1 bright offset(0, 49)
      CNNG B 1 bright offset(0, 45)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG A 1 bright offset(0, 34)
      CNNG A 1 offset(0, 32)
      CNNG A 8
      CNNG AAAAAAAAAA 1 A_ReFire
      goto Ready
   
   FireRail:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 5 offset(-1, 32) A_PlaySound("weapons/rail/charge", CHAN_5)
      CNNG A 5 offset(0, 32)
      CNNG A 5 offset(1, 32)
      CNNG A 5 offset(0, 33)
      CNNG A 5 offset(-1, 33)
      CNNG A 5 offset(0, 33)
      CNNG A 5 offset(1, 33)
      CNNG A 5 offset(0, 33)
   FireRailLoop:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto Ready
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_GunFlash("DummyFlash")
      CNNG A 1 offset(0, 34)
      CNNG A 1 offset(0, 35)
      CNNG A 1 bright offset(0, 45)
      CNNG B 1 bright offset(0, 55) A_PlaySound("weapons/rail/fire", CHAN_WEAPON)
      CNNG B 1 bright offset(0, 56)
      CNNG B 0 A_FireCustomMissile("Lith_OmegaRailHax", 0, false, 0, -19)
      CNNG C 1 bright offset(0, 57) A_RailAttack(500, 0, false, "None", "None", RGF_SILENT, 0, "Lith_OmegaRailDummy", 0, 0, 0, 0, 0.9, 0, "Lith_OmegaRailBeam", -19)
      CNNG C 1 bright offset(0, 47)
      CNNG A 1 offset(0, 42)
      CNNG A 2 offset(0, 37)
      CNNG A 3 offset(0, 35)
      CNNG A 4 offset(0, 33)
      CNNG AAAAAA 1 offset(0, 32) A_ReFire("FireRailLoop")
      goto Ready
   AltReady:
      SCOP A 0 A_JumpIfInventory("Lith_GTFO", 1, "ScopeOut")
      SCOP A 1 A_WeaponReady(WRF_NOBOB)
      loop
   
   OverlayFirePunct:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_PunctuatorFire", 0)
      stop
   FirePunct:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 0 A_AlertMonsters
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_PlaySound("weapons/punct/fire", CHAN_WEAPON)
      CNNG A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FirePunct_Scoped")
      CNNG A 3
      CNNG A 1 bright offset(0, 36)
      CNNG B 1 bright offset(0, 38)
      CNNG B 1 bright offset(0, 49)
      CNNG C 1 bright offset(0, 52) A_GunFlash("OverlayFirePunct")
      CNNG C 1 bright offset(0, 37)
      CNNG A 1 bright offset(0, 36)
      CNNG A 2 bright offset(0, 35)
      CNNG AAAAAAAAAA 1 offset(0, 34) A_ReFire
      goto LoadAnim
   FirePunct_Scoped:
      SCOP A 3
      SCOP A 1 A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.80)
      SCOP A 1 A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.85)
      SCOP A 1 bright A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.90)
      SCOP A 0 bright A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.95)
      SCOP A 1 bright A_GunFlash("OverlayFirePunct")
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      SCOP AAAAAAAAAA 1 A_ReFire
      goto LoadAnim
   
   LoadAnim:
      "####" A 1 offset(1, 32)
      "####" A 1 offset(3, 32)
      "####" A 1 offset(4, 32)
      "####" A 1 offset(8, 38)
      "####" A 1 offset(12, 45)
      "####" A 1 offset(16, 53)
      "####" A 1 offset(19, 60)
      "####" A 1 offset(21, 64)
      "####" A 1 offset(23, 67)
      "####" A 1 offset(22, 69) A_PlaySound("weapons/punct/load", CHAN_5)
      "####" A 1 offset(19, 70)
      "####" A 7 offset(18, 72)
      "####" A 1 offset(17, 69)
      "####" A 1 offset(19, 67)
      "####" A 1 offset(20, 66)
      "####" A 1 offset(21, 62)
      "####" A 1 offset(20, 60)
      "####" A 1 offset(21, 63)
      "####" A 1 offset(23, 66)
      "####" A 1 offset(24, 68)
      "####" A 1 offset(25, 63)
      "####" A 1 offset(24, 59)
      "####" A 1 offset(22, 57)
      "####" A 1 offset(20, 54)
      "####" A 1 offset(16, 49)
      "####" A 1 offset(12, 44)
      "####" A 1 offset(10, 42)
      "####" A 1 offset(8, 40)
      "####" A 1 offset(5, 38)
      "####" A 1 offset(2, 35)
      "####" A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "AltReady")
      goto Ready
   
   ScopeToggle:
      "####" A 1
      "####" A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" A 0 A_TakeInventory("Lith_WeaponScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      goto Ready
   ScopeIn:
      "####" A 0 A_GiveInventory("Lith_WeaponScopedToken", 1)
      "####" A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomin", 6)
      goto AltReady
   }
}

// EOF

