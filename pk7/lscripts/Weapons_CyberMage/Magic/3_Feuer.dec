actor Lith_FeuerTest
{
   RenderStyle "Add"
   Scale 0.3

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      BLLT E 1
      BLLT E 1 A_FadeOut((sin(CallACS("Lith_Timer") * 5) + 1) * 0.5)
      stop
   }
}

actor Lith_FeuerFX
{
   RenderStyle "Add"
   Scale 1.5

   +NOINTERACTION

   states
   {
   Spawn:
      BLLT R 0
      BLLT R 0 A_Jump(256, 1, 2, 3)
      BLLT R 0 A_SetScale(-scalex,  scaley)
      goto Done
      BLLT R 0 A_SetScale( scalex, -scaley)
      goto Done
      BLLT R 0 A_SetScale(-scalex, -scaley)
   Done:
      BLLT R 0 A_SetScale(scalex * frandom(0.5, 1))
      BLLT R 10
      BLLT R 1 A_FadeOut
      stop
   }
}

actor Lith_FeuerFinal
{
   RenderStyle "Add"
   Scale 0.3
   DamageType "Lith_Magic"
   Damage 40

   Projectile
   +NOGRAVITY

   states
   {
   Spawn:
      BLLT L 0
   Death:
      BLLT LLLLLLLLLLLLLLL 0 A_SpawnItemEx("Lith_FeuerFX", 0,0,0, frandom(-4,4),frandom(-4,4),frandom(-2,2))
      BLLT L 0 A_PlaySound("weapons/feuer/final", CHAN_WEAPON)
      BLLT L 0 A_Explode(96, 128, 0)
      BLLT L 5
      BLLT L 1 A_FadeOut
      wait
   }
}

actor Lith_FeuerExplosion
{
   RenderStyle "Add"
   Scale 0.15
   DamageType "Lith_Magic"
   Damage 40

   Projectile
   +NOGRAVITY

   states
   {
   Spawn:
      BLLT L 1
   Death:
      BLLT LLLLL 0 A_SpawnItemEx("Lith_FeuerFX", 0,0,0, frandom(-1,1),frandom(-1,1),frandom(-1,1))
      BLLT L 0 A_PlaySound("weapons/feuer/explode", CHAN_BODY)
      BLLT L 0 A_Explode(32, 20, 0)
      BLLT L 5
      BLLT L 1 A_FadeOut
      wait
   }
}

actor Lith_Feuer : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Feuer"
   Weapon.SlotNumber 0
   Weapon.SlotPriority 1
   Weapon.UpSound "weapons/feuer/draw"

   -WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      TNT1 A 1 bright A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      /*
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Feuer", true,  false)
      TNT1 A 1 ACS_NamedExecuteWithResult("Lith_Feuer", false, false)
      TNT1 A 1 A_ReFire
      */
      TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 50, 1)
      goto Ready
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 25)
      TNT1 A 0 A_PlaySound("weapons/feuer/fire", CHAN_WEAPON)
      TNT1 A 15 ACS_NamedExecuteWithResult("Lith_Feuer", true,  true)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 25)
      TNT1 A 0 A_PlaySound("weapons/feuer/fire", CHAN_WEAPON)
      TNT1 A 20 ACS_NamedExecuteWithResult("Lith_Feuer", false, true)
      goto Ready
   }
}

// EOF

