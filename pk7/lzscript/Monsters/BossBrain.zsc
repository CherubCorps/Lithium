class Lith_BossBrainExplosion : Actor
{
   default
   {
      +NOGRAVITY
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_PlaySound("weapons/rocklx");
      MEXP AB 2 bright;
      MEXP CD 3 bright;
      MEXP EF 4 bright;
      TNT1 A 3;
      TNT1 A 0 A_SpawnItemEx("Lith_BossBrainExplosion", frandom(-8,8));
      stop;
   }
}

class Lith_BossBrain : Actor replaces BossBrain
{
   default
   {
      Health 500;
      Mass int.max;
      PainChance 256;
      +SOLID
      +SHOOTABLE
      +NOICEDEATH
      +OLDRADIUSDMG
   }

   int m_boom;

   states
   {
   Spawn:
      BBRN A -1;
      stop;
   Pain:
      TNT1 A 0 A_Quake(1, 36, 0, 4096, "");
      BBRN B 36 A_PlaySound("brain/pain", CHAN_VOICE, 1, 0, 0.1);
      goto Spawn;
   Death:
      TNT1 A 0 {
         A_Quake(9, 120, 0, 4096, "");
         A_PlaySound("brain/death", CHAN_VOICE, 1, 0, 0.1);
         m_boom = 32;
      }
   DeathLoop:
      BBRN A 0 A_JumpIf(m_boom-- == 0, "Done");
      BBRN AA 0 A_SpawnItemEx("Lith_BossBrainExplosion", frandom(-256,256),-320,frandom(0,510) - pos.z, flags: SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION);
      BBRN A 1;
      loop;
   Done:
      BBRN A 98;
      BBRN A -1 A_BrainDie;
      stop;
   }
}

// EOF
