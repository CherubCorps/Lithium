/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_4Bore : Lith_DWeapon;

default
{
   Lith_Weapon.MagSize 2;
   Tag "$LITH_INFO_SHORT_4Bore";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 1;
}

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_ShotgunPickup");
   stop;
}

action(Weapon) void Lith_A_Fire4Bore()
{
   Lith_A_MagIncr();
   A_AlertMonsters();
   A_TakeInventory("Lith_ShellAmmo", 1, TIF_NOTAKEINFINITE);
   A_SetBlend("White", 0.2, 6);
   A_GunFlash("DummyFlash");
   Lith_FireBullets(4, 2, 4, 30, FBF_NORANDOM, "Lith_700Bullet");
   A_PlaySound("weapons/4bore/fire", CHAN_WEAPON);
   Lith_A_RecoilUp(0.8);
}

states(Weapon)
{
Ready:
   SHTG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   SHTG AA 0 A_Lower;
   SHTG A 1;
   loop;
Select:
   SHTG AA 0 A_Raise;
   SHTG A 1;
   loop;

Fire:
   #### # 0 A_JumpIf(CountInv("Lith_ShellAmmo") == 0, "FireEmpty");
   #### # 0 Lith_A_JumpIfMagEmpty;
   SHTG C 2 bright offset(0, 42) Lith_A_Fire4Bore;
   SHTG C 2 bright offset(0, 38) Lith_A_RecoilDn(0.4);
   SHTG B 2 bright offset(0, 35) Lith_A_RecoilDn(0.4);
   SHTG B 1 bright offset(0, 34);
   SHTG A 1 offset(0, 33);
   SHTG A 1 offset(0, 32);
   goto ReadySelector;
FireEmpty:
   SHTG A 2 offset(0, 42);
   SHTG A 2 offset(0, 38);
   SHTG A 2 offset(0, 35) A_PlaySound("weapons/cshotgun/empty", CHAN_5);
   SHTG A 1 offset(0, 34);
   SHTG A 1 offset(0, 33);
   SHTG A 1 offset(0, 32);
   goto ReadySelector;
Reload:
   #### # 0 Lith_A_JumpIfMagFull;
   SHT2 D 2 offset(-10, 32) A_PlaySound("weapons/ssg/open", CHAN_5);
   SHT2 D 2 offset(-5, 32);
   SHT2 D 3 offset(1, 32);
   SHT2 Z 10;
   SHT2 E 4;
   SHT2 F 5 A_PlaySound("weapons/ssg/load", CHAN_6);
   SHT2 G 5;
   SHT2 H 6 A_PlaySound("weapons/ssg/close", CHAN_7);
   SHTG A 0 Lith_A_ReloadFinish;
   goto ReadySelector;
}

/* EOF */
