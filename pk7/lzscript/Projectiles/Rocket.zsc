/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_RocketTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.076;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      SMOK A 1 A_FadeOut(0.05);
      wait;
   }
}

class Lith_Rocket : Lith_Explosive;

default
{
   Damage 20;
   DamageType "Shrapnel";
   Gravity 0.2;
   Height 2;
   Radius 2;
   Scale 0.7;
   Species "Lith_Player";
   Speed 20;

   Projectile;
   +EXPLODEONWATER
   +MTHRUSPECIES
   +ROCKETTRAIL
   -NOGRAVITY
}

vector3 m_PVel;

states
{
Spawn:
   MISL A 0;
   MISL A 0 {m_PVel = Vel;}
   MISL A 0 A_ChangeVelocity(Vel.X*0.05, Vel.Y*0.05, 0, CVF_REPLACE);
   MISL A 7 bright;
   MISL A 0 {bNOGRAVITY = true;}
   MISL A 0 A_ChangeVelocity(m_PVel.X, m_PVel.Y, m_PVel.Z + 0.5, CVF_REPLACE);
   MISL A 0 A_PlaySound("effects/rocket/fly", CHAN_AUTO, 0.65, true);
   MISL A 0 ResolveState("SpawnLoop");
SpawnLoop:
   MISL A 0 A_SpawnItemEx("Lith_RocketTrail", 0, 0, 4, frandom(-0.5,0.5), frandom(-0.5,0.5), 0, 0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION);
   goto Done;
Done:
   MISL A 1 bright;
   MISL A 0 ResolveState("SpawnLoop");
Death:
   TNT1 A 0 A_Explode;
   TNT1 A 0 Lith_A_DoExplosion;
   stop;
}

/* EOF */
