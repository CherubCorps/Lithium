// The naming of actors for Heretic are really stupid.. I took some liberties here.

actor Lith_Heretic_Gargoyle : HereticImp replaces HereticImp
{
   Tag "Gargoyle"
   
   states
   {
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_LostSoul * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_LostSoul)
      goto Super::Death
   }
}

actor Lith_Heretic_FireGargoyleBall : HereticImpBall replaces HereticImpBall
{
   DamageType "Lith_Fire"
}

actor Lith_Heretic_FireGargoyle : HereticImpLeader replaces HereticImpLeader
{
   Tag "Fire Gargoyle"
   MinMissileChance 150
   
   states
   {
   XDeath:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Imp * 1.5)
      goto Super::XDeath
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Imp)
      goto Super::Death
   }
}

actor Lith_Heretic_DiscipleProjectile : WizardFX1 replaces WizardFX1
{
   Radius 8
   DamageType "Lith_Magic"
   Species "Lith_IAmAWizard"
   +MTHRUSPECIES
}

actor Lith_Heretic_Disciple : Wizard replaces Wizard
{
   Health 300
   Tag "Disciple of D'Sparil"
   Species "Lith_IAmAWizard"
   
   states
   {
   Missile:
      WZRD C 2 A_WizAtk1
      WZRD C 2 A_WizAtk2
      WZRD C 2 A_WizAtk1
      WZRD C 2 A_WizAtk2
      WZRD D 10 A_WizAtk3
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Cacodemon)
      goto Super::Death
   }
}

actor Lith_Heretic_OphidianIceProjectile : SnakeProjA replaces SnakeProjA
{
   DamageType "Ice"
   Radius 6 // WHY WAS THIS WIDER THAN THE WIZARD PROJECTILE??????????
   SeeSound ""
   
   states
   {
   Spawn:
      TNT1 A 0 nodelay A_PlaySound("enemies/ophidian/ice")
      goto Super::Spawn
   }
}

actor Lith_Heretic_OphidianFireProjectile : SnakeProjB replaces SnakeProjB
{
   DamageType "Lith_Fire"
   SeeSound ""
   
   states
   {
   Spawn:
      TNT1 A 0 nodelay A_PlaySound("enemies/ophidian/fire")
      goto Super::Spawn
   }
}

actor Lith_Heretic_Ophidian : Snake replaces Snake
{
   Health 500
   Speed 13
   Tag "Ophidian"
   
   +MISSILEMORE
   
   states
   {
   Missile:
      SNKE F 3 A_FaceTarget
   MissileLoop:
      SNKE FFF 4 A_CustomMissile("SnakeProjA", 32, 0, 0, CMF_CHECKTARGETDEAD)
      SNKE F 6 A_FaceTarget
      SNKE F 0 A_MonsterRefire(40, "MissileLoopDone")
      SNKE F 0 A_Jump(128, "MissileLoopDone")
      loop
   MissileLoopDone:
      SNKE F 6 A_FaceTarget
      SNKE F 2 A_CustomMissile("SnakeProjB", 32, 0, 0, CMF_CHECKTARGETDEAD)
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_HellKnight)
      goto Super::Death
   }
}

actor Lith_Heretic_Golem : Mummy replaces Mummy
{
   Health 150
   Speed 24
   Tag "Golem"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Demon)
      goto Super::Death
   }
}

actor Lith_Heretic_GhostGolem : MummyGhost replaces MummyGhost
{
   Health 150
   Speed 24
   Tag "Ethereal Golem"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Demon)
      goto Super::Death
   }
}

actor Lith_Heretic_NitrgolemSkull : MummyFX1 replaces MummyFX1
{
   DamageType "Lith_FireMagic"
}

actor Lith_Heretic_Nitrogolem : MummyLeader replaces MummyLeader
{
   Health 180
   Speed 24
   Tag "Nitrogolem"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Nitrogolem)
      goto Super::Death
   }
}

actor Lith_Heretic_GhostNitrogolem : MummyLeaderGhost replaces MummyLeaderGhost
{
   Health 180
   Speed 24
   Tag "Ethereal Nitrogolem"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Nitrogolem)
      goto Super::Death
   }
}

actor Lith_Heretic_MaulotaurFireball : MinotaurFX1 replaces MinotaurFX1
{
   Damage 4
}

actor Lith_Heretic_MaulotaurFirewall : MinotaurFX2 replaces MinotaurFX2
{
   DamageType "Lith_FireMagic"
}

actor Lith_Heretic_MaulotaurFirewall2 : MinotaurFX3 replaces MinotaurFX3
{
   DamageType "Lith_FireMagic"
}

// This was just silly. How could anyone excuse these beasts being so weak?
actor Lith_Heretic_Maulotaur : Minotaur replaces Minotaur
{
   Health 5000
   Mass 9000
   Speed 32
   Damage 10
   PainChance 0
   Tag "Maulotaur"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Cyberdemon)
      goto Super::Death
   }
}

actor Lith_Heretic_KnightAxe : KnightAxe replaces KnightAxe
{
   DamageType "Melee"
}

actor Lith_Heretic_RedAxe : RedAxe replaces RedAxe
{
   DamageType "Melee"
}

actor Lith_Heretic_UndeadWarrior : Knight replaces Knight
{
   Health 500
   Tag "Undead Warrior"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_HellKnight)
      goto Super::Death
   }
}

actor Lith_Heretic_SpectralWarrior : KnightGhost replaces KnightGhost
{
   Health 500
   Tag "Spectral Warrior"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_HellKnight)
      goto Super::Death
   }
}

actor Lith_Heretic_LichIce : HeadFX1 replaces HeadFX1
{
   DamageType "Ice"
   Damage 4
}

actor Lith_Heretic_LichIce2 : HeadFX2 replaces HeadFX2
{
   DamageType "Ice"
}

actor Lith_Heretic_LichFire : HeadFX3 replaces HeadFX3
{
   DamageType "Lith_Fire"
}

actor Lith_Heretic_LichWhirlwind : Whirlwind replaces Whirlwind
{
   DamageType "Lith_Shrapnel"
}

// [disappointment is emitted in the general direction of Raven Software]
actor Lith_Heretic_IronLich : IronLich replaces IronLich
{
   Health 1500
   Mass 9000
   PainChance 0
   Tag "Iron Lich"
   
   +MISSILEMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_PainElemental)
      goto Super::Death
   }
}

actor Lith_Heretic_Sabreclaw : Clink replaces Clink
{
   Speed 24
   Tag "Sabreclaw"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Demon)
      goto Super::Death
   }
}

actor Lith_Heretic_WeredragonFireball : BeastBall replaces BeastBall
{
   DamageType "Fire"
}

actor Lith_Heretic_Weredragon : Beast replaces Beast
{
   Health 800
   Tag "Weredragon"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Revenant)
      goto Super::Death
   }
}

actor Lith_Heretic_DSparilMountFireball : SorcererFX1 replaces SorcererFX1
{
   Speed 24
   Species "Lith_IAmAWizard"
   
   +MTHRUSPECIES
}

actor Lith_Heretic_DSparilMount : Sorcerer1 replaces Sorcerer1
{
   Health 4000
   Speed 32
   MinMissileChance 100
   Species "Lith_IAmAWizard"
}

actor Lith_Heretic_DSparilLightning2 : Sorcerer2FX1
{
   Species "Lith_IAmAWizard"
   
   +MTHRUSPECIES
}

actor Lith_Heretic_DSparilLightning3 : Sorcerer2FX1
{
   Species "Lith_IAmAWizard"
   Speed 15
   
   +MTHRUSPECIES
   +SEEKERMISSILE
   
   states
   {
   Spawn:
      FX16 A 0 bright nodelay A_SeekerMissile(5, 25, SMF_LOOK)
      FX16 A 2 bright A_BlueSpark
      FX16 A 1 bright A_SeekerMissile(5, 25, SMF_LOOK)
      
      FX16 B 0 bright A_SeekerMissile(5, 25, SMF_LOOK)
      FX16 B 2 bright A_BlueSpark
      FX16 B 1 bright A_SeekerMissile(5, 25, SMF_LOOK)
      
      FX16 C 0 bright A_SeekerMissile(5, 25, SMF_LOOK)
      FX16 C 2 bright A_BlueSpark
      FX16 C 1 bright A_SeekerMissile(5, 25, SMF_LOOK)
      loop
   }
}

actor Lith_Heretic_DSparilLightning : Sorcerer2FX1 replaces Sorcerer2FX1
{
   DamageType "Lith_Energy"
   Speed 24
   Damage 100
   Species "Lith_IAmAWizard"
   
   +MTHRUSPECIES
   
   states
   {
   Spawn:
      TNT1 AAAAA 0 nodelay A_CustomMissile("Lith_Heretic_DSparilLightning2", frandom(-16, 16), frandom(-16, 16), angle, CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE)
      TNT1 AAA 0 A_CustomMissile("Lith_Heretic_DSparilLightning2", frandom(-64, 64), frandom(-64, 64), angle, CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE)
      TNT1 AA 0 A_CustomMissile("Lith_Heretic_DSparilLightning3", frandom(-64, 64), frandom(-64, 64), angle, CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_ABSOLUTEANGLE)
      goto Super::Spawn
   }
}

actor Lith_Heretic_DSparilDiscipleSpawner : Sorcerer2FX2 replaces Sorcerer2FX2
{
   Species "Lith_IAmAWizard"
   
   +MTHRUSPECIES
   
   var int user_wizards;
   
   states
   {
   Spawn:
      FX11 A 35 bright
   SpawnLoop:
      FX11 B 5 bright A_SpawnItemEx("Wizard")
      FX11 A 15 bright A_SetUserVar("user_wizards", user_wizards + 1)
      FX11 A 0 bright A_JumpIf(user_wizards == 3, "Death")
      loop
   Death:
      FX11 BCDEFG 5 bright
      stop
   
   }
}

// THE FINAL BOSS OF THE ENTIRE GAME HAS LESS HEALTH THAN A CYBERDEMON
// THE RULER OF A REALM OF DEATH AND CHAOS IS WEAKER THAN THE THING DOOMGUY KILLS
// LIKE
// 20 OF? MORE?
// FUCK
actor Lith_Heretic_DSparil : Sorcerer2 replaces Sorcerer2
{
   Health 7000
   Mass 9000
   Speed 20
   PainChance 0
   Radius 12
   MinMissileChance 50
   Species "Lith_IAmAWizard"
   Tag "D'Sparil"
   
   +MISSILEMORE
   +MISSILEEVENMORE
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_DSparil)
      goto Super::Death
   }
}

// FINALLY, I AM FREE
// EOF
