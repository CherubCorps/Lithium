class Lith_Boss_Makarov : Lith_Phantom
{
   default
   {
      Tag "Makarov, Avarum Est";
      Health 3000;
   }

   states
   {
   See:
      PLAY AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase;
      loop;
   Missile:
      PLAY E 0 A_JumpIf(m_phase < 2, "MissileStart");
      PLAY F 0 A_JumpIf(m_meleetime > 0, "MissileStart");
      PLAY E 0 A_JumpIfCloser(384, "Lunge");
   MissileStart:
      PLAY E 0 A_Jump(100, "Missile2", "Missile3");
   Missile1:
      PLAY E 0 A_PlaySound("enemies/makarov/fire", CHAN_6);
      PLAY EEEEEEEE 0 A_SpawnProjectile("Lith_MakarovShotgunBullet", 32, 0, frandom(-20, 20), CMF_OFFSETPITCH|CMF_BADPITCH, frandom(-5, 5));
      PLAY E 5 A_FaceTarget;
      PLAY F 2 A_FaceTarget;
      PLAY E 10 A_PlaySound("enemies/makarov/pump", CHAN_5);
      PLAY F 10;
      PLAY F 0 A_Jump(100, "See");
      PLAY F 2 A_MonsterRefire(30, "See");
      loop;
   Missile2:
      PLAY E 0 A_JumpIfCloser(200, "Missile1");
      PLAY E 0 A_PlaySound("enemies/makarov/throw", CHAN_6);
      PLAY E 0 A_SpawnProjectile("Lith_MakarovGrenade", 32, 0, frandom(-4,4), CMF_ABSOLUTEPITCH|CMF_SAVEPITCH|CMF_BADPITCH, -10);
      PLAY EF 10;
      goto See;
   Missile3:
      PLAY E 0 A_JumpIf(m_phase < 3, "Missile2");
      PLAY E 0 A_PlaySound("enemies/makarov/chainthrow", CHAN_6);
      PLAY E 0 A_SpawnProjectile("Lith_MakarovChain", 32, 0, 0, CMF_BADPITCH);
      PLAY EF 10;
      goto See;
   Melee:
      PLAY F 0 A_JumpIf(m_phase < 2, "Missile");
      PLAY F 0 A_JumpIf(m_meleetime > 0, "Missile");
      PLAY F 0 {m_meleetime = 35;}
      PLAY F 0 A_FaceTarget;
      PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "enemies/makarov/scythehit", "enemies/makarov/scythe", "Melee");
      PLAY F 7;
      goto See;
   Lunge:
      PLAY A 0 A_PlaySound("enemies/phantom/lunge", CHAN_5, 1.0, false, 0.5);
      PLAY A 10 A_FaceTarget;
      PLAY A 0 A_FaceTarget;
      PLAY A 5 ACS_NamedExecuteWithResult("Lith_PhantomTeleport");
      goto Melee;
   }
}

class Lith_MakarovShotgunTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.5;
      Alpha 0.5;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      PUFF A 1 A_FadeOut;
      wait;
   }
}

class Lith_MakarovShotgunBullet : FastProjectile
{
   default
   {
      MissileType "Lith_MakarovShotgunTrail";
      RenderStyle "Translucent";
      DamageType "Bullet";
      Alpha 0.9;
      Scale 0.5;
      Radius 24;
      Height 20;
      DamageFunction 3 * random(1, 5);
      Speed 96;

      +BLOODSPLATTER
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   Crash:
   Death:
      PUFF CD 4;
      stop;
   XDeath:
      TNT1 A 0;
      stop;
   }
}

class Lith_MakarovGrenade : Lith_RifleGrenade
{
   default
   {
      Speed 30;
      Species "Lith_Phantom";

      +DONTHARMSPECIES
   }
}

class Lith_MakarovChainStopper : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_Stop;
      stop;
   }
}

class Lith_MakarovChainFX : Actor
{
   states
   {
   Spawn:
      BLLT I 10 bright;
      stop;
   }
}

class Lith_MakarovChain : Actor
{
   default
   {
      Damage 2;
      Speed 30;
      DamageType "Melee";

      Projectile;
      +HITTRACER
   }

   states
   {
   Spawn:
      TNT1 A 1 A_SpawnItemEx("Lith_MakarovChainFX");
      wait;
   XDeath:
      TNT1 A 0 A_PlaySound("enemies/makarov/chainhit", CHAN_5);
   Death:
      TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_GiveInventory("Lith_MakarovChainStopper", 1, AAPTR_TRACER);
      stop;
   }
}

// EOF
