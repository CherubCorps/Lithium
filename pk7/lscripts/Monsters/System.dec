actor Lith_MonsterID : Inventory
{
   Inventory.MaxAmount 0x7FFFFFFF
   +INVENTORY.UNDROPPABLE
}

actor Lith_MonsterInvalid : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_MonsterBarrier
{
   RenderStyle "Add"
   XScale 0.3
   YScale 0.6
   VSpeed 2
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      LBAR A 1
   SpawnLoop:
      LBAR A 1 A_FadeOut
      loop
   }
}

actor Lith_MonsterBarrierLook : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_BeginAngles", x, y)
      TNT1 A 0 A_RadiusGive("Lith_GetAngles", 256, RGF_PLAYERS)
      stop
   }
}

actor Lith_RunMonsterInfo : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfInventory("Lith_MonsterID", 1, "Nope")
      TNT1 A 0 A_JumpIfInventory("Lith_MonsterInvalid", 1, "Nope")
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MonsterInfo")
      stop
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_MonsterInfoEmitter
{
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_RadiusGive("Lith_RunMonsterInfo", 16384, RGF_MONSTERS|RGF_NOSIGHT|RGF_CUBE)
      stop
   }
}

// EOF

