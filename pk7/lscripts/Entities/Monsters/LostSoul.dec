actor Lith_LostSoul : LostSoul replaces LostSoul
{
   Health 70
   RenderStyle Add
   Alpha 0.7
   
   +QUICKTORETALIATE
   
   var int user_invisible;
   
   states
   {
   Spawn:
      SKUL AB 10 bright A_Look
      loop
   GoInvisible:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.08)
      SKUL A 0 A_SetUserVar(user_invisible, true)
      goto See
   StopBeingInvisibleYouIdiot:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.7)
      SKUL A 0 A_SetUserVar(user_invisible, false)
      goto Missile
   See:
      SKUL A 0 A_JumpIf((!user_invisible) && (random(0, 128) < 64), "GoInvisible")
      SKUL AAAAABBBBB 1 bright A_FastChase
      loop
   Missile:
      SKUL A 0 A_JumpIf(user_invisible, "StopBeingInvisibleYouIdiot")
      SKUL C 10 bright A_FaceTarget
      SKUL D 4 bright A_SkullAttack
   MissileLoop:
      SKUL CD 4 bright
      goto MissileLoop
   Pain:
      SKUL E 0 A_SetTranslucent(1, 0.7)
      SKUL E 3 bright
      SKUL E 3 bright A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteAlways("Lith_GiveScoreToTarget", 0, Score_LostSoul)
   DeathReal:
      SKUL F 0 A_SetTranslucent(1, 0.7)
      SKUL F 6 bright
      SKUL G 6 bright A_Scream
      SKUL H 6 bright
      SKUL I 6 bright A_NoBlocking
      SKUL J 6
      SKUL K 6
      stop
   }
}

