actor Lith_SwordHipcher
{
   RenderStyle Add
   Alpha 0.25
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK A 15 A_FadeOut(0.1)
      wait
   }
}

actor Lith_SwordHitler
{
   RenderStyle Add
   Scale 0.5
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SHKR B 0
      SHKR BBBBBBBB 0 A_SpawnItemEx("Lith_SwordHipcher",
                                    frandom(-24,24),frandom(-24,24),frandom(-24,24),
                                    frandom(-8,8),frandom(-8,8),frandom(-8,8),
                                    0, SXF_NOCHECKPOSITION, 240)
      SHKR B 1 bright A_FadeOut(0.1)
      wait
   }
}

actor Lith_SwordHither
{
   RenderStyle Add
   Scale 0.5
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SHKR B 0
      SHKR B 1 bright A_FadeOut(0.1)
      wait
   }
}

actor Lith_SwordHitter
{
   Radius 6
   Height 6
   Speed 40
   Damage 30
   Species "Lith_Player"
   DamageType "Lith_Melee"
   
   Projectile
   +MTHRUSPECIES
   +EXPLODEONWATER
   +RIPPER
   
   var int user_trailstep;
   var int user_delta;
   
   states
   {
   ded:
      TNT1 A 0
      stop
   Spawn:
      TNT1 A 0
      TNT1 A 3 A_PlaySound("weapons/sword/attack")
   SpawnLoop:
      TNT1 A 0 A_JumpIf(user_trailstep >= 8, "TrailDone")
      TNT1 A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      TNT1 A 0 A_SpawnItemEx("Lith_SwordHither",
                                   ((user_trailstep * 4.0) * velx) / -35.0,
                                  -((user_trailstep * 4.0) * vely) / -35.0,
                             2.0 + ((user_trailstep * 4.0) * velz) / -35.0,
                             0, 0, 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION,
                             user_delta * 40)
      loop
   TrailDone:
      TNT1 A 0 A_SetUserVar(user_trailstep, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_SwordHitler", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
      TNT1 A 0 A_SetUserVar(user_delta, user_delta + 1)
      TNT1 A 0 A_JumpIf(user_delta >= 15, "ded")
      TNT1 A 1
      goto SpawnLoop
   }
}

actor Lith_SwordHipper : Lith_SwordHitter
{
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 3
      goto SpawnLoop
   }
}

actor Lith_SwordBlitter
{
   Speed 0
   
   Projectile
   +NOBLOCKMAP
   +EXPLODEONWATER
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnProjectile("Lith_SwordHitter",  16,   8, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_BADPITCH)
      TNT1 A 1 A_SpawnProjectile("Lith_SwordHipper",   8,   0, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_BADPITCH)
      TNT1 A 0 A_SpawnProjectile("Lith_SwordHitter",  -8,  -8, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_BADPITCH)
      TNT1 A 1 A_SpawnProjectile("Lith_SwordHipper", -16, -16, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER|CMF_BADPITCH)
      stop
   }
}

