actor Lith_IsPhantom : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_PhantomAuraFX
{
   RenderStyle "Subtract"
   Alpha 0.6
   Scale 0.05
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      LDTH A 1 A_FadeOut(0.01)
      wait
   }
}

actor Lith_PhantomAura : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 AAAAAAAA 0 A_SpawnItemEx("Lith_PhantomAuraFX", frandom(-20,20),frandom(-20,20),frandom(0,16), 0,0,frandom(0,1), 0, 0, 128)
      stop
   }
}

actor Lith_PhantomTeleportFX
{
   RenderStyle "Stencil"
   StencilColor "Black"
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      "####" "#" 3
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_PhantomTeleport : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_SpawnItemEx("Lith_PhantomTeleportFX", 0,0,0, 0,0,0.5, 0, SXF_TRANSFERSPRITEFRAME)
      stop
   }
}

actor Lith_PhantomPhase2Flags : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_ChangeFlag("MISSILEMORE", true)
      stop
   }
}

actor Lith_PhantomPhase3Flags : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_ChangeFlag("MISSILEMORE", true)
      TNT1 A 0 A_ChangeFlag("MISSILEEVENMORE", true)
      stop
   }
}

actor Lith_PhantomTarget : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIf(CallACS("Lith_PhantomTargetCheck"), "Nope")
      TNT1 A 0 A_CheckFlag("COUNTKILL", 1)
      goto Nope
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_PhantomTarget")
      stop
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_PhantomHunt : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_RadiusGive("Lith_PhantomTarget", 1024, RGF_MONSTERS)
      stop
   }
}

actor Lith_PhantomLOSCheck : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfTargetInLOS("Nope")
      TNT1 A 0 A_ClearTarget
      stop
   
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_Phantom
{
   Species "Lith_Phantom"
   Radius 20
   Height 56
   Speed 4
   RenderStyle "Stencil"
   StencilColor "Black"
   
   Monster
   +NOPAIN
   +THRUSPECIES
   +DONTHARMSPECIES
   +BOSS
   +MISSILEMORE
   +NOTARGET
   +NEVERTARGET
   
   var int user_phase;
   var int user_donespawn;
   var int user_step;
   var int user_meleetime;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_JumpIf(user_donespawn, "Spawn1")
      TNT1 A 0 A_GiveInventory("Lith_IsPhantom", 1)
      TNT1 A 0 A_SetUserVar(user_donespawn, true)
      TNT1 A 0 A_Jump(256, "PostInit")
   PostInit:
      TNT1 A 0 A_Jump(256, "CallPhantomMain")
   CallPhantomMain:
      TNT1 A 0 A_PlaySound("enemies/phantom/aura", CHAN_7, 0.7, true, 0.7)
      TNT1 A 0 ACS_NamedExecute("Lith_PhantomMain")
      goto Spawn1
   Spawn1:
      PLAY A 0 A_JumpIf(user_step <= 1, "Spawn2")
      PLAY B 0 A_JumpIf(user_step == 2, "Spawn2")
      PLAY C 0 A_JumpIf(user_step == 3, "Spawn2")
      PLAY D 0 A_SetUserVar(user_step, 0)
   Spawn2:
      PLAY "#" 0 A_SetUserVar(user_step, user_step + 1)
      PLAY "#" 0 A_Look
      PLAY "#" 1 A_Wander
      PLAY "#" 0 A_Look
      PLAY "#" 1 A_Wander
      PLAY "#" 0 A_Look
      PLAY "#" 1 A_Wander
      PLAY "#" 0 A_Look
      PLAY "#" 1 A_Wander
      goto Spawn1
   }
}

actor Lith_BasicPhantom : Lith_Phantom
{
   states
   {
   CallPhantomMain:
      TNT1 A 0 ACS_NamedExecute("Lith_PhantomMain2")
      goto Spawn1
   }
}

// EOF

