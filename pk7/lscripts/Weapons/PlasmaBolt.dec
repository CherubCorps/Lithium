actor Lith_PlasmaBoltTrail
{
   RenderStyle "Add"
   Alpha 0.2
   Scale 0.125
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_Jump(256, "Particles")
   SpawnPick:
      TNT1 A 0 A_Jump(256, "SpawnA", "SpawnB")
   SpawnA:
      PBAL A 1 bright
      stop
   SpawnB:
      PBAL B 1 bright
      stop
   }
}

actor Lith_PlasmaBoltTrail_Green : Lith_PlasmaBoltTrail
{
   states
   {
   Particles:
      TNT1 A 0 A_SpawnParticle(Green, SPF_FULLBRIGHT, 6, 2, 0,
         0, 0, 0,
         frandom[Lith_PlasmaBolt](-2, 2),
         frandom[Lith_PlasmaBolt](-2, 2),
         frandom[Lith_PlasmaBolt](-2, 2))
      goto SpawnPick
   }
}

actor Lith_PlasmaBoltTrail_Red : Lith_PlasmaBoltTrail
{
   Translation "0:255=%[0,0,0]:[1,0,0]"
   
   states
   {
   Particles:
      TNT1 A 0 A_SpawnParticle(Red, SPF_FULLBRIGHT, 6, 2, 0,
         0, 0, 0,
         frandom[Lith_PlasmaBolt](-2, 2),
         frandom[Lith_PlasmaBolt](-2, 2),
         frandom[Lith_PlasmaBolt](-2, 2))
      goto SpawnPick
   }
}

actor Lith_PlasmaBolt
{
   Radius 12
   Height 12
   Speed 50
   Damage (10)
   DamageType "Lith_Energy"
   Species "Lith_Player"
   
   Projectile
   +MTHRUSPECIES
   
   var int user_trailstep;

   states
   {
   Spawn:
      TNT1 A 3
      TNT1 A 0 A_Jump(256, "Trail")
   Trail:
      TNT1 A 0 A_JumpIf(user_trailstep >= 5, "TrailDone")
      TNT1 A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaBoltTrail_Green",
                                   ((user_trailstep * 10.0) * velx) / -35.0,
                                  -((user_trailstep * 10.0) * vely) / -35.0,
                             2.0 + ((user_trailstep * 10.0) * velz) / -35.0,
                             0, 0, 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      TNT1 A 0 A_SetUserVar(user_trailstep, 0)
      goto Step
   Step:
      TNT1 A 1
      TNT1 A 0 A_Jump(256, "Trail")
   Death:
      TNT1 AAAA 0 A_SpawnParticle(Green, SPF_FULLBRIGHT, 15, 4, 0,
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5))
      TNT1 A 2 A_PlaySound("weapons/plasma/explode", CHAN_AUTO)
      stop
   }
}

actor Lith_PenetratingPlasmaBolt : Lith_PlasmaBolt
{
   Damage (5)
   Speed 47
   
   +RIPPER
   
   states
   {
   Trail:
      TNT1 A 0 A_JumpIf(user_trailstep >= 10, "TrailDone")
      TNT1 A 0 A_SetUserVar(user_trailstep, user_trailstep + 1)
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaBoltTrail_Red",
                                   ((user_trailstep * 10.0) * velx) / -35.0,
                                  -((user_trailstep * 10.0) * vely) / -35.0,
                             2.0 + ((user_trailstep * 10.0) * velz) / -35.0,
                             0, 0, 0,
                             0, SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION)
      loop
   TrailDone:
      TNT1 A 0 A_SetUserVar(user_trailstep, 0)
      goto Step
   Death:
      TNT1 AAAA 0 A_SpawnParticle(Red, SPF_FULLBRIGHT, 15, 4, 0,
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5),
         frandom[Lith_PlasmaBolt](-5, 5))
      TNT1 A 2 A_PlaySound("weapons/plasma/explode", CHAN_AUTO)
      stop
   }
}

