actor Lith_StarShrapnel
{
   Damage (15)
   Speed 32
   Radius 2
   Height 2
   Scale 0.7
   Mass 0
   RenderStyle "Add"
   
   Projectile
   +NODAMAGETHRUST
   +SEEKERMISSILE
   +SCREENSEEKER
   
   states
   {
   Spawn:
      BLLT M 4 bright A_SeekerMissile(0, 2, SMF_LOOK)
      loop
   Death:
      MICX A 0 bright A_PlaySound("weapons/dark/impact", CHAN_BODY, 0.3)
      MICX A 0 bright A_SetScale(0.5)
      MICX ABCDEFG 1 bright
      stop
   }
}

actor Lith_StarShrapnelHoming : Lith_StarShrapnel
{
   Damage (30)
   Scale 1.4
   Translation "160:167=192:199", "224:231=192:199"
   
   states
   {
   Spawn:
      BLLT M 4 bright A_SeekerMissile(0, 360, SMF_LOOK)
      loop
   }
}

actor Lith_StarWoosh1
{
   Radius 8
   Height 8
   Scale 0.7
   RenderStyle "Add"
   
   +NOINTERACTION
   
   states
   {
   Death:
   Spawn:
      TNT1 A 0
      BLLT L 0 bright A_JumpIf(scalex < 0.01, "ded")
      BLLT L 2 bright A_SetScale(scalex*frandom(0.9,1.0))
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarWoosh2 : Lith_StarWoosh1
{
   states
   {
   Spawn:
      BLLT L 0 bright A_SetScale(scalex*frandom(0.95,1.0))
      BLLT L 1 bright A_FadeOut(0.05)
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarProjectile
{
   Damage (20)
   Speed 2
   Scale 0.15
   Radius 8
   Height 8
   Mass 500
   RenderStyle "Add"
   
   Projectile
   +RIPPER
   +FORCERADIUSDMG
   
   var int user_soundtimer;
   var int user_rushtimer;
   var int user_homingshrapneltimer;
   
   states
   {
   Spawn:
      TNT1 A 4
      TNT1 A 0 A_PlaySound("weapons/dark/wind", CHAN_7, 1.0, true)
   Spawn1:
      BLLT L 0 bright A_AlertMonsters
      BLLT L 0 bright A_SetUserVar(user_rushtimer, user_rushtimer + 1)
      BLLT L 0 bright A_JumpIf(user_rushtimer > 35*7, "Rush")
      BLLT L 0 bright A_JumpIf(scalex < 0.7, "ScaleUp")
   SoundCheck:
      BLLT L 0 bright A_SetUserVar(user_homingshrapneltimer, user_homingshrapneltimer + 1)
      BLLT L 0 bright A_JumpIf(user_homingshrapneltimer >= 10, "DoHomingMissile")
   SoundCheck1:
      BLLT L 0 bright A_SetUserVar(user_soundtimer, user_soundtimer - 1)
      BLLT L 0 bright A_JumpIf(user_soundtimer > 0, "DoMissile")
      BLLT L 0 bright A_SetUserVar(user_soundtimer, 3)
      BLLT L 0 bright A_PlaySound("weapons/dark/fire", CHAN_5)
   DoMissile:
      BLLT L 0 bright A_Explode(40, 8 + 45 * scalex, 0)
      BLLT LLLLL 0 bright A_CustomMissile("Lith_StarShrapnel",
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, false)/FIX,
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, true)/FIX,
         0, CMF_TRACKOWNER|CMF_AIMDIRECTION)
      goto DoWoosh
   DoHomingMissile:
      BLLT L 0 bright A_SetUserVar(user_homingshrapneltimer, 0)
      BLLT LL 0 bright A_CustomMissile("Lith_StarShrapnelHoming",
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, false)/FIX,
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, true)/FIX,
         0, CMF_TRACKOWNER|CMF_AIMDIRECTION)
      goto SoundCheck1
   ScaleUp:
      BLLT L 0 bright A_SetScale(scalex * 1.01)
      goto SoundCheck
   Rush:
      BLLT L 0 bright A_ChangeVelocity(velx*1.1, vely*1.1, velz*1.1, CVF_REPLACE)
   DoWoosh:
      BLLT L 0 bright A_SpawnItemEx("Lith_StarWoosh1", 0,0,0, 4,0,0, 180, SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION)
      BLLT L 0 bright A_SpawnItemEx("Lith_StarWoosh2", 0,0,0, 3,0,0, 180, SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION)
      BLLT L 0 bright A_Quake(1, 2, 0, 512)
      BLLT L 0 bright A_Quake(3, 2, 0, 256)
      BLLT L 0 bright A_Quake(5, 2, 0, 64)
      BLLT L 1 bright
      goto Spawn1
   Death:
      SDXP A 0 A_Explode(128, 512, 0)
      SDXP A 0 A_StopSound(CHAN_7)
      SDXP A 0 A_PlaySound("weapons/shipgun/explode", CHAN_6)
      SDXP A 0 A_SetScale(1)
      SDXP ABCDEFGHIJKLMNOPQRS 1 bright
      SDXP T 1 bright A_FadeOut
      wait
   }
}

actor Lith_StarDestroyer : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_StarDestroyer"
   Weapon.UpSound "weapons/shipgun/draw"
   Weapon.SlotNumber 7
   Weapon.SlotPriority 1
   Weapon.BobStyle "InverseSmooth"
   Weapon.BobRangeX 1.7
   Weapon.BobSpeed 0.7
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_BFG9000Pickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      SDSG AABBCCDDEEFFGGHH 1 A_WeaponReady
      loop
   
   Deselect:
      SDSG AAA 0 A_Lower
      SDSG A 1
      loop
   
   Select:
      SDSG AA 0 A_Raise
      SDSG A 1
      loop
   
   Fire:
      SDSG A 0 A_JumpIfInventory("Lith_CannonAmmo", 5, 1)
      goto FireEmpty
      SDSG A 1 A_PlaySound("weapons/shipgun/prepare", CHAN_WEAPON)
      SDSG ABCDEFFGGHHAABCDEFGH 1 bright
      SDSG ABBCCDDEFG 1 bright offset(0, 33)
      SDSG HADEFHBCGH 1 bright offset(0, 34)
      SDSG A 0 A_TakeInventory("Lith_CannonAmmo", 5, TIF_NOTAKEINFINITE)
      SDSG A 0 A_AlertMonsters
      SDSG A 1 bright
      SDSG A 1 bright offset(-1, 33)
      SDSG A 1 bright offset(-1, 34)
      SDSG A 1 bright offset(-1, 36)
      SDSG A 2 bright offset(1, 37)
      SDSF A 3 bright offset(1, 38)
      SDSF A 0 bright A_GunFlash("DummyFlash")
      SDSF A 0 bright A_PlaySound("weapons/shipgun/fire", CHAN_5)
      SDSF B 2 bright offset(0, 62) A_FireCustomMissile("Lith_StarProjectile")
      SDSF C 1 bright offset(0, 57)
      SDSF D 1 bright offset(0, 47)
      SDSF E 1 bright offset(0, 44)
      SDSF F 1 bright offset(0, 40)
      SDSF G 1 bright offset(0, 37)
      SDSF H 1 bright offset(0, 35)
      SDSG AABBCCDDEEFFGGHH 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireEmpty:
      SDSG A 2 A_PlaySound("weapons/shipgun/empty", 7)
      SDSG B 1 offset(0, 28)
      SDSG C 1 offset(0, 29)
      SDSG D 1 offset(0, 32)
      SDSG E 1 offset(0, 35)
      SDSG F 1 offset(0, 40)
      SDSG G 1 offset(0, 42)
      SDSG H 1 offset(0, 43)
      SDSG A 1 offset(0, 44)
      SDSG B 1 offset(0, 40)
      SDSG C 1 offset(0, 37)
      SDSG D 1 offset(0, 34)
      SDSG EFG 1 offset(0, 32)
      SDSG H 1 A_ReFire
      goto Ready
   }
}

// EOF

