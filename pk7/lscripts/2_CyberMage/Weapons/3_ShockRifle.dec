actor Lith_ShockRifleMagazine : Lith_DroppedMagazine
{
   Scale 0.7
   Mass 150

   states
   {
   Spawn1:
      MGZE ABCD 3 A_SetUserVar(user_side, 0)
      MGZE EFGH 3 A_SetUserVar(user_side, 1)
      loop
   Death:
      MGZE C 0 A_JumpIf(user_side == 0, "Done")
      MGZE G 0 A_JumpIf(user_side == 1, "Done")
      stop
   }
}

actor Lith_ShockRifleMagazine2 : Lith_ShockRifleMagazine
{
   BounceSound "weapons/magbouncef"
   Mass 155

   states
   {
   Spawn1:
      MGZE IJKL 3 A_SetUserVar(user_side, 0)
      MGZE MNOP 3 A_SetUserVar(user_side, 1)
      loop
   Death:
      MGZE K 0 A_JumpIf(user_side == 0, "Done")
      MGZE O 0 A_JumpIf(user_side == 1, "Done")
      stop
   }
}

actor Lith_ShockRifleShotsFired : Inventory
{
   Inventory.MaxAmount 7
}

actor Lith_ShockFX
{
   RenderStyle "AddStencil"
   StencilColor "LightBlue"
   Alpha 0.5

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      "####" "#" 1
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_ShockEnemyGiver : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP
   +FORCEPAIN

   states
   {
   Pickup:
      TNT1 A 0 A_DamageSelf(5, "Lith_Energy", "None", "None", AAPTR_TARGET)
      TNT1 A 0 A_SpawnItemEx("Lith_ShockFX", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE)
      stop
   }
}

actor Lith_ShockEffect
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.6

   +NOINTERACTION
   +RANDOMIZE
   +BRIGHT

   states
   {
   Spawn:
      ZAP1 FEDCBA 1
      stop
   }
}

actor Lith_ShockEffect2 : Lith_ShockEffect
{
   -NOINTERACTION
   +NOGRAVITY
   +NOBLOCKMAP
   +FRIENDLY

   states
   {
   Spawn:
      ZAP1 FEDCBA 1 A_CustomRailgun(5, 0, "None", "White", RGF_SILENT|RGF_NOPIERCING|RGF_EXPLICITANGLE|RGF_FULLBRIGHT|RGF_CENTERZ, 0, 32, "Lith_PlayerDummyPuff", frandom(0, 360), frandom(0, 360), frandom(64,100), 10, 0.2, 0)
      stop
   }
}

actor Lith_SuperShockEffect
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.6
   Translation "160:167=192:199"

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      SDXP IJKLMNOPQRS 1
      SDXP S 1 A_FadeOut(0.2)
      wait
   }
}

actor Lith_ShockHitCount : Inventory
{
   Inventory.MaxAmount 6
}

actor Lith_ShockPuff : Lith_BulletPuff
{
   +HITTRACER
   +FORCEPAIN
   +PUFFONACTORS
   +PUFFGETSOWNER

   states
   {
   Crash:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/erifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_ShockRif_A, true), "Charged")
      goto Super::Spawn
   Melee:
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/erifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_ShockRif_B, true), "Binding")
      TNT1 A 0 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_ShockRif_A, true), "Charged")
      stop
   Charged:
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect2", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
      stop
   Binding:
      TNT1 A 0 A_GiveInventory("Lith_ShockHitCount", 1, AAPTR_TRACER)
      TNT1 A 0 A_JumpIfInventory("Lith_ShockHitCount", 0, "Explode", AAPTR_TRACER)
      TNT1 AAA 3 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      stop
   Explode:
      TNT1 A 0 A_TakeInventory("Lith_ShockHitCount", 999, 0, AAPTR_TRACER)
      TNT1 A 0 A_SpawnItemEx("Lith_SuperShockEffect")
      TNT1 A 0 A_PlaySound("weapons/erifle/shock")
      TNT1 A 0 A_Explode(64, 512, 0)
      TNT1 AAAAA 3 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      stop
   }
}

actor Lith_ShockRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_ShockRifle"
   Weapon.UpSound "weapons/erifle/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE
   
   states(actor)
   {
   SpawnPickup:
      TNT1 A 0 Lith_A_SpawnPickup("Lith_ShotgunPickup")
      stop
   }
   
   states(weapon)
   {
   Ready:
      CSRG A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      CSRG AAA 0 A_Lower
      CSRG A 1
      loop
   
   Select:
      CSRG AAA 0 A_Raise
      CSRG A 1
      loop

   FireEmpty:
      "####" "#" 1
      "####" "#" 2 offset(0, 41)
      "####" "#" 1 offset(0, 45)
      "####" "#" 1 offset(0, 43)
      "####" "#" 1 offset(0, 40) A_PlaySound("weapons/ionrifle/empty", CHAN_5)
      "####" "#" 1 offset(0, 38)
      "####" "#" 1 offset(0, 36)
      "####" "#" 1 offset(0, 33)
      "####" "#" 1 offset(0, 32)
      "####" "#" 5
      "####" "####" 1 A_ReFire
      goto Ready

   Fire:
      "####" "#" 0 Lith_A_JumpIfNoSlot3OptAmmo
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_ShockRifleShotsFired", 1)
      TNT1 A 0 Lith_A_TakeSlot3OptAmmo
      CSRG A 1
      TNT1 A 0 A_SetPitch(pitch - CallACS("LPData", pdata_recoil)/FIX*0.5)
      TNT1 A 0 A_Recoil(0.1)
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_PlaySound("weapons/erifle/fire", CHAN_WEAPON)
      TNT1 A 0 A_FireBullets(1, 0, -1, 37*frandom(1,1.5), "Lith_ShockPuff", FBF_NORANDOM)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawnerSmall", 0, 0, 0, 6)
      CSRG B 1 bright offset(0, 40) A_Light(1)
      TNT1 A 0 A_SetPitch(pitch + CallACS("LPData", pdata_recoil)/FIX*0.25)
      CSRG C 1 bright offset(0, 35) A_Light(0)
      TNT1 A 0 A_SetPitch(pitch + CallACS("LPData", pdata_recoil)/FIX*0.25)
      CSRG A 1 offset(0, 34)
      CSRG A 2 offset(0, 33)
      goto Ready

   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 1, 1)
      goto Ready
      CSRG D 1 offset(1, 32)
      CSRG E 1 offset(3, 32)
      CSRG F 1 offset(4, 32)
      CSRG F 1 offset(8, 38)
      CSRG F 1 offset(12, 45)
      CSRG F 1 offset(16, 53)
      CSRG F 1 offset(19, 60)
      CSRG F 0 A_PlaySound("weapons/erifle/reloadout", CHAN_6)
      CSRG F 1 offset(21, 64)
      CSRG A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 0, 2)
      CSRG A 0 A_SpawnItemEx("Lith_ShockRifleMagazine2", 8,-1,20, frandom(0,1),frandom(1,3),0)
      goto Reload2
      CSRG A 0 A_SpawnItemEx("Lith_ShockRifleMagazine",  8,-1,20, frandom(0,1),frandom(1,3),0)
   Reload2:
      CSRG F 1 offset(23, 67)
      CSRG F 1 offset(22, 69)
      CSRG F 1 offset(19, 70)
      CSRG F 7 offset(18, 72)
      CSRG F 1 offset(17, 69)
      CSRG F 1 offset(19, 67)
      CSRG F 1 offset(20, 66)
      CSRG F 1 offset(21, 62)
      CSRG E 1 offset(20, 60)
      CSRG E 0 A_TakeInventory("Lith_ShockRifleShotsFired", 999)
      CSRG D 1 offset(21, 63) A_PlaySound("weapons/erifle/reloadin", CHAN_7)
      CSRG A 1 offset(23, 66)
      CSRG A 1 offset(24, 68)
      CSRG A 1 offset(25, 63)
      CSRG A 1 offset(24, 59)
      CSRG A 1 offset(22, 57)
      CSRG A 1 offset(20, 54)
      CSRG A 1 offset(16, 49)
      CSRG A 1 offset(12, 44)
      CSRG A 1 offset(10, 42)
      CSRG A 1 offset(8, 40)
      CSRG A 1 offset(5, 38)
      CSRG A 1 offset(2, 35)
      goto Ready
   }
}

// EOF
