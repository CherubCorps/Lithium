actor Lith_SuperShotgunCharge : Inventory
{
   Inventory.MaxAmount 14
}

actor Lith_SuperShotgunChargeIter : Inventory
{
   Inventory.MaxAmount 14
}

actor Lith_SuperShotgun : Lith_Weapon replaces SuperShotgun
{
   Tag "Super Shotgun"
   Inventory.PickupMessage "You got the Sbepru Shtetgenu!"
   Weapon.SlotNumber 3
   
   const int chargemax = 14;
   
   states
   {
   Ready:
      SHT2 A 1 A_WeaponReady
      wait
   Deselect:
      SHT2 A 0 A_TakeInventory("Lith_SuperShotgunCharge", 999)
      SHT2 A 0 A_TakeInventory("Lith_SuperShotgunChargeIter", 999)
   DeselectLoop:
      SHT2 A 1 { A_Lower; A_Lower; }
      loop
   Select:
      SHT2 A 0 A_TakeInventory("Lith_SuperShotgunCharge", 999)
      SHT2 A 0 A_TakeInventory("Lith_SuperShotgunChargeIter", 999)
   SelectLoop:
      SHT2 A 1 { A_Raise; A_Raise; }
      loop
   Fire:
   FireSingle:
      SHT2 A 6 {
         A_TakeInventory("Lith_SuperShotgunChargeIter", 999);
         A_GiveInventory("Lith_SuperShotgunCharge", 1);
         A_WeaponReady(Lith_BOBONLY); }
      goto FirePellets
   AltFire:
      SHT2 A 6 {
         A_TakeInventory("Lith_SuperShotgunChargeIter", 999);
         A_WeaponReady(Lith_BOBONLY); }
      SHT2 A 3 {
         A_GiveInventory("Lith_SuperShotgunCharge", 1);
         A_PlaySound("weapons/ssg/charge");
         A_WeaponReady(Lith_BOBONLY); }
      SHT2 A 0 {
         if(CountInv("Lith_SuperShotgunCharge") == chargemax)
            { return state("FirePelletsStart"); }
         A_ReFire;
         return state(0);
      }
   FirePelletsStart:
      SHT2 A 0 A_PlaySound("weapons/ssg/chargefinish", 6)
   FirePellets:
      SHT2 A 1 {
         // gyaaaaaaaaaaaa
         if(CountInv("Lith_SuperShotgunChargeIter") ==
            CountInv("Lith_SuperShotgunCharge"))
            { return state("FireDone"); }
         A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff");
         A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6);
         A_PlaySound("weapons/sshotf", 7);
         A_GiveInventory("Lith_SuperShotgunChargeIter", 1);
         A_Recoil(1.2);
         return state("FirePelletsAnim");
      }
   FirePelletsAnim:
      SHT2 IJ 1 bright
      goto FirePellets
   FireDone:
      SHT2 B 7
      SHT2 C 7 A_CheckReload
      SHT2 D 7 A_PlaySound("weapons/ssg/open", CHAN_WEAPON)
   ReloadChecks:
      SHT2 D 0 A_JumpIf(CountInv("Lith_SuperShotgunCharge"), "Reload")
      goto ReloadDone
   Reload:
      SHT2 Z 6
      SHT2 E 4
      SHT2 F 5 A_PlaySound("weapons/ssg/load", CHAN_WEAPON)
      SHT2 A 0 A_TakeInventory("Lith_SuperShotgunCharge", 1)
      goto ReloadChecks
   ReloadDone:
      SHT2 G 6
      SHT2 H 6 A_PlaySound("weapons/ssg/close", CHAN_WEAPON)
      SHT2 A 5 A_ReFire("FireSingle")
      goto Ready
   Spawn:
      SGN2 A 0
      SGN2 A 0 A_SetUserVar("user_pickupparm", weapon_supershotgun)
      goto SpawnLoop
   }
}

