class Lith_Boss_Michael : Lith_BasicPhantom
{
   default
   {
      Tag "Michael, Brother of Isaac";
      Health 4000;
      Speed 7;
      
      +THRUSPECIES
      +NOINFIGHTING
   }
   
   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("Melee", null);
      loop;
   Melee:
      PLAY F 0 A_FaceTarget;
      PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "enemies/michael/hit", "enemies/michael/swing", "Lith_Melee");
      PLAY F 5;
      goto See;
   Death:
      TNT1 A 0;
      TNT1 A 0 {A_NoBlocking(); A_KillMaster();}
      PLAY H 10;
      PLAY I 60;
      TNT1 A 70;
      stop;
   }
}

class Lith_Boss_Johnson : Lith_BasicPhantom
{
   default
   {
      Tag "Isaac, Superbiorem Est";
      Health 40;
      Speed 3;
      
      +THRUSPECIES
      +NOINFIGHTING
   }

   override void Tick()
   {
      if(health < 20) setTag("Johnson, Ally of Isaac");
      Super.Tick();
   }

   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
      loop;
   Missile:
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON);
      PLAY EE 0 A_CustomMissile("Lith_IsaacThompsonBullet2", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5));
      PLAY E 1 A_FaceTarget;
      PLAY F 1 A_FaceTarget;
      PLAY F 2 A_MonsterRefire(0, "See");
      loop;
   Death:
      PLAY A 0 {
         let m = Lith_Boss_Isaac(master);
         if(m) m.lith_dupes--;
      }
      PLAY H 10 A_NoBlocking;
      PLAY I 1 A_FadeOut(0.01);
      wait;
   }
}

class Lith_Boss_Isaac : Lith_Phantom
{
   default
   {
      Tag "Isaac, Superbiorem Est";
      Health 4000;
   }

   override void Lith_PostInit()
   {
      if(user_phase == 3)
         A_SpawnItemEx("Lith_Boss_Michael", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION);
      Console.printf("phase %i", user_phase);

      Super.Lith_PostInit();
   }

   void Lith_SpawnDupe()
   {
      if(random(0, 32) == 0 && user_phase == 3 && lith_dupes < 3)
      {
         lith_dupes++;
         A_SpawnItemEx("Lith_Boss_Johnson", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION);
      }
   }

   action void Lith_A_SpawnDupe()
   {
      invoker.Lith_SpawnDupe();
   }

   int lith_dupes;

   states
   {
   See:
      TNT1 A 0 Lith_A_SpawnDupe;
      PLAY AAAABBBBCCCCDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
      loop;
   Missile:
      TNT1 A 0 Lith_A_SpawnDupe;
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON);
      PLAY EE 0 A_CustomMissile("Lith_IsaacThompsonBullet", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5));
      PLAY E 1 A_FaceTarget;
      PLAY F 1 A_FaceTarget;
      PLAY F 0 A_Chase(null, null, CHF_FASTCHASE);
      PLAY F 2 A_MonsterRefire(0, "See");
      loop;
   Death:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_PhantomDeath", 3, user_phase);
      TNT1 A 0 {A_NoBlocking(); A_KillChildren();}
      PLAY H 10;
      PLAY I 60;
      TNT1 A 70;
      stop;
   }
}

// EOF

