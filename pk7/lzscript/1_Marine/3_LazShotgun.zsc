// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_LazShotgunTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.34;

      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT E 4 bright;
      BLLT E 2 bright A_FadeOut(0.5);
      BLLT E 1 bright A_FadeOut(0.2);
      stop;
   }
}

class Lith_LazShotgunPuff : Actor
{
   default
   {
      DamageType "Electric";
      +ALWAYSPUFF
      +FORCEDECAL
   }
}

class Lith_LazShotgun : Lith_MWeapon;

default
{
   Tag "Laser Shotgun";
   Weapon.UpSound "weapons/lshotgun/draw";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;

   +WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   LSHT A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   LSHT AAA 0 A_Lower;
   LSHT A 1;
   loop;

Select:
   LSHT AAA 0 A_Raise;
   LSHT A 1;
   loop;

Fire:
   LSHT A 0 A_AlertMonsters;
   LSHT AAAAAAA 0 A_RailAttack(7 * random(1, 4), 0, true, "", "Red", RGF_SILENT | RGF_FULLBRIGHT, 2, "Lith_LazShotgunPuff", 4, 4, 0, 10, 5, 0, "Lith_LazShotgunTrail", -8);
   LSHT A 0 A_PlaySound("weapons/lshotgun/fire", CHAN_WEAPON);
   LSHT A 0 A_Recoil(0.5);
   LSHT I 2 bright Lith_A_RecoilUp(0.75);
   LSHT H 1 bright;
   LSHT A 4;
   LSHT BB 1 Lith_A_RecoilDn(0.375);
   LSHT C 2 A_PlaySound("weapons/lshotgun/open", CHAN_6);
   LSHT D 2;
   LSHT E 3;
   LSHT F 5;
   LSHT G 2 A_PlaySound("weapons/lshotgun/close", CHAN_7);
   LSHT ED 2;
   LSHT CA 3;
   // [screams externally]
   LSHT A 2;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   LSHT A 0;
   LSHT A 1 A_ReFire;
   goto Ready;
}

// EOF
