#include "lscripts/Weapons/CannonBall.dec"

actor Lith_CannonScopedToken : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_GTFO : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_PunctuatorExplosion
{
   RenderStyle "Add"
   Alpha 0.2
   Scale 0.9
   DamageType "Lith_PlayerMissile"
   
   +BLOODLESSIMPACT
   +NOINTERACTION
   
   states
   {
   Spawn:
      TNT1 A 0 nodelay A_Explode(32, 64, 0)
      TNT1 A random(1, 5)
      MEXP AB 1 bright
      MEXP CD 2 bright
      MEXP EF 3 bright
      stop
   }
}

actor Lith_PunctuatorPuff
{
   Species "Lith_Player"
   DamageType "Lith_Shrapnel"
   
   +NOINTERACTION
   +PUFFONACTORS
   +NODECAL
   +BLOODLESSIMPACT
   +MTHRUSPECIES
   
   states
   {
   Spawn:
      MEXP AB 2 bright
      MEXP CD 3 bright
      MEXP EF 4 bright
      stop
   }
}

actor Lith_BFG9000 : Lith_Weapon replaces BFG9000
{
   Tag "$LITH_TXT_INFO_SHORT_Cannon"
   Weapon.UpSound "weapons/cannon/draw"
   Weapon.SlotNumber 7
   Weapon.BobStyle InverseSmooth
   Weapon.BobRangeX 2.0
   Weapon.BobSpeed 0.85
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   //
   //
   //
   AltReady:
      SCOP A 0 A_JumpIfInventory("Lith_GTFO", 1, "ScopeOut")
      SCOP A 1 A_WeaponReady(WRF_NOBOB)
      loop
   Ready:
      CNNG A 0 A_TakeInventory("Lith_GTFO", 999)
      CNNG A 1 A_WeaponReady
      wait
   Deselect:
      CNNG A 0 A_TakeInventory("Lith_CannonScopedToken", 1)
      CNNG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      CNNG AAA 0 A_Lower
      CNNG A 1
      loop
   Select:
      CNNG A 0 A_ZoomFactor(1.0)
   SelectLoop:
      CNNG AA 0 A_Raise
      CNNG A 1
      loop
   Fire:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 1, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 1, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      //
      CNNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_punctuator), "FirePunct")
      goto FireCannonSmall
   //
   // OverlayFirePunct
   //
   OverlayFirePunct:
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_PunctuatorFire", 0)
      stop
   //
   // FirePunct
   //
   FirePunct:
      TNT1 A 0 A_PlaySound("weapons/punct/fire", CHAN_WEAPON)
      TNT1 A 0 A_JumpIfInventory("Lith_CannonScopedToken", 1, "FirePunct_Scoped")
      CNNG A 5
      CNNG B 1 bright offset(0, 48)
      CNNG B 1 bright offset(0, 49) A_GunFlash("OverlayFirePunct")
      CNNG C 1 bright offset(0, 47)
      CNNG C 2 bright offset(0, 37)
      CNNG B 1 bright offset(0, 34)
      CNNG AAAAAAAAAA 1 offset(0, 32) A_ReFire("Fire")
      goto LoadAnim
   FirePunct_Scoped:
      SCOP A 3
      SCOP A 1 A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.80)
      SCOP A 1 A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.85)
      SCOP A 1 bright A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.90)
      SCOP A 0 bright A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.95)
      SCOP A 1 bright A_GunFlash("OverlayFirePunct")
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 1 bright
      SCOP A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      SCOP AAAAAAAAAA 1 A_ReFire("Fire")
   LoadAnim:
      "####" A 1 offset(1, 32)
      "####" A 1 offset(3, 32)
      "####" A 1 offset(4, 32)
      "####" A 1 offset(8, 38)
      "####" A 1 offset(12, 45)
      "####" A 1 offset(16, 53)
      "####" A 1 offset(19, 60)
      "####" A 1 offset(21, 64)
      "####" A 1 offset(23, 67)
      "####" A 1 offset(22, 69) A_PlaySound("weapons/punct/load", CHAN_5)
      "####" A 1 offset(19, 70)
      "####" A 7 offset(18, 72)
      "####" A 1 offset(17, 69)
      "####" A 1 offset(19, 67)
      "####" A 1 offset(20, 66)
      "####" A 1 offset(21, 62)
      "####" A 1 offset(20, 60)
      "####" A 1 offset(21, 63)
      "####" A 1 offset(23, 66)
      "####" A 1 offset(24, 68)
      "####" A 1 offset(25, 63)
      "####" A 1 offset(24, 59)
      "####" A 1 offset(22, 57)
      "####" A 1 offset(20, 54)
      "####" A 1 offset(16, 49)
      "####" A 1 offset(12, 44)
      "####" A 1 offset(10, 42)
      "####" A 1 offset(8, 40)
      "####" A 1 offset(5, 38)
      "####" A 1 offset(2, 35)
      "####" A 0 A_JumpIfInventory("Lith_CannonScopedToken", 1, "AltReady")
      goto Ready
   //
   // AltFire
   //
   AltFire:
      CNNG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_punctuator), "ScopeToggle")
      //
      CNNG A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
   //
   // FireCannonBig
   //
   FireCannonBig:
      CNNG A 10 A_PlaySound("weapons/cannon/prepare", CHAN_WEAPON)
      CNNG A 1 bright
      CNNG A 1 bright offset(-1, 33)
      CNNG A 1 bright offset(-1, 34)
      CNNG A 1 bright offset(-1, 36)
      CNNG A 2 bright offset(1, 37)
      CNNG A 3 bright offset(1, 38)
      CNNG C 2 bright offset(0, 62) A_FireCustomMissile("Lith_CannonBall")
      CNNG C 1 bright offset(0, 57)
      CNNG C 1 bright offset(0, 47)
      CNNG C 1 bright offset(0, 44)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG B 1 bright offset(0, 35)
      CNNG B 1 bright offset(0, 32)
      CNNG AAAAAAAAAAAAAAAAAAAA 1 offset(0, 32) A_ReFire("AltFire")
      goto Ready
   //
   // FireCannonSmall
   //
   FireCannonSmall:
      CNNG C 1 bright offset(0, 48)
      CNNG C 1 bright offset(0, 57) A_FireCustomMissile("Lith_SmallCannonBall")
      CNNG C 1 bright offset(0, 55)
      CNNG C 1 bright offset(0, 53)
      CNNG C 1 bright offset(0, 49)
      CNNG B 1 bright offset(0, 45)
      CNNG B 1 bright offset(0, 40)
      CNNG B 1 bright offset(0, 37)
      CNNG A 1 bright offset(0, 34)
      CNNG A 1 offset(0, 32)
      CNNG A 8
      CNNG AAAAAAAAAA 1 A_ReFire("Fire")
      goto Ready
   //
   // FireEmpty
   //
   FireEmpty:
      "####" A 2 A_PlaySound("weapons/cannon/empty", 7)
      "####" A 1 offset(0, 28)
      "####" A 1 offset(0, 29)
      "####" A 1 offset(0, 32)
      "####" A 1 offset(0, 35)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 42)
      "####" A 1 offset(0, 43)
      "####" A 1 offset(0, 44)
      "####" A 1 offset(0, 40)
      "####" A 1 offset(0, 37)
      "####" A 1 offset(0, 34)
      "####" A 1 offset(0, 32)
      "####" A 1 A_ReFire
      "####" A 0 A_JumpIfInventory("Lith_CannonScopedToken", 1, "AltReady")
      goto Ready
   //
   // ScopeToggle
   //
   ScopeToggle:
      "####" A 1
      "####" A 0 A_JumpIfInventory("Lith_CannonScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" A 0 A_TakeInventory("Lith_CannonScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      goto Ready
   ScopeIn:
      "####" A 0 A_GiveInventory("Lith_CannonScopedToken", 1)
      "####" A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomin", 6)
      goto AltReady
   //
   //
   //
   Spawn:
      BFUG A 0
      BFUG A 0 A_SetUserVar("user_pickupparm", weapon_bfg)
      goto SpawnLoop
   }
}
