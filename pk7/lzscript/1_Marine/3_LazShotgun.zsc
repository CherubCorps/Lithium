/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_LazShotgunTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.34;

      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT E 0 bright;
      BLLT E 0 bright A_JumpIf(Random(0, 255) < 10, "Spawn2");
   Spawn1:
      BLLT E 4 bright;
      goto Spawn3;
   Spawn2:
      BLLT E 4 bright light("Lith_FinalizerLight");
   Spawn3:
      BLLT E 2 bright A_FadeOut(0.5);
      BLLT E 1 bright A_FadeOut(0.2);
      stop;
   }
}

class Lith_LazShotgunPuff : Actor
{
   default
   {
      DamageType "Electric";

      +ALWAYSPUFF
      +FORCEDECAL
   }
}

class Lith_LazShotgun : Lith_MWeapon;

default
{
   Tag "$LITH_INFO_SHORT_LazShotgun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/lshotgun/draw";

   +WEAPON.NOAUTOFIRE
}

action(Weapon) void Lith_A_LaserFire()
{
   A_AlertMonsters();
   class<Actor> trail = "Lith_LazShotgunTrail";
   if(lith_weapons_fastlazshot) {
      trail = "";
   }
   for(int i = 0; i < 7; i++)
      A_RailAttack(7 * Random(1, 4), 0, true, "", "#eb002b", RGF_SILENT | RGF_FULLBRIGHT, 2, "Lith_LazShotgunPuff", 4, 4, 0, 10, 5, 0, trail, -8);
   A_PlaySound("weapons/lshotgun/fire", CHAN_WEAPON);
   A_Recoil(0.5);
   Lith_A_RecoilUp(0.75);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

states(Weapon)
{
Ready:
   LSHT A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   LSHT AA 0 A_Lower;
   LSHT A 1 A_Lower;
   loop;

Select:
   LSHT AA 0 A_Raise;
   LSHT A 1 A_Raise;
   loop;

Fire:
   LSHT I 2 bright Lith_A_LaserFire;
   LSHT H 1 bright;
   LSHT A 4;
   LSHT BB 1 Lith_A_RecoilDn(0.375);
   LSHT C 2 A_PlaySound("weapons/lshotgun/open", CHAN_6);
   LSHT D 2;
   LSHT E 3;
   LSHT F 5;
   LSHT G 2 A_PlaySound("weapons/lshotgun/close", CHAN_7);
   LSHT ED 2;
   LSHT CA 3;
   LSHT A 2;
   LSHT AAAAAAA 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
