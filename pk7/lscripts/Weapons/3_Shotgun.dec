#include "lscripts/Weapons/ShotgunTrail.dec"
#include "lscripts/Weapons/Gauss.dec"
#include "lscripts/Weapons/PoisonBullet.dec"

actor Lith_ShotgunScopedToken : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_ShotgunJustFrame : Inventory
{
   Inventory.MaxAmount 3
}

actor Lith_Shotgun : Lith_Weapon replaces Shotgun
{
   Tag "$LITH_TXT_INFO_SHORT_Shotgun"
   Weapon.UpSound "weapons/shotgun/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      SHOT A 0
      SHOT A 0 A_SetUserVar("user_pickupparm", weapon_shotgun)
      goto SpawnLoop
   
   AltReady:
      SCOP A 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
      loop
   Ready:
      SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      SHTG A 0 A_TakeInventory("Lith_ShotgunJustFrame", 1)
      loop
   
   Deselect:
      SHTG A 0 A_TakeInventory("Lith_ShotgunScopedToken", 1)
      SHTG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      SHTG AAA 0 A_Lower
      SHTG A 1
      loop
   
   Select:
      SHTG A 0 A_ZoomFactor(1.0)
      SHTG A 0 A_GiveInventory("Lith_ShotgunJustFrame", 3)
   SelectLoop:
      SHTG AAA 0 A_Raise
      SHTG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunJustFrame", 1, "FireJust")
   FireCheck:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_GaussShotty), "FireGauss")
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PoisonShot),  "FirePoison")
      goto FireRail
   FireJust:
      TNT1 A 0 A_TakeInventory("Lith_ShotgunJustFrame", 0x7FFFFFFF)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_PlaySound("weapons/shotgun/just", 7)
      TNT1 A 0 A_RailAttack(20, 0, false, "None", "LightBlue", RGF_SILENT | RGF_FULLBRIGHT, 128, "BulletPuff", 0, 0, 0, 48, 3, 3)
      goto FireCheck
   FireFinish:
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "AltReady")
      goto Ready
   FirePoison:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(5.3, 2.3, 7, 5, "Lith_PoisonBullet")
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/shotgun/poison/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.7)
      goto FireFinish_Rail
      
   FireRail:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(5.3, 2.3, 7, 5)
      TNT1 A 0 A_RailAttack(20, 0, true, "None", "Green", RGF_SILENT | RGF_FULLBRIGHT, 2, "BulletPuff", 1, 1, 0, 0, 0.9, 1, "Lith_ShotgunTrail", 0)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/shotgun/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.7)
   FireFinish_Rail:
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "FireFinish_Rail_Scoped")
      // ---
      SHTG B 2 bright A_Light(1)
      SHTG C 2 bright A_Light(2)
      SHTG D 2 A_Light(0)
      SHTG EA 2
      SHTG FG 3
      SHTG H 2
      SHTG I 1
      SHTG J 3
      SHTG KI 1
      SHTG H 3
      SHTG G 1 offset(-13, 32)
      SHTG G 1 offset(0, 32)
      SHTG F 1 offset(-9, 32)
      SHTG F 1 offset(0, 32)
      // [screams externally]
      SHTG A 2 A_WeaponReady(LITH_BOBONLY)
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      SHTG A 0 A_WeaponReady(LITH_BOBONLY)
      SHTG A 1 A_ReFire
      goto FireFinish
   FireFinish_Rail_Scoped:
      SCOP A 1 bright A_Light1
      SCOP A 1 bright A_Light2
      SCOP A 2 A_Light0
      SCOP A 24
      SCOP A 5 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP A 0 A_ReFire
      SCOP A 7 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      goto FireFinish
   
   AltFire:
      "####" A 1
      "####" A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "ScopeOut")
      goto ScopeIn
   ScopeOut:
      "####" A 0 A_TakeInventory("Lith_ShotgunScopedToken", 1)
      "####" A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      goto Ready
   ScopeIn:
      "####" A 0 A_GiveInventory("Lith_ShotgunScopedToken", 1)
      "####" A 0 A_ZoomFactor(CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX)
      "####" A 0 A_PlaySound("weapons/shotgun/zoomin", 6)
      goto AltReady
   
   Reload:
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_GaussShotty), 1)
      goto FireFinish
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, 1)
      goto Reload1
      TNT1 A 0 A_PlaySound("weapons/shotgun/zoomout", 6)
      TNT1 A 0 A_TakeInventory("Lith_ShotgunScopedToken", 999)
      TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
   Reload1:
      TNT1 A 0 A_JumpIfInventory("Lith_GaussShotsFired", 1, 1)
      goto FireFinish
      SHTG A 2
      SHTG F 3 A_PlaySound("weapons/shotgun/gauss/reloadout", CHAN_6)
      SHTG G 3
      SHTG H 2
      SHTG I 3 offset(-11, 57)
      SHTG I 8 offset(-10, 60)
      SHTG I 8 offset(-11, 60)
      SHTG I 0 A_TakeInventory("Lith_GaussShotsFired", 999)
      SHTG I 2 offset(-13, 59) A_PlaySound("weapons/shotgun/gauss/reloadin", CHAN_7)
      SHTG I 2 offset(-11, 49)
      SHTG I 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG G 1 offset(-13, 32)
      SHTG G 1 offset(1, 32)
      SHTG F 1 offset(-9, 32)
      SHTG F 1 offset(1, 32)
      SHTG A 3
      SHTG AAAAAAA 1 A_ReFire
      goto FireFinish
   
   //
   // Gauss
   //
   FireGauss:
      TNT1 A 0 A_JumpIfInventory("Lith_GaussShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_GaussShotsFired", 1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(0, 0, 1, 14, "Lith_GaussPuff")
      TNT1 A 0 A_Quake(1, 5, 0, 64)
      TNT1 A 0 A_RailAttack(9, 0, false, LightBlue, Blue, RGF_NOPIERCING|RGF_FULLBRIGHT, 0,
                            "Lith_Dummy", 0.05, 0.05, 0, 5, frandom(0.35, 1), 0.5)
      TNT1 A 0 A_PlaySound("weapons/shotgun/gauss/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.777)
      TNT1 A 0 A_JumpIfInventory("Lith_ShotgunScopedToken", 1, "FireFinish_Gauss_Scoped")
      // ---
      SHTG A 1
      SHTG L 1 A_Light(3)
      SHTG M 1 A_Light(2)
      SHTG D 1 A_Light(1)
      SHTG E 1 A_Light(0)
      SHTG A 2
      SHTG AAAAAAA 1 A_ReFire
      goto FireFinish
   FireFinish_Gauss_Scoped:
      SCOP A 1 bright A_Light(3)
      SCOP A 1 bright A_Light(2)
      SCOP A 0 A_Light(1)
      SCOP A 1 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP A 0 A_Light0
      SCOP A 4 A_WeaponReady(WRF_NOPRIMARY | WRF_NOBOB)
      SCOP AAAAAAA 1 A_ReFire
      goto FireFinish
   }
}

