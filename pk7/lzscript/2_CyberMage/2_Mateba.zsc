class Lith_FinalizerKill2 : Actor
{
   default
   {
      RenderStyle "AddStencil";
      StencilColor "Red";
      Alpha 0.9;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      #### # 5;
      #### # 1 A_FadeOut(0.05);
      wait;
   }
}

class Lith_FinalizerKill : Actor
{
   default
   {
      RenderStyle "Add";

      +NOINTERACTION
   }

   states
   {
   Spawn:
      #### # 10;
      #### # 0 A_SpawnItemEx("Lith_FinalizerKill2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
      #### # 1 A_FadeOut(0.05);
      wait;
   }
}

class Lith_Finalizer : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIf(bBOSS, "Nope");
      TNT1 A 0 A_JumpIf(float(health) / CallACS("Lith_GetFinalizerMaxHealth") > 0.5, "Nope");
      TNT1 A 0 A_SpawnItemEx("Lith_FinalizerKill", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MonsterFinalized");
      TNT1 A 0 A_BossDeath;
      TNT1 A 0 Thing_Remove(0);
      stop;
   Nope:
      TNT1 A 0 A_DamageSelf(50);
      stop;
   }
}

class Lith_FinalizerEffect : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.2;
      Radius 0;
      Height 0;

      +FORCEXYBILLBOARD
      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT O 10 bright;
      BLLT O 1 bright A_FadeOut(0.2);
      wait;
   }
}

class Lith_FinalizerShotEffect : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.8;

      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT E 1 bright A_FadeOut;
      wait;
   }
}

class Lith_FinalizerShot : FastProjectile
{
   default
   {
      RenderStyle "Add";
      Speed 90;
      Height 6;
      Radius 6;
      MissileType "Lith_FinalizerShotEffect";

      Projectile;
      +HITTRACER
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   Death:
      TNT1 A 0 A_GiveInventory("Lith_Finalizer", 1, AAPTR_TRACER);
      stop;
   }
}

class Lith_Mateba : Lith_CWeapon;

default
{
   Tag "Mateba";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 1;
   Weapon.BobSpeed 2.5;
   Weapon.BobRangeX 0.2;
   Weapon.BobRangeY 0.3;
   Weapon.UpSound "weapons/mateba/draw";
   Lith_Weapon.MagSize 12;

   +WEAPON.NOAUTOFIRE
}

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_PistolPickup");
   stop;
}

states(Weapon)
{
Ready:
   MTBG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   MTBG AAA 0 A_Lower;
   MTBG A 1;
   loop;
Select:
   MTBG AAA 0 A_Raise;
   MTBG A 1;
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 Lith_A_MagIncr;
   TNT1 A 0 A_GunFlash("DummyFlash");
   TNT1 A 0 {
      if(invoker.m_fired & 1)
         Lith_A_Casing("Lith_CasSmallHot", 14, 2, 32, frandom(1,2), frandom(3,5), frandom(2,3));
   }
   TNT1 A 0 A_JumpIf(!CallACS("LPData", pdata_upgrade, UPGR_Mateba_A), 2);
   TNT1 A 0 Lith_A_JumpIfMagEmpty(false, "Finalize");
   TNT1 A 0 Lith_A_RecoilDn(0.2);
   TNT1 A 0 Lith_FireBullets(2, 2, -1, 30, FBF_NORANDOM);
   TNT1 A 0 Lith_A_GunSmokeSmall;
   TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON);
   MTBG B 1 bright offset(0, 36);
   TNT1 A 0 Lith_A_RecoilUp(0.1);
   MTBG C 1 bright offset(0, 43);
   TNT1 A 0 Lith_A_RecoilUp(0.1);
   MTBG G 1 offset(3, 38);
   MTBG G 1 offset(0, 37);
   MTBG G 1 offset(0, 34) A_WeaponReady(WRF_NOBOB);
   MTBG F 1 offset(2, 35) A_WeaponReady(WRF_NOBOB);
   MTBG F 1 offset(0, 33) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(1, 34) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB);
   goto Ready;
Finalize:
   TNT1 A 0 Lith_A_GunSmokeSmall;
   TNT1 A 0 A_SpawnItemEx("Lith_FinalizerEffect", cos(pitch)*24, 0, ((height-19)+2) - sin(pitch)*24);
   TNT1 A 0 A_FireProjectile("Lith_FinalizerShot", 0, false, 0, 10);
   TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON);
   TNT1 A 0 A_PlaySound("weapons/mateba/finalize", CHAN_6);
   TNT1 A 0 A_PlaySound("weapons/shotgun/just", CHAN_5);
   MTBG B 1 bright offset(0, 55);
   MTBG C 1 bright offset(0, 51);
   MTBG A 1 offset(0, 45);
   MTBG A 1 offset(0, 43);
   MTBG A 1 offset(0, 40);
   MTBG A 1 offset(0, 37);
   MTBG A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB);
   MTBG A 1 A_WeaponReady(WRF_NOBOB);
   goto Ready;

FireEmpty:
   MTBG A 1;
   MTBG A 1 offset(0, 45);
   MTBG A 1 offset(0, 43);
   MTBG A 1 offset(0, 40) A_PlaySound("weapons/empty", CHAN_7);
   MTBG A 1 offset(0, 38);
   MTBG A 1 offset(0, 36);
   MTBG A 1 offset(0, 33);
   MTBG A 1 offset(0, 32);
   MTBG A 2;
   goto Ready;

Reload:
   TNT1 A 0 Lith_A_JumpIfMagFull;
   MTBG E 2;
   MTBG F 2;
   MTBG G 2;
   MTBG H 2;
   MTBG I 4 A_PlaySound("weapons/mateba/open", CHAN_7);
   MTBG J 3;
   MTBG K 2;
   MTBG L 0 Lith_A_ReloadBobDn(0.1);
   MTBG L 3 A_PlaySound("weapons/mateba/eject", CHAN_6);
   MTBG L 0 {
      for(int i = 0; i < invoker.m_fired / 2; i++)
         Lith_A_Casing("Lith_CasSmall", 10, frandom(1,2), 32, frandom(1,2), frandom(1,3), 0);
   }
   MTBG M 3 Lith_A_ReloadFinish;
   MTBG N 3;
   MTBG O 0 Lith_A_ReloadBobDn(0.1);
   MTBG O 2 A_PlaySound("weapons/mateba/load", CHAN_5);
   MTBG P 3;
   MTBG Q 0 Lith_A_ReloadBobUp(0.1);
   MTBG Q 3 A_PlaySound("weapons/mateba/close", CHAN_7);
   MTBG R 3;
   MTBG S 4;
   goto Ready;
}

// EOF
