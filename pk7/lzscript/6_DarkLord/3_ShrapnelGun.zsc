// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_ShrapnelProjectile : Actor {
   default {
      BounceType "Doom";
      BounceSound "weapons/shrapnel/bounce";
      RipSound "weapons/shrapnel/hit";
      DamageFunction (11);
      DamageType "Shrapnel";
      Gravity 0.5;
      Height 8;
      Radius 8;
      Species "Lith_Player";
      Speed 40;
      Projectile;
      +ExplodeOnWater
      +MThruSpecies
      -NoGravity
   }

   state lith_a_jumpScale(double scale) {
      a_setScale(scale);
      return a_jump(255/17, "Spawn2");
   }

   states {
   Spawn:
      TNT1 A 0;
      SBRK ABCDEFGHIJKLMN 0 lith_a_jumpScale(0.26);
      BLLT I 0 lith_a_jumpScale(1.0);
      SHCA D 0 lith_a_jumpScale(0.4);
      SHCA E 0 lith_a_jumpScale(0.3);
   Spawn2:
      #### # 1 A_SpawnItemEx("Lith_CasingSmoke", 0,0,3, frandom(-0.2,0),frandom(-0.2,0));
      loop;
   Death:
      #### # 140;
      #### # 1 A_FadeOut;
      wait;
   }
}

class Lith_ShrapnelGun : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_ShotgunPickup";
   Tag "$LITH_INFO_SHORT_ShrapnelGun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/shrapnel/draw";
   Lith_Weapon.AmmoType "Lith_ShellAmmo";
}

action(Weapon) void Lith_A_FireShrapnel() {
   lith_takeAmmo(1);
   A_StartSound("weapons/shrapnel/fire", lch_weapon);
   A_AlertMonsters();
   A_GunFlash("DummyFlash");
   Lith_A_RecoilUp(5);
   lith_a_setInterpolation(true);
   for(int i = 0; i < 12; ++i) {
      A_FireProjectile("Lith_ShrapnelProjectile", frandom(-5, 5), false, frandom(5, 10), frandom(-7, 7), FPF_NOAUTOAIM);
   }
}

states(Weapon) {
Ready:
   SHCA A 1 Lith_A_Ready;
   loop;

Deselect:
   SHCA A 1 Lith_A_Lower(_raise_fast);
   loop;

Select:
   SHCA A 1 Lith_A_Raise(_raise_fast);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo;
   #### # 0 lith_a_setInterpolation(true);
   SHCA A 1;
   SHCA A 1 offset(2, 33);
   SHCA C 1 offset(164, 109) Lith_A_FireShrapnel();
   SHCA B 1 offset(173, 117);
   SHCA B 1 offset(180, 121);
   SHCA B 1 offset(183, 123);
   SHCA B 1 offset(178, 120);
   SHCA B 1 offset(169, 114);
   SHCA C 1 offset(165, 108);
   SHCA C 1 offset(152, 104);
   #### # 0 lith_a_setInterpolation(false);
   SHCA A 1 offset(1, 32);
   goto ReadySelector;
FireEmpty:
   SHCA A 2 offset(3, 38);
   SHCA A 1 offset(7, 40);
   SHCA A 1 offset(5, 37) A_StartSound("weapons/spas/empty", lch_dryweapon);
   SHCA A 1 offset(4, 34);
   SHCA A 1 offset(2, 33);
   SHCA A 5 offset(1, 32);
   goto ReadySelector;
}

/* EOF */
