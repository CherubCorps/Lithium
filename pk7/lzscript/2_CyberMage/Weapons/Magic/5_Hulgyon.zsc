class Lith_HulgyonEffect : Actor
{
   default
   {
      RenderStyle "Add";

      +NOGRAVITY
      +THRUACTORS
      +NODAMAGETHRUST
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT P 0;
      BLLT P 0 A_Explode(48, 90, 0);
      BLLT P 5;
      BLLT P 1 A_FadeOut(0.2);
      wait;
   }
}

class Lith_HulgyonSprite : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.3;

      +NOTIMEFREEZE
      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT T 0;
      BLLT T 0 A_Warp(AAPTR_MASTER, pos.x,pos.y,pos.z, 0, WARPF_NOCHECKPOSITION|WARPF_ABSOLUTEOFFSET|WARPF_COPYINTERPOLATION);
   Spawn1:
      BLLT T 1 bright;
      stop;
   }
}

class Lith_HulgyonSprite2 : Lith_HulgyonSprite
{
   default
   {
      Alpha 0.3;
   }

   states
   {
   Spawn:
      BLLT T 7 bright;
      stop;
   }
}

class Lith_Hulgyon : Lith_CWeapon;

default
{
   Tag "Hulgyon";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/hulgyon/draw";

   +WEAPON.DONTBOB
   -WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   TNT1 A 0 A_SpawnItemEx("Lith_HulgyonSprite", cos(pitch)*32, 0, (height-19)-(sin(pitch)*32),vel.x,vel.y,vel.z,0,SXF_ISMASTER|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 5);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 50, 1);
   goto Ready;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 50);
   TNT1 A 10 A_FireProjectile("Lith_HulgyonProjectile");
   TNT1 AAAAAAAAAA 1 A_SpawnItemEx("Lith_HulgyonSprite2", cos(pitch)*32, 0, (height-19)-(sin(pitch)*32),0,0,0,0,SXF_NOCHECKPOSITION);
   goto Ready;
}

// EOF
