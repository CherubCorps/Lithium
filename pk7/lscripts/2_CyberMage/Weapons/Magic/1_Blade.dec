actor Lith_BladeHitSound : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_PlaySound("weapons/blade/hit", CHAN_5)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Blade", true)
      stop
   }
}

actor Lith_BladeHit : FastProjectile
{
   Damage (40 * random(1, 2))
   Radius 24
   Height 32
   Speed  128

   +FORCEPAIN

   states
   {
   Spawn:
      TNT1 A 1
      stop
   XDeath:
      TNT1 A 0 A_GiveInventory("Lith_BladeHitSound", 1, AAPTR_TARGET)
      stop
   }
}

actor Lith_BladeHit2 : Lith_BladeHit
{
   Damage (40 * random(2, 3))
}

actor Lith_Blade : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Blade"
   Weapon.SlotNumber 0
   Weapon.SlotPriority 1
   Weapon.UpSound "weapons/blade/draw"

   +WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      TNT1 A 1 bright A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 1)
   SelectLoop:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 5, 1)
      goto Ready
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 5)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Blade", false)
      TNT1 A 0 A_PlaySound("weapons/blade/slash", CHAN_7)
      TNT1 A 0 A_JumpIfInventory("PowerStrength", 0, 2)
      TNT1 A 0 A_FireCustomMissile("Lith_BladeHit")
      goto FireDone
      TNT1 A 0 A_FireCustomMissile("Lith_BladeHit2")
   FireDone:
      TNT1 A 20
      goto Ready
   }
}

// EOF
