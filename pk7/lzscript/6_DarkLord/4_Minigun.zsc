/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_Minigun : Lith_DWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_ChaingunPickup";
   Lith_Weapon.MagSize 200;
   Tag "$LITH_INFO_SHORT_Minigun";
   Weapon.SlotNumber 4;
   Weapon.SlotPriority 1;
}

double     m_windUp;
int        m_sndState;
bool       m_reloading;
statelabel m_lastState;

override void tick() {
   if(owner && owner.player && owner.health > 0 && !isFrozen()) {
      if(owner.player.readyWeapon != self) {
         m_windUp = 0;
         return;
      }

      if(owner.player.buttons & (BT_ATTACK|BT_USER1) && !m_reloading) {
         if((m_windUp += 0.04) > 1) m_windUp = 1;

         owner.A_AlertMonsters(256);

         if(!m_sndState) {
            owner.A_StartSound("weapons/minigun/up", lch_weapon2, CHANF_LOOP);
            m_sndState++;
         } else if(m_sndState++ == 15) {
            owner.A_StartSound("weapons/minigun/rotate", lch_weapon2, CHANF_LOOP);
         }
      } else {
         if(m_sndState) {
            owner.A_StartSound("weapons/minigun/down", lch_weapon2);
            m_sndState = 0;
         }

         if(m_windUp) {
            m_windUp -= 0.08;

            if(m_windUp < 0) {
               m_windUp = m_sndState = 0;
               owner.A_StartSound("weapons/minigun/place", lch_weapon3);
            }
         }
      }
   }

   super.tick();
}

int Lith_WindupTime()
{
   return int((1.0 - m_windUp) * 10.0);
}

action(Weapon) state Lith_A_MinigunReady(statelabel lb, statelabel l)
{
   int flags = WRF_NOPRIMARY;

   if(invoker.m_windUp ~== 0) flags |= WRF_ALLOWRELOAD;
   else                       flags |= WRF_NOSECONDARY;

   invoker.m_lastState = lb;

   A_SetTics(invoker.Lith_WindupTime() + 1);
   state st = Lith_A_Ready(flags);
   if(st) return st;

   if(player.buttons & BT_ATTACK && Level.TotalTime & 1)
   {
      if(!invoker.Lith_MagEmpty())
         return ResolveState(l);
      else
      {
         A_StartSound("weapons/minigun/empty", lch_dryweapon);
         A_WeaponOffset(FRandom(2, 3), FRandom(32, 34), WOF_INTERPOLATE);
         return null;
      }
   }
   else
      return null;
}

action(Weapon) state Lith_A_MinigunPostReady(statelabel l)
{
   return invoker.m_windUp ? ResolveState(l) : null;
}

action(Weapon) void Lith_A_MinigunFire()
{
   Lith_A_MagIncr();
   A_AlertMonsters();
   A_StartSound("weapons/minigun/fire", lch_weapon);
   A_WeaponOffset(FRandom(3, 8), FRandom(34, 39), WOF_INTERPOLATE);
   Lith_FireBullets(7, 1.7, 2, 7, tracer_x: 1);
   Lith_A_RecoilUp(0.15);
   A_Recoil(0.07);
}

action(Weapon) void Lith_A_MinigunPostFire()
{
   Lith_A_RecoilDn(0.35);
   A_SetTics(invoker.Lith_WindupTime());
}

states(Weapon)
{
Ready:
   MNGG A 1 Lith_A_MinigunReady("Ready", "Fire");
   MNGG A 0 Lith_A_MinigunPostReady("Ready2");
   loop;
Ready2:
   MNGG B 0 A_StartSound("weapons/minigun/click", lch_dryweapon, volume: 0.4);
Ready2Loop:
   MNGG B 1 Lith_A_MinigunReady("Ready2Loop", "Fire2");
   MNGG B 0 Lith_A_MinigunPostReady("Ready3");
   loop;
Ready3:
   MNGG C 1 Lith_A_MinigunReady("Ready3", "Fire3");
   MNGG C 0 Lith_A_MinigunPostReady("Ready");
   loop;

Deselect:
   #### # 0 {A_StopSound(lch_weapon2); invoker.m_reloading = false;}
   #### # 1 A_Lower(24);
   loop;

Select:
   MNGG A 1 A_Raise(24);
   loop;

Fire:
   MNGG D 1 bright Lith_A_MinigunFire;
   MNGG A 1 Lith_A_MinigunPostFire;
   goto Ready2;
Fire2:
   MNGG E 1 bright Lith_A_MinigunFire;
   MNGG B 1 Lith_A_MinigunPostFire;
   goto Ready3;
Fire3:
   MNGG F 1 bright Lith_A_MinigunFire;
   MNGG C 1 Lith_A_MinigunPostFire;
   goto Ready;

Reload:
   MNGG # 1 Lith_A_JumpIfMagFull;
   MNGG S 1 offset(2, 34) {invoker.m_reloading = true;}
   MNGG T 1 offset(4, 37);
   MNGG U 1 offset(9, 37) A_StartSound("weapons/minigun/magout", lch_reload);
   MNGG G 1 offset(12, 37);
   MNGG H 1 offset(14, 42) Lith_A_ReloadBobDn(0.7);
   MNGG I 2 offset(12, 45);
   MNGG J 3 offset(11, 47);
   MNGG K 9 offset(10, 50);
   MNGG L 19 offset(11, 50);
   MNGG M 1 offset(13, 49) {
      A_StartSound("weapons/minigun/magin", lch_reload);
      Lith_A_ReloadFinish();
   }
   MNGG N 2 offset(11, 39) Lith_A_ReloadBobUp(0.4);
   MNGG O 1 offset(11, 37);
   MNGG P 1 offset(11, 35);
   MNGG Q 4 offset(11, 35);
   MNGG Q 3 offset(12, 39);
   MNGG Q 2 offset(8, 43);
   MNGG A 2 offset(1, 47);
   MNGG A 1 offset(-3, 46);
   MNGG A 2 offset(-8, 42);
   MNGG A 2 offset(-10, 39);
   MNGG A 2 offset(-8, 37);
   MNGG A 3 offset(-5, 33);
   MNGG A 3 offset(-3, 30);
   MNGG A 3 offset(-1, 31);
   MNGG A 2 offset(-1, 32);
   MNGG A 1;
   MNGG A 0 {invoker.m_reloading = false;}
   TNT1 A 0 ResolveState(invoker.m_lastState);
   stop;
}

/* EOF */
