// IDOL: Inventory Dummies are Overly Luxurious
class Lith_IDOL : Inventory
{
   default
   {
      +INVENTORY.KEEPDEPLETED
      +INVENTORY.UNTOSSABLE
      +INVENTORY.UNDROPPABLE
   }

   Lith_InventoryItem Lith_Find(uint ip)
   {
      for(Inventory it = inv; it; it = it.inv)
         if(it.owner == self && it is "Lith_InventoryItem")
      {
         let item = Lith_InventoryItem(it);
         if(item.m_invitem == ip)
            return item;
      }

      return null;
   }

   bool Lith_Attach(uint ip)
      {return owner && CallACS("Lith_ItemAttach", owner.playerNumber(), ip);}

   bool Lith_SpaceFree(uint ip)
      {return owner && CallACS("Lith_ItemCanPlace", owner.playerNumber(), ip);}

   void Lith_Unlink(uint ip)
      {if(owner) CallACS("Lith_ItemUnlink", owner.playerNumber(), ip);}

   void Lith_Remove(uint ip)
   {
      let it = Lith_Find(ip);
      it.Lith_Detach();
   }

   bool Lith_Use(uint ip)
   {
      let it = Lith_Find(ip);
      return it.Lith_UseItem(owner);
   }
}

class Lith_InventoryItem : Inventory
{
   default
   {
      +INVENTORY.UNTOSSABLE
      +INVENTORY.UNDROPPABLE
   }

   string m_invtype;
   uint   m_invitem;

   property InvType: m_invtype;

   bool Lith_Attach()
   {
      if(Lith_IDOL(owner).Lith_Attach(m_invitem))
      {
         Lith_OnAttach();
         return true;
      }
      else
         return false;
   }

   void Lith_Unlink()
      {Lith_IDOL(owner).Lith_Unlink(m_invitem);}

   void Lith_Detach()
      {m_invitem = 0; destroy();}

   virtual bool Lith_UseItem(Actor mo) {return false;}
   virtual void Lith_OnAttach() {}

   virtual void Lith_Destroy()
   {
      if(m_invitem)
      {
         uint ip = m_invitem;
         m_invitem = 0;
         CallACS("Lith_ItemDetach", ip);
      }
   }

   private void Lith_CreateItem()
      {m_invitem = CallACS("Lith_ItemCreate");}

   override Inventory CreateCopy(Actor mo)
   {
      let copy = Lith_InventoryItem(Super.CreateCopy(mo));

      copy.m_invtype = m_invtype;

      if(copy != self) copy.Lith_CreateItem();

      return copy;
   }

   override bool HandlePickup(Inventory item)
      {return false;}

   override void PostBeginPlay()
   {
      Super.PostBeginPlay();
      Lith_CreateItem();
   }

   override void AttachToOwner(Actor mo)
   {
      let id = Lith_IDOL(mo.findInventory("Lith_IDOL"));
      id.Lith_Attach(m_invitem);
      Super.AttachToOwner(id);
   }

   override bool CanPickup(Actor mo)
   {
      if(Super.CanPickup(mo))
      {
         let id = Lith_IDOL(mo.findInventory("Lith_IDOL"));
         return m_invitem && id && id.Lith_SpaceFree(m_invitem);
      }
      else
         return false;
   }
}

class Lith_BasicPickup : Inventory
{
   default
   {
      Inventory.PickupMessage "";
      Inventory.PickupSound "";
      Lith_BasicPickup.LogType msg_both;
      +INVENTORY.TRANSFER
   }

   private readonly string m_logname;
   private readonly int    m_logtype;

   readonly bool m_vacuum;

   property LogName: m_logname;
   property LogType: m_logtype;
   property Vacuum:  m_vacuum;

   override bool TryPickup(in out Actor mo)
   {
      if(m_logname) Lith_HERMES.Log(mo, m_logname, m_logtype);
      GoAwayAndDie();
      return true;
   }
}

class Lith_CustomFunction : CustomInventory
{
   default
   {
      Inventory.PickupMessage "";
      +INVENTORY.ALWAYSPICKUP
      +NOTIMEFREEZE
   }
}

class Lith_CustomInventory : CustomInventory
{
   default
   {
      Inventory.PickupMessage "";
   }

   void Lith_A_Log(string name, int type)
      {Lith_HERMES.Log(self, name, type);}

   states
   {
   Nope:
      TNT1 A 0;
      fail;
   }
}

// EOF
