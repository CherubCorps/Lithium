class Lith_BaronHellfireTrailSpawner : Actor
{
   default
   {
      Radius 2;
      Height 2;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnItemEx("Lith_BaronHellfireTrail", -16, 0, 0, frandom(0.0, -1.0));
      TNT1 A 0 A_SpawnParticle("37732B", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 5, 0, frandom(-8,8), frandom(-8,8), frandom(-2,2), frandom(0, 10), frandom(-0.1, 0.1), frandom(-1, 1), 0, 0, 0.02, 1.0, -1);
      stop;
   }
}

class Lith_BaronHellfireTrail : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Scale 0.666;
      Renderstyle "Translucent";
      Alpha 0.7;

      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      BAL7 C 2;
      BAL7 DE 3 A_FadeOut(0.333);
      stop;
   }
}

class Lith_BaronBall : BaronBall replaces BaronBall
{
   default
   {
      DamageType "Magic";
      Damage 4;
      RenderStyle "Translucent";
      Alpha 1;

      +RANDOMIZE
   }

   states
   {
   Spawn:
      HLF1 A 0;
      HLF1 AAAABBBB 1 bright A_SpawnItemEx("Lith_BaronHellfireTrailSpawner", 0, frandom(-0.5, 0.5), 0, frandom(0,-1));
      loop;
   Death:
      BAL7 CDE 6 bright;
      stop;
   }
}

class Lith_BaronBallSilent : Lith_BaronBall
{
   default
   {
      SeeSound "";
      DeathSound "";
   }
}

class Lith_XH_BaronOfHell : Lith_BaronOfHell
{
}

class Lith_BaronOfHell : Lith_Monster replaces BaronOfHell;

default
{
   Lith_Monster.Score Score_BaronOfHell;
   Health 1000;
   Radius 24;
   Height 64;
   Mass 1000;
   Speed 8;
   PainChance 55;
   Species "Lith_HellKnight";
   BloodColor "DarkGreen";
   SeeSound "baron/sight";
   PainSound "baron/pain";
   DeathSound "baron/death";
   ActiveSound "baron/active";
   Obituary "$OB_BARON";
   HitObituary "$OB_BARONHIT";
   Tag "Hell Noble";

   +MISSILEMORE
   +FLOORCLIP
   +BOSSDEATH
}

int m_angle;

states
{
Spawn:
   BOSS AB 10 A_Look;
   loop;

See:
   BOSS AABBCCDD 3 A_Chase;
   loop;

Melee:
   BOSS EF 6 A_FaceTarget;
   BOSS G 4 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee");
   goto See;

Missile:
   BOSS EF 4 A_FaceTarget;
   BOSS G 0 A_FaceTarget;
   BOSS G 1 A_SpawnProjectile("Lith_BaronBall", 32, 0, 0, CMF_BADPITCH);
   BOSS G 0 A_Jump(32, "MissileDone");
   BOSS G 0 A_FaceTarget;
   BOSS G 0 {m_angle = frandompick(-45, 45, 0);}
   BOSS G 1 A_SpawnProjectile("Lith_BaronBallSilent", 32, 0, m_angle, CMF_AIMDIRECTION|CMF_BADPITCH);
   BOSS G 0 ResolveState("See");
MissileDone:
   BOSS G 3;
   BOSS G 0 ResolveState("See");
   stop;

Pain:
   BOSS H 1;
   BOSS H 1 A_Pain;
   goto See;

Death:
   BOSS I -1;
Crash:
   BOSS I 8 Lith_A_Die;
   BOSS JKLMN 8;
   BOSS O -1 A_BossDeath;
   stop;

Raise:
   BOSS ONMLKJI 8;
   BOSS A 0 ResolveState("See");
   stop;
}

// EOF
