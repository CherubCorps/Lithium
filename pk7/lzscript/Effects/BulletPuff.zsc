// Taken from Psychic and Enblanco with permission.
class Lith_PuffSmoke : Actor
{
   default
   {
      Radius 1;
      Height 1;
      RenderStyle "Add";
      Alpha 0.3;
      Scale 0.4;

      +NOGRAVITY
      +NOBLOCKMAP
      +FLOORCLIP
      +FORCEXYBILLBOARD
   }

   states
   {
   Spawn:
      SMK5 ABCDEFGHIJKLMNOP 1;
      stop;
   }
}

class Lith_HotEmber : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Mass 8;
      Gravity 0.45;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +NOBLOCKMAP
      -NOGRAVITY
   }

   states
   {
   Spawn:
      EPUF A 1 bright;
      EPUF BBCC 4 bright A_SpawnItemEx("Lith_HotEmberTrail", frandom(0, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
      EPUF DEFG 4 bright;
      stop;
   }
}

class Lith_HotEmberTrail : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +NOGRAVITY
      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      EPUF EFG 5;
      stop;
   }
}

class Lith_BulletAfterSmoke : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.01;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;

      Projectile;
      +THRUACTORS
      +FORCEXYBILLBOARD
      +DONTSPLASH
   }

   float m_xs, m_ys;

   override void PostBeginPlay()
   {
      scale.x *= frandom(2, 6);
      scale.y *= frandom(0.4, 1.5);
      m_xs = frandom(0.005, 0.01);
      m_ys = frandom(0.001, 0.005);
   }

   override void Tick()
   {
      Super.Tick();
      scale.x += m_xs;
      scale.y += m_ys;
   }

   states
   {
   Spawn:
      TNT1 A 0;
      SMOK AAAAAAAAAABBBBBBBBBBCCCCCCCCCC 1 A_FadeIn(frandom(0.001, 0.005));
      SMOK DEFG random(14, 56);
      SMOK HHHHHHHHHIIIIIIIIIJJJJJJJJJJJJJJKKKKKKKKK 1 A_FadeOut(0.005);
      stop;
   }
}

class Lith_BulletPuff : Actor replaces BulletPuff
{
   default
   {
      RenderStyle "Add";
      DamageType "Bullet";
      Species "Lith_Player";
      Alpha 0.9;

      +DONTHARMSPECIES
      +NOGRAVITY
      +NOBLOCKMAP
      +FLOORCLIP
      +FORCEXYBILLBOARD
      +PUFFGETSOWNER
      +MTHRUSPECIES
      -ALLOWPARTICLES
   }

   void Lith_A_SmokeFX()
   {
      for(int i = 0, m = random(0, 2); i < m; i++)
         A_SpawnItemEx("Lith_BulletAfterSmoke", frandom(-1,1), frandom(-1,1), frandom(-2,2),
            frandom(-0.5,0.5), frandom(-0.5,0.5), frandom(-0.2,0.2));
   }

   void Lith_A_RicochetFX()
   {
      if(random() < 32) return;

      for(int i = 0, m = random(0, 3); i < m; i++)
         A_SpawnItemEx("Lith_HotEmber", frandom(-1, 1), frandom(-1, 1), frandom(0, 1), frandom(-4, 4), frandom(-4, 4), frandom(2, 5), 0, SXF_NOCHECKPOSITION);

      A_SpawnParticle("FFFFFF", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
         frandom(-8,8), frandom(-8,8), frandom(-2,2),
         frandom( 0,1), frandom(-5,5), frandom( 5,10),
         0, 0, frandom(-0.5, -3),
         1.0, -1);
   }

   states
   {
   explod:
      MISL B 0 A_SetScale(0.3);
      MISL B 0 A_PlaySound("explosion");
      MISL B 8 bright A_Explode;
      MISL C 6 bright;
      MISL D 4 bright;
      stop;
   Spawn:
      XPUF Q 0;
      XPUF Q 0 Lith_A_SmokeFX;
      XPUF Q 0 Lith_A_RicochetFX;
      XPUF Q 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_TorgueMode, true), "explod");
      XPUF Q 0 A_SpawnItemEx("Lith_PuffSmoke", 0, 0, 4.0 + 0.1 * random(-10, 10));
      XPUF Q 0 A_Jump(256, "PuffNormal", "PuffMirrored");
   PuffNormal:
      XPUF Q 0 A_Jump(32, "PuffNormalAlt");
      XPUF QRSTU 1 bright;
   PuffNormalEnd:
      XPUF FGH 1 bright;
      stop;
   PuffNormalAlt:
      XPUF A 0 A_PlaySound("effects/puff/ricochet");
      XPUF ABCDE 1 bright;
      goto PuffNormalEnd;
   PuffMirrored:
      XPUF V 0 A_Jump(32, "PuffMirroredAlt");
      XPUF VWXYZ 1 bright;
   PuffMirroredEnd:
      XPUF NOP 1 bright;
      stop;
   PuffMirroredAlt:
      XPUF I 0 A_PlaySound("effects/puff/ricochet");
      XPUF IJKLM 1 bright;
      goto PuffMirroredEnd;
   }
}

// EOF
