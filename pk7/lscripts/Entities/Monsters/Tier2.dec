actor Lith_Demon : Demon replaces Demon
{
   Radius 29
   MeleeRange 52
   Health 200
   
   states
   {
   Missile:
      SARG A 0 A_JumpIfCloser(192, "MissileReal")
      goto See
   MissileReal:
      SARG A 2 A_FaceTarget
      SARG E 0 A_PlaySound("demon/melee", CHAN_BODY)
      SARG E 0 A_FaceTarget
      SARG E 0 ThrustThingZ(0, 16, 0, 1)
      SARG E 8 ThrustThing(angle * 256 / 360, 16, 0, 0)
      SARG F 8
      SARG G 8 A_CustomMeleeAttack(4 * random(1, 10), "demon/melee")
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Demon)
      goto Super::Death
   }
}

actor Lith_Spectre : Lith_Demon replaces Spectre
{
   +SHADOW
   RenderStyle OptFuzzy
   Alpha 0.5
}

actor Lith_LostSoul : LostSoul replaces LostSoul
{
   Health 70
   RenderStyle Add
   Alpha 0.7
   
   +QUICKTORETALIATE
   +THRUSPECIES
   
   var int user_invisible;
   
   states
   {
   Spawn:
      SKUL AB 10 bright A_Look
      loop
   GoInvisible:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.08)
      SKUL A 0 A_SetUserVar(user_invisible, true)
      goto See
   StopBeingInvisibleYouIdiot:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.7)
      SKUL A 0 A_SetUserVar(user_invisible, false)
      goto Missile
   See:
      SKUL A 0 A_JumpIf((!user_invisible) && (random(0, 128) < 64), "GoInvisible")
      SKUL AAAAABBBBB 1 bright A_FastChase
      loop
   Missile:
      SKUL A 0 A_JumpIf(user_invisible, "StopBeingInvisibleYouIdiot")
      SKUL C 10 bright A_FaceTarget
      SKUL D 4 bright A_SkullAttack
   MissileLoop:
      SKUL CCCCDDDD 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   Pain:
      SKUL E 0 A_SetTranslucent(0.7, 1)
      SKUL E 3 bright
      SKUL E 3 bright A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_LostSoul)
   DeathReal:
      SKUL F 0 A_SetTranslucent(0.7, 1)
      SKUL F 6 bright
      SKUL G 6 bright A_Scream
      SKUL H 6 bright
      SKUL I 6 bright A_NoBlocking
      SKUL J 6
      SKUL K 6
      stop
   }
}

// EOF

