actor Lith_PistolShotsFired : Inventory
{
   Inventory.MaxAmount 14
}

actor Lith_PistolScopedToken : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_Pistol : Lith_Weapon replaces Pistol
{
   Tag "$LITH_TXT_INFO_SHORT_Pistol"
   Weapon.SlotNumber 2
   Weapon.BobSpeed 3
   Weapon.BobRangeX 0.2
   Weapon.BobRangeY 0.3
   Weapon.UpSound "weapons/pistol/draw"
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      PIST A 0
      PIST A 0 A_SetUserVar(user_pickupparm, weapon_pistol)
      goto SpawnLoop
   
   AltReady:
      SCOP B 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
      loop
   Ready:
      PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      wait
   
   Deselect:
      PISG A 0 A_TakeInventory("Lith_PistolScopedToken", 1)
      PISG A 0 A_ZoomFactor(1.0)
   DeselectLoop:
      PISG AAA 0 A_Lower
      PISG A 1
      loop
   
   Select:
      // fuck youuuuuuuuuuuuuuuuuuuuuuuuuuuuu
      PISG A 0 A_ZoomFactor(1.0)
   SelectLoop:
      PISG AAA 0 A_Raise
      PISG A 1
      loop
   
   // SPAGHETTI
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_PistolShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_PistolShotsFired", 1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(0, 0, -1, 20, "BulletPuff", FBF_NORANDOM)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawnerSmall", 0, 0, 0, 6)
      
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_AutoPistol), 3)
      TNT1 A 0 A_PlaySound("weapons/pistol/fire", CHAN_WEAPON)
      TNT1 A 0 A_Jump(256, 2)
      
      TNT1 A 0 A_PlaySound("weapons/pistol/fireauto", CHAN_WEAPON)
      
      TNT1 A 0 A_JumpIfInventory("Lith_PistolScopedToken", 1, "FireFinish_Scoped")
      goto FireFinish_NoScope
   FireFinish_Scoped:
      SCOP B 5 bright
      SCOP B 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_AutoPistol), "FireFinish_Auto")
      SCOP BBBBB 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
      goto FireFinish
   FireFinish_NoScope:
      PISG A 1 A_WeaponReady(LITH_BOBONLY)
      PISF AB 1 bright A_WeaponReady(LITH_BOBONLY)
      PISG C 2
      PISG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_AutoPistol), "FireFinish_Auto_NoScope")
      PISG B 2 A_WeaponReady(WRF_ALLOWRELOAD)
      PISG AAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD)
      goto FireFinish
   FireFinish_Auto_NoScope:
      PISG B 2 A_ReFire
      PISG A 0
   FireFinish_Auto:
      "####" "#####" 1 A_ReFire
   FireFinish:
      TNT1 A 0 A_JumpIfInventory("Lith_PistolScopedToken", 1, "AltReady")
      goto Ready
   
   AltFire:
      "####" B 1
      "####" B 0 A_JumpIfInventory("Lith_PistolScopedToken", 1, "ScopeOut")
   ScopeIn:
      "####" B 0 A_GiveInventory("Lith_PistolScopedToken", 1)
      "####" B 0 A_ZoomFactor((CallACS("Lith_GetPlayerData", pdata_weapon_zoom) / FIX) * 0.5)
      "####" B 0 A_PlaySound("weapons/pistol/zoomin", 6)
      goto AltReady
   ScopeOut:
      "####" B 0 A_TakeInventory("Lith_PistolScopedToken", 1)
      "####" B 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      "####" B 0 A_PlaySound("weapons/pistol/zoomout", 6)
      goto Ready
   
   Reload:
      PISG A 0 A_JumpIfInventory("Lith_PistolScopedToken", 1, "ReloadZoomSound")
      goto Reload1
   ReloadZoomSound:
      TNT1 A 0 A_PlaySound("weapons/pistol/zoomout", 6)
   Reload1:
      TNT1 A 0 A_TakeInventory("Lith_PistolScopedToken", 999)
      TNT1 A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
      TNT1 A 0 A_JumpIfInventory("Lith_PistolShotsFired", 1, "Reload2")
      goto Ready
   Reload2:
      PISG A 0 A_PlaySound("weapons/pistol/reload", 7)
      PISG A 2 A_WeaponReady(LITH_BOBONLY)
      PISG B 7 A_WeaponReady(LITH_BOBONLY)
      PISG CB 5 A_WeaponReady(LITH_BOBONLY)
      PISG A 0 A_TakeInventory("Lith_PistolShotsFired", 999)
      PISG A 3 A_WeaponReady
      goto FireFinish
   }
}

