class Lith_DarkLordPlayer : Lith_Player
{
   default
   {
      Lith_Player.ViewBob 0.4;
      Player.StartItem "Lith_700Express";
      Player.StartItem "Lith_SubweaponShots", 2;
      Player.DisplayName "Dark Lord";
      Player.SoundClass "darklord";
      Player.Portrait "lgfx/Portraits/DarkLord.png";
      Player.ViewHeight 50;
      Player.AttackZOffset 17;
      Player.MaxHealth 125;
      Health 125;
      Tag "DarkLord";
   }

   uint8 m_subweapon;
   uint8 m_hassubweapon;

   override void PostBeginPlay()
   {
      Super.PostBeginPlay();

      if(__lith_debug_items || __lith_debug_all)
         m_hassubweapon = 0xFF;
   }

   void Lith_SwapSubweapon()
   {
      Console.printf("%X", m_hassubweapon);
   }
}

class Lith_SubweaponCharge : Inventory
{
   default
   {
      Inventory.MaxAmount 100;
   }
}

class Lith_SubweaponShots : Inventory
{
   default
   {
      Inventory.MaxAmount 2;
   }
}

class Lith_DWeapon : Lith_Weapon
{
   default
   {
      Inventory.RestrictedTo "Lith_DarkLordPlayer";

      +WEAPON.NOAUTOFIRE
   }

   action(weapon) void Lith_ArmGunPreFire()
   {
      player.getPSprite(7).frame = randompick(1, 3);
   }

   action(weapon) void Lith_ArmGunFire()
   {
      player.getPSprite(7).frame = randompick(0, 2);
      A_PlaySound("weapons/subweapon/armgun");
      A_AlertMonsters();
      A_FireBullets(1, 2, -1, 15);
      A_Recoil(0.07);
   }

   states(Weapon)
   {
   User4:
      #### # 0 {
         Lith_DarkLordPlayer(self).Lith_SwapSubweapon();
         return ResolveState("ReadySelector");
      }
   AltFire:
      #### # 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "ReadySelector");
      #### # 0 A_JumpIf(CountInv("Lith_SubweaponShots") == 0, "ReadySelector");
      #### # 0 A_TakeInventory("Lith_SubweaponShots", 1);
      #### # 1 offset(1, 34);
      #### # 1 offset(4, 36);
      #### # 0 A_Overlay(7, "_ArmGun");
      #### # 0 A_OverlayFlags(7, PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_POWDOUBLE|PSPF_CVARFAST, false);
      #### # 1 offset(7, 39);
      #### # 3;
      #### # 4 offset(8, 39);
      #### # 2 offset(5, 35);
      #### # 3 offset(2, 33);
      #### # 1 offset(0, 32);
      #### # 0 ResolveState("ReadySelector");
   _ArmGun:
      ARGN B 1 bright Lith_ArmGunPreFire;
      ARGN A 1 bright Lith_ArmGunFire;
      TNT1 A 1;
      ARGN B 1 bright Lith_ArmGunPreFire;
      ARGN A 1 bright Lith_ArmGunFire;
      TNT1 A 1;
      ARGN B 1 bright Lith_ArmGunPreFire;
      ARGN A 1 bright Lith_ArmGunFire;
      TNT1 A 1;
      ARGN B 1 bright Lith_ArmGunPreFire;
      ARGN A 1 bright Lith_ArmGunFire;
      TNT1 A 1;
      ARGN B 1 bright Lith_ArmGunPreFire;
      ARGN A 1 bright Lith_ArmGunFire;
      TNT1 A 1;
      stop;
   }
}

#include "lzscript/6_DarkLord/Weapons/2_700Express.zsc"
#include "lzscript/6_DarkLord/Weapons/3_4Bore.zsc"
#include "lzscript/6_DarkLord/Weapons/3_ShrapnelGun.zsc"
#include "lzscript/6_DarkLord/Weapons/4_Minigun.zsc"
#include "lzscript/6_DarkLord/Weapons/5_DualRocket.zsc"
#include "lzscript/6_DarkLord/Weapons/6_FatMac.zsc"
#include "lzscript/6_DarkLord/Weapons/7_FortuneGun.zsc"

// EOF
