/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_RemsBulletTrail : Actor {
   default {
      RenderStyle "Add";
      Scale 0.1;
      +NoInteraction
   }
   states {
   Spawn:
      BLLT Y 5;
      stop;
   }
}

class Lith_RemsBullet1 : FastProjectile {
   default {
      RenderStyle "Add";
      Radius 5;
      Height 5;
      DamageFunction 100;
      Speed 100;
      MissileType "Lith_RemsBulletTrail";
      MissileHeight 8;
      +FlatSprite
      +Bright
   }

   states {
   Spawn:
      REMS X 0;
   SpawnLoop:
      #### # 1 {
         a_spawnParticle("Purple", SPF_FULLBRIGHT, 15, 4, 0, fRandom(-5, 5),fRandom(-5, 5),fRandom(-5, 5), fRandom(-5, 5),fRandom(-5, 5),fRandom(-5, 5));
      }
      loop;
   Death:
      TNT1 A 0;
      stop;
   }
}

class Lith_RemsBullet2 : Lith_RemsBullet1 {
   states {
   Spawn:
      REMS Y 0;
      goto SpawnLoop;
   }
}

class Lith_Rems : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_BFG9000Pickup";
   Tag "$LITH_INFO_SHORT_Rems";
   Weapon.SlotNumber 7;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/rems/draw";
   Lith_Weapon.AmmoType "Lith_CannonAmmo";

   +Weapon.NoAutoFire
}

int m_steamy, m_fireLoop;

states(Weapon) {
Ready:
   REMS A 0 {
      if(invoker.m_steamy) {
         if(callAcs("Lith_Timer") % 3 == 0) {
            a_fireProjectile("Lith_GunSmokeEmitter", frandom(-10, 10), 0, 1.5, 9, FPF_NOAUTOAIM, frandom(-10, 10));
         }
         invoker.m_steamy--;
      }
   }
   REMS A 1 Lith_A_Ready;
   loop;

Deselect:
   REMS A 1 Lith_A_Lower(_raise_slow);
   wait;

Select:
   REMS A 0 { invoker.m_steamy = 0; }
   REMS A 1 Lith_A_Raise(_raise_slow);
   wait;

Fire:
   REMS A 1 offset(0, 32) lith_a_setInterpolation(true);
   REMS A 1 offset(1, 33) {
      a_startSound("weapons/rems/fire", lch_weapon);
      invoker.m_fireLoop = 0;
      invoker.m_steamy = 175;
   }
   REMS B 1 offset(171, 88) bright;
   REMS C 1 offset(154, 81) bright;
FireLoop:
   REMS BC 1 bright {
      a_overlayOffset(_psp_weapon, fRandom(128, 164), FRandom(73, 80), WOF_INTERPOLATE);
      a_fireProjectile("Lith_RemsBullet1", spawnofs_xy: fRandom(-2, 4), spawnheight: fRandom(8, 10));
      a_fireProjectile("Lith_RemsBullet2", spawnofs_xy: fRandom(-2, 4), spawnheight: fRandom(5, 7));
      a_stop();
      return invoker.m_fireLoop++ == 170 ? resolveState("FireDone") : state(null);
   }
   loop;
FireDone:
   REMS C 1 bright offset(151, 80);
   REMS B 1 bright offset(155, 76);
   REMS D 1 bright offset(141, 74);
   REMS A 2 offset(4, 39);
   REMS A 3 offset(2, 35);
   REMS A 4 offset(1, 33) lith_a_setInterpolation(false);
   goto ReadySelector;
}

/* EOF */
