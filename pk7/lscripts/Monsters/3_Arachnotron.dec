actor Lith_Arachnotron : Arachnotron replaces Arachnotron
{
   DamageFactor "Lith_Energy", 0.6
   Tag "$LITH_INFO_SHORT_Arachnotron"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Arachnotron)
      goto Super::Death
   }
}

actor Lith_ArchoPlasmTrail
{
   Radius 2
   Height 2
   Renderstyle "Translucent"
   Alpha 0.666
   Scale 0.8

   +DONTSPLASH
   +NOINTERACTION

   states
   {
   Spawn:
      ACPX A 0
      ACPX AA 1 bright A_SpawnItemEx("Lith_ArchoPlasmParticles", -1, frandom(-1, 1), frandom(-1, 1), frandom(-1, 0), frandom(-0.5, 0.5), frandom(-0.5, 0.5))
      ACPX AA 0 A_SpawnParticle("37732B", SPF_FULLBRIGHT | SPF_RELATIVE, 10, 5, 0,
                          frandom(-8,8), frandom(-8,8), frandom(-2,2),
                          frandom(0, 10), frandom(-0.1, 0.1), frandom(-1.0, 1.0),
                          0, 0, 0.02,
                          1.0, -1)
      ACPX BCDEF 2 A_FadeOut(0.2)
      stop
   }
}

actor Lith_ArchoPlasmParticles
{
   Radius 2
   Height 2
   Renderstyle "Translucent"
   Alpha 0.4

   +DONTSPLASH
   +NOINTERACTION

   states
   {
   Spawn:
      ACPT A 2 bright
      ACPT BC 3
      ACPT DDEE 2 A_FadeOut(0.15)
      stop
   }
}

actor Lith_ArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
   DamageType "Lith_Energy"
   Radius 13
   Height 8
   Speed 25
   Damage 5
   Scale 0.9

   Projectile
   +RANDOMIZE

   states
   {
   Spawn:
      ACPL ABCDABCD 1 bright A_SpawnItemEx("Lith_ArchoPlasmTrail", -1, frandom(-1, 1), frandom(-1, 1), frandom(-1, 0), frandom(-0.5, 0.5), frandom(-0.5, 0.5))
      loop
   Death:
      APLS A 2 bright
      ACPX ABC 2 bright
      ACPX DEFG 3
      stop
   }
}

// EOF
