/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson & Yholl. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_FistCharge : Inventory {default {Inventory.MaxAmount 0x7FFFFFFF;}} /* Yes. */
class Lith_FistHit : Powerup {default {Powerup.Duration 5;}}

class Lith_ChargeFistQuakerOats : Actor
{
   default
   {
      +NOGRAVITY
      +NOINTERACTION
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 2 A_Quake(5, 2, 0, 128, "");
      TNT1 A 2 A_Quake(2, 2, 0, 128, "");
      TNT1 A 2 A_Quake(1, 2, 0, 128, "");
      stop;
   }
}

class Lith_ChargeFistPuff : Actor
{
   default
   {
      Alpha 0.6;
      RenderStyle "Translucent";

      +HITTRACER
      +NOINTERACTION
      +PUFFGETSOWNER
      +PUFFONACTORS
   }

   states
   {
   Crash:
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_GiveInventory("Lith_FistHit", 1, AAPTR_TARGET);
      goto Animation;
   XDeath:
      TNT1 A 0;
      TNT1 A 0 {
         A_GiveInventory("Lith_FistHit", 1, AAPTR_TARGET);
         A_StartSound("weapons/cfist/hit", lch_body, attenuation: 1.3); /* much squish loud yes */
         A_StartSound("weapons/cfist/hit", lch_body2, attenuation: 1.3);
      }
   Animation:
      FHFX S 3 {
         A_SpawnItemEx("Lith_ChargeFistQuakerOats", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION);
         A_StartSound("weapons/cfist/hitwall", lch_body, attenuation: 0.9);
      }
      FHFX TUVW 3;
      stop;
   }
}

class Lith_ChargeFist : Lith_OutcastsWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_ChainsawPickup";
   DamageType "Melee";
   Tag "$LITH_INFO_SHORT_ChargeFist";
   Weapon.Kickback 100;
   Weapon.SlotNumber 1;
   Weapon.SlotPriority 1;

   +WEAPON.CHEATNOTWEAPON
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 0 A_TakeInventory("Lith_FistCharge", 0x7FFFFFFF);
   TNT1 A 1 Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_StopSound(lch_weapon);
   TNT1 A 0 A_Lower;
   wait;

Select:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 9 A_GiveInventory("Lith_FistCharge", 35);
   TNT1 A 0 A_ReFire("Fire1");
   goto Release1;
Fire1:
   CFIS A 0 A_GiveInventory("Lith_FistCharge", 35);
   CFIS A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_pclass) == pcl_marine, 2);
   CCFS A 0;
   #### A 1 A_StartSound("weapons/cfist/ready", lch_weapon2);
   #### B 1 A_StartSound("weapons/cfist/charge", lch_weapon, CHANF_LOOP);
   #### C 2;
   #### D 2;
   #### E 3;
   #### F 4;
   #### G 7;
   goto WaitForRelease;
WaitForRelease:
   #### G 1 A_GiveInventory("Lith_FistCharge", 2);
   #### G 1 A_ReFire("WaitForRelease");
Release:
   TNT1 A 0 A_StartSound("weapons/cfist/chargeend", lch_weapon);
   TNT1 A 0 A_GunFlash("SekritHax", GFF_NOEXTCHANGE); /* OH NO NOT yholehax */
Release1:
   CFI2 A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_pclass) == pcl_marine, "Release2");
   CCF2 A 0;
   /* GOTTA RELEASE FAST */
Release2:
   #### A 0 A_ChangeVelocity(Cos(Pitch) * 3, 0, Sin(Pitch) * -3, 1);
   #### A 0 A_AlertMonsters(128);
   #### A 1 A_StartSound("weapons/cfist/fire", lch_weapon2);
   #### B 1;
   #### A 0 A_CustomPunch(CallACS("Lith_ChargeFistDamage"), true, 0, "Lith_ChargeFistPuff", 128);
   #### A 0 A_JumpIfInventory("Lith_FistHit", 1, "HitRecoil");
   #### C 2; /* Missed, so you quickly bring your fist back */
   #### D 5;
   #### H 2;
   #### I 1;
   #### J 1;
   #### K 1;
   TNT1 A 1;
   TNT1 AAAAAAAAAA 1 A_ReFire;
   goto ReadySelector;
HitRecoil:
   #### A 0 {
      A_AlertMonsters();
      A_ChangeVelocity(Cos(Pitch) * -2, 0, Sin(Pitch) * 2, 1);
   }
   #### C 1;
   #### DEFG 1; /* Shaking from the force of the impact */
   #### DEFG 2;
   #### H 2;
   #### I 2;
   #### J 2;
   #### K 2;
   TNT1 A 1;
   TNT1 AAAAAAAAAA 1 A_ReFire;
   goto ReadySelector;
   /* the crimera has struck again */
SekritHax:
   CFIS A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_pclass) == pcl_marine, 2);
   CCFS A 0;
   #### HI 1;
   stop;
}

/* EOF */
