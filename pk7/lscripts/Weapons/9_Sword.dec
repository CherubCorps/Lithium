actor Lith_SwordHipcher
{
   RenderStyle Add
   Alpha 0.25
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK A 15 A_FadeOut(0.1)
      wait
   }
}

actor Lith_SwordHitler
{
   RenderStyle Add
   Scale 0.1
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      SMOK A 0
      SMOK AAAAAAAA 0 A_SpawnItemEx("Lith_SwordHipcher",
         frandom(-24,24),frandom(-24,24),frandom(-24,24),
         frandom(-8,8),frandom(-8,8),frandom(-8,8),
         0, SXF_NOCHECKPOSITION, 240)
      SMOK A 1 bright A_FadeOut(0.1)
      wait
   }
}

actor Lith_SwordHitter
{
   Radius 6
   Height 6
   Speed 20
   Damage 30
   
   Projectile
   +RIPPER
   
   states
   {
   Spawn:
      TNT1 A 3
      TNT1 A 1 A_SpawnItemEx("Lith_SwordHitler")
      wait
   }
}

actor Lith_SwordBlitter
{
   Speed 0
   
   Projectile
   +NOBLOCKMAP
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1 A_CustomMissile("Lith_SwordHitter", 24, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER)
      TNT1 A 1
      {
         A_CustomMissile("Lith_SwordHitter", 8, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
         A_CustomMissile("Lith_SwordHitter", 16, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
      }
      TNT1 A 1
      {
         A_CustomMissile("Lith_SwordHitter", -8, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
         A_CustomMissile("Lith_SwordHitter", 0, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
      }
      TNT1 A 1
      {
         A_CustomMissile("Lith_SwordHitter", -24, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
         A_CustomMissile("Lith_SwordHitter", -16, 0, 0, CMF_AIMDIRECTION | CMF_TRACKOWNER);
      }
      stop
   }
}

actor Lith_Sword : Lith_Weapon
{
   Tag "sord"
   Weapon.Kickback 100
   Weapon.SlotNumber 1
   
   +WEAPON.MELEEWEAPON
   
   states
   {
   Ready:
      SORD A 1 A_WeaponReady
      Loop
   Deselect:
      TNT1 A 0 A_JumpIfHealthLower(1, "DeselectLoop") // this check required for death sequences
      goto Ready
   DeselectLoop:
      TNT1 A 0 A_Lower
      loop
   Select:
      SORD AAAA 0 A_Raise
      SORD A 1
      Loop
   Fire:
      PUNG B 1 A_SpawnItemEx("Lith_SwordBlitter", 0, 0, 24, 0, 0, 0, 0, SXF_SETTARGET | SXF_NOCHECKPOSITION)
      PUNG CD 1
      PUNG C 10
      PUNG B 1 A_ReFire
      Goto Ready
   Spawn:
      FIST A 0
      FIST A 0 A_SetUserVar(user_pickupparm, weapon_unknown)
      goto SpawnLoop
   }
}

