class Lith_LostSoul : LostSoul replaces LostSoul
{
   default
   {
      Health 70;
      RenderStyle "Add";
      Alpha 0.7;
      Tag "$LITH_INFO_SHORT_LostSoul";

      +QUICKTORETALIATE
      +THRUSPECIES
      -COUNTKILL
   }

   int m_invisible;

   states
   {
   Spawn:
      SKUL AB 10 bright A_Look;
      loop;
   GoInvisible:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.08);
      SKUL A 0 {m_invisible = true;}
      goto See;
   StopBeingInvisibleBaka:
      SKUL AABBAABBAA 1 bright A_FadeTo(0.7);
      SKUL A 0 {m_invisible = false;}
      goto Missile;
   See:
      SKUL A 0 A_JumpIf(!m_invisible && random(0, 128) < 64, "GoInvisible");
      SKUL AAAAABBBBB 1 bright A_FastChase;
      loop;
   Missile:
      SKUL A 0 A_JumpIf(m_invisible, "StopBeingInvisibleBaka");
      SKUL C 10 bright A_FaceTarget;
      SKUL D 4 bright A_SkullAttack;
   MissileLoop:
      SKUL CCCCDDDD 1 bright;
      loop;
   Pain:
      SKUL E 0 A_SetTranslucent(0.7, 1);
      SKUL E 3 bright;
      SKUL E 3 bright A_Pain;
      goto See;
   Death:
      #### # 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_LostSoul);
   DeathReal:
      SKUL F 0 A_SetTranslucent(0.7, 1);
      SKUL F 6 bright;
      SKUL G 6 bright A_Scream;
      SKUL H 6 bright;
      SKUL I 6 bright A_NoBlocking;
      SKUL J 6;
      SKUL K 6;
      stop;
   }
}

// EOF
