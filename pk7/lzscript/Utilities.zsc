// UTIL: Utilities and Tools Interpserse Languages
struct Lith_UTIL
{
   // fucking degrees system...
   static double ConstDeg(double x)
   {
      x %= 360;
      if(x < 0) x += 360;
      return x;
   }

   static double DiffDeg(double a, double b)
   {
      double d = (b - a) % 360;
      if(d < 0) d += 360;
      return d;
   }

   static double SDiffDeg(double a, double b)
   {
      double d = b - a;
      if(d ~== 0) return 0;
      d %= 360;
      return 2 * d % 360 - d;
   }

   static double AngleLerp(double a, double b, double t)
   {
      return a + SDiffDeg(a, b) * t;
   }

   static double Lerp(double a, double b, double t)
   {
      return (1 - t) * a + t * b;
   }

   static vector2 Vec2Lerp(vector2 a, vector2 b, double t)
   {
      let vec = (Lerp(a.x, b.x, t), Lerp(a.y, b.y, t));
      return vec;
   }

   static vector3 Vec3Lerp(vector3 a, vector3 b, double t)
   {
      let vec = (Lerp(a.x, b.x, t), Lerp(a.y, b.y, t), Lerp(a.z, b.z, t));
      return vec;
   }

   static int Round(double n)
   {
      return int(n * 1000) % 1000 < 500 ? int(n) : int(n+0.5);
   }
}

class Lith_EAngle
{
   float p, b, h;
}

class Lith_Quaternion
{
   float x, y, z, w;

   static Lith_Quaternion Create(float angle, float lx, float ly, float lz)
   {
      let q = new("Lith_Quaternion");
      q.make(angle, lx, ly, lz);
      return q;
   }

   static Lith_Quaternion CreateVector(float lx, float ly, float lz)
   {
      let q = new("Lith_Quaternion");
      q.makeVector(lx, ly, lz);
      return q;
   }

   void make(float angle, float lx, float ly, float lz)
   {
      x = lx * sin(angle / 2);
      y = ly * sin(angle / 2);
      z = lz * sin(angle / 2);
      w =      cos(angle / 2);
   }

   void makeVector(float lx, float ly, float lz)
   {
      x = lx;
      y = ly;
      z = lz;
      w = 0;
   }

   Lith_Quaternion makeConjugate()
   {
      let q = new("Lith_Quaternion");
      q.x = -x;
      q.y = -y;
      q.z = -z;
      q.w =  w;
      return q;
   }

   Lith_Quaternion multiply(Lith_Quaternion a2)
   {
      let q = new("Lith_Quaternion");
      q.x = w * a2.x + x * a2.w + y * a2.z - z * a2.y;
      q.y = w * a2.y + y * a2.w + z * a2.x - x * a2.z;
      q.z = w * a2.z + z * a2.w + x * a2.y - y * a2.x;
      q.w = w * a2.w - x * a2.x - y * a2.y - z * a2.z;
      return q;
   }

   Lith_EAngle generateEuler()
   {
      float ysqr = y * y;
      float t0 = 2 * (w * x + y * z);
      float t1 = 1 - 2 * (x * x + ysqr);
      float t2 = clamp(2 * (w * y - z * x), -1, 1);
      float t3 = 2 * (w * z + x * y);
      float t4 = 1 - 2 * (ysqr + z * z);

      let a = new("Lith_EAngle");

      a.b = atan2(t0, t1);
      a.p = asin(t2);
      a.h = atan2(t3, t4);

      return a;
   }

   vector3 generateVector()
   {
      vector3 vec;
      vec.x = x;
      vec.y = y;
      vec.z = z;
      return vec;
   }
}

// EOF
