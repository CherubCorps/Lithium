// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │                       Effects taken from Realm667.                       │
// │   I think Tormenter made these originally, sorry if that's incorrect.    │
// │               Thanks to Kate for making these look better.               │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RainDrop : Actor {
   default {
      Alpha 0.6;
      RenderStyle "Add";
      Height 2;
      Radius 2;
      +FlatSprite
      +DontSplash
      +DropOff
      +NoBlockmap
      +NoInteraction
      +NoTeleport
      +NoTrigger
      +RollSprite
   }
   bool m_done;
   float m_rollSpeed;
   override void beginPlay() {
      super.beginPlay();
      vel.z    = fRandom[lith_rain](-19, -26);
      scale.y *= vel.z;
      m_done   = false;
   }
   override void tick() {
      if(isFrozen()) {
      } else if(m_done) {
         Lith_UTIL.stateTick(self);
      } else {
         if(m_rollSpeed) {
            a_setRoll(roll + m_rollSpeed, SPF_INTERPOLATE);
         }
         if(waterLevel > 0 || pos.z <= floorZ) {
            bRollSprite = false;
            bFlatSprite = false;
            setOrigin((pos.xy, floorZ), false);
            vel    = (0, 0, 0);
            roll   = 0;
            scale  = (1, 1);
            m_done = true;
            setState(resolveState("Death"));
         } else {
            setOrigin(pos + vel, true);
         }
      }
   }
   states {
   Spawn:
      RNDR A -1;
      stop;
   Death:
      RNDR BCDEFGH 3;
      stop;
   }
}

class Lith_BloodRainDrop : Lith_RainDrop {
   default {
      Alpha 0.5;
      RenderStyle "Subtract";
      Translation "Lith_BloodRainDrop";
      +Bright
   }
}

class Lith_AbyssRainDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      YScale 0.25;
      RenderStyle "Normal";
   }
   states {
   Spawn:
      RNDZ A -1;
      stop;
   Death:
      RNDZ BCDEFGH 3;
      stop;
   }
}

class Lith_SnowDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
      YScale 0.1;
   }
   states {
   Spawn:
      RNDS A -1;
      stop;
   Death:
      RNDS BCDE 12;
      stop;
   }
}

class Lith_Ash : Lith_SnowDrop {
   default {
      Alpha 0.9;
      XScale 0.75;
      YScale 0.06;
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      m_rollSpeed = fRandom[lith_rain](0.4, 0.9) * fRandomPick[lith_rain](-1, 1);
      scale      *=  fRandom[lith_rain](0.25, 1.0);
      vel.z       = -fRandom[lith_rain](0.84, 1.2);
   }
   states {
   Spawn:
      RNDA A 40;
   Death:
      RNDA A 1 a_fadeOut(0.25 / 21.0);
      wait;
   }
}

/* EOF */
