/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_Shell : Lith_AmmoItem replaces Shell {
   default {
      Inventory.PickupSound "player/pickup/shells";
      Lith_BasicPickup.LogName "Shell", 2;
      Lith_ScoreItem.Score Score_Shell;
      Lith_AmmoItem.Give "Lith_ShellAmmo", AmmoAmt_Shell;
   }

   states {
   Spawn:
      SHEL A -1;
      stop;
   }
}

class Lith_ShellBox : Lith_AmmoItem replaces ShellBox {
   default {
      Inventory.PickupSound "player/pickup/shellb";
      Lith_BasicPickup.LogName "ShellBox", 2;
      Lith_ScoreItem.Score Score_ShellBox;
      Lith_AmmoItem.Give "Lith_ShellAmmo", AmmoAmt_ShellBox;
   }

   states {
   Spawn:
      SBOX A -1;
      stop;
   }
}

class Lith_RocketAmmoPickup : Lith_AmmoItem replaces RocketAmmo {
   default {
      Inventory.PickupSound "player/pickup/rockets";
      Lith_BasicPickup.LogName "Rocket", 2;
      Lith_ScoreItem.Score Score_Rocket;
      Lith_AmmoItem.Give "Lith_RocketAmmo", AmmoAmt_Rocket;
   }

   states {
   Spawn:
      ROCK A -1;
      stop;
   }
}

class Lith_RocketBox : Lith_AmmoItem replaces RocketBox {
   default {
      Inventory.PickupSound "player/pickup/rocketb";
      Lith_BasicPickup.LogName "RocketBox", 2;
      Lith_ScoreItem.Score Score_RocketBox;
      Lith_AmmoItem.Give "Lith_RocketAmmo", AmmoAmt_RocketBox;
   }

   states {
   Spawn:
      BROK A -1;
      stop;
   }
}

class Lith_Cell : Lith_AmmoItem replaces Cell {
   default {
      Inventory.PickupSound "player/pickup/cells";
      Lith_BasicPickup.LogName "Cell", 2;
      Lith_ScoreItem.Score Score_Cell;
      Lith_AmmoItem.Give "Lith_PlasmaAmmo", AmmoAmt_Cell;
   }

   states {
   Spawn:
      CELL A -1;
      stop;
   }
}

class Lith_CellPack : Lith_AmmoItem replaces CellPack {
   default {
      Inventory.PickupSound "player/pickup/cellb";
      Lith_BasicPickup.LogName "CellBox", 2;
      Lith_ScoreItem.Score Score_CellPack;
      Lith_AmmoItem.Give  "Lith_PlasmaAmmo", AmmoAmt_CellPack;
      Lith_AmmoItem.Give2 "Lith_CannonAmmo", AmmoAmt_CannonPack;
   }

   states {
   Spawn:
      CELP A -1;
      stop;
   }
}

class Lith_AmmoItem : Lith_ScoreItem abstract;

default {
   Lith_BasicPickup.LogType msg_ammo;
   +CastSpriteShadow
}

private meta class<Inventory> m_type, m_type2;
private int m_amount, m_amount2;

property Give:  m_type,  m_amount;
property Give2: m_type2, m_amount2;

override bool lith_canGiveScore() {return !lith_sv_noscoreammo;}

private bool lith_ammoNotFull(Actor mo) {
   let t1 = m_type;
   let t2 = m_type2;
   let a1 = m_amount;
   let a2 = m_amount2;

   if(mo is "Lith_ThothPlayer") {
      if(t1) {t1 = "Lith_ManaAmmo"; a1 *= 7;}
      if(t2) {t2 = "Lith_ManaAmmo"; a2 *= 7;}
   }

   let i1 = mo.findInventory(t1);
   let i2 = mo.findInventory(t2);
   return m_type  && m_amount  && (!i1 || i1.amount < i1.maxAmount) ||
          m_type2 && m_amount2 && (!i2 || i2.amount < i2.maxAmount);
}

override bool canPickup(Actor mo) {
   if(super.canPickup(mo)) {
      return !lith_sv_nofullammo || lith_ammoNotFull(mo);
   } else {
      return false;
   }
}

private void lith_absorbAmmo(Actor mo, class<Inventory> typ, in out int amt) {
   if(!typ) return;

   if(mo is "Lith_ThothPlayer") {typ = "Lith_ManaAmmo"; amt *= 7;}

   let it = mo.findInventory(typ);
   if(!it) {it = Inventory(spawn(typ)); it.amount = 0; it.callTryPickup(mo);}

   if((it.amount + amt - it.maxAmount) <= 0) {
      /* less than or equals max, absorb the whole item */
      it.amount += amt;
      amt        = 0;
   } else {
      /* more than max, absorb some of the item */
      amt      -= it.maxAmount - it.amount;
      it.amount = it.maxAmount;
   }
}

override bool tryPickup(in out Actor mo) {
   lith_absorbAmmo(mo, m_type,  m_amount);
   lith_absorbAmmo(mo, m_type2, m_amount2);

   if(lith_sv_noscoreammo && lith_sv_nofullammo) {
      if(m_amount == 0 && m_amount2 == 0) {
         return super.tryPickup(mo);
      } else {
         return true;
      }
   } else {
      return super.tryPickup(mo);
   }
}

/* EOF */
