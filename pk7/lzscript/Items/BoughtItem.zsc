/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ExtraUpgradeSprite : Lith_AnimItemSprite
{
   default {Lith_AnimItemSprite.Phases 10;}
   states
   {
   SetSpr:
      IOBJ A 0 {bBRIGHT = true;}
      IOBJ AAA 0 Lith_A_JumpPhase;
      IOBJ B 0 {bBRIGHT = false;}
      IOBJ BB 0 Lith_A_JumpPhase;
      IOBJ B 0 {bBRIGHT = true;}
      IOBJ BBB 0 Lith_A_JumpPhase;
      IOBJ B 0 {bBRIGHT = false;}
      IOBJ AA 0 Lith_A_JumpPhase;
      stop;
   }
}

class Lith_BoughtItem : Lith_CustomInventory
{
   default
   {
      Inventory.PickupMessage "";
      Inventory.PickupSound "";

      +INVENTORY.ALWAYSPICKUP
      +INVENTORY.TRANSFER
      +FORCEXYBILLBOARD
   }

   int user_UpgradeId;
   int user_UpgradeWeap;
   int user_UpgradeBody;
   int user_UpgradeExtr;

   states(Actor)
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 Lith_A_SetNamedScriptSpecial("Lith_BoughtItemPickup", user_UpgradeId);
      TNT1 A random(1, 10);
      TNT1 A 0 A_SpawnItemEx("Lith_P67TeleFog");
      UCAN A 0 A_JumpIf(user_UpgradeBody, "Animate");
      WUPG A 0 A_JumpIf(user_UpgradeWeap, "Animate");
      BPAK A 0 A_JumpIf(user_UpgradeExtr, "Extra");
   Animate:
      #### # 5 bright;
      #### # 5;
      loop;
   Extra:
      TNT1 A 0 A_SpawnItemEx("Lith_ExtraUpgradeSprite", flags: SXF_SETMASTER);
      TNT1 A 1;
      wait;
   }

   states(Item)
   {
   Pickup:
      TNT1 A 0;
      stop;
   }
}

/* EOF */
