actor Lith_ItemGlow
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.1
   Translation "112:127=197:207", "224:231=192:199"
   
   +NOINTERACTION
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      PBAL A 0
      PBAL A 0 A_JumpIf(CallACS("Lith_GetWorldData", wdata_brightweps), "SpawnBright")
   Spawn1:
      PBAL A 5 bright
      PBAL A 1 bright A_FadeOut(1.0 / 35.0)
      wait
   SpawnBright:
      PBAL A 0 A_SpawnItemEx("Lith_ItemGlowBright")
      stop
   }
}

actor Lith_ItemGlowBright : Lith_ItemGlow
{
   Translation "112:127=192:207", "224:231=192:194"
   
   states
   {
   Spawn:
      PBAL A 0
      goto Spawn1
   }
}

actor Lith_UpgrGlow : Lith_ItemGlow
{
   Translation "None"
   
   states
   {
   Spawn:
      PBAL A 0
      goto Spawn1
   }
}

actor Lith_ItemSprite
{
   +NOINTERACTION
   +NOTIMEFREEZE
   
   var int user_origx;
   var int user_origy;
   var int user_origz;
   var int user_floatangle;
   var int user_floatzangle;
   
   states
   {
   Spawn:
      "####" "#" 0
      "####" "#" 0 A_SetUserVar(user_floatangle,  random(0, 360))
      "####" "#" 0 A_SetUserVar(user_floatzangle, random(0, 360))
   SpawnLoop:
      "####" "#" 0 A_CheckFlag("SPECIAL", 1, AAPTR_MASTER)
      goto Done
      "####" "#" 0 A_SetUserVar(user_floatangle,  (user_floatangle  + 2) % 360)
      "####" "#" 0 A_SetUserVar(user_floatzangle, (user_floatzangle + 3) % 360)
      "####" "#" 0 A_SetUserVar(user_origx, x * FIX)
      "####" "#" 0 A_SetUserVar(user_origy, y * FIX)
      "####" "#" 0 A_SetUserVar(user_origz, z * FIX)
      "####" "#" 1 bright A_Warp(AAPTR_MASTER, cos(user_floatangle) * 4.0,
                                               sin(user_floatangle) * 4.0,
                                               16.0 + (sin(user_floatzangle) * 12.0),
                                 0.0, WARPF_NOCHECKPOSITION)
      "####" "#" 0 bright A_Warp(AAPTR_MASTER, user_origx / FIX,
                                               user_origy / FIX,
                                               user_origz / FIX,
                                 0, WARPF_ABSOLUTEPOSITION)
      loop
   Done:
      TNT1 A 0
      stop
   }
}

actor Lith_ItemSpriteMarine : Lith_ItemSprite
{
   VisibleToPlayerClass "Lith_MarinePlayer"
}

actor Lith_ItemSpriteMage : Lith_ItemSprite
{
   VisibleToPlayerClass "Lith_MagePlayer"
}

// EOF

