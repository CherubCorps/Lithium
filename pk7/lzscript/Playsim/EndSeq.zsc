struct Lith_EndSeqNode
{
   name type;
   uint tics;
   string text;
}

extend class Lith_Events;

enum Lith_EndSeqState
{
   ESEQ_HALT,

   ESEQ_RUN,

   ESEQ_STOP,

   ESEQ_WAIT,
   ESEQ_WRITEOUT,
   ESEQ_FADEOUT,
   ESEQ_FADEIN,
   ESEQ_FADEMUS,
}

const fxk = 0xFFFF;

// Program
private ui uint m_en;
private ui Lith_EndSeqNode m_node[32];
private ui uint16 m_jmp[99];

// State
private ui uint m_eseq;
private ui color m_bgcolo, m_fdcolo;
private ui string m_text;
private ui textureid m_bgimg;
private ui int m_time, m_tics, m_skip;

ui void LoadEnding(string fname)
{
   let lmp = Lith_HERMES.ReadLump(Lith_HERMES.FindLump(fname));

   array<string> lns, arg;

   lmp.split(lns, "\n");

   m_bgcolo = "Black";
   m_fdcolo = "Black";
   m_text   = "";

   for(uint i = 0, l = lns.size(); i < l; i++)
   {
      if(lns[i] == "") continue;

      string ln = lns[i]. mid(3);
      string jp = lns[i].left(2);
      uint space = ln.indexof(" ");

      string fn = space >= 0 ? ln.left(space  ) : ln;
      string ar = space >= 0 ? ln. mid(space+1) : "";

      if(jp.toint() > 0)
         m_jmp[jp.toint()] = i;

      if(fn.toint() > 0 || fn == "0")
      {
         m_node[i].type = 'text';
         m_node[i].tics = fn.toint();
         m_node[i].text = StringTable.Localize(ar, true);
         continue;
      }

      if(ar != "")
      {
         arg.clear();
         ar.split(arg, ",");
      }

      let nam = name(fn);
      switch(nam) {
      case 'stop':
      case 'done': m_node[i].type = nam; break;
      case  'img':
      case  'mus':
      case   'bg': m_node[i].type = nam; m_node[i].text = arg[0];         break;
      case 'fmus':
      case 'wait': m_node[i].type = nam; m_node[i].tics = arg[0].toint(); break;
      case 'case':
         m_node[i].type = nam;
         m_node[i].text = arg[0];
         m_node[i].tics = arg[1].toint();
         break;
      case 'fade':
         m_node[i].type = nam;
         m_node[i].tics = arg[0].toint();
         m_node[i].text = arg[1];
         break;
      }
   }

   m_eseq = ESEQ_RUN;
}

ui void DrawEndSequence(RenderEvent evt)
{
   Screen.Dim(m_bgcolo, 1, 0, 0, Screen.GetWidth(), Screen.GetHeight());

   if(m_bgimg)
      Screen.DrawTexture(m_bgimg, false, 0, 0,
         DTA_VirtualWidth, 320, DTA_VirtualHeight, 200);

   string txt = m_text;

   if(m_eseq == ESEQ_WRITEOUT)
   {
      uint len = m_text.length();
      uint prc = m_time*fxk / m_tics; // divide (k/i)
      uint amt = len * prc / fxk; // multiply (i*k) and remove decimal
      txt = m_text.left(amt);
   }

   if(txt != "")
      Screen.DrawText("smallfnt", Font.CR_WHITE, 10, 10, txt,
         DTA_VirtualWidth, 320, DTA_VirtualHeight, 240);

   switch(m_eseq) {
   case ESEQ_FADEMUS:
      SetMusicVolume(1 - m_time / float(m_tics));
      break;
   case ESEQ_FADEIN:
   case ESEQ_FADEOUT: {
      float fd = m_time / float(m_tics);
      if(m_eseq == ESEQ_FADEIN) fd = 1 - fd;
      Screen.Dim(m_fdcolo, fd, 0, 0, Screen.GetWidth(), Screen.GetHeight());
      break;
   }
   }

   if(m_skip)
      Screen.DrawText("smallfnt", Font.CR_WHITE, 5, 0, String.Format("Skip: %u", 1 + m_skip / 10),
         DTA_VirtualWidth, 320, DTA_VirtualHeight, 240);
}

ui void RunEndSequence()
{
   PlayerInfo p = players[consoleplayer];
   let btn = p.cmd.buttons;
   let old = p.oldbuttons;

   if(m_eseq >= ESEQ_WAIT)
   {
      if(btn & BT_ALTATTACK)
      {
         if(m_skip)
         {
            if(--m_skip == 0) {
               m_eseq = ESEQ_RUN;
               m_en++;
            }
         }
         else
            m_skip = 25;
      }
      else if(m_time < m_tics)
      {
         m_skip = 0;

         if(btn & BT_USE && m_time > 12) m_time += 3;
         else                            m_time++;

         if(m_time > m_tics) m_time = m_tics;
      }
      else
      {
         m_skip = 0;

         m_eseq = ESEQ_RUN;
         m_en++;
      }
   }
   else if(m_eseq == ESEQ_STOP)
   {
      if(btn & BT_ALTATTACK || (btn ^ old && (btn & old) == p.oldbuttons && p.bot == null))
      {
         m_eseq = ESEQ_RUN;
         m_en++;
      }
   }

   while(m_eseq == ESEQ_RUN)
   {
      switch(m_node[m_en].type) {
      case  'mus': S_ChangeMusic(m_node[m_en++].text); break;
      case 'done':
         Menu.SetMenu('EndGameMenu');
         MessageBoxMenu(Menu.GetCurrentMenu()).HandleResult(true);
         m_eseq = ESEQ_HALT;
         break;
      case 'stop': m_eseq = ESEQ_STOP;             break;
      case  'img': {
         string tex = m_node[m_en++].text;

         if(tex.charCodeAt(0) == Ch_Colon)
            tex = StringTable.Localize(String.Format("LITH%s", tex), false);

         m_bgimg = TexMan.CheckForTexture(tex, TexMan.Type_Any);
         break;
      }
      case   'bg':
         m_bgcolo = m_node[m_en++].text;
         m_bgimg.setNull();
         break;
      case 'case': {
         bool cnd;

         switch(name(m_node[m_en].text)) {
         case 'marine':    cnd = p.cls == "Lith_MarinePlayer";    break;
         case 'cybermage': cnd = p.cls == "Lith_CyberMagePlayer"; break;
         default:          cnd = false;                           break;
         }

         if(cnd) m_en++;
         else    m_en = m_jmp[m_node[m_en].tics];

         break;
      }
      case 'wait':
         m_tics = m_node[m_en].tics;
         m_time = 1;
         m_eseq = ESEQ_WAIT;
         break;
      case 'fmus':
         m_tics = m_node[m_en].tics;
         m_time = 1;
         m_eseq = ESEQ_FADEMUS;
         break;
      case 'fade':
         m_fdcolo = m_node[m_en].text;
         m_tics   = m_node[m_en].tics;
         m_time   = 1;
         if(m_tics < 0) {m_eseq = ESEQ_FADEIN; m_tics = -m_tics;}
         else            m_eseq = ESEQ_FADEOUT;
         break;
      case 'text':
         m_text = m_node[m_en].text;
         m_tics = m_node[m_en].tics;
         m_time = 1;
         if(m_tics > 0) m_eseq = ESEQ_WRITEOUT;
         else           m_en++;
         break;
      }
   }
}

// EOF
