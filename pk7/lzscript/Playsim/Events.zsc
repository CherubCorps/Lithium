#include "lzscript/Playsim/EndSeq.zsc"

class Lith_Events : EventHandler;

bool m_bosslevel;

override void WorldLoaded(WorldEvent evt)
{
   static const string[] bosslevels = {
      "058fb092ea1b70da1e3cbf501c4a91a1", // E1M8
      "effe91df41ad41f6973c06f0ad67ddb9", // E2M8
      "ef128313112110ed6c1549af96af26c9", // E3M8
      "2dc939e508ab8eb68af79d5b60568711", // E4M8
      "5eecd88f4491f516d590ce4bbf45f532"  // MAP30
   };

   if(evt.isSaveGame)
      return;

   if(evt.isReopen)
      CallACS("Lith_WorldReopen");
   else
   {
      CallACS("__lithlib.bin_init");
      CallACS("__lithmain.bin_init");

      let sum = level.getChecksum();
      for(int i = 0; i < bosslevels.size(); i++) if(sum == bosslevels[i])
      {
         m_bosslevel = true;
         break;
      }
   }
}

override void WorldThingDestroyed(WorldEvent evt)
{
   if(evt.thing is "Lith_InventoryItem")
      Lith_InventoryItem(evt.thing).Lith_Destroy();
}

override void WorldUnloaded(WorldEvent evt)
{
   let it = ThinkerIterator.Create("Lith_InventoryItem");
   for(Lith_InventoryItem mo; mo = Lith_InventoryItem(it.next());)
      if(!mo.owner) mo.Lith_Destroy();
}

override void UiTick()
{
   // because we override finales in the MAPINFO, this should only trigger
   // at the very end of the game. hopefully.
   // EDIT: it didn't, so I had to add more hacks
   if(gamestate == GS_FINALE && (!level.nextmap || level.nextmap.left(6) == "enDSeQ"))
      CallACS("Lith_Finale");

   if(m_eseq == ESEQ_HALT && CVar.FindCVar('__lith_end').getString() != "")
   {
      LoadEnding(String.Format("lfiles/End_%s.txt", CVar.FindCVar('__lith_end').getString()));
      CVar.FindCVar('__lith_end').setString("");
   }
   else if(m_eseq >= ESEQ_RUN)
      RunEndSequence();
}

override void RenderOverlay(RenderEvent evt)
   {if(m_eseq >= ESEQ_RUN) DrawEndSequence(evt);}

override void PlayerEntered(PlayerEvent evt)
{
   PlayerInfo p = players[evt.playerNumber];

   // in case the player isn't derived from Lith_Player
   if(p && p.mo && !p.mo.findInventory("Lith_AAGH"))
   {
      p.mo.giveInventoryType("Lith_AAGH");
      p.mo.giveInventoryType("Lith_IDOL");
   }
}

override void WorldThingSpawned(WorldEvent evt)
{
   let th = evt.thing;

   if(th.bISMONSTER)
   {
      th.ACS_NamedExecuteAlways("Lith_MonsterInfo");

      if(CallACS("LWData", wdata_fun) & lfun_ragnarok)
         Lith_HERMES.PrepareForRagnarok(th);

      if(m_bosslevel && th.bBOSS)
      {
         th.health *= frandom(2, 3);
         th.reactiontime = 0;
         th.damageMultiply *= frandom(1.1, 1.6);
         th.damageFactor   *= frandom(0.3, 0.5);
      }
   }
}

override void WorldTick()
{
   if(m_bosslevel) for(int i = 0, m = level.lines.size(); i < m; i++) for(int j = 0; j < 3; j++)
      level.lines[i].sidedef[0].setTextureYoffset(j, sin(level.totaltime)*4);
}

override void WorldThingDamaged(WorldEvent evt)
{
   let src = evt.damagesource;
   let dst = evt.thing;

   if(!dst) return;

   if(evt.damageType == 'Bullet' && evt.damage >= 8 && !dst.bNOBLOOD)
   {
      let mo = Actor.Spawn("Lith_Dummy", dst.pos + (0, 0, dst.height / 2));
      float vol = clamp(evt.damage / 14.0, 0, 1);
      mo.A_PlaySound("body/bullethit", CHAN_BODY, vol * frandom(0.5, 1.1));
   }

   if(!src) return;

   if(src != dst && src.countInv("Lith_MonsterID") && (dst.bCOUNTKILL || dst.player))
      src.ACS_ScriptCall("Lith_GiveMonsterEXP", evt.damage * 3.77);
}

override bool InputProcess(InputEvent evt)
{
   let cl = Lith_URANUS.Instance();

   if(!cl) return false;

   if((evt.type == InputEvent.Type_KeyDown ||
       evt.type == InputEvent.Type_KeyUp) &&
      (evt.keyScan == InputEvent.Key_LShift ||
       evt.keyScan == InputEvent.Key_RShift))
   {
      cl.m_pdata[consoleplayer].shiftDown =
         (evt.type == InputEvent.Type_KeyDown);

      return false;
   }
   else if(Lith_Player(players[consoleplayer].mo).m_grabInput &&
      evt.type == InputEvent.Type_KeyDown && evt.keyString)
   {
      int ch = evt.keyChar;

      if(cl.m_pdata[consoleplayer].shiftDown &&
         (ch >= Ch_A && ch <= Ch_Z || ch >= Ch_L_A && ch <= Ch_L_Z))
      {
         ch -= 32;
      }

      EventHandler.SendNetworkEvent("Lith_Key", ch);
      return true;
   }
   
   return false;
}

override void NetworkProcess(ConsoleEvent evt)
{
   if(evt.name == "Lith_Key")
      CallACS("Lith_KeyDown", evt.player, evt.args[0]);
}

// EOF
