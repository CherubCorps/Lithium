actor Lith_IsaacThompsonTrail
{
   RenderStyle "Add"
   Scale 0.8
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PUFF A 1 A_FadeOut
      wait
   }
}

actor Lith_IsaacThompsonBullet : FastProjectile
{
   MissileType "Lith_IsaacThompsonTrail"
   RenderStyle "Translucent"
   Species "Lith_Phantom"
   DamageType "Lith_Bullets"
   Alpha 0.9
   Scale 0.3
   Radius 4
   Height 4
   Damage (4 * random(1, 4))
   Speed 128
   
   +BLOODSPLATTER
   +MTHRUSPECIES
   +DONTHURTSPECIES
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   Crash:
   Death:
      PUFF CD 4
      stop
   XDeath:
      TNT1 A 0
      stop
   }
}

actor Lith_IsaacThompsonBullet2 : Lith_IsaacThompsonBullet
{
   Damage 1
}

actor Lith_Boss_Michael : Lith_BasicPhantom
{
   Tag "Michael, Brother of Isaac"
   Health 4000
   Speed 7
   
   +THRUSPECIES
   +NOINFIGHTING
   
   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("Melee", "")
      loop
   
   Melee:
      PLAY F 0 A_FaceTarget
      PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "enemies/michael/hit", "enemies/michael/swing", "Lith_Melee")
      PLAY F 5
      goto See
   
   Death:
      TNT1 A 0 A_KillMaster
      PLAY H 10 A_NoBlocking
      PLAY I 60
      TNT1 A 70
      stop
   }
}

actor Lith_Boss_Johnson : Lith_BasicPhantom
{
   Tag "Isaac, Superbiorem Est"
   Health 40
   Speed 3
   
   +THRUSPECIES
   +NOINFIGHTING
   
   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("", "Missile", CHF_FASTCHASE)
      loop
   
   Missile:
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON)
      PLAY EE 0 A_CustomMissile("Lith_IsaacThompsonBullet2", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5))
      PLAY E 1 A_FaceTarget
      PLAY F 1 A_FaceTarget
      PLAY F 2 A_MonsterRefire(0, "See")
      loop
   
   Death:
      PLAY A 0 ACS_NamedExecuteWithResult("Lith_PhantomUnsetDupe")
      PLAY H 10 A_NoBlocking
      PLAY I 1 A_FadeOut(0.01)
      wait
   }
}

actor Lith_Boss_Isaac : Lith_Phantom
{
   Tag "Isaac, Superbiorem Est"
   Health 4000
   
   var int user_spawnedduplicates;
   
   states
   {
   PostInit:
      TNT1 A 0 A_JumpIf(user_phase < 2, 2)
      TNT1 A 0 A_SpawnItemEx("Lith_Boss_Michael", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
      TNT1 A 0 A_Jump(256, "CallPhantomMain")
   
   See:
      TNT1 A 0 A_Jump(32, "Missile2_1")
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("", "Missile", CHF_FASTCHASE)
      loop
   
   Missile:
      TNT1 A 0 A_Jump(256, "Missile1", "Missile2")
   Missile1:
      PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON)
      PLAY EE 0 A_CustomMissile("Lith_IsaacThompsonBullet", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5))
      PLAY E 1 A_FaceTarget
      PLAY F 1 A_FaceTarget
      PLAY F 0 A_Chase("", "", CHF_FASTCHASE)
      PLAY F 2 A_MonsterRefire(0, "See")
      goto Missile
   
   Missile2:
      TNT1 A 0 A_JumpIf(user_phase < 3, "Missile1")
   Missile2_1:
      TNT1 A 0 A_JumpIf(user_phase < 3, "See")
      TNT1 A 0 A_JumpIf(user_spawnedduplicates > 3, "See")
      TNT1 A 0 A_SetUserVar("user_spawnedduplicates", user_spawnedduplicates + 1)
      TNT1 A 0 A_SpawnItemEx("Lith_Boss_Johnson", 0,0,0, 0,0,0, 0, SXF_SETMASTER|SXF_NOCHECKPOSITION)
      goto See
   
   Death:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_PhantomDeath", 3, user_phase)
      TNT1 A 0 A_KillChildren
      PLAY H 10 A_NoBlocking
      PLAY I 60
      TNT1 A 70
      stop
   }
}

// EOF

