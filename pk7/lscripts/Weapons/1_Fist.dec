// ALL THINGS HERE TAKEN FROM LEGENDOOM
actor Lith_Knif : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_KnifPuf
{
   RenderStyle "Translucent"
   Alpha 0.5
   VSpeed 0.5
   Mass 5
   
   +NOINTERACTION
   -ALLOWPARTICLES
   +PUFFONACTORS
   +THRUGHOST
   +MTHRUSPECIES
   +NOEXTREMEDEATH
   +FORCEPAIN
   
   states
   {
   Spawn:
   XDeath:
      TNT1 A 0
      TNT1 A 0 A_CheckFlag("NOBLOOD", "Crash", AAPTR_TRACER)
      TNT1 A 0 A_PlaySound("weapons/knife/hitflesh", CHAN_WEAPON, 1, 0, 1.8)
      stop
   
   Melee:
   Crash:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/knife/hitwall", CHAN_WEAPON, 1, 0, 1.8)
      PUFF A 2 bright
      PUFF BCD 2
      stop
   }
}

actor Lith_Fist : Lith_Weapon replaces Fist // why would you pick up a fist
{
   Tag "Knife"
   Weapon.Kickback 100
   Weapon.SlotNumber 1
   Weapon.SlotPriority 0.9
   AttackSound "*fist" // there is nothing wrong here MOVE ALONG
   DamageType "Lith_Melee"
   
   +WEAPON.WIMPY_WEAPON
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      FIST A 0 // don't do this
      FIST A 0 A_SetUserVar(user_pickupparm, weapon_fist)
      goto SpawnLoop // pls
   
   Ready:
      TNT1 A 1 A_WeaponReady
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "Fire1")
      KNIF A 1 A_PlaySound("weapons/knife/swing1", CHAN_WEAPON)
      KNIF BCD 1
      goto FireNormal
   Fire1:
      TNT1 A 1 A_PlaySound("weapons/knife/swing2", CHAN_WEAPON)
      KNIF FGH 1
   FireNormal:
      TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "FireBerserk")
      TNT1 A 0 A_CustomPunch(5  * random(1, 10), true, CPF_NOTURN, "Lith_KnifPuf", 96)
      goto FireEnd1
   FireBerserk:
      TNT1 A 0 A_CustomPunch(20 * random(1, 10), true, CPF_NOTURN, "Lith_KnifPuf", 96)
      goto FireEnd1
   FireEnd1:
      TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "FireEnd2")
      KNIF E 1
      TNT1 A 1
      goto FireEnd
   FireEnd2:
      KNIF IJ 1
   FireEnd:
      TNT1 A 11
      TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "FireTakeKnif")
      TNT1 A 0 A_GiveInventory("Lith_Knif", 1)
      TNT1 AAAAA 1 A_ReFire
      TNT1 A 0 A_TakeInventory("Lith_Knif", 1)
      goto Ready
   FireTakeKnif:
      TNT1 A 0 A_TakeInventory("Lith_Knif", 1)
      TNT1 AAAAA 1 A_ReFire
      goto Ready
   }
}

// EOF

