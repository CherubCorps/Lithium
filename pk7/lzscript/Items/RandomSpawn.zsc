class Lith_RandomSpawn : Actor
{
   private meta int m_rsn;
   private class<Actor> m_type;

   property SpawnNum: m_rsn;

   static void Set(Actor mo, string tospawn)
      {Lith_RandomSpawn(mo).m_type = tospawn;}

   static void CopyImportantStuff(Actor lhs, Actor rhs)
   {
      lhs.special  = rhs.special;
      lhs.special1 = rhs.special1;
      lhs.special2 = rhs.special2;

      for(int i = 0; i < 5; i++)
         lhs.args[i] = rhs.args[i];

      lhs.changeTID(rhs.tid);

      lhs.spawnflags = rhs.spawnflags & ~MTF_SECRET;
      lhs.handleSpawnFlags();
      lhs.spawnflags = rhs.spawnflags;

      lhs.bCOUNTITEM = rhs.bCOUNTITEM;

      rhs.bCOUNTITEM = false;
   }

   override void PostBeginPlay()
   {
      CallACS("Lith_RandomSpawn", m_rsn);

      let mo = Spawn(m_type, pos, NO_REPLACE);

      if(mo)
      {
         mo.spawnAngle = spawnAngle;
         mo.spawnPoint = spawnPoint;
         mo.angle      = angle;
         mo.vel        = vel;
         mo.bDROPPED   = bDROPPED;

         CopyImportantStuff(mo, self);
      }

      Destroy();
   }
}

// EOF
