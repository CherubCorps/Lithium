class Lith_DualRocket : Lith_DWeapon;

default
{
   Tag "Dual Rocket Launchers";
   Weapon.SlotNumber 5;
   Weapon.SlotPriority 1;
}

int m_side;

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_RocketLauncherPickup");
   stop;
}

states(Weapon)
{
Ready:
   GRLG A 1 Lith_A_Ready;
   loop;

Deselect:
   GRLG AA 0 A_Lower;
   GRLG A 1;
   loop;

Select:
   GRLG AA 0 A_Raise;
   GRLG A 1;
   loop;

Fire:
   GRLG A 0 {
      invoker.m_side = !invoker.m_side;
      return invoker.m_side ? ResolveState("FireLeft") : ResolveState("FireRight");
   }
FireLeft:
   GRLG B 2 bright {
      A_WeaponReady(Lith_BOBONLY);
      A_FireProjectile("Lith_Rocket", 0, true, -5, -2);
   }
   GRLG CDE 3 bright A_WeaponReady(Lith_BOBONLY);
   goto FireDone;
FireRight:
   GRLG B 2 bright {
      A_WeaponReady(Lith_BOBONLY);
      A_FireProjectile("Lith_Rocket", 0, true, 5, -2);
   }
   GRLG CDE 3 bright A_WeaponReady(Lith_BOBONLY);
FireDone:
   GRLG A 0 A_ReFire;
   goto Ready;
}

// EOF
