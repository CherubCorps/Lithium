actor Lith_ItemGlow
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.1
   Translation "112:127=168:183", "5:15=185:185", "224:231=168:171"
   
   +NOINTERACTION
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      PBAL A 0
      PBAL A 0 A_JumpIf(CallACS("LWData", wdata_noitemfx), "Nope")
      PBAL A 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_marine,    "SpawnDone")
      PBAL C 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_cybermage, "SpawnDone")
      PBAL D 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_informant, "SpawnDone")
      PBAL E 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_wanderer,  "SpawnDone")
      PBAL F 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_assassin,  "SpawnDone")
      PBAL G 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_darklord,  "SpawnDone")
      PBAL H 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_thoth,     "SpawnDone")
      FDLI T 0 A_JumpIf(CallACS("LWData", wdata_pclass) == pcl_fdoomer,   "SpawnDone")
      UNKN A 0
   SpawnDone:
      "####" "#" 5 bright
      "####" "#" 1 bright A_FadeOut(1.0 / 35.0)
      wait
   Nope:
      TNT1 A 0
      stop
   }
}

actor Lith_WeapGlow : Lith_ItemGlow
{
   Translation "112:127=197:207", "224:231=192:199"
   
   states
   {
   Spawn:
      PBAL A 0
      PBAL A 0 A_JumpIf(CallACS("LWData", wdata_brightweps), "SpawnBright")
      goto Super::Spawn
   SpawnBright:
      PBAL A 0 A_SpawnItemEx("Lith_ItemGlowBright")
      stop
   }
}

actor Lith_ItemGlowBright : Lith_ItemGlow
{
   Translation "112:127=192:207", "224:231=192:194"
}

actor Lith_UpgrGlow : Lith_ItemGlow
{
   Translation "None"
}

// EOF

