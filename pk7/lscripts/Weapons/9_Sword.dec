#include "lscripts/Weapons/SwordSwing.dec"

actor Lith_SwordCharge : Inventory
{
   Inventory.MaxAmount 25
}

actor Lith_Sword : Lith_Weapon
{
   Tag "sord"
   Weapon.Kickback 100
   Weapon.SlotNumber 1
   
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      SORD A 0 A_TakeInventory("Lith_SwordCharge", 0x7FFFFFFF)
      SORD A 1 A_WeaponReady
      Loop
   Deselect:
      TNT1 A 0 A_JumpIfHealthLower(1, "DeselectLoop") // this check required for death sequences
      goto Ready
   DeselectLoop:
      TNT1 A 0 A_Lower
      loop
   Select:
      SORD AAAA 0 A_Raise
      SORD A 1
      Loop
   Fire:
      SORD A 0
      goto Ready
   AltFire:
      SORD A 0 A_JumpIfInventory("Lith_SwordCharge", 1, "AltFireSkipSound")
      SORD A 0 A_PlaySound("weapons/sword/charge")
   AltFireSkipSound:
      SORD A 1 A_WeaponReady(LITH_BOBONLY)
      SORD A 0 A_JumpIfInventory("Lith_SwordCharge", 0, "AltFireForReal")
      SORD A 0 A_GiveInventory("Lith_SwordCharge", 1)
      SORD A 0 A_Refire
      goto Ready
   AltFireForReal:
      SORD A 15 A_PlaySound("weapons/sword/chargeup", CHAN_WEAPON)
      SORD AB 2
      SORD CD 1
      TNT1 A 5
      SORD E 1 A_PlaySound("weapons/sword/swing", 7)
      SORD F 1 A_SpawnItemEx("Lith_SwordBlitter", 0, 0, 24, 0, 0, 0, 0, SXF_SETTARGET | SXF_NOCHECKPOSITION)
      SORD GH 1
      TNT1 A 4
      SORD A 1 offset(-17, 50)
      SORD A 1 offset(-13, 49)
      SORD A 1 offset(-10, 46)
      SORD A 1 offset(-8 , 43)
      SORD A 1 offset(-4 , 40)
      SORD A 1 offset(-2 , 38)
      SORD A 1 offset(-1 , 35)
      SORD A 1 offset(-1 , 33)
      SORD A 1 offset(0  , 32)
      Goto Ready
   Spawn:
      FIST A 0
      FIST A 0 A_SetUserVar(user_pickupparm, weapon_unknown)
      goto SpawnLoop
   }
}

