/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_InformantPlayer : Lith_Player
{
   default
   {
      Health 50;
      Lith_Player.ViewBob 0.1;
      Player.DisplayName "$LITH_CLASS_INFORMANT";
      Player.SoundClass "informant";
      Player.Portrait "lgfx/Portraits/Informant.png";
      Player.ViewHeight 44;
      Player.AttackZOffset 11;
      Player.JumpZ 14;
      Tag "$LITH_CLASS_INFORMANT";

      +NOBLOOD;
   }

   private int m_dodgeDbc;
   private int m_iFrames;
   int m_overdrive;

   override void giveDefaultInventory()
   {
      super.giveDefaultInventory();
      lith_giveDefWeapon("Lith_Flintlock");
   }

   override void tick()
   {
      if(m_iFrames) m_iFrames--;

      if(health > 0)
      {
         if(m_dodgeDbc)
            m_dodgeDbc--;
         else if(player && player.buttons & BT_SPEED)
         {
            let a = angle - atan2(player.cmd.sidemove, player.cmd.forwardmove);
            let c = cos(a) * 32;
            let s = sin(a) * 32;
            callACS("Lith_DodgeView");
            A_ChangeVelocity(c, s);
            m_iFrames = 20;
            m_dodgeDbc = TICRATE;
         }
      }

      super.tick();
   }

   override int damageMobj(Actor inflictor, Actor source, int damage, name mod, int flags, double angle)
   {
      if(m_iFrames)
         return 0;
      else
         return super.damageMobj(inflictor, source, damage, mod, flags, angle);
   }
}

class Lith_IWeapon : Lith_Weapon
{
   default
   {
      Inventory.RestrictedTo "Lith_InformantPlayer";
   }
}

#include "lzscript/3_Informant/2_Flintlock.zsc"

/* EOF */
