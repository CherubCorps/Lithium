/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ParticleBeamPuff : Actor
{
   default
   {
      DamageType "Electric";

      +ALWAYSPUFF
      +FORCEDECAL
   }
}

class Lith_ParticleBeam : Lith_PlasmaLaze
{
   default
   {
      DamageType "Electric";
      Mass 0;
      RenderStyle "Add";
      Scale 0.4;
      Species "Lith_Player";
      Translation "Lith_ParticleBeam";

      +DONTHARMSPECIES
      +FORCERADIUSDMG
      +FORCEXYBILLBOARD
      -NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT A 0;
      BLLT A 2 A_Explode(8, 256, 0);
      BFS1 A 1 A_FadeOut(0.1);
      wait;
   }
}

class Lith_PlasmaRifle : Lith_MWeapon;

default
{
   Lith_Weapon.MagSize 5;
   Tag "Plasma Rifle";
   Weapon.SlotNumber 6;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/plasma/draw";
}

action(Weapon) void Lith_A_FirePlasma()
{
   A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE);
   A_GunFlash("DummyFlash");
   A_PlaySound("weapons/plasma/fire", CHAN_WEAPON);
   for(int i = 0; i < 4; i++)
      A_FireProjectile("Lith_PlasmaBolt", CallACS("Lith_CircleSpread", 7 * IFIX, 3 * IFIX, false) / FIX, false, 0, -9, 0, -(CallACS("Lith_CircleSpread", 0, 0, true) / FIX));
   A_FireProjectile("Lith_LightProjectileGreenShort", 0);
}

action(Weapon) void Lith_A_FireLaser()
{
   A_TakeInventory("Lith_PlasmaAmmo", 10, TIF_NOTAKEINFINITE);
   A_AlertMonsters();
   A_FireBullets(0, 0, 1, 0, "Lith_PlasmaHit", FBF_NORANDOMPUFFZ);
   A_RailAttack(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_TorgueMode) ? 1 : 12, 0, false, "", "Red", RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 0, "Lith_Dummy", 0, 0, 0, 1, 3, 0, "Lith_PlasmaLaze");
   A_FireProjectile("Lith_LightProjectileRedShort", 0);
}

action(Weapon) void Lith_A_FireRail()
{
   Lith_A_MagIncr();
   A_AlertMonsters();
   A_TakeInventory("Lith_PlasmaAmmo", 200, TIF_NOTAKEINFINITE);
   A_PlaySound("weapons/plasma/part/fire", CHAN_WEAPON);
   A_RailAttack(400, 0, false, "", "Blue", RGF_SILENT|RGF_FULLBRIGHT, 0, "Lith_ParticleBeamPuff", 0, 0, 0, 0, 16, 0, "Lith_ParticleBeam", -8);
   A_FireProjectile("Lith_LightProjectileSkyLong", 0);
}

action(Weapon) void Lith_A_FireAltPlasma()
{
   A_TakeInventory("Lith_PlasmaAmmo", 90, TIF_NOTAKEINFINITE);
   A_GunFlash("DummyFlash");
   A_FireProjectile("Lith_PenetratingPlasmaBolt", 0, false, 0, -9);
   A_FireProjectile("Lith_PenetratingPlasmaBolt", frandom(-2.7, -1.9), false, 0, -9);
   A_FireProjectile("Lith_PenetratingPlasmaBolt", frandom(2.7, 1.9), false, 0, -9);
   A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

action(Weapon) void Lith_A_EmitIons(int spars)
{
   A_RailAttack(0, 0, false, "", "White", RGF_SILENT|RGF_FULLBRIGHT, spars, "Lith_Dummy", 0, 0, 0, 1, 0.75, 0, "", -2);
}

states(Actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_PlasmaRiflePickup");
   stop;
}

states(Weapon)
{
Ready:
   PLSG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   PLSG A 0 {A_StopSound(CHAN_7); A_StopSound(CHAN_6);}
DeselectLoop:
   PLSG AA 0 A_Lower;
   PLSG A 1 A_Lower;
   loop;

Select:
   PLSG AA 0 A_Raise;
   PLSG A 1 A_Raise;
   loop;

Fire:
   PLSG A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_PlasLaser), "FireLaser");
   PLSG A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_PartBeam),  "FireParticleBeam");
FireReal:
   PLSG A 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") == 0, "FireEmpty");
   PLSJ A 1 offset(0, 37) bright Lith_A_FirePlasma;
   PLSJ B 1 offset(0, 36) bright;
   PLSJ C 1 offset(0, 32);
   PLSG AAAA 1 A_ReFire;
   goto ReadySelector;
FireLaser:
   PLSG A 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") == 0, "FireEmpty");
   PLSK A 0 {
      A_PlaySound("weapons/plasma/laser/fire_start", CHAN_WEAPON);
      A_PlaySound("weapons/plasma/laser/fire_loop", CHAN_7, 0.4, true);
      A_PlaySound("weapons/plasma/laser/fire_hum",  CHAN_6, 0.4, true);
      Lith_A_FireLaser();
   }
   PLSK AA 1 offset(0, 35) bright Lith_A_FireLaser;
FireLaserLoop:
   PLSG A 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") == 0, "FireEmpty");
   PLSK B 1 offset(0, 39) bright Lith_A_FireLaser;
   PLSK BB 1 offset(-2, 38) bright Lith_A_FireLaser;
   PLSK B 1 offset(1, 37) bright Lith_A_FireLaser;
   PLSK B 1 offset(-1, 40) bright Lith_A_FireLaser;
   PLSK CC 1 offset(1, 37) bright A_ReFire("FireLaserLoop");
FireLaserDone:
   PLSG A 1 offset(1, 36) {
      A_StopSound(CHAN_7);
      A_StopSound(CHAN_6);
      A_PlaySound("weapons/plasma/laser/fire_end");
   }
   PLSG A 1 offset(0, 35);
   PLSG A 1 offset(0, 34);
   PLSG A 1 offset(0, 33);
   PLSG A 1 offset(0, 32);
   PLSG A 1;
   goto ReadySelector;
FireParticleBeam:
   PLSG A 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") == 0, "FireEmpty");
   PLSG A 0 Lith_A_JumpIfMagEmpty;
   PLSG A 3 A_PlaySound("weapons/plasma/part/charge", CHAN_6);
   PLSG A 3 offset(0, 33) Lith_A_EmitIons(5);
   PLSG A 3;
   PLSG A 3 Lith_A_EmitIons(4);
   PLSG A 2;
   PLSG A 3 offset(0, 34) Lith_A_EmitIons(3);
   PLSG A 1;
   PLSG A 3 Lith_A_EmitIons(2);
   PLSG A 1;
   PLSG A 3 offset(0, 35) Lith_A_EmitIons(2);
   PLSF A 1 bright offset(0, 55) Lith_A_FireRail;
   PLSF A 1 bright offset(0, 45);
   PLSF B 1 bright offset(0, 37);
   PLSF B 1 bright offset(0, 36);
   PLSF C 1 bright offset(0, 35);
   PLSG A 1 offset(0, 35);
   PLSG A 3 offset(0, 34);
   PLSG A 4 offset(0, 33);
   PLSG A 5 offset(0, 32);
   PLSG AAAAA 1 A_ReFire;
   goto ReadySelector;
FireEmpty:
   #### # 1 {
      A_StopSound(CHAN_7);
      A_StopSound(CHAN_6);
      A_PlaySound("weapons/plasma/empty", 7);
   }
   #### # 1 offset(0, 37);
   #### # 1 offset(0, 36);
   #### # 1 offset(0, 35);
   #### # 1 offset(0, 34);
   #### # 1 offset(0, 32);
   #### # 1 A_ReFire;
   goto ReadySelector;

AltFire:
   PLSG A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_PlasLaser), "FireReal");
   PLSG A 0 A_JumpIf(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_PartBeam), "FireReal");
   #### # 0 A_JumpIf(CountInv("Lith_PlasmaAmmo") == 0, "FireEmpty");
   PLSK A 1 offset(0, 37) bright Lith_A_FireAltPlasma;
   PLSK C 1 offset(0, 36) bright;
   PLSG A 1 offset(0, 35);
   PLSG A 1 offset(0, 34);
   PLSG A 1 offset(0, 32);
   PLSG AAAA 1 A_ReFire;
   goto ReadySelector;

Reload:
   #### # 1 Lith_A_JumpIfMagFull;
   PLSG A 1 offset(-2, 34);
   PLSG A 1 offset(-4, 37);
   PLSG A 1 offset(-9, 43) {
      A_PlaySound("weapons/plasma/part/reloadout", CHAN_6);
      Lith_A_ReloadBobDn(0.5);
   }
   PLSG A 1 offset(-12, 47);
   PLSG A 1 offset(-14, 52);
   PLSG A 2 offset(-12, 55);
   PLSG A 3 offset(-11, 57);
   PLSG A 8 offset(-10, 60);
   PLSG A 8 offset(-11, 60);
   PLSG A 2 offset(-13, 59) {
      A_PlaySound("weapons/plasma/part/reloadin", CHAN_7);
      Lith_A_ReloadFinish();
      Lith_A_ReloadBobUp(0.5);
   }
   PLSG A 2 offset(-11, 49);
   PLSG A 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 7 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   PLSG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB);
   PLSG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB);
   PLSG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB);
   PLSG A 4 A_WeaponReady(WRF_NOBOB);
   goto ReadySelector;
}

/* EOF */
