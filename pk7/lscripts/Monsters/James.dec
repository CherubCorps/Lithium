actor Lith_JamesRevolverTrail
{
   RenderStyle "Add"
   Scale 0.7
   Alpha 0.8
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PUFF A 1 A_FadeOut
      wait
   }
}

actor Lith_JamesRevolverBullet : FastProjectile
{
   MissileType "Lith_JamesRevolverTrail"
   RenderStyle "Translucent"
   DamageType "Lith_Bullets"
   Alpha 0.9
   Scale 0.8
   Radius 20
   Height 4
   Damage (10 * random(1, 4))
   Speed 64
   
   +BLOODSPLATTER
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   Crash:
   Death:
      PUFF CD 4
      stop
   XDeath:
      TNT1 A 0
      stop
   }
}

actor Lith_Boss_James : Lith_Phantom
{
   Tag "James, Vanagloriam Est"
   Health 1500
   
   states
   {
   See:
      PLAY AAAABBBBCCCCDDDD 1 A_Chase("Melee", "Missile", CHF_FASTCHASE)
      loop
   
   Missile:
      PLAY E 0 A_JumpIf(user_phase != 2, "Missile1")
      PLAY F 0 A_JumpIf(user_meleetime > 0, "Missile1")
      PLAY E 0 A_JumpIfCloser(384, "Lunge")
   
   Missile1:
      PLAY E 0 A_PlaySound("enemies/james/fire", CHAN_WEAPON)
      PLAY F 0 A_FaceTarget
      PLAY E 5 A_CustomMissile("Lith_JamesRevolverBullet", 32, 0, frandom(-20, 20), CMF_OFFSETPITCH, frandom(-5, 5))
      PLAY F 2 A_FaceTarget
      PLAY F 0 A_FaceTarget
      PLAY F 0 A_Jump(128, "See")
      PLAY F 2 A_MonsterRefire(0, "See")
      loop
   
   Melee:
      PLAY F 0 A_JumpIf(user_phase != 2, "Missile")
      PLAY F 0 A_JumpIf(user_meleetime > 0, "Missile")
      PLAY F 0 A_SetUserVar(user_meleetime, 35)
      PLAY F 0 A_FaceTarget
      PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "skeleton/melee", "skeleton/swing", "Lith_Melee")
      goto See
   
   Lunge:
      PLAY A 0 A_PlaySound("skeleton/trace", CHAN_5, 1.0, false, 0.5)
      PLAY A 10 A_FaceTarget
      PLAY A 0 A_FaceTarget
      PLAY A 5 ACS_NamedExecuteWithResult("Lith_PhantomTeleport")
      goto Melee
   
   Death:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_PhantomDeath", 1, user_phase)
      PLAY H 10 A_NoBlocking
      PLAY I 60
      TNT1 A 70
      stop
   }
}

// EOF

