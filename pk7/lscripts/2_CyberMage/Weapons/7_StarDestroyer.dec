actor Lith_StarShrapnel
{
   Damage (10)
   Speed 32
   Radius 2
   Height 2
   Scale 0.7
   Mass 0
   RenderStyle "Add"
   
   Projectile
   +NODAMAGETHRUST
   
   states
   {
   Spawn:
      BLLT M 4 bright
      loop
   Death:
      MICX A 0 bright A_PlaySound("weapons/dark/impact", CHAN_BODY, 0.3)
      MICX A 0 bright A_SetScale(0.5)
      MICX ABCDEFG 1 bright
      stop
   }
}

actor Lith_StarShrapnelHoming : Lith_StarShrapnel
{
   Damage (30)
   Scale 1.4
   Translation "Lith_StarShrapnelHoming"
   
   states
   {
   Spawn:
      BLLT M 4 bright A_SeekerMissile(45, 360, SMF_LOOK)
      loop
   }
}

actor Lith_StarWoosh1
{
   Radius 8
   Height 8
   Scale 0.7
   RenderStyle "Add"
   
   +NOINTERACTION
   
   states
   {
   Death:
   Spawn:
      TNT1 A 0
      BLLT L 0 bright A_JumpIf(scalex < 0.01, "ded")
      BLLT L 2 bright A_SetScale(scalex*frandom(0.9,1.0))
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarWoosh2 : Lith_StarWoosh1
{
   states
   {
   Spawn:
      BLLT L 0 bright A_SetScale(scalex*frandom(0.95,1.0))
      BLLT L 1 bright A_FadeOut(0.05)
      loop
   ded:
      TNT1 A 0
      stop
   }
}

actor Lith_StarProjectile
{
   Damage 20
   Speed 2
   Scale 0.15
   Radius 8
   Height 8
   Mass 500
   RenderStyle "Add"
   
   Projectile
   +RIPPER
   +FORCERADIUSDMG
   +SEEKERMISSILE
   
   var int user_soundtimer;
   var int user_rushtimer;
   var int user_homingshrapneltimer;
   
   states
   {
   Spawn:
      TNT1 A 4
      TNT1 A 0 A_PlaySound("weapons/dark/wind", CHAN_7, 1.0, true)
   Spawn1:
      BLLT L 0 bright A_AlertMonsters
      BLLT L 0 bright A_SetUserVar(user_rushtimer, user_rushtimer + 1)
      BLLT L 0 bright A_JumpIf(user_rushtimer > 35*7, "Rush")
      BLLT L 0 bright A_JumpIf(scalex < 0.7, "ScaleUp")
   SoundCheck:
      BLLT L 0 bright A_SetUserVar(user_homingshrapneltimer, user_homingshrapneltimer + 1)
      BLLT L 0 bright A_JumpIf(user_homingshrapneltimer >= 10, "DoHomingMissile")
   SoundCheck1:
      BLLT L 0 bright A_SetUserVar(user_soundtimer, user_soundtimer - 1)
      BLLT L 0 bright A_JumpIf(user_soundtimer > 0, "DoMissile")
      BLLT L 0 bright A_SetUserVar(user_soundtimer, 3)
      BLLT L 0 bright A_PlaySound("weapons/dark/fire", CHAN_5)
   DoMissile:
      BLLT L 0 bright A_Explode(40, 8 + 45 * scalex, 0)
      BLLT LLLLL 0 bright A_SpawnProjectile("Lith_StarShrapnel",
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, false)/FIX,
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, true)/FIX,
         0, CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_BADPITCH)
      goto DoSeek
   DoHomingMissile:
      BLLT L 0 bright A_SetUserVar(user_homingshrapneltimer, 0)
      BLLT LL 0 bright A_SpawnProjectile("Lith_StarShrapnelHoming",
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, false)/FIX,
         CallACS("Lith_CircleSpread", 45*scalex*FIX, 45*scalex*FIX, true)/FIX,
         0, CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_BADPITCH)
      goto SoundCheck1
   ScaleUp:
      BLLT L 0 bright A_SetScale(scalex * 1.01)
      goto SoundCheck
   DoSeek:
      BLLT L 0 bright A_SeekerMissile(0, 1, SMF_LOOK)
      goto DoWoosh
   Rush:
      BLLT L 0 bright A_ChangeFlag("RIPPER", false)
      BLLT L 0 bright A_ChangeVelocity(velx*1.1, vely*1.1, velz*1.1, CVF_REPLACE)
   DoWoosh:
      BLLT L 0 bright A_SpawnItemEx("Lith_StarWoosh1", 0,0,0, 4,0,0, 180, SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION)
      BLLT L 0 bright A_SpawnItemEx("Lith_StarWoosh2", 0,0,0, 3,0,0, 180, SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION)
      BLLT L 0 bright A_Quake(1, 2, 0, 512)
      BLLT L 0 bright A_Quake(3, 2, 0, 256)
      BLLT L 0 bright A_Quake(5, 2, 0, 64)
      BLLT L 0 bright A_RadiusGive("Lith_BlindingLight1", 256, RGF_PLAYERS)
      BLLT L 0 bright A_RadiusGive("Lith_BlindingLight2", 128, RGF_PLAYERS)
      BLLT L 0 bright A_RadiusGive("Lith_BlindingLight3", 64,  RGF_PLAYERS)
      BLLT L 1 bright
      goto Spawn1
   Death:
      SDXP A 0 A_Explode(128, 512, 0)
      SDXP A 0 A_StopSound(CHAN_7)
      SDXP A 0 A_PlaySound("weapons/shipgun/explode", CHAN_6)
      SDXP A 0 A_SetScale(1)
      SDXP A 0 bright A_RadiusGive("Lith_BlindingLight3", 512, RGF_PLAYERS)
      SDXP ABCDEFGHIJKLMNOPQRS 1 bright
      SDXP T 1 bright A_FadeOut
      wait
   }
}

actor Lith_SolspearThingy
{
   Scale 0.15
   RenderStyle "Add"
   
   +BRIGHT
   +NOGRAVITY
   +NOCLIP
   
   states
   {
   Spawn:
      BLLT L 0
      BLLT L 5 A_Explode(128, 128, 0)
      BLLT L 1 A_FadeOut(0.025)
      wait
   }
}

actor Lith_Solspear
{
   Damage 500
   Speed 48
   Radius 8
   Height 8
   Scale 0.15
   RenderStyle "Add"
   BounceType "Hexen"
   BounceSound "weapons/dark/fire"
   BounceFactor 1
   WallBounceFactor 1
   BounceCount 15
   
   Projectile
   +BRIGHT
   +RIPPER
   
   const int flags = SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION;
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -8 ,0,0, 0,0,0, 0, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -16,0,0, 0,0,0, 0, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -24,0,0, 0,0,0, 0, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -32,0,0, 0,0,0, 0, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -40,0,0, 0,0,0, 0, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_SolspearThingy", -48,0,0, 0,0,0, 0, flags)
      BLLT L 1
      loop
   }
}

actor Lith_StarBarrier
{
   Translation "Lith_StarBarrier"
   RenderStyle "Add"
   XScale 0.3
   YScale 0.4
   Alpha 0.8

   +NOTIMEFREEZE
   +NOINTERACTION

   states
   {
   Spawn:
      LBAR A 1 A_ScaleVelocity(frandom(0.5,1))
   SpawnLoop:
      LBAR A 1 A_FadeOut(frandom(0.04, 0.09))
      loop
   }
}

actor Lith_SurgeProtector : PowerProtection
{
   DamageFactor "Normal", 0.3
   Powerup.Duration 3
}

actor Lith_SurgeOfDestiny : Lith_CustomFunction
{
   const int flags = SXF_NOCHECKPOSITION;

   states
   {
   Pickup:
      TNT1 A 0 A_GiveInventory("Lith_SurgeProtector")
      TNT1 A 0 A_Explode(128, 192, 0)
      TNT1 A 0 bright A_PlaySound("weapons/dark/fire", CHAN_7)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2,  50, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2,  90, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2, 135, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2, 180, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2, 225, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2, 270, flags)
      TNT1 A 0 A_SpawnItemEx("Lith_StarBarrier", 24,0,32, 2,0,2, 310, flags)
      TNT1 AAAAAAAAA 0 bright A_FireProjectile("Lith_StarShrapnel", frandom(0, 360), false, 0, 32, FPF_AIMATANGLE|FPF_NOAUTOAIM)
      TNT1 AAA 0 bright A_FireProjectile("Lith_StarShrapnelHoming", frandom(0, 360), false, 0, 32, FPF_AIMATANGLE|FPF_NOAUTOAIM)
      stop
   }
}

actor Lith_StarDestroyer : Lith_CWeapon
{
   Tag "$LITH_INFO_SHORT_StarDestroyer"
   Weapon.UpSound "weapons/shipgun/draw"
   Weapon.SlotNumber 7
   Weapon.SlotPriority 1
   Weapon.BobStyle "InverseSmooth"
   Weapon.BobRangeX 1.7
   Weapon.BobSpeed 0.7
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   SpawnPickup:
      TNT1 A 0 A_SpawnItemEx("Lith_BFG9000Pickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      SDSG AABBCCDDEEFFGGHH 1 Lith_A_Ready
      loop
   
   Deselect:
      SDSG A 0 A_StopSound(CHAN_6)
   DeselectLoop:
      SDSG AAA 0 A_Lower
      SDSG A 1
      loop
   
   Select:
      SDSG A 0 A_PlaySound("weapons/shipgun/idle", CHAN_6, 1, true)
   SelectLoop:
      SDSG AA 0 A_Raise
      SDSG A 1
      loop
   
   Fire:
      SDSG A 0 A_JumpIfInventory("Lith_CannonAmmo", 5, 1)
      goto FireEmpty
      SDSG A 1 A_PlaySound("weapons/shipgun/prepare", CHAN_WEAPON)
      SDSG ABCDEFFGGHHAABCDEFGH 1 bright
      SDSG ABBCCDDEFG 1 bright offset(0, 33)
      SDSG HADEFHBCGH 1 bright offset(0, 34)
      SDSG A 0 A_TakeInventory("Lith_CannonAmmo", 5, TIF_NOTAKEINFINITE)
      SDSG A 0 A_AlertMonsters
      SDSG A 1 bright
      SDSG A 1 bright offset(-1, 33)
      SDSG A 1 bright offset(-1, 34)
      SDSG A 1 bright offset(-1, 36)
      SDSG A 2 bright offset(1, 37)
      SDSF A 3 bright offset(1, 38)
      SDSF A 0 A_GunFlash("DummyFlash")
      
      SDSF A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_ShipGun_A), "FireSolspear")
      SDSF A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_ShipGun_B), "FireSurge")
      
   FireStar:
      SDSF A 0 A_PlaySound("weapons/shipgun/fire", CHAN_5)
      SDSF A 0 A_FireProjectile("Lith_StarProjectile")
      goto FireAnim
      
   FireSolspear:
      SDSF A 0 A_PlaySound("weapons/shipgun/firesol", CHAN_5)
      SDSF A 0 A_FireProjectile("Lith_Solspear")
      goto FireAnim

   FireSurge:
      SDSF A 0 A_PlaySound("weapons/shipgun/firesurge", CHAN_5)
      SDSF A 0 ACS_NamedExecuteWithResult("Lith_SurgeOfDestiny")

   FireAnim:
      SDSF B 2 bright offset(0, 62)
      SDSF C 1 bright offset(0, 57)
      SDSF D 1 bright offset(0, 47)
      SDSF E 1 bright offset(0, 44)
      SDSF F 1 bright offset(0, 40)
      SDSF G 1 bright offset(0, 37)
      SDSF H 1 bright offset(0, 35)
      SDSG AABBCCDDEEFFGGHH 1 offset(0, 32) A_ReFire
      goto Ready
   
   FireEmpty:
      SDSG A 2 A_PlaySound("weapons/shipgun/empty", CHAN_7)
      SDSG B 1 offset(0, 28)
      SDSG C 1 offset(0, 29)
      SDSG D 1 offset(0, 32)
      SDSG E 1 offset(0, 35)
      SDSG F 1 offset(0, 40)
      SDSG G 1 offset(0, 42)
      SDSG H 1 offset(0, 43)
      SDSG A 1 offset(0, 44)
      SDSG B 1 offset(0, 40)
      SDSG C 1 offset(0, 37)
      SDSG D 1 offset(0, 34)
      SDSG EFG 1 offset(0, 32)
      SDSG H 1 A_ReFire
      goto Ready
   }
}

// EOF

