actor Lith_RifleBullet : Lith_BulletPuff
{
   Mass 9999
   DamageType "Lith_Bullets"
   Species "Lith_Player"
   
   +MTHRUSPECIES
   +FORCERADIUSDMG
   +PUFFONACTORS
   +PUFFGETSOWNER
   
   states
   {
   Crash:
      TNT1 A 0
      TNT1 A 0 A_Explode(7, 32, 0)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true), "CrashMarathon")
      goto Super::Spawn
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_Explode(7, 32, 0)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true), "SpawnMarathon")
      goto Super::Spawn
   XDeath:
      TNT1 A 0
      TNT1 A 0 A_JumpIf(!CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true), 2)
      TNT1 A 0 A_PlaySound("marathon/hit", 6)
      TNT1 A 0 A_Explode(7, 32, 0)
      stop
   CrashMarathon: TNT1 A 0 A_PlaySound("marathon/hitwall",  6) goto MarathonPuff
   SpawnMarathon: TNT1 A 0 A_PlaySound("marathon/hitsolid", 6) goto MarathonPuff
   MarathonPuff:
      TNT1 A 0 A_SetScale(0.5)
      TNT1 A 0 A_SetTranslucent(1, 1)
      MPUF ABCDEFG 2 bright
      stop
   }
}

actor Lith_RifleBulletPainless : Lith_RifleBullet
{
   +PAINLESS
}

