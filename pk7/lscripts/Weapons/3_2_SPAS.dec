actor Lith_SPASShotsFired : Inventory
{
   Inventory.MaxAmount 5
}

actor Lith_SPAS : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_SPAS"
   Weapon.UpSound "weapons/cshotgun/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 0.95
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_SuperShotgunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CSHG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      loop
   
   Deselect:
      CSHG AAA 0 A_Lower
      CSHG A 1
      loop
   
   Select:
      CSHG AAA 0 A_Raise
      CSHG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShellAmmo", 1, 1)
      goto FireEmpty
      TNT1 A 0 A_JumpIfInventory("Lith_SPASShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_SPASShotsFired", 1)
      TNT1 A 0 A_TakeInventory("Lith_ShellAmmo", 1, TIF_NOTAKEINFINITE)
      
      // don't ask
      TNT1 A 0 A_JumpIf(z - floorz < 16, 3)
      TNT1 A 0 A_Recoil(20)
      TNT1 A 0 A_FireBullets(11, 5, 3, 3)
      
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(11, 5, 12, 5)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/cshotgun/fire", CHAN_WEAPON)
      TNT1 A 0 A_Recoil(0.7)
      CSHG B 2 bright A_Light(1)
      CSHG C 2 bright A_Light(2)
      CSHG D 2 A_Light(0)
      CSHG A 2
      goto Ready
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_SPASShotsFired", 1, 1)
      goto Ready
      CSHG A 1
      CSHG A 1 offset(-2, 34)
      CSHG A 1 offset(-4, 37)
      CSHG A 1 offset(-9, 43) A_PlaySound("weapons/cshotgun/load", CHAN_6)
      CSHG A 1 offset(-12, 47)
      CSHG A 1 offset(-14, 52)
      CSHG A 2 offset(-12, 55)
      CSHG A 3 offset(-11, 57)
      CSHG A 8 offset(-10, 60)
      CSHG A 8 offset(-11, 60)
      CSHG A 0 A_TakeInventory("Lith_SPASShotsFired", 999)
      CSHG A 2 offset(-13, 59) A_PlaySound("weapons/cshotgun/pump", CHAN_7)
      CSHG E 1 offset(-10, 57)
      CSHG F 1 offset(-8, 55)
      CSHG G 2 offset(-6, 53)
      CSHG H 6 offset(-4, 50)
      CSHG G 2 offset(-2, 48)
      CSHG F 3 offset(1, 42)
      CSHG E 3 offset(0, 38)
      goto Ready
   
   FireEmpty:
      CSHG A 1
      CSHG A 2 offset(0, 38)
      CSHG A 1 offset(0, 40)
      CSHG A 1 offset(0, 37) A_PlaySound("weapons/cshotgun/empty", CHAN_5)
      CSHG A 1 offset(0, 34)
      CSHG A 1 offset(0, 33)
      CSHG A 5 offset(0, 32)
      CSHG AAAA 1 A_ReFire
      goto Ready
   }
}

