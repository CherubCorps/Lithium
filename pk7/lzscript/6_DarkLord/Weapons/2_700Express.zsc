class Lith_700Express : Lith_DWeapon;

default
{
   Tag "$LITH_TXT_INFO_SHORT_700Express";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 1;
   Lith_Weapon.MagSize 4;
   +WEAPON.NOAUTOFIRE;
}

states(actor)
{
SpawnPickup:
   TNT1 A 0 Lith_A_SpawnPickup("Lith_PistolPickup");
   stop;
}

states(weapon)
{
Ready:
   700E A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   700E AAA 0 A_Lower;
   700E A 1;
   loop;
Select:
   700E AAA 0 A_Raise;
   700E A 1;
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 Lith_A_MagIncr;
   700E A 1 offset(2, 31);
   TNT1 A 0 A_GunFlash("DummyFlash");
   TNT1 A 0 A_Recoil(1.5);
   TNT1 A 0 A_Quake(2, 3, 0, 128);
   TNT1 A 0 A_FireBullets(2, 2, -1, 60*frandom(1,1.7), "Lith_700Bullet", FBF_NORANDOM);
   TNT1 A 0 {
      for(int i = 0; i < 2; i++)
         A_FireProjectile("Lith_GunSmokeSpawner", 0, 0, 7, 3+i);
   }
   TNT1 A 0 A_PlaySound("weapons/700e/fire", CHAN_WEAPON);
   700E C 4 bright offset(10, 28) Lith_A_RecoilUp(1.5);
   700E C 2 bright offset(9, 28);
   700E C 1 bright offset(7, 30) Lith_A_RecoilDn(0.25);
   700E C 1 bright offset(5, 31) Lith_A_RecoilDn(0.25);
   700E C 1 bright offset(4, 31) Lith_A_RecoilDn(0.25);
   700E C 1 bright offset(3, 32) Lith_A_RecoilDn(0.25);
   700E B 3 bright offset(2, 32) Lith_A_RecoilDn(0.25);
   700E B 3 bright offset(1, 32) Lith_A_RecoilDn(0.25);
   700E A 2;
   700E A 3 offset(0, 32);
   goto Ready;

Reload:
   MTBG E 2 Lith_A_JumpIfMagFull;
   MTBG F 2;
   MTBG G 2;
   MTBG H 2;
   MTBG I 4 A_PlaySound("weapons/mateba/open", 7);
   MTBG J 3;
   MTBG K 2;
   MTBG L 3 A_PlaySound("weapons/mateba/eject", 6);
   MTBG M 3 Lith_A_ReloadFinish;
   MTBG N 3;
   MTBG O 2 A_PlaySound("weapons/mateba/load", 5);
   MTBG P 3;
   MTBG Q 3 A_PlaySound("weapons/mateba/close", 7);
   MTBG R 3;
   MTBG S 4;
   goto Ready;
}

// EOF
