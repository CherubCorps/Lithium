actor Lith_PlasmaPistolShotTrail
{
   Scale 0.2
   RenderStyle "Add"
   Alpha 1
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      BLLT A 0
      BLLT A 0 A_SetScale(scalex * 0.9)
      BLLT A 1 bright A_FadeOut(0.1)
      loop
   }
}

actor Lith_PlasmaPistolShot
{
   Radius 6
   Height 16
   Speed 40
   Damage 5
   RenderStyle "Add"
   Alpha 1
   DeathSound "weapons/pistol/plasma/explode"
   Scale 0.2
   
   Projectile
   
   states
   {
   Spawn:
      BLLT A 1 bright A_SpawnItemEx("Lith_PlasmaPistolShotTrail")
      loop
   Death:
      PLSE ABCDE 3 bright
      stop
   }
}

actor Lith_PistolMagazine : Lith_DroppedMagazine
{
   Scale 0.7

   states
   {
   Spawn1:
      MGZP ABCD 2 A_SetUserVar(user_side, 0)
      MGZP EFGH 2 A_SetUserVar(user_side, 1)
      loop
   Death:
      MGZP C 0 A_JumpIf(user_side == 0, "Done")
      MGZP G 0 A_JumpIf(user_side == 1, "Done")
      stop
   }
}

actor Lith_PistolMagazine2 : Lith_PistolMagazine
{
   BounceSound "weapons/magbouncef"
   Mass 145

   states
   {
   Spawn1:
      MGZP IJKL 2 A_SetUserVar(user_side, 0)
      MGZP MNOP 2 A_SetUserVar(user_side, 1)
      loop
   Death:
      MGZP K 0 A_JumpIf(user_side == 0, "Done")
      MGZP O 0 A_JumpIf(user_side == 1, "Done")
      stop
   }
}

actor Lith_Pistol : Lith_MWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Pistol"
   Weapon.SlotNumber 2
   Weapon.SlotPriority 1
   Weapon.BobSpeed 3
   Weapon.BobRangeX 0.2
   Weapon.BobRangeY 0.3
   Weapon.UpSound "weapons/pistol/draw"
   Lith_Weapon.MagSize 14
   Lith_Weapon.ScopeOutSound "weapons/pistol/zoomout"
   Lith_Weapon.ScopeInSound "weapons/pistol/zoomin"
   Lith_Weapon.ScopeZoom 0.5
   
   +WEAPON.NOAUTOFIRE
   
   states(actor)
   {
   SpawnPickup:
      TNT1 A 0 A_SpawnItemEx("Lith_PistolPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   }
   
   states(weapon)
   {
   ReadySelector:
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "AltReady")
      goto Ready

   AltReady:
      SCOP B 1 Lith_A_Ready(WRF_ALLOWRELOAD | WRF_NOBOB)
      loop
   Ready:
      PISG A 1 Lith_A_Ready(WRF_ALLOWRELOAD)
      loop
   
   Deselect:
      PISG A 0 Lith_A_ScopeOut
   DeselectLoop:
      PISG AAA 0 A_Lower
      PISG A 1
      loop
   
   Select:
      PISG A 0 Lith_A_ScopeOut
   SelectLoop:
      PISG AAA 0 A_Raise
      PISG A 1
      loop
   
   Fire:
      TNT1 A 0 Lith_A_JumpIfMagEmpty
      TNT1 A 0 Lith_A_MagIncr
      TNT1 A 0 A_AlertMonsters
      
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_PlasPistol), "FirePlasma")
      
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_AutoPistol), 3)
      TNT1 A 0 A_FireBullets(0, 0, -1, 9)
      TNT1 A 0 A_Jump(256, 2)
      
      TNT1 A 0 A_FireBullets(5, 3, 1, 7)
      
      TNT1 A 0 Lith_A_GunSmokeSmall
      
      TNT1 A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_AutoPistol), 3)
      TNT1 A 0 A_PlaySound("weapons/pistol/fire", CHAN_WEAPON)
      TNT1 A 0 A_Jump(256, 2)
      
      TNT1 A 0 A_PlaySound("weapons/pistol/fireauto", CHAN_WEAPON)
      
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FireFinish_Scoped")
      goto FireFinish_NoScope
      
   FirePlasma:
      TNT1 A 0 A_PlaySound("weapons/pistol/plasma/fire", CHAN_WEAPON)
      TNT1 A 0 A_FireCustomMissile("Lith_PlasmaPistolShot")
      
      TNT1 A 0 A_JumpIfInventory("Lith_WeaponScopedToken", 1, "FireFinish_Scoped")
      goto FireFinish_NoScope
      
   FireFinish_Scoped:
      SCOP B 5 bright
      SCOP B 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_AutoPistol), "FireFinish_Auto")
      SCOP BBBBB 1 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOBOB)
      goto ReadySelector
   FireFinish_NoScope:
      PISG A 1
      PISF AB 1 bright
      PISF B 1 bright offset(0, 34)
      PISG C 1 offset(0, 32)
      PISG A 0 A_JumpIf(CallACS("LPData", pdata_upgrade, UPGR_AutoPistol), "FireFinish_Auto_NoScope")
      PISG B 2 A_WeaponReady(WRF_ALLOWRELOAD)
      PISG AAAAA 1 A_WeaponReady(WRF_ALLOWRELOAD)
      goto ReadySelector
   FireFinish_Auto_NoScope:
      PISG BB 1 A_ReFire
      PISG A 0
   FireFinish_Auto:
      "####" "#####" 1 A_ReFire
      goto ReadySelector
   
   AltFire:
      "####" B 1
      "####" B 0 Lith_A_ScopeToggle
      goto ReadySelector
   
   Reload:
      TNT1 A 0 Lith_A_JumpIfMagFull
      TNT1 A 0 Lith_A_ScopeOut
      PISG A 0 A_PlaySound("weapons/pistol/reload", 7)
      PISG A 1 offset(0, 34)
      PISG A 1 offset(0, 36)
      PISG B 1 offset(0, 50)
      PISG B 1 offset(-1, 51)
      PISG B 1
      PISG B 4 offset(-2, 53) Lith_A_DropMag("Lith_PistolMagazine", "Lith_PistolMagazine2", 5,0,20, frandom(1,3),0,0)
      PISG C 5
      PISG B 1 offset(-1, 50)
      PISG B 1 offset(0, 45)
      PISG B 1 offset(0, 36)
      PISG B 2 A_WeaponReady(LITH_BOBONLY)
      PISG A 0 Lith_A_ReloadFinish
      PISG A 3 A_WeaponReady
      goto ReadySelector
   }
}

