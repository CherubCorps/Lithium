class Lith_SuperShotgun : Lith_MWeapon
{
   default
   {
      Tag "$LITH_TXT_INFO_SHORT_SuperShotgun";
      Weapon.UpSound "weapons/ssg/draw";
      Weapon.SlotNumber 3;
      Weapon.SlotPriority 0.95;
   }

   int m_iter;
   int m_shots;
   
   states(actor)
   {
   SpawnPickup:
      TNT1 A 0 A_SpawnItemEx("Lith_SuperShotgunPickup", 0,0,0, 0,0,0, 0, Lith_PF);
      stop;
   }
   
   states(weapon)
   {
   Ready:
      SHT2 A 0 {invoker.m_iter = invoker.m_shots = 0;}
      SHT2 A 1 Lith_A_Ready;
      loop;
   Deselect:
      SHTG A 0 A_Lower;
      SHT2 A 1 A_Lower;
      loop;
   Select:
      SHTG A 0 A_Raise;
      SHT2 A 1 A_Raise;
      loop;
   FireEmpty:
      SHT2 A 1;
      SHT2 A 2 offset(0, 38);
      SHT2 A 1 offset(0, 40);
      SHT2 A 1 offset(0, 37) A_PlaySound("weapons/ssg/empty", CHAN_5);
      SHT2 A 1 offset(0, 34);
      SHT2 A 1 offset(0, 33);
      SHT2 A 5 offset(0, 32);
      SHT2 AAAA 1 A_ReFire;
      goto Ready;
   Fire:
      SHT2 A 0 A_JumpIfInventory("Lith_ShellAmmo", 4, 1);
      goto AltFire;
      SHT2 A 2;
      SHT2 A 0 {invoker.m_iter = invoker.m_shots = 2;}
      goto Attack;
   AltFire:
      SHT2 A 0 A_JumpIfInventory("Lith_ShellAmmo", 2, 1);
      goto FireEmpty;
      SHT2 A 2;
      SHT2 A 0 {invoker.m_iter = invoker.m_shots = 1;}
   Attack:
      SHT2 A 0 A_TakeInventory("Lith_ShellAmmo", 2, TIF_NOTAKEINFINITE);
      SHT2 A 0 A_AlertMonsters;
      SHT2 A 0 A_FireBullets(9, 6, 16, 6, "BulletPuff");
      SHT2 A 0 Lith_A_GunSmoke;
      SHT2 A 0 A_PlaySound("weapons/ssg/fire", 7);
      SHT2 A 0 A_Recoil(1.2);
      SHT2 A 0 Lith_A_RecoilUp(1.5);
      SHT2 A 1;
      SHT2 A 0 Lith_A_RecoilDn(0.5);
      SHT2 I 1 bright offset(0, 40);
      SHT2 A 0 Lith_A_RecoilDn(0.5);
      SHT2 J 1 bright offset(0, 37);
      SHT2 A 0 Lith_A_RecoilDn(0.5);
      SHT2 A 0 A_JumpIf(--invoker.m_shots != 0, "Attack");
      SHT2 A 1 offset(0, 35);
      SHT2 A 1 offset(0, 33);
      SHT2 A 1 offset(0, 32);
      SHT2 A 0 A_JumpIfInventory("Lith_ShellAmmo", 2, 1);
      goto Ready;
      SHT2 B 1 offset(1, 32);
      SHT2 B 1 offset(-5, 32);
      SHT2 B 2 offset(-10, 32);
      SHT2 C 2 offset(1, 32);
      SHT2 C 2 offset(-3, 32);
      SHT2 C 3 offset(-5, 32);
      SHT2 D 0 A_PlaySound("weapons/ssg/open", CHAN_5);
      SHT2 D 2 offset(-10, 32);
      SHT2 D 2 offset(-5, 32);
      SHT2 D 3 offset(1, 32);
      SHT2 Z 0 {
         for(int i = 0; i < invoker.m_iter; i++)
         {
            Lith_A_Casing("Lith_CasShellHot", 20, -1, 30, frandom(3,4), frandom(-7,-8), frandom(2,9));
            Lith_A_Casing("Lith_CasShellHot", 20, -4, 30, frandom(3,4), frandom(-7,-8), frandom(2,9));
         }
      }
   LoadAnim:
      SHT2 Z 6;
      SHT2 E 4;
      SHT2 F 5 A_PlaySound("weapons/ssg/load", CHAN_6);
      SHT2 A 0 A_JumpIf(--invoker.m_iter != 0, "LoadAnim");
      SHT2 G 6;
      SHT2 H 6 A_PlaySound("weapons/ssg/close", CHAN_7);
      SHT2 AAAAA 1 A_ReFire;
      goto Ready;
   }
}

// EOF

