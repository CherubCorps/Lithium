actor Lith_CPlasmaRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_CPlasmaRifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   Weapon.SlotPriority 1
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_PlasmaRiflePickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      CPLG A 1 A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      CPLG AAA 0 A_Lower
      CPLG A 1
      loop
   
   Select:
      CPLG AAA 0 A_Raise
      CPLG A 1
      loop
   
   Fire:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      CPLG A 0 A_TakeInventory("Lith_PlasmaAmmo", 30, TIF_NOTAKEINFINITE)
      CPLG A 0 A_GunFlash("DummyFlash")
      CPLG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      CPLG AAAAA 0 A_FireCustomMissile("Lith_PlasmaBolt",
                                       CallACS("Lith_CircleSpread", 5 * IFIX, 3 * IFIX, false) / FIX,
                                       0, 0, -9, 0,
                                       CallACS("Lith_CircleSpread", 0, 0, true) / FIX)
      CPLG B 2 offset(0, 36) bright
      CPLG C 1 offset(0, 35) bright
      CPLG D 1 offset(0, 32)
      CPLG AAAA 1 A_ReFire
      goto Ready
   
   FireEmpty:
      CPLG A 0 A_StopSound(CHAN_7)
      CPLG A 0 A_StopSound(CHAN_6)
      CPLG A 1 A_PlaySound("weapons/plasma/empty", 7)
      CPLG A 1 offset(0, 37)
      CPLG A 1 offset(0, 36)
      CPLG A 1 offset(0, 35)
      CPLG A 1 offset(0, 34)
      CPLG A 1 offset(0, 32)
      CPLG A 1 A_ReFire
      goto Ready
   }
}
