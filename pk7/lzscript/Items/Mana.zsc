class Lith_ManaPickupTrail : Actor
{
   default
   {
      Scale 0.4;
      RenderStyle "Add";
      
      +NOTIMEFREEZE
      +NOINTERACTION
      +BRIGHT
   }
      
   states
   {
   Spawn:
      BLLT Q 0 A_SetScale(scale.x * 0.9);
      BLLT Q 1 A_FadeOut(0.05);
      loop;
   }
}

class Lith_ManaPickup : Lith_BasicPickup
{
   default
   {
      RenderStyle "Add";
      Radius 4;
      Height 4;
      Inventory.PickupSound "player/pickup/mana";
      Inventory.PickupMessage "";
      
      +INVENTORY.ALWAYSPICKUP
      +INVENTORY.TRANSFER
      +INVENTORY.NOSCREENFLASH
      +NOGRAVITY
      +NOCLIP
      +SEEKERMISSILE
      +BRIGHT
   }

   override void PostBeginPlay()
   {
      A_SetTranslation("Lith_Mana" .. random(1, 8));
      A_SetScale(0.8 * frandom(0.5, 1));
      A_GiveInventory("Lith_MagicAmmo", random(7, 10));
      ThrustThingZ(0, 16, 0, 1);
   }
   
   int user_deathwait;
   
   states(actor)
   {
   Spawn:
      BLLT Q 0;
      BLLT Q 0 A_JumpIf(user_deathwait++ >= 35 * 15, "Death");
      BLLT Q 0 A_ChangeVelocity(14, 14, vel.z*0.9, CVF_REPLACE);
      BLLT Q 0 A_SpawnItemEx("Lith_ManaPickupTrail", flags: SXF_TRANSFERTRANSLATION);
      BLLT Q 0 {
         if(CheckIfTargetInLOS())
            A_SeekerMissile(360, 40, SMF_PRECISE|SMF_CURSPEED);
      }
      BLLT Q 1;
      loop;
   Death:
      BLLT Q 1 A_FadeOut;
      wait;
   }
}

// EOF
