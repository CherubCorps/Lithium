actor Lith_ShockRifleShotsFired : Inventory
{
   Inventory.MaxAmount 7
}

actor Lith_ShockFX
{
   RenderStyle "AddStencil"
   StencilColor "LightBlue"
   Alpha 0.5

   +NOINTERACTION
   +BRIGHT

   states
   {
   Spawn:
      "####" "#" 1
      "####" "#" 1 A_FadeOut
      wait
   }
}

actor Lith_ShockEnemyGiver : CustomInventory
{
   +INVENTORY.ALWAYSPICKUP

   states
   {
   Pickup:
      TNT1 A 0 A_DamageSelf(5, "Lith_Energy")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockFX", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME)
      stop
   }
}

actor Lith_ShockEffect
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.6

   +NOINTERACTION
   +RANDOMIZE

   states
   {
   Spawn:
      ZAP1 FEDCBA 1 bright
      stop
   }
}

actor Lith_ShockPuff : Lith_BulletPuff
{
   +HITTRACER
   +FORCEPAIN
   +PUFFONACTORS

   states
   {
   Crash:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/crifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      goto Super::Spawn
   Melee:
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_GiveInventory("Lith_ShockEnemyGiver", 1, AAPTR_TRACER)
      TNT1 A 0 A_PlaySound("weapons/crifle/zap")
      TNT1 A 0 A_SpawnItemEx("Lith_ShockEffect")
      stop
   }
}

actor Lith_ShockRifle : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_ShockRifle"
   Weapon.UpSound "weapons/crifle/draw"
   Weapon.SlotNumber 3
   Weapon.SlotPriority 1
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_ShotgunPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD|CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      SHTG AAA 0 A_Lower
      SHTG A 1
      loop
   
   Select:
      SHTG AAA 0 A_Raise
      SHTG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_ShockRifleShotsFired", 1)
      SHTG A 1
      TNT1 A 0 A_GunFlash("DummyFlash")
      TNT1 A 0 A_PlaySound("weapons/crifle/fire", CHAN_WEAPON)
      TNT1 A 0 A_FireBullets(1, 0, -1, 37*frandom(1,1.5), "Lith_ShockPuff", FBF_NORANDOM)
      SHTG B 2 bright A_Light(1)
      SHTG C 2 bright A_Light(0)
      SHTG D 2
      goto Ready

   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_ShockRifleShotsFired", 1, 1)
      goto Ready
      SHTG N 2
      SHTG F 2 A_PlaySound("weapons/crifle/reloadout", CHAN_6)
      SHTG G 2
      SHTG H 2
      SHTG I 2 offset(-11, 57)
      SHTG I 5 offset(-10, 60)
      SHTG I 5 offset(-11, 60)
      SHTG I 0 A_TakeInventory("Lith_ShockRifleShotsFired", 999)
      SHTG I 2 offset(-13, 59) A_PlaySound("weapons/crifle/reloadin", CHAN_7)
      SHTG I 2 offset(-11, 49)
      SHTG I 2 offset(-11, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 5 offset(-11, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG I 1 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE)
      SHTG G 1 offset(-13, 32)
      SHTG G 1 offset(1, 32)
      SHTG F 1 offset(-9, 32)
      SHTG F 1 offset(1, 32)
      SHTG NNNNNNN 1 A_ReFire
      goto Ready
   }
}

// EOF
