actor Lith_PKTrail
{
   Speed 14
   Radius 6
   Height 6
   Damage 2
   RenderStyle "Add"
   Alpha 1
   DamageType "Lith_Magic"
   
   Projectile
   
   states
   {
   Spawn:
      BAL7 AAAABBBB 1 bright
      loop
   Death:
      BAL7 CDE 6 bright
      stop
   }
}

actor Lith_PolarKnight
{
   Scale 0.45
   Radius 5
   Height 5
   Speed 14
   Damage 7
   RenderStyle "Add"
   Alpha 0.8
   SeeSound "enemies/hellknight/throw"
   Decal "BaronScorch"
   DamageType "Lith_Magic"
   
   Projectile
   
   states
   {
   Spawn:
      BLLT F 1 bright A_RadiusGive("Lith_AdrenalineToken", 64, RGF_PLAYERS)
      loop
   Death:
      BLLT F 1 bright A_FadeOut(0.08)
      loop
   }
}

actor Lith_HellKnight : HellKnight replaces HellKnight
{
   Speed 4
   Species "Lith_HellKnight"
   Tag "$LITH_TXT_INFO_SHORT_HellKnight"
   
   states
   {
   Spawn:
      BOS2 AB 10 A_Look
      loop
   See:
      BOS2 AAAAAABBBBBBCCCCCCDDDDDD 1 A_Chase
      loop
   Melee:
   Missile:
      BOS2 EF 6 A_FaceTarget
      BOS2 G 1 A_CustomComboAttack("Lith_PolarKnight", 32, 10 * random(1, 8), "baron/melee")
      BOS2 GGG 1 A_CustomMissile("Lith_PKTrail")
      goto See
   Pain:
      BOS2 H 2
      BOS2 H 2 A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_HellKnight)
      goto Super::Death
   Raise:
      BOS2 O 8
      BOS2 NMLKJI 8
      goto See
   }
}

actor Lith_BaronBallSound : BaronBall replaces BaronBall
{
   DamageType "Lith_Magic"
   
   states
   {
   Spawn:
      BAL7 AAAABBBB 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   }
}

actor Lith_BaronBall : Lith_BaronBallSound
{
   SeeSound ""
   DeathSound ""
   Decal ""
}

actor Lith_BaronOfHell : BaronOfHell replaces BaronOfHell
{
   PainChance 55
   DamageFactor "Lith_Bullets", 0.6
   Tag "$LITH_TXT_INFO_SHORT_BaronOfHell"
   
   +MISSILEMORE
   
   var int user_angle;
   
   states
   {
   Spawn:
      BOSS AB 10 A_Look
      loop
   See:
      BOSS AABBCCDD 3 A_Chase
      loop
   Melee:
      BOSS EF 6 A_FaceTarget
      BOSS G 4 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee")
      goto See
   Missile:
      BOSS EF 4 A_FaceTarget
      BOSS G 0 A_FaceTarget
      BOSS G 1 A_CustomMissile("Lith_BaronBallSound")
      BOSS G 0 A_Jump(32, "MissileDone")
      BOSS G 0 A_FaceTarget
      BOSS G 0 A_SetUserVar("user_angle", frandompick(-45, 45, 0))
      BOSS GGG 1 A_CustomMissile("Lith_BaronBall", 32, 0, user_angle, CMF_AIMDIRECTION)
      goto See
   MissileDone:
      BOSS G 3
      goto See
   Pain:
      BOSS H 1
      BOSS H 1 A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_BaronOfHell)
      goto Super::Death
   Raise:
      BOSS O 8
      BOSS NMLKJI 8
      goto See
   }
}

actor Lith_RevenantTracer : RevenantTracer replaces RevenantTracer
{
   DamageType "Lith_Shrapnel"
   
   states
   {
   Spawn:
      FATB A 1 bright A_Tracer
      FATB A 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      FATB B 1 bright A_Tracer
      FATB B 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   }
}

actor Lith_Revenant : Revenant replaces Revenant
{
   Health 350
   Tag "$LITH_TXT_INFO_SHORT_Revenant"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Revenant)
      goto Super::Death
   }
}

actor Lith_CacodemonBall : CacodemonBall replaces CacodemonBall
{
   DamageType "Lith_Energy"
   
   states
   {
   Spawn:
      BAL2 AAAABBBB 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   }
}

actor Lith_Cacodemon : Cacodemon replaces Cacodemon
{
   DamageFactor "Lith_Bullets", 0.6
   Tag "$LITH_TXT_INFO_SHORT_Cacodemon"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Cacodemon)
      goto Super::Death
   }
}

actor Lith_ArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
   DamageType "Lith_Energy"
   
   states
   {
   Spawn:
      APLS AAAAABBBBB 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   }
}

actor Lith_Arachnotron : Arachnotron replaces Arachnotron
{
   DamageFactor "Lith_Energy", 0.6
   Tag "$LITH_TXT_INFO_SHORT_Arachnotron"
   
   states
   {
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Arachnotron)
      goto Super::Death
   }
}

actor Lith_FatShot : FatShot replaces FatShot
{
   DamageType "Lith_Fire"
   Damage 7
   Speed 10
   
   states
   {
   Spawn:
      MANF AAAABBBB 1 bright A_RadiusGive("Lith_AdrenalineToken", 48, RGF_PLAYERS)
      loop
   }
}

actor Lith_Mancubus : Fatso replaces Fatso
{
   PainChance 50
   Tag "$LITH_TXT_INFO_SHORT_Mancubus"
   
   states
   {
   Missile:
      FATT G 10 A_FatRaise
      FATT H 0 A_PlaySound("enemies/mancubus/fire", CHAN_5)
      FATT HH 5 bright A_FatAttack1
      FATT IG 6 A_FaceTarget
      FATT H 0 A_PlaySound("enemies/mancubus/fire", CHAN_5)
      FATT HH 5 bright A_FatAttack2
      FATT IG 6 A_FaceTarget
      FATT H 0 A_PlaySound("enemies/mancubus/fire", CHAN_5)
      FATT HH 5 bright A_FatAttack3
      FATT IG 6 A_FaceTarget
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_Mancubus)
      goto Super::Death
   }
}

// EOF

