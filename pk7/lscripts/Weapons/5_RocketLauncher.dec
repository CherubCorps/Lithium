#include "lscripts/Weapons/Rocket.dec"

actor Lith_RocketLauncherHand : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_RocketLauncher : Lith_Weapon replaces RocketLauncher
{
   Tag "Rocket Launcher"
   Inventory.PickupMessage "You got the Ruckebt Laurence!"
   Weapon.SlotNumber 5
   Weapon.BobStyle Smooth
   Weapon.BobSpeed 1.3
   Weapon.BobRangeY 1.1
   Weapon.BobRangeX 0.9
   
   states
   {
   Ready:
      MISD A 1 A_WeaponReady(WRF_ALLOWUSER1 | WRF_ALLOWUSER2)
      wait
   Deselect:
      MISD A 1 { A_Lower; A_Lower; }
      loop
   Select:
      MISD A 1 { A_Raise; A_Raise; }
      loop
   Fire:
      MISD A 0 {
         if(CountInv("Lith_RocketLauncherHand"))
         {  A_TakeInventory("Lith_RocketLauncherHand", 1);
            return state("User2"); }
         else
         {  A_GiveInventory("Lith_RocketLauncherHand", 1);
            return state("User1"); }
      }
   User1:
      MISD A 1 A_WeaponReady(LITH_BOBONLY)
      MISD F 2 bright {
         A_WeaponReady(LITH_BOBONLY);
         A_FireCustomMissile("Lith_HomingRocket", 0, true, -5, -2); }
      MISD GHI 3 bright A_WeaponReady(LITH_BOBONLY)
      goto FireDone
   User2:
      MISD A 1 A_WeaponReady(LITH_BOBONLY)
      MISD B 2 bright {
         A_WeaponReady(LITH_BOBONLY);
         A_FireCustomMissile("Lith_Rocket", 0, true, 5, -2); }
      MISD CDE 3 bright A_WeaponReady(LITH_BOBONLY)
      goto FireDone
   FireDone:
      MISD A 0 A_ReFire
      goto Ready
   Spawn:
      LAUN A 0
      LAUN A 0 A_SetUserVar("user_pickupparm", weapon_rocketlauncher)
      goto SpawnLoop
   }
}

