class Lith_Star : Actor
{
   default
   {
      DamageFunction 5 * random(2, 3);
      Speed 32;
      Radius 2;
      Height 2;
      Scale 0.7;
      Mass 0;
      RenderStyle "Add";
      DamageType "Magic";
      SeeSound "weapons/sshot/fire";

      Projectile;
      +NODAMAGETHRUST
      +SEEKERMISSILE
      +SCREENSEEKER
      +BRIGHT
   }

   states
   {
   Spawn:
      TNT1 A 0;
      STSH A 0 A_JumpIf(random(0, 100) < 75, "Spawn1");
      STS2 A 0;
   Spawn1:
      #### ABC 1 A_SetTics(random(0, 1));
   SpawnLoop:
      #### ABC 1 A_SeekerMissile(0, 1, SMF_LOOK);
      loop;
   Death:
      TNT1 A 0 A_PlaySound("weapons/sshot/hit", CHAN_BODY, 0.3);
      stop;
   }
}

class Lith_StarShot : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_StarShot";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/starshot/draw";
   Lith_Weapon.MagSize 150;

   -WEAPON.NOAUTOFIRE
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 A_Lower;
   loop;

Select:
   TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 6);
SelectLoop:
   TNT1 A 0 A_Raise;
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 0 Lith_A_MagIncr(randompick(2, 4));
   TNT1 A 0 A_AlertMonsters;
   TNT1 A 0 A_PlaySound("weapons/sshot/firero", CHAN_7, CallACS("Lith_AmmoRunOut", true, 0.5*FIX)/FIX);
   TNT1 A 0 A_FireProjectile("Lith_Star", frandom(-5,5), false, -20, 10, FPF_NOAUTOAIM, -frandom(-2,3));
   TNT1 A 0 A_FireProjectile("Lith_Star", frandom(-5,5), false,  20, 10, FPF_NOAUTOAIM, -frandom(-2,3));
   TNT1 A 0 A_FireProjectile("Lith_Star", frandom(-2,2), false, -16, 16, FPF_NOAUTOAIM, -frandom(-2,3));
   TNT1 A 0 A_FireProjectile("Lith_Star", frandom(-2,2), false,  16, 16, FPF_NOAUTOAIM, -frandom(-2,3));
   TNT1 A 0 A_Recoil(0.01);
   TNT1 A 0 A_SetAngle(angle + frandom(-0.2, 0.2));
   TNT1 A 0 A_Quake(1, 4, 0, 128);
   TNT1 A 2 bright;
   goto Ready;

Reload:
   TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 100, 1);
   goto Ready;
   TNT1 A 0 Lith_A_JumpIfMagFull;
   TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 100);
   TNT1 A 0 A_PlaySound("weapons/delear/open", CHAN_6);
   TNT1 A 22;
   TNT1 A 10 A_PlaySound("weapons/delear/charge", CHAN_5);
   TNT1 A 0 Lith_A_ReloadFinish;
   TNT1 A 4;
   TNT1 A 4 A_PlaySound("weapons/delear/close", CHAN_6);
   goto Ready;
}

// EOF
