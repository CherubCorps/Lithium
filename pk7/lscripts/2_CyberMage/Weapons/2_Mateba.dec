actor Lith_MatebaShotsFired : Inventory
{
   Inventory.MaxAmount 12
}

actor Lith_FinalizerKill2
{
   RenderStyle "AddStencil"
   StencilColor "Red"
   Alpha 0.9

   +NOINTERACTION

   states
   {
   Spawn:
      "####" "#" 5
      "####" "#" 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_FinalizerKill
{
   RenderStyle "Add"

   +NOINTERACTION

   states
   {
   Spawn:
      "####" "#" 10
      "####" "#" 0 A_SpawnItemEx("Lith_FinalizerKill2", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE)
      "####" "#" 1 A_FadeOut(0.05)
      wait
   }
}

actor Lith_Finalizer : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_CheckFlag("BOSS", "Nope")
      TNT1 A 0 A_JumpIf(((health*1.0) / CallACS("Lith_GetFinalizerMaxHealth")) > 0.5, "Nope")
      TNT1 A 0 A_SpawnItemEx("Lith_FinalizerKill", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MonsterFinalized")
      TNT1 A 0 A_BossDeath
      TNT1 A 0 Thing_Remove(0)
      stop
   Nope:
      TNT1 A 0 A_DamageSelf(50)
      stop
   }
}

actor Lith_FinalizerEffect
{
   RenderStyle "Add"
   Scale 0.2
   Radius 0
   Height 0
   
   +FORCEXYBILLBOARD
   +NOINTERACTION
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      BLLT O 10 bright
      BLLT O 1 bright A_FadeOut(0.2)
      wait
   }
}

actor Lith_FinalizerShotEffect
{
   RenderStyle "Add"
   Scale 0.8
   
   +NOINTERACTION
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      BLLT E 1 bright A_FadeOut
      wait
   }
}

actor Lith_FinalizerShot : FastProjectile
{
   RenderStyle "Add"
   Speed 90
   Height 6
   Radius 6
   MissileType "Lith_FinalizerShotEffect"
   
   Projectile
   +HITTRACER
   +NOTIMEFREEZE
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   Death:
      TNT1 A 0 A_GiveInventory("Lith_Finalizer", 1, AAPTR_TRACER)
      stop
   }
}

actor Lith_Mateba : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Mateba"
   Weapon.SlotNumber 2
   Weapon.SlotPriority 1
   Weapon.BobSpeed 2.5
   Weapon.BobRangeX 0.2
   Weapon.BobRangeY 0.3
   Weapon.UpSound "weapons/mateba/draw"
   
   +WEAPON.NOAUTOFIRE
   
   states
   {
   SpawnPickup:
      TNT1 A 0 A_SpawnItemEx("Lith_PistolPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      MTBG A 1 Lith_A_Ready(WRF_ALLOWRELOAD)
      loop
   
   Deselect:
      MTBG AAA 0 A_Lower
      MTBG A 1
      loop
   Select:
      MTBG AAA 0 A_Raise
      MTBG A 1
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_MatebaShotsFired", 0, "Reload")
      TNT1 A 0 A_GiveInventory("Lith_MatebaShotsFired", 1)
      TNT1 A 0 A_GunFlash("DummyFlash")
      
      TNT1 A 0 A_JumpIf(!CallACS("LPData", pdata_upgrade, UPGR_Mateba_A), 2)
      TNT1 A 0 A_JumpIfInventory("Lith_MatebaShotsFired", 0, "Finalize")
      
      TNT1 A 0 Lith_A_RecoilDn(0.2)
      TNT1 A 0 A_FireBullets(2, 2, -1, 30, "BulletPuff", FBF_NORANDOM)
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawnerSmall", 0, 0, 0, 6)
      TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON)
      MTBG B 1 bright offset(0, 50)
      TNT1 A 0 Lith_A_RecoilUp(0.1)
      MTBG C 1 bright offset(0, 46)
      TNT1 A 0 Lith_A_RecoilUp(0.1)
      MTBG A 1 offset(0, 40)
      MTBG A 1 offset(0, 37)
      MTBG A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 A_WeaponReady(WRF_NOBOB)
      goto Ready
   
   Finalize:
      TNT1 A 0 A_FireCustomMissile("Lith_GunSmokeSpawnerSmall", 0, 0, 0, 6)
      TNT1 A 0 A_SpawnItemEx("Lith_FinalizerEffect", cos(pitch)*24, 0, ((height-19)+2) - sin(pitch)*24)
      TNT1 A 0 A_FireCustomMissile("Lith_FinalizerShot", 0, 0, 0, 10)
      TNT1 A 0 A_PlaySound("weapons/mateba/fire", CHAN_WEAPON)
      TNT1 A 0 A_PlaySound("weapons/mateba/finalize", CHAN_6)
      TNT1 A 0 A_PlaySound("weapons/shotgun/just", CHAN_5)
      MTBG B 1 bright offset(0, 55)
      MTBG C 1 bright offset(0, 51)
      MTBG A 1 offset(0, 45)
      MTBG A 1 offset(0, 43)
      MTBG A 1 offset(0, 40)
      MTBG A 1 offset(0, 37)
      MTBG A 1 offset(0, 34) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 offset(0, 33) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 offset(0, 32) A_WeaponReady(WRF_NOBOB)
      MTBG A 1 A_WeaponReady(WRF_NOBOB)
      goto Ready
   
   Reload:
      TNT1 A 0 A_JumpIfInventory("Lith_MatebaShotsFired", 1, 1)
      goto Ready
      MTBG E 2
      MTBG F 2
      MTBG G 2
      MTBG H 2
      MTBG I 4 A_PlaySound("weapons/mateba/open", 7)
      MTBG J 3
      MTBG K 2
      MTBG L 3 A_PlaySound("weapons/mateba/eject", 6)
      MTBG M 3 A_TakeInventory("Lith_MatebaShotsFired", 999)
      MTBG N 3
      MTBG O 2 A_PlaySound("weapons/mateba/load", 5)
      MTBG P 3
      MTBG Q 3 A_PlaySound("weapons/mateba/close", 7)
      MTBG R 3
      MTBG S 4
      goto Ready
   }
}

