/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ShrapnelProjectileFX : Actor {
   default {
      RenderStyle "Add";
      Scale 1.5;

      +BRIGHT
      +NOINTERACTION
   }

   states {
   Spawn:
      BLLT U 1 A_Warp(AAPTR_MASTER, flags: WARPF_INTERPOLATE);
      loop;
   }
}

class Lith_ShrapnelProjectile : Actor {
   default {
      BounceType "Doom";
      Damage 8;
      DamageType "Shrapnel";
      Gravity 0.5;
      Height 8;
      Radius 8;
      Species "Lith_Player";
      Speed 25;
      Scale 0.26;

      Projectile;
      +EXPLODEONWATER
      +MTHRUSPECIES
      -NOGRAVITY
   }

   states {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_SpawnItemEx("Lith_ShrapnelProjectileFX", flags: SXF_SETMASTER);
      SBRK ABCDEFGHIJKLMN 0 A_Jump(16, "Spawn2");
      BLLT I 0 A_SetScale(1.0);
   Spawn2:
      #### # 1 A_SpawnItemEx("Lith_CasingSmoke", 0,0,3, frandom(-0.2,0),frandom(-0.2,0));
      wait;
   }
}

class Lith_ShrapnelGun : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_ShrapnelGun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Lith_Weapon.AmmoType "Lith_ShellAmmo";
}

action(Weapon) void Lith_A_FireShrapnel() {
   A_FireProjectile("Lith_ShrapnelProjectile", frandom(-5, 5), false, frandom(-5, 5), frandom(-7, 7), FPF_NOAUTOAIM);
}

states(Weapon) {
Ready:
   SCRP A 1 Lith_A_Ready;
   loop;

Deselect:
   SCRP A 1 Lith_A_Lower(_raise_fast);
   loop;

Select:
   SCRP A 1 Lith_A_Raise(_raise_fast);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo(4);
   SCRP A 0 {
      lith_takeAmmo(4);
      A_StartSound("weapons/shrapnel/fire", lch_weapon);
      A_AlertMonsters();
      A_GunFlash("DummyFlash");
      Lith_A_RecoilUp(8);
      lith_a_setInterpolation(true);
   }
   SCRP B 1 {for(int i = 0; i < 6; i++) Lith_A_FireShrapnel();}
   SCRP B 1;
   SCRP B 1;
   SCRP B 1 {for(int i = 0; i < 4; i++) Lith_A_FireShrapnel();}
   SCRP C 1;
   SCRP C 1;
   SCRP C 1 {for(int i = 0; i < 2; i++) Lith_A_FireShrapnel();}
   SCRP C 1;
   SCRP C 1;
   SCRP C 1;
   SCRP A 1;
   SCRP A 1;
   SCRP A 1;
   SCRP A 10 lith_a_setInterpolation(false);
   goto ReadySelector;
FireEmpty:
   SCRP A 2 offset(0, 42);
   SCRP A 2 offset(0, 38);
   SCRP A 2 offset(0, 35) A_StartSound("weapons/spas/empty", lch_dryweapon);
   SCRP A 1 offset(0, 34);
   SCRP A 1 offset(0, 33);
   SCRP A 1 offset(0, 32);
   goto ReadySelector;
}

/* EOF */
