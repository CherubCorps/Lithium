// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_FadeOutItemSprite : Lith_ItemSprite {
   states {
   Done:
      #### # 1 A_FadeOut;
      wait;
   }
}

class Lith_ItemSprite : Actor;

int m_fAng;
int m_fzAng;
default {
   +NoInteraction
   +CastSpriteShadow
}
override void postBeginPlay() {
   super.postBeginPlay();
   m_fAng  = random(0, 359);
   m_fzAng = random(0, 359);
}
override void tick() {
   if(isFrozen()) {
      return;
   }
   Lith_UTIL.stateTick(self);
   if(master && (master.bSpecial || master.special)) {
      let fAng  = (msTimeF() + m_fAng)  * 0.06666;
      let fzAng = (msTimeF() + m_fzAng) * 0.1;
      setOrigin(master.pos + (cos(fAng) * 4.0, sin(fAng) * 4.0, 16.0 + sin(fzAng) * 12.0), true);
   } else if(!inStateSequence(curState, resolveState("Done"))) {
      setState(resolveState("Done"));
   }
}
states {
Spawn:
   #### # -1 bright;
   stop;
Done:
   TNT1 A 0;
   stop;
}

/* EOF */
