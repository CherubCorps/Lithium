#include "lscripts/Weapons/PlasmaBolt.dec"
#include "lscripts/Weapons/PlasmaLaser.dec"

actor Lith_PlasmaRifle : Lith_Weapon replaces PlasmaRifle
{
   Tag "$LITH_TXT_INFO_SHORT_PlasmaRifle"
   Weapon.UpSound "weapons/plasma/draw"
   Weapon.SlotNumber 6
   
   states
   {
   Spawn:
      PLAS A 0
      PLAS A 0 A_SetUserVar("user_pickupparm", weapon_plasma)
      goto SpawnLoop
   
   Ready:
      PLSG A 1 A_WeaponReady
      wait
   
   Deselect:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
   DeselectLoop:
      PLSG AAA 0 A_Lower
      PLSG A 1
      loop
   
   Select:
      PLSG AAA 0 A_Raise
      PLSG A 1
      loop
   
   Fire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PlasLaser), "FireLaser")
   FireReal:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSG A 0 A_GunFlash("DummyFlash")
      PLSG A 0 A_PlaySound("weapons/plasma/fire", CHAN_WEAPON)
      PLSG AAAA 0 A_FireCustomMissile("Lith_PlasmaBolt",
                                      CallACS("Lith_CircleSpread", 5 * IFIX, 3 * IFIX, false) / FIX,
                                      0, 0, -9, 0,
                                      CallACS("Lith_CircleSpread", 0, 0, true) / FIX)
      PLSJ A 1 offset(0, 37) bright
      PLSJ C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      goto Ready
   
   FireEmpty:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 A_PlaySound("weapons/plasma/empty", 7)
      PLSG A 1 offset(0, 37)
      PLSG A 1 offset(0, 36)
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG A 1 A_ReFire
      goto Ready
   
   AltFire:
      PLSG A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_PlasLaser), "FireReal")
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 90, TIF_NOTAKEINFINITE)
      PLSK A 0 A_GunFlash("DummyFlash")
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", 0, 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(-2.7, -1.9), 0, 0, -9)
      PLSK A 0 A_FireCustomMissile("Lith_PenetratingPlasmaBolt", frandom(2.7, 1.9), 0, 0, -9)
      PLSK A 1 offset(0, 37) bright A_PlaySound("weapons/plasma/altfire", CHAN_WEAPON)
      PLSK C 1 offset(0, 36) bright
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 32)
      PLSG AAAA 1 A_ReFire
      Goto Ready
   
   //
   // Laser
   //
   FireLaserReal:
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireBullets(0, 0, 1, 0, "Lith_PlasmaHit", FBF_NORANDOMPUFFZ)
      TNT1 A 0 A_RailAttack(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_TorgueMode) ? 1 : 15, 0, false, None, Red, RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 0, "Lith_Dummy", 0, 0, 0, 1, 3, 0, "Lith_PlasmaLaze")
      stop
   
   FireLaser:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A 0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_start")
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_loop", CHAN_7, 0.7, true)
      PLSG A 0 A_PlaySound("weapons/plasma/laser/fire_hum",  CHAN_6, 0.5, true)
      PLSG AA 0 A_GunFlash("FireLaserReal")
      PLSK A 2 offset(0, 35) bright A_GunFlash("FireLaserReal")
   FireLaserLoop:
      PLSG A 0 A_JumpIfInventory("Lith_PlasmaAmmo", 1, 1)
      goto FireEmpty
      PLSK A  0 A_TakeInventory("Lith_PlasmaAmmo", 35, TIF_NOTAKEINFINITE)
      PLSK B  1 offset(0, 39)  bright A_GunFlash("FireLaserReal")
      PLSK BB 1 offset(-2, 38) bright A_GunFlash("FireLaserReal")
      PLSK B  1 offset(1, 37)  bright A_GunFlash("FireLaserReal")
      PLSK B  1 offset(-1, 40) bright A_GunFlash("FireLaserReal")
      PLSK CC 1 offset(1, 37)  bright A_ReFire("FireLaserLoop")
   FireLaserDone:
      PLSG A 0 A_StopSound(CHAN_7)
      PLSG A 0 A_StopSound(CHAN_6)
      PLSG A 1 offset(1, 36) A_PlaySound("weapons/plasma/laser/fire_end")
      PLSG A 1 offset(0, 35)
      PLSG A 1 offset(0, 34)
      PLSG A 1 offset(0, 33)
      PLSG A 1 offset(0, 32)
      PLSG A 1
      goto Ready
   }
}
