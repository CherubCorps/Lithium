actor Lith_BaronOfHell : BaronOfHell replaces BaronOfHell
{
   PainChance 55
   Tag "$LITH_INFO_SHORT_BaronOfHell"
   Species "Lith_HellKnight"
   BloodColor "DarkGreen"
   
   +MISSILEMORE
   
   var int user_angle;
   
   states
   {
   Spawn:
      BOSS AB 10 A_Look
      loop
   See:
      BOSS AABBCCDD 3 A_Chase
      loop
   Melee:
      BOSS EF 6 A_FaceTarget
      BOSS G 4 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee")
      goto See
   Missile:
      BOSS EF 4 A_FaceTarget
      BOSS G 0 A_FaceTarget
      BOSS G 1 A_SpawnProjectile("Lith_BaronBallSound", 0, 0, CMF_BADPITCH)
      BOSS G 0 A_Jump(32, "MissileDone")
      BOSS G 0 A_FaceTarget
      BOSS G 0 A_SetUserVar("user_angle", frandompick(-45, 45, 0))
      BOSS GGG 1 A_SpawnProjectile("Lith_BaronBall", 32, 0, user_angle, CMF_AIMDIRECTION|CMF_BADPITCH)
      goto See
   MissileDone:
      BOSS G 3
      goto See
   Pain:
      BOSS H 1
      BOSS H 1 A_Pain
      goto See
   Death:
      "####" "#" 0 ACS_NamedExecuteWithResult("Lith_EmitScore", Score_BaronOfHell)
      goto Super::Death
   Raise:
      BOSS O 8
      BOSS NMLKJI 8
      goto See
   }
}

actor Lith_BaronBallSound : BaronBall replaces BaronBall
{
   DamageType "Lith_Magic"
   Damage 4
   
   states
   {
   Spawn:
      BAL7 AB 1 bright
      loop
   }
}

actor Lith_BaronBall : Lith_BaronBallSound
{
   SeeSound ""
   DeathSound ""
   Decal ""
}

// EOF
