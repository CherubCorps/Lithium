/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ShrapnelProjectile : Actor {
   default {
      BounceType "Doom";
      Damage 8;
      DamageType "Shrapnel";
      Gravity 0.5;
      Height 8;
      Radius 8;
      Species "Lith_Player";
      Speed 25;

      Projectile;
      +EXPLODEONWATER
      +MTHRUSPECIES
      -NOGRAVITY
   }

   states {
   Spawn:
      SGRN A 1;
      wait;
   }
}

class Lith_ShrapnelGun : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_ShrapnelGun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
}

action(Weapon) void Lith_A_FireShrapnel() {
   A_FireProjectile("Lith_ShrapnelProjectile", frandom(-5, 5), false, frandom(-5, 5), frandom(-7, 7), FPF_NOAUTOAIM);
}

states(Weapon) {
Ready:
   PISG A 1 Lith_A_Ready;
   loop;

Deselect:
   PISG A 1 A_Lower(12);
   loop;

Select:
   PISG A 1 A_Raise(12);
   loop;

Fire:
   #### # 0 A_JumpIf(countInv("Lith_ShellAmmo") < 4, "FireEmpty");
   #### # 0 A_TakeInventory("Lith_ShellAmmo", 4, TIF_NOTAKEINFINITE);
   PISG A 3 {for(int i = 0; i < 6; i++) Lith_A_FireShrapnel();}
   PISG A 3 {for(int i = 0; i < 4; i++) Lith_A_FireShrapnel();}
   PISG A 3 {for(int i = 0; i < 2; i++) Lith_A_FireShrapnel();}
   PISG A 34;
   goto ReadySelector;
FireEmpty:
   PISG A 2 offset(0, 42);
   PISG A 2 offset(0, 38);
   PISG A 2 offset(0, 35) A_StartSound("weapons/cshotgun/empty", lch_dryweapon);
   PISG A 1 offset(0, 34);
   PISG A 1 offset(0, 33);
   PISG A 1 offset(0, 32);
   goto ReadySelector;
}

/* EOF */
