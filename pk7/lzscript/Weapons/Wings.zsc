// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_WingsOfDeath : Lith_Weapon;

default
{
   Tag "$LITH_TAG_WingsOfDeath";
   Weapon.SlotNumber 0;
   Inventory.AltHudIcon "DWNGA0";

   +WEAPON.CHEATNOTWEAPON
}

states(Weapon)
{
Ready:
   TNT1 A 1 A_WeaponReady(WRF_NOFIRE);
   TNT1 A 0 {
      A_SetBlend("FF FF FF", 0.1, 5);
      Lith_CopyEffect.create("Lith_HeavenlyImage", self, (-4,-4,0), (-5,-7,1));
      Lith_CopyEffect.create("Lith_HeavenlyImage", self, (-4, 4,0), (-5, 7,1));
      self.GiveInventory("PowerFlight", 1);
      A_AlertMonsters();
      for(let it = BlockThingsIterator.create(self, 1024); it.next();) {
         let mo = it.thing;
         if(mo != self && mo.bSHOOTABLE && mo.checkSight(self)) {
            mo.target = self;
            mo.A_Die();
         }
      }
   }
   TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 40 == 0, "Flap");
   TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 2  == 0, "Ready");
   TNT1 A 0 A_ChangeVelocity(0, 0, -0.2);
   loop;
Flap:
   TNT1 A 0 {
      A_ChangeVelocity(0, 0, 4);
      A_StartSound("player/wings", lch_weapon);
   }
   goto ReadySelector;

Deselect:
   TNT1 A 0 {self.TakeInventory("PowerFlight", 1);}
   TNT1 A 0 Lith_A_Lower(_raise_instant);
   wait;
Select:
   TNT1 A 0 Lith_A_Raise(_raise_instant);
   wait;

Fire:
   goto ReadySelector;
}

/* EOF */
