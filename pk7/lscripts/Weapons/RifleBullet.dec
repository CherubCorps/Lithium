actor Lith_MarathonBulletPuff
{
   RenderStyle "Add"
   Alpha 1
   Scale 0.5
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      MPUF ABCDEFG 2 bright
      stop
   }
}

actor Lith_RifleBullet : FastProjectile
{
   Damage (1)
   Speed 512
   Radius 2
   Height 2
   Mass 9999
   DamageType "Lith_Bullets"
   Species "Lith_Player"
   
   +MTHRUSPECIES
   +BLOODSPLATTER
   
   states
   {
   Spawn:
      TNT1 A 1
      wait
   Crash:
      TNT1 A 0
      TNT1 A 0 A_Explode(7, 32, 0)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true) == false, 2)
      TNT1 A 0 A_PlaySound("marathon/hitsolid", 6)
      stop
   Death:
      TNT1 A 0
      TNT1 A 0 A_Explode(7, 32, 0)
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true) == false, "Death1")
      TNT1 A 0 A_PlaySound("marathon/hitwall", 6)
      TNT1 A 0 A_SpawnItemEx("Lith_MarathonBulletPuff")
      stop
   Death1:
      TNT1 A 0 A_SpawnItemEx("BulletPuff")
      stop
   XDeath:
      TNT1 A 0
      TNT1 A 0 A_JumpIf(CallACS("Lith_GetPlayerData", pdata_upgrade, UPGR_7777777, true) == false, 2)
      TNT1 A 0 A_PlaySound("marathon/hit", 6)
      
      TNT1 A 0 A_Explode(7, 32, 0)
      stop
   }
}

actor Lith_RifleBulletPainless : Lith_RifleBullet
{
   +PAINLESS
}

