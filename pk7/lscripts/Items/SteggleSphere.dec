actor Lith_RealSpectralSteggles
{
   RenderStyle "Add"
   Alpha 0.8
   Mass 5
   
   +NOBLOCKMAP
   +NOTELEPORT
   
   states
   {
   Spawn:
      DOGS A 0 nodelay A_PlaySound("dog/sight")
      DOGS A 24
      MISL B 0 A_PlaySound("explosion")
      MISL B 0 A_Explode(128, 128, 0)
      MISL BCD 2
      stop
   }
}

actor Lith_SpectralSteggles : CustomInventory
{
   Mass 2
   
   -NOGRAVITY
   
   states
   {
   Pickup:
      TNT1 A 0 A_JumpIfInventory("Lith_Dogs", 1, "Dogs")
      TNT1 A 0 A_RailWait
      stop
   Dogs:
      TNT1 A 0 A_SpawnItemEx("Lith_RealSpectralSteggles", 0, 0, 32, 10, 0, 9, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_SETTARGET)
      stop
   }
}

actor Lith_Dogs : CustomInventory
{
   Inventory.MaxAmount 1
   Inventory.InterHubAmount 0
   
   +INVENTORY.UNDROPPABLE
   
   states
   {
   Pickup:
      TNT1 A 0 A_GiveInventory("HealthBonus", 1)
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, log_dogs)
      stop
   Use:
      TNT1 A 0
      fail
   }
}

actor Lith_Steggles : MBFHelperDog
{
   Health 10000000
   PainChance 0
   
   +THRUACTORS
   +FRIENDLY
   
   states
   {
   Energize:
      DOGS A 0 A_GiveToTarget("Lith_Dogs", 1)
      stop
   PureSteggleEnergy:
      DOGS A 0 A_ChangeFlag("NOCLIP", true)
      DOGS A 0 A_ChangeFlag("NOBLOCKMAP", true)
      DOGS A 0 A_ChangeFlag("NOGRAVITY", true)
      DOGS A 0 A_SetTranslucent(0.8, 1)
      DOGS A 0 ACS_NamedExecuteAlways("Lith_SteggleEnergy", 0)
      DOGS A 1 A_JumpIfCloser(radius, "Energize")
      wait
   }
}

actor Lith_SteggleSphere : Lith_CustomInventory
{
   Inventory.PickupSound "misc/p_pkup"
   
   +COUNTITEM
   +INVENTORY.BIGPOWERUP
   +INVENTORY.FANCYPICKUPSOUND
   +FLOATBOB
   
   states
   {
   Spawn:
      DOGO ABCB 6 bright
      loop
   Pickup:
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_LogName", 0, log_doggosphere)
      TNT1 A 0 ACS_NamedExecuteAlways("Lith_DOGS", 0)
      stop
   }
}

// EOF

