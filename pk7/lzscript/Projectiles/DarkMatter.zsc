// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_DarkMatterDie : CustomInventory
{
   states(Item)
   {
   Pickup:
      TNT1 A 0
      {
         self.A_SetRenderStyle(frandom(0.1, 0.9), STYLE_Subtract);

         for(int i = 0; i < 77; i++)
            self.A_SpawnItemEx("Lith_PlayerDeathParticle", frandom(-32,32), frandom(-32,32), frandom(0, 64), frandom(-2, 2), frandom(-2, 2), frandom(1, 2), 0, SXF_NOCHECKPOSITION);

         self.A_Die();

         Actor.Spawn("Lith_DarkMatterDeath").target = self;
         return true;
      }
      stop;
   }
}

class Lith_DarkMatterDeath : Actor
{
   states
   {
   Spawn:
      TNT1 A 1
      {
         if(target) target.A_FadeOut(0.01, FTF_REMOVE);
         else       destroy();
      }
      loop;
   }
}

class Lith_DarkMatterPuff : Actor
{
   default
   {
      DamageType "DarkMatter";
      Species "Lith_Player";

      +DONTHARMSPECIES
      +NOGRAVITY
      +ALWAYSPUFF
      +MTHRUSPECIES
      +HITTRACER
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_PlaySound("weapons/dark/impact", CHAN_AUTO, 0.1);
      stop;
   }
}

class Lith_DarkMatterRailgun : Actor
{
   default
   {
      DamageType "DarkMatter";
      Species "Lith_Player";

      +DONTHARMSPECIES
      +NOGRAVITY
      +NOBLOCKMAP
      +NOCLIP
      +MTHRUSPECIES
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 0 A_CustomRailgun(10000, 0, 0, 0, RGF_SILENT|RGF_FULLBRIGHT|RGF_EXPLICITANGLE,
         0, 0.99, "Lith_DarkMatterPuff", frandom(0, 360), frandom(0, 360), 0, 1, 2, 1,
         "Lith_DarkMatterParticle");
      stop;
   }
}

class Lith_DarkMatterParticle : Actor
{
   default
   {
      RenderStyle "Subtract";
      Alpha 0.9;

      +NOINTERACTION
      +BRIGHT
   }

   states
   {
   Spawn:
      TNT1 A random(2, 5);
      BLLT A 1 {A_SetScale(frandom(1, 2)); A_FadeOut(0.05);}
      wait;
   }
}

class Lith_DarkMatter : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Speed 1;
      Damage 0;
      Scale 0.25;
      Species "Lith_Player";
      RenderStyle "Subtract";
      DamageType "DarkMatter";
      Mass 999999;

      Projectile;
      +DONTHARMSPECIES
      +FORCEXYBILLBOARD
      +MTHRUSPECIES
      +BRIGHT
      +FORCERADIUSDMG
   }

   states
   {
   Spawn:
      BLLT F 0;
      BLLT F 0 A_PlaySound("weapons/dark/wind", CHAN_5, 1, true, ATTN_NONE);
      BLLT F 1
      {
         A_RadiusGive("Lith_DarkMatterDie", 128, RGF_MONSTERS|RGF_NOTARGET);

         if(random(0, 20) == 2)
            A_PlaySound("weapons/dark/water", CHAN_6, 1, false, 0.25);

         for(int i = 0; i < 2; i++)
         {
            if(random(0, 15) == 5)
            {
               A_PlaySound("weapons/dark/fire", CHAN_WEAPON, 1, false, 0.5);
               int num = random(0, 10);
               for(int j = 0; j < num; j++)
                  A_SpawnItemEx("Lith_DarkMatterRailgun", frandom(-32,32),frandom(-32,32),frandom(-16,16), flags: SXF_SETTARGET);
            }
         }

         for(int i = 0; i < 7; i++)
            A_SpawnItemEx("Lith_DarkMatterParticle", frandom(-16,16),frandom(-16,16),frandom(-16,16), frandom(-2,2),frandom(-2,2),frandom(-2,0));
      }
      wait;
   }
}

// EOF

