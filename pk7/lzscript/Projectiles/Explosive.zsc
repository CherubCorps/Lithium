/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ExplosionAfterSmoke : Lith_BulletAfterSmoke {
   default {
      Alpha 0.1;
   }
}

class Lith_Explosive : Actor;

bool CheckMarathon(bool isTarget = true) {
   return Lith_UTIL.pData(isTarget ? target : Actor(self), pdata_upgrade, UPGR_Seven7s);
}

void Lith_A_ExplodeSound(int chan = lch_body2, bool target = true) {
   double p = FRandom(0.9, 1.1);
   if(CheckMarathon(target)) A_StartSound("marathon/explode", chan, pitch: p);
   else                      A_StartSound("explosion",        chan, pitch: p);
}

state Lith_A_JumpExplosion(bool target = true) {
   if(CheckMarathon(target)) return ResolveState("ExplodeMarathon");
   else                      return ResolveState("Explode");
}

state Lith_A_DoExplosion(float fxAmount = 1.0) {
   int numEmbers    = int(frandom(10.0, 20.0) * fxAmount);
   int numParticles = int(50.0 * fxAmount);

   if(fxAmount < 1.0) {
      fxAmount *= 2.0;
   }

   for(int i = 0; i < numEmbers; i++) {
      A_SpawnItemEx("Lith_HotEmber",
                    FRandom(-4, 4) * fxAmount,
                    FRandom(-4, 4) * fxAmount,
                    FRandom(-4, 4) * fxAmount,

                    FRandom(-8, 8)  * fxAmount,
                    FRandom(-8, 8)  * fxAmount,
                    FRandom(-4, 14) * fxAmount,

                    0, SXF_NOCHECKPOSITION);
   }

   for(int i = 0; i < numParticles; i++) {
      A_SpawnParticle("FFFF00", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
                      FRandom(-8,8) * fxAmount,
                      FRandom(-8,8) * fxAmount,
                      FRandom(-2,2) * fxAmount,

                      FRandom( 0,1)  * fxAmount,
                      FRandom(-5,5)  * fxAmount,
                      FRandom( 5,10) * fxAmount,

                      0, 0, FRandom(-0.5, -3),
                      1.0, -1);

      A_SpawnParticle("444444", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
                      FRandom(-8,8) * fxAmount,
                      FRandom(-8,8) * fxAmount,
                      FRandom(-2,2) * fxAmount,

                      FRandom( 0,1)  * fxAmount,
                      FRandom(-5,5)  * fxAmount,
                      FRandom( 5,10) * fxAmount,

                      0, 0, FRandom(-0.5, -3),
                      1.0, -1);
   }

   A_SpawnItemEx("Lith_ExplosionAfterSmoke",
                 FRandom(-1,1) * fxAmount,
                 FRandom(-1,1) * fxAmount,
                 FRandom(-2,2) * fxAmount,

                 FRandom(-0.5,0.5) * fxAmount,
                 FRandom(-0.5,0.5) * fxAmount,
                 FRandom(-0.2,0.2) * fxAmount);

   bNOGRAVITY = true;
   return Lith_A_JumpExplosion();
}

states {
Explode:
   MEXP AB 1 bright light("Lith_Explode1");
   MEXP CD 2 bright light("Lith_Explode2");
   MEXP EF 3 bright light("Lith_Explode3");
   stop;
ExplodeMarathon:
   MA7G BC 4 bright light("Lith_Explode1");
   MA7G DE 4 bright light("Lith_Explode2");
   MA7G FG 4 bright light("Lith_Explode3");
   stop;
}

/* EOF */
