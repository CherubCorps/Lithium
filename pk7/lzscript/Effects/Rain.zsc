// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │                       Effects taken from Realm667.                       │
// │   I think Tormenter made these originally, sorry if that's incorrect.    │
// │               Thanks to Kate for making these look better.               │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RainDrop : Actor {
   default {
      Alpha 0.25;
      Height 2;
      Radius 2;
      RenderStyle "Add";
      XScale 0.5;
      YScale 1.0;

      +DontSplash
      +DropOff
      +ForceYBillboard
      +NoBlockmap
      +NoInteraction
      +NoTeleport
      +NoTimeFreeze
      +NoTrigger
   }

   protected bool m_done;

   override void postBeginPlay() {
      super.postBeginPlay();
      vel      = (0, 0, fRandom(-12, -16));
      scale.y *= fRandom(1.5, 3.5);
      m_done   = false;
   }

   override void tick() {
      if(m_done) {
         Lith_UTIL.stateTick(self);
         a_fadeOut(0.25 / 21.0);
      } else if(waterLevel > 0 || pos.z <= floorZ) {
         setOrigin((pos.xy, floorZ), false);
         scale = (1, 1);
         m_done = true;
         setState(resolveState("Death"));
      } else {
         setOrigin(pos + vel, true);
      }
   }

   states {
   Spawn:
      RNDR A -1;
      stop;
   Death:
      RNDR BCDEFGH 3;
      stop;
   }
}

class Lith_BloodRainDrop : Lith_RainDrop {
   default {
      Alpha 0.5;
      RenderStyle "Subtract";
      Translation "Lith_BloodRainDrop";
      +Bright
   }
}

class Lith_AbyssRainDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
   }

   states {
   Spawn:
      RNDZ A -1;
      stop;
   Death:
      RNDZ BCDEFGH 3;
      stop;
   }
}

class Lith_SnowDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
      YScale 0.5;
   }

   states {
   Spawn:
      RNDS A -1;
      stop;
   Death:
      RNDS BCDE 12;
      stop;
   }
}

class Lith_Ash : Lith_SnowDrop {
   default {
      Scale 0.5;
      +RollSprite
   }
   private spriteId m_rnda;
   override void postBeginPlay() {
      m_rnda = getSpriteIndex("RNDA");
   }
   override void tick() {
      if(!m_done) {
         Lith_UTIL.stateTick(self);
         if(sprite == m_rnda) {
            a_setRoll(roll + 1, SPF_INTERPOLATE);
         }
      }
      super.tick();
   }
   states {
   Spawn:
      RDTF ABCDEFG 1 bright;
      RNDA A -1;
      wait;
   }
}

/* EOF */
