actor Lith_RendHitSound : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 A_PlaySound("weapons/rend/hit", CHAN_5)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Rend", true)
      stop
   }
}

actor Lith_RendHit : FastProjectile
{
   Damage (50*random(1,2))
   Radius 24
   Height 32
   Speed  196

   +FORCEPAIN

   states
   {
   Spawn:
      TNT1 A 1
      stop
   XDeath:
      TNT1 A 0 A_GiveInventory("Lith_RendHitSound", 1, AAPTR_TARGET)
      stop
   }
}

actor Lith_Rend : Lith_CWeapon
{
   Tag "$LITH_TXT_INFO_SHORT_Rend"
   Weapon.SlotNumber 0
   Weapon.SlotPriority 1
   Weapon.UpSound "weapons/rend/draw"

   +WEAPON.NOAUTOFIRE
   
   states
   {
   Ready:
      TNT1 A 1 bright A_WeaponReady(CallACS("Lith_GetWRF"))
      loop
   
   Deselect:
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_MagicSelect", 4)
   SelectLoop:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 0 A_JumpIfInventory("Lith_MagicAmmo", 10, 1)
      goto Ready
      TNT1 A 0 A_TakeInventory("Lith_MagicAmmo", 10)
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_Rend", false, random(1, 4))
      TNT1 A 0 A_PlaySound("weapons/rend/slash", CHAN_7)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_FireCustomMissile("Lith_RendHit")
      TNT1 A 5
      TNT1 A 0 A_ReFire
      TNT1 A 5 A_PlaySound("weapons/rend/end", CHAN_6)
      goto Ready
   }
}

// EOF
