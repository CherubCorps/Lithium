actor Lith_FistCharge : Inventory
{
   Inventory.MaxAmount 0x7FFFFFFF // Yes.
}

actor Lith_FistHit : Inventory
{
   Inventory.MaxAmount 1
}

actor Lith_ChargeFistPuff
{
   AttackSound "weapons/cfist/hitwall"
   RenderStyle "Translucent"
   Alpha 0.6
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      TNT1 A 0
      FHFX STUVW 3
      stop
   }
}

actor Lith_ChargeFistPickup : Lith_WeaponPickup
{
   states
   {
   Pickup:
      TNT1 A 0 ACS_NamedExecuteWithResult("Lith_WeaponPickup", weapon_cfist)
      stop
   }
}

actor Lith_ChargeFist : Lith_Weapon replaces Chainsaw
{
   Tag "$LITH_TXT_INFO_SHORT_ChargeFist"
   Weapon.Kickback 100
   Weapon.SlotNumber 1
   Weapon.SlotPriority 1
   DamageType "Lith_Melee"
   AttackSound "weapons/cfist/hit"
   
   +WEAPON.MELEEWEAPON
   +WEAPON.NOAUTOFIRE
   
   states
   {
   Spawn:
      TNT1 A 0
      CFIS Z 0 A_SpawnItemEx("Lith_ChargeFistPickup", 0,0,0, 0,0,0, 0, Lith_PF)
      stop
   
   Ready:
      TNT1 A 0 A_TakeInventory("Lith_FistHit", 999)
      TNT1 A 0 A_TakeInventory("Lith_FistCharge", 0x7FFFFFFF)
      TNT1 A 1 A_WeaponReady
      loop
   
   Deselect:
      TNT1 A 0 A_StopSound(CHAN_5)
      TNT1 A 0 A_Lower
      loop
   Select:
      TNT1 A 0 A_Raise
      loop
   
   Fire:
      TNT1 A 6 offset(0, 32)
      CFIS B 1 offset(264, 191) A_PlaySound("weapons/cfist/ready", CHAN_6)
      CFIS B 1 offset(240, 179) A_PlaySound("weapons/cfist/charge", CHAN_5, 1, true)
      CFIS B 1 offset(215, 162)
      CFIS B 1 offset(197, 151)
      CFIS B 1 offset(182, 145)
      CFIS B 2 offset(172, 141)
      CFIS B 2 offset(164, 137)
      CFIS B 2 offset(156, 133)
      CFIS B 2 offset(153, 132)
      CFIS B 3 offset(150, 131)
      CFIS B 4 offset(149, 131) A_GiveInventory("Lith_FistCharge", 35)
      CFIS B 0 offset(149, 131)
   WaitForRelease:
      CFIS B 1 offset(149, 131) A_GiveInventory("Lith_FistCharge", 2)
      CFIS B 1 offset(149, 131) A_ReFire("WaitForRelease")
   Release:
      CFIS B 3 offset(149, 131)
      CFIS B 2 offset(151, 133)
      CFIS B 1 offset(162, 144)
      CFIS B 1 offset(183, 168)
      CFIS B 0 offset(183, 168) A_PlaySound("weapons/cfist/chargeend", CHAN_5)
      CFIS B 1 offset(206, 188)
      CFIS A 0 A_AlertMonsters
      CFIS A 1 Offset(4, 135) A_PlaySound("weapons/cfist/fire", CHAN_6)
      CFIS A 2 Offset(36, 93)
      CFIS A 0 offset(35, 94) A_CustomPunch(CallACS("Lith_ChargeFistDamage"), true, CPF_PULLIN, "Lith_ChargeFistPuff", 96)
      CFIS A 0 A_JumpIfInventory("Lith_FistHit", 1, "HitRecoil")
      CFIS A 1 offset(36, 93)
      CFIS A 2 offset(40, 91)
      CFIS A 3 offset(43, 89)
      CFIS A 2 offset(43, 91)
      CFIS A 1 offset(30, 108)
      CFIS A 1 offset(3, 139)
      CFIS A 1 offset(-38, 179)
      TNT1 A 1
      TNT1 AAAAA 1 A_ReFire
      goto Ready
   HitRecoil:
      CFIS A 3 Offset(38, 91)
      CFIS A 1 Offset(43, 89)
      CFIS A 1 Offset(48, 92)
      CFIS A 1 Offset(53, 98)
      CFIS A 1 Offset(56, 108)
      CFIS A 1 Offset(66, 124)
      CFIS A 1 Offset(71, 151)
      CFIS A 1 Offset(78, 167)
      CFIS A 1 Offset(82, 175)
      CFIS A 1 Offset(85, 185)
      TNT1 A 1
      TNT1 AAAAA 1 A_ReFire
      goto Ready
   }
}
   
// EOF

