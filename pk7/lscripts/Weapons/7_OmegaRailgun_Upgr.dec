actor Lith_Weapon_OmegaRailgun : Lith_Weapon_PunctuatorCannon
{
   states
   {
   FireRail:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto FireEmpty
      CNNG A 5 offset(-1, 32) A_PlaySound("weapons/rail/charge", CHAN_5)
      CNNG A 5 offset(0, 32)
      CNNG A 5 offset(1, 32)
      CNNG A 5 offset(0, 33)
      CNNG A 5 offset(-1, 33)
      CNNG A 5 offset(0, 33)
      CNNG A 5 offset(1, 33)
      CNNG A 5 offset(0, 33)
   FireRailLoop:
      "####" A 0 A_JumpIfInventory("Lith_CannonAmmo", 2, 1)
      goto Ready
      CNNG A 0 A_TakeInventory("Lith_CannonAmmo", 2, TIF_NOTAKEINFINITE)
      CNNG A 0 A_AlertMonsters
      CNNG A 1 offset(0, 34)
      CNNG A 1 offset(0, 35)
      CNNG A 1 bright offset(0, 45)
      CNNG B 1 bright offset(0, 55) A_PlaySound("weapons/rail/fire", CHAN_WEAPON)
      CNNG B 1 bright offset(0, 56)
      CNNG B 0 A_FireCustomMissile("Lith_OmegaRailHax", 0, false, 0, -19)
      CNNG C 1 bright offset(0, 57) A_RailAttack(300, 0, false, "None", "None", RGF_SILENT, 0, "Lith_Dummy", 0, 0, 0, 0, 0.9, 0, "Lith_OmegaRailBeam", -19)
      CNNG C 1 bright offset(0, 47)
      CNNG A 1 offset(0, 42)
      CNNG A 2 offset(0, 37)
      CNNG A 3 offset(0, 35)
      CNNG A 4 offset(0, 33)
      CNNG AAAAAA 1 offset(0, 32) A_ReFire("FireRailLoop")
      goto Ready
   }
}

actor Lith_OmegaRailFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "112:127=168:191", "160:167=168:191", "224:231=168:168"
   Height 1
   Radius 1
   
   +NOBLOCKMAP
   +NOGRAVITY
}

actor Lith_OmegaRailParticle1
{
   RenderStyle "Add"
   Alpha 0.8
   Translation "192:207=168:191", "240:247=191:191"
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PLSS A 10
      PLSS A 1 A_FadeOut(0.01)
      wait
   }
}

actor Lith_OmegaRailParticle2 : Lith_OmegaRailFXBase
{
   Alpha 0.5
   XScale 0.3
   YScale 0.5
   
   states
   {
   Spawn:
      BFE1 C 10 bright A_FadeOut(0.1)
      loop
   }
}

actor Lith_OmegaRailBeam : Lith_OmegaRailFXBase
{
   RenderStyle "Add"
   Alpha 0.75
   Translation "112:127=168:191", "160:167=168:191", "224:231=168:168"
   XScale 0.8
   YScale 0.7
   DamageType "Lith_PlayerMissile"
   
   +EXTREMEDEATH
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_SpawnItemEx("Lith_OmegaRailParticle1", frandom(-57, 57),frandom(-57, 57),frandom(-57, 57))
      BFE1 C 25 bright A_Explode(24, 256, 0, false, 256)
      BFE1 DEF 4 bright
      stop
   }
}

actor Lith_OmegaRailExplosion : Lith_OmegaRailFXBase
{
   Scale 2
   
   +EXTREMEDEATH
   
   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/rail/impact")
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_OmegaRailParticle2", frandom(-32, 32),frandom(-32, 32),frandom(-32, 32), frandom(-2, 2),frandom(-2, 2),frandom(-2, 2))
      BFE1 C 15 bright A_Explode(128, 512, 0, false, 512)
      BFE1 DEF 3 bright
      stop
   }
}

actor Lith_OmegaRailHax : FastProjectile
{
   Speed 32000
   Damage 300
   Height 1
   Radius 1
   
   +NOGRAVITY
   +NOBLOCKMAP
   +THRUACTORS
   +EXTREMEDEATH
   
   states
   {
   Spawn:
      TNT1 A 1
      loop
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_OmegaRailExplosion", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
      stop
   }
}

// EOF