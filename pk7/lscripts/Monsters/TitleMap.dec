// Code from Fuck Super Shotguns, which I made. I probably released that one.
actor Lith_TitleMarine : ScriptedMarine
{
   Speed 4
   PainChance 0
   MaxTargetRange 16384
   MinMissileChance 2
   Species "Lith_Player"

   +FRIENDLY
   +MISSILEMORE
   +MISSILEEVENMORE
   +DONTHARMCLASS
   +DONTHARMSPECIES
   +NOINFIGHTING
   +INVULNERABLE

   States
   {
   See:
      "####" A 0 A_JumpIfCloser(128, "RunAway")
      "####" AAAA 1 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
      "####" BBB 1 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
      "####" B 1 A_Chase("Melee", "Missile", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" CCCC 1 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
      "####" DDD 1 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST)
      "####" D 1 A_Chase("Melee", "Missile", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      Loop
   Missile.Shotgun:
      "####" E 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_M_CheckAttack
      "####" E 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" E 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 bright A_CustomBulletAttack(5, 0, 10, 10, "Lith_Dummy")
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" F 0 A_JumpIfCloser(128, "RunAway")
      "####" A 0 A_JumpIf(true, "See")
   Missile.Chaingun:
      "####" E 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_FaceTarget
      "####" E 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" F 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_CustomBulletAttack(0, 0, 3, 5, "Lith_Dummy")
      "####" F 1 bright A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" F 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_CustomBulletAttack(0, 0, 3, 5, "Lith_Dummy")
      "####" F 1 bright A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
   Missile.ChaingunLoop:
      "####" F 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 0 A_CustomBulletAttack(5, 0, 3, 5, "Lith_Dummy")
      "####" F 1 bright A_FaceTarget
      "####" F 0 A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 0 A_CustomBulletAttack(5, 0, 3, 5, "Lith_Dummy")
      "####" F 1 bright A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" F 0 A_FaceTarget
      "####" F 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" F 0 A_JumpIfCloser(128, "RunAway")
      "####" F 0 A_Jump(32, "RunAway")
      "####" A 0 A_M_Refire
      "####" A 0 A_JumpIf(true, "Missile.ChaingunLoop")
      "####" A 0
      "####" A 0 A_JumpIf(true, "See")
   RunAway:
   "####" A 0 A_ChangeFlag("FRIGHTENED", 1)
      "####" AAAABBBBCCCCDDDD 1 A_Chase("Melee", "Missile", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" A 0 A_JumpIfCloser(128, "RunAway")
      "####" A 0 A_ChangeFlag("FRIGHTENED", 0)
      "####" A 0 A_JumpIf(true, "See")
   Missile:
      "####" A 0 A_JumpIfCloser(128, "RunAway")
      "####" A 0 A_Jump(256, "Missile.Shotgun", "Missile.Chaingun")
   Melee:
      "####" E 0 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_M_Saw
      "####" E 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_Chase("", "", CHF_NIGHTMAREFAST)
      "####" E 1 A_Chase("", "", CHF_FASTCHASE | CHF_NIGHTMAREFAST)
      "####" E 0 A_M_SawRefire
      "####" A 0 A_JumpIf(true, "Melee")
      "####" A 0
      "####" A 0 A_JumpIf(true, "See")
   }
}

actor Lith_TitleImpBall : Lith_XH_ImpBall
{
   SeeSound ""
   DeathSound ""
   Speed 20
}

actor Lith_TitleBaronBall : BaronBall
{
   SeeSound ""
   DeathSound ""
}

actor Lith_TitleCyberdemon : Lith_XH_Cyberdemon
{
   Species "Lith_TitleEnemy"
   
   +NOINFIGHTING
   +DONTHARMCLASS
   +DONTHARMSPECIES
}

actor Lith_TitleHellKnight : HellKnight
{
   SeeSound ""
   ActiveSound ""
   PainSound ""
   DeathSound ""
   Species "Lith_TitleEnemy"
   
   +NOINFIGHTING
   +DONTHARMCLASS
   +DONTHARMSPECIES
   
   states
   {
   Melee:
   Missile:
      BOS2 EF 8 A_FaceTarget
      BOS2 G 8 A_CustomComboAttack("Lith_TitleBaronBall", 32, 10 * random(1, 8), "baron/melee")
      goto See
   Death:
      BOS2 I 8
      BOS2 J 8 A_Scream
      BOS2 K 8
      BOS2 L 8 A_NoBlocking
      BOS2 MN 8
      BOS2 O 1000
      stop
   }
}

actor Lith_TitleImp : Lith_XH_Imp
{
   SeeSound ""
   ActiveSound ""
   PainSound ""
   DeathSound ""
   Species "Lith_TitleEnemy"
   
   +NOINFIGHTING
   +DONTHARMCLASS
   +DONTHARMSPECIES
   
   states
   {
   Melee:
   Missile:
      TROO EF 6 A_FaceTarget
      TROO G 5 A_CustomComboAttack("Lith_TitleImpBall", 32, 3 * random(1, 8), "imp/melee")
      TROO G 2
      goto See
   Death:
      TROO I 8
      TROO J 8 A_Scream
      TROO K 6
      TROO L 6 A_NoBlocking
      TROO M 1000
      stop
   }
}

// EOF

