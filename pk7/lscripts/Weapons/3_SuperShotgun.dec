actor Lith_SuperShotgunCharge : Inventory
{
   Inventory.MaxAmount 14
}

actor Lith_SuperShotgunChargeIter : Inventory
{
   Inventory.MaxAmount 14
}

actor Lith_SuperShotgun : Lith_Weapon replaces SuperShotgun
{
   Tag "Super Shotgun"
   Weapon.UpSound "weapons/ssg/draw"
   Weapon.SlotNumber 3
   
   const int chargemax = 14;
   
   states
   {
   Ready:
      SSGG A 1 A_WeaponReady
      wait
   Deselect:
      SSGG A 0 A_TakeInventory("Lith_SuperShotgunCharge", 999)
      SSGG A 0 A_TakeInventory("Lith_SuperShotgunChargeIter", 999)
   DeselectLoop:
      SSGG AAA 0 A_Lower
      SSGG A 1
      loop
   Select:
      SSGG A 0 A_TakeInventory("Lith_SuperShotgunCharge", 999)
      SSGG A 0 A_TakeInventory("Lith_SuperShotgunChargeIter", 999)
   SelectLoop:
      SSGG AAA 0 A_Raise
      SSGG A 1
      loop
   Flash:
      SSGG B 1 bright A_Light1
      SSGG C 1 bright A_Light2
      goto LightDone
   Fire:
      SSGG A 6
      {
         A_TakeInventory("Lith_SuperShotgunChargeIter", 999);
         A_GiveInventory("Lith_SuperShotgunCharge", 1);
         A_WeaponReady(LITH_BOBONLY);
      }
      goto FirePellets
   AltFire:
      SSGG A 7
      {
         A_TakeInventory("Lith_SuperShotgunChargeIter", 999);
         A_WeaponReady(LITH_BOBONLY);
      }
      SSGG A 3
      {
         A_GiveInventory("Lith_SuperShotgunCharge", 1);
         A_PlaySound("weapons/ssg/charge");
         A_WeaponReady(LITH_BOBONLY);
      }
      SSGG A 0
      {
         if(CountInv("Lith_SuperShotgunCharge") != chargemax)
            { A_ReFire; }
      }
      SSGG A 0 A_PlaySound("weapons/ssg/chargefinish", 6)
   FirePellets:
      SSGG A 1 offset(0, 32)
      {
         if(CountInv("Lith_SuperShotgunChargeIter") == CountInv("Lith_SuperShotgunCharge"))
            { return state("FireDone"); }
         return state(0);
      }
      SSGG AAAAAAAAAAAAAAAAAAAAA 0 A_FireBullets(11.2, 7.1, -1, 6 * random(1, 4), "BulletPuff", FBF_NORANDOM)
      SSGG A 0
      {
         A_FireCustomMissile("Lith_GunSmokeSpawner", 0, 0, 0, 6);
         A_PlaySound("weapons/sshotf", 7);
         A_GiveInventory("Lith_SuperShotgunChargeIter", 1);
         A_Recoil(1.2);
      }
      SSGG A 1 offset(0, 36) A_GunFlash
      SSGG A 1 offset(0, 38)
      goto FirePellets
   FireDone:
      SSGG D 3 A_WeaponReady(LITH_BOBONLY)
      SSGG E 3
      SSGG F 4
      SSGG GHI 3
      SSGG J 2 A_PlaySound("weapons/ssg/open", CHAN_WEAPON)
   Reload:
      SSGG Z 1
      SSGG KL 3
      SSGG M 4
      SSGG N 2 A_PlaySound("weapons/ssg/load", CHAN_WEAPON)
      SSGG O 2
      SSGG Z 0 A_TakeInventory("Lith_SuperShotgunCharge", 1)
      SSGG Z 0 A_JumpIf(CountInv("Lith_SuperShotgunCharge"), "Reload")
      SSGG P 3
      SSGG Q 3
      SSGG R 3 A_PlaySound("weapons/ssg/close", CHAN_WEAPON)
      SSGG S 3
      SSGG A 5 A_WeaponReady
      goto Ready
   Spawn:
      SSGG X 0
      SSGG X 0 A_SetUserVar("user_pickupparm", weapon_supershotgun)
      goto SpawnLoop
   }
}

